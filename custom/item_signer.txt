//===== rAthena Script =======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,24,61,7	script	Perchik	47,{
	
	setarray .@Item[0],644,3;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[捏]";
	mes "我幾乎可以 ^0055fffsign您的名字 ^000000，幾乎所有您擁有的稀有物品。";
	next;
	if(select("告訴我更多...:請在我的物品上簽名！") == 1) {
		mes "[捏]";
		mes "我可以將您的名字放在任何無槽設備或武器上。";
		emotion ET_AHA;
		next;
		mes "[捏]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "對於我的工作，我接受：";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes "～"+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes "～"+callfunc("F_InsertComma",.@Cost[0])+" Zeny";
			if (.@Cost[1]) mes "～"+callfunc("F_InsertComma",.@Cost[1])+" Zeny per refine";
		}
		else mes "我免費工作，但是...";
		next;
		emotion ET_CRY;
		mes "[捏]";
		mes "las，我有12個飢餓的孩子";
		mes "還有一個非常生氣的妻子。";
		next;
		mes "[捏]";
		mes "或者是12個生氣的孩子";
		mes "還有一個非常飢餓的妻子...";
		emotion ET_SCRATCH;
		close;
	}
	mes "[捏]";
	mes "告訴我您的物品要簽名...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[捏]";
	if (!getequipisequiped(.@part)) {
		mes "那裡什麼都沒有！";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "las，此項目已經簽名。";
		mes "我永遠不會碰到主人的作品。";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "卡？這裡？ ！";
		mes "正如我之前說的，我不簽署帶卡的物品。";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id, ITEMINFO_SLOT)) {
		mes "抱歉，我不簽署開槽的物品。";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "抱歉，我不簽署出租物品！";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "我需要：";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes "～"+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes "～"+.@price+" Zeny";
	}
	mes "我可以簽署你的 ^0055ff嗎"+getitemname(.@id)+"^000000?";
	next;
	if(select("好的！ :離開")==2) {
		mes "[捏]";
		mes "再見...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[捏]";
	if (Zeny < .@price || .@nr) {
		mes "我不為“謝謝”工作。";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "在哪裡"+getitemname(@id)+"...?";
		npctalk "Perchik : You're a snoozy cheater!";
		logmes "黑客:嘗試簽署一個沒有它的項目:"+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "完畢！";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
