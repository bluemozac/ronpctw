//===== rAthena Script ======================================= 
//= Event King Items
//===== By: ================================================== 
//= $ephiroth
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Grand Circlet Quest combined with Excalibur Quest.
//===== Additional Comments: =================================
//= 2006/09/27: 1.0 Release and fully working. [$ephiroth]
//= 1.1 Optimized. [Euphy]
//============================================================

prt_castle,80,170,4	script	King of Prontera	108,{
	mes "[Prontera之王]";
	mes "你好，你能幫我嗎？";
	next;
	if(select("當然，我能做什麼？ :抱歉，我很忙。")==2) {
		mes "[Prontera之王]";
		mes "好的，很好，我敢肯定別人會有所幫助。";
		close; }
	mes "[Prontera之王]";
	mes "謝謝。好吧，我旁邊的女兒有點吵...";
	mes "如果您給她這些物品，我敢肯定她會冷靜下來。";
	mes "我會給你我的王冠或劍作為獎勵。";
	next;
	setarray .@Items1[0],5007,742,30,741,50,753,20,732,1,710,20,629,20;
	setarray .@Items2[0],1137,2254,1,2282,1,714,1;
	switch(select("你需要什麼？ :我有東西！ :算了。")) {
		case 1:
			for(set .@i,1; .@i<3; set .@i,.@i+1) {
				mes "[Prontera之王]";
				mes "如果你能得到我：";
				for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
					mes "〜^0055ff"+getd(".@Items"+.@i+"["+(.@j+1)+"]")+"^000000x "+getitemname(getd(".@Items"+.@i+"["+.@j+"]"));
				mes "我給我的"+((.@i == 2 )?"sword.":"crown.");
				if (.@i==2) close;
				next; }
		case 2:
			mes "[Prontera之王]";
			mes "您想要什麼項目？";
			next;
			set .@i, select("皇冠:劍");
			for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
				if (countitem(getd(".@Items"+.@i+"["+.@j+"]")) < getd(".@Items"+.@i+"["+(.@j+1)+"]")) set .@nr,1;
			mes "[Prontera之王]";
			if (.@nr) { 
				mes "您缺少一些東西。";
				mes "收到所有物品後回來。";
				close; }
			for(set .@j,1; .@j<getarraysize(getd(".@Items"+.@i)); set .@j,.@j+2)
				delitem getd(".@Items"+.@i+"["+.@j+"]"), getd(".@Items"+.@i+"["+(.@j+1)+"]");
			getitem getd(".@Items"+.@i+"[0]"),1;
			mes "謝謝你！";
			mes "這肯定會使她平靜下來。";
			mes "如所承諾的，這是我的"+((.@i==1)?"crown.":"sword.");
			close;
		case 3:
			mes "[Prontera之王]";
			mes "好的，很好，我敢肯定別人會有所幫助。";
			close;
	}
}

prt_castle,84,170,4	script	King's Daughter	62,{
	mes "[國王的女兒]";
	mes "嗯，我現在討厭我的父親！";
	next;
	if(select("為什麼？ :不管怎樣...")==2) {
		mes "[國王的女兒]";
		mes "很好！ hmph！";
		close; }
	mes "[國王的女兒]";
	mes "好吧，他不會給我我想要的！";
	mes "他是普羅屬國王。他應該能給我任何東西！ ！ ！";
	close;
}
