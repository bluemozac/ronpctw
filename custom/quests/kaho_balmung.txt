//===== rAthena Script =======================================
//= Balmung & Lord Kaho's Horns Quest
//===== By: ==================================================
//= MagicalTux
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Custom quests for Balmung and Lord Kaho's Horns.
//= WARNING: The quests are very easy, so be careful!
//===== Additional Comments: =================================
//= 1.1 Cleaned and optimized. [Euphy]
//============================================================

prontera,158,356,4	script	Royal Messenger	105,{
	
	setarray .@Req1[0],1161,999,120,984,10,996,10,994,10,995,10,997,10;
	setarray .@Req2[0],5013,2298,1,1001,10,996,10,994,10,995,10,997,10;
	
	mes "[皇家信使]";
	mes "歡迎來到prontera。";
	mes "我是負責皇家任務的使者。";
	next;
	if(select("聽著……:走開")==2) close;
	mes "[皇家信使]";
	mes "有兩個任務。";
	mes "選擇您喜歡的那個。";
	next;
	set .@i, select("巴爾蒙:卡霍大人的角:^777777沒關係。 ^000000");
	mes "[皇家信使]";
	if (.@i==3) {
		mes "祝你今天過得愉快。";
		close; }
	mes "您將需要以下項目：";
	for(set .@j,1; .@j<13; set .@j,.@j+2) {
		mes "～"+getd(".@Req"+.@i+"["+(.@j+1)+"]")+"x ^0055FF"+getitemname(getd(".@Req"+.@i+"["+.@j+"]"))+"^000000";
		if (!.@nr && countitem(getd(".@Req"+.@i+"["+.@j+"]")) < getd(".@Req"+.@i+"["+(.@j+1)+"]")) set .@nr,1; }
	mes "~1,000,000Z";
	next;
	mes "[皇家信使]";
	if (.@nr || Zeny < 1000000) {
		mes "滿足所有要求時回來。";
		close; }
	for(set .@j,1; .@j<13; set .@j,.@j+2)
		delitem getd(".@Req"+.@i+"["+.@j+"]"), getd(".@Req"+.@i+"["+(.@j+1)+"]");
	set Zeny, Zeny-1000000;
	mes "我看到您已經擁有了所有您需要的項目。這是你的"+getitemname(getd(".@Req"+.@i+"[0]"))+", as promised.";
	getitem getd(".@Req"+.@i+"[0]"),1;
	close;
}
