//===== rAthena Script =======================================
//= The 2nd Bank of Prontera (with daily 0.01% income!)
//===== By: ==================================================
//= Lupus (1.0)
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A bank which has an interest %
//===== Additional Comments: =================================
// Look for this formula and setup your Bank daily % interest
// #kafrabank/1000 = 0.1% of interest per day
// #kafrabank/100  =  1%  of interest per day
// #kafrabank/10   =  10% of interest per day
//
// 1.1 Added log of bank operation -> logmes "一些信息";
// 1.2 Set max income to 100,000z. It would help to avoid
//     zeny exploits when you change DATE at your server
// 1.2a Corrected bad duplicate names. (bugreport:921) [Samuray22]
//============================================================

-	script	Bank Clerk::bankg	833,{
	mes"[Maniss]";
	mes strcharinfo(0)+"，歡迎來到Prontera的第二岸！";

	set @kb_int,(gettime(DT_MONTH)*31)+gettime(DT_DAYOFMONTH); //today's number
	set @income,0;
	//calculate %
	if (#kafrabank<=0 || #kb_int>=@kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank/1000)*(@kb_int-#kb_int); //@income == % of the sum
	//max income constant:
	if (@income>100000) set @income,100000;
L_NoIncomeToday:
	set #kb_int,@kb_int; //reset days timer

	if(#kafrabank==0) mes "我們可以為您開設一個帳戶。";
	if(@income>0) mes "今日收入:^135445" + callfunc("F_InsertComma",@income) + "^000000 zeny.";
	set #kafrabank,#kafrabank+@income;
	if(#kafrabank>0) mes "您的帳號:^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny.";
	mes "你要什麼？";
	next;
	if(#kafrabank==0) menu "-Open an account",-,"-Quit",B_EXIT2;
	if(#kafrabank>0) menu "-Deposit money",-,"-Withdraw money",M_WITHDRAW,"-Quit",B_EXIT2;

	mes"[Maniss]";
	mes "請告訴我您想存入多少Zeny。";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1000) goto L_LESS_1000;
	if(@kafrabank>zeny) goto L_NOT_ENOUGH;
	set Zeny, Zeny-@kafrabank;
	set #kafrabank,#kafrabank+@kafrabank;
	mes"[Maniss]";
	mes "您已經存入了 ^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000.";
	//we log these zeny operations into the log db
	logmes "銀行 ％:" + @income +"z, Deposit: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

M_WITHDRAW:
	if(#kafrabank==0) goto L_ZERO_ACCOUNT;
	mes"[Maniss]";
	mes "您的帳戶： ^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny.";
	mes "您想撤回多少Zeny？";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1) goto B_EXIT2;
	if(@kafrabank>#kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank-@kafrabank;
	set Zeny, Zeny+@kafrabank;
	mes"[Maniss]";
	mes "這是您的 ^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000, put your sign here...";
	//we log these zeny operations into the log db
	logmes "銀行 ％:" + @income +"z, Withdraw: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[Maniss]";
	mes "您沒有足夠的Zeny來進行此操作。";
	next;
	goto B_EXIT2;

L_ZERO_ACCOUNT:
	mes"[Maniss]";
	mes "您的帳戶上沒有任何Zeny！";
	next;
	goto B_EXIT2;

L_TOO_BIG_AMOUNT:
	mes"[Maniss]";
	mes "對不起。您一度可以進行的最大存款是10,000,000 Zeny。";
	next;
	goto B_EXIT2;

L_LESS_1000:
	mes"[Maniss]";
	mes "抱歉，您可以存入的最低Zeny數量是1,000 Zeny。";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "很好...很快再來！";
	next;

B_EXIT2:
	mes"[Maniss]";
	mes "感謝您使用我們的銀行服務。我們希望很快能再次見到你。";
	close;
}

prontera,131,190,1	duplicate(bankg)	Bank Clerk#1-1	833
geffen,125,73,3	duplicate(bankg)	Bank Clerk#2-1	833
izlude,145,107,1	duplicate(bankg)	Bank Clerk#3-1	833
morocc,147,84,7	duplicate(bankg)	Bank Clerk#4-1	833
