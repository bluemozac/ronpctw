//===== rAthena Script =======================================
//= Black Jack
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Black Jack card game.  Gameplay based off standard casino
//= black jack rules.  Dealer must have at least 17 to stay and will
//= automatically stay at 17 and up.  Player must have at least
//= 13 to stay.  Aces counted as 11 or 1.  Option to "Double Down".
//= Currently does not allow for "insurance", or "splitting"
//= of pairs.
//===== Additional Comments: =================================
//= 1.0 First version.
//============================================================

cmd_in02,188,89,1	script	Black Jack Dealer	57,{
	mes "[經銷商]";
	mes "你好呀！您想玩黑色傑克遊戲嗎？";
M_Menu:
	next;
	menu "What are the rules?",M_0, "I want to play.",L_Play, "No thanks.",M_End;

M_0:
	mes "[經銷商]";
	mes "您想知道什麼？";
sM_Menu0:
	next;
	menu "The basics.",sM_0a, "Winning and loosing.",sM_0b, "'Double Down'.",sM_0c,
	"Ace value.",sM_0d, "Nothing.",M_Menu;

sM_0a:
	mes "[經銷商]";
	mes "黑傑克是一款紙牌遊戲，目標是獲得21個或";
	mes "盡可能接近21分，而無需超過21分。";
	next;
	mes "[經銷商]";
	mes "在比賽開始之前，玩家必須下注。  一旦下注";
	mes "已經製作了，經銷商和玩家都犯了2張卡片";
	mes "每個。  根據您擁有的卡，您可以選擇 ^5533ff'stay' ^000000，";
	mes "或者，您可以選擇 ^5533ff'pull' ^000000。";
	next;
	mes "[經銷商]";
	mes "當您選擇“留下”時，您會告訴經銷商您不這樣做";
	mes "需要再使用卡。  這允許經銷商在他/她的情況下拉";
	mes "想要。  為了留下來，您必須擁有 ^ff3355，至少13分 ^000000。這";
	mes "經銷商只有在他/她獲得^ff3355 17點或更多^000000時才能留下。";
	next;
	mes "[經銷商]";
	mes "當您選擇“拉”時，您會告訴經銷商您想要的";
	mes "另一張卡。  通過拉更多的卡，您可以提高觀點";
	mes "全部的。  只要您沒有 ^ff33555，比21 ^000000點可以拉動";
	mes "甲板上的卡。";
	goto sM_Menu0;
sM_0b:
	mes "[經銷商]";
	mes "黑色傑克有3種勝利和鬆動的方法。";
	next;
	mes "[經銷商]";
	mes "1.）在黑色千斤頂結束時，如果您有更多的積分";
	mes "比經銷商，您將贏得這一輪。  相反，如果您有";
	mes "比您會失去的經銷商要少。";
	next;
	mes "[經銷商]";
	mes "2.）如果您拉了一張可以使您的觀點總數超過21的卡";
	mes "將自動放鬆這一輪。  這稱為a ^5533ff'bust' ^000000。  如果";
	mes "經銷商破產，然後您將贏得比賽。";
	next;
	mes "[經銷商]";
	mes "3.）如果您的前2張卡總共有21點，您";
	mes "將自動贏得這一輪。  這稱為a ^5533ff'black jack' ^000000";
	mes "當您獲得“ Ace”和“ 10值”卡時，就會發生。如果是";
	mes "經銷商會得到黑色傑克，他/她將自動贏得這一勝利。";
	next;
	mes "[經銷商]";
	mes "4.）除了獲勝和失敗外，您還可以與經銷商保持聯繫。  如果";
	mes "您和經銷商在一個結束時都有相同的總數";
	mes "圓，這將導致沒有贏家，也沒有損失或收穫";
	mes "金錢。";
	next;
	mes "[經銷商]";
	mes "這被稱為a ^5533ff'push' ^000000與經銷商一樣。  這也是如此";
	mes "適用於您和擁有黑色傑克的經銷商";
	mes "時間。";
	goto sM_Menu0;
sM_0c:
	mes "[經銷商]";
	mes "“ double down”選項使您可以將當前的賭注加倍，";
	mes "但是，有了您只能拉一個的缺點";
	mes "附加卡。  此選項僅在開頭";
	mes "每個回合。";
	next;
	mes "[經銷商]";
	mes "何時加倍有用的一個例子是";
	mes "您的前2張卡總共有11分。";
	mes "您繪製的下一張卡有21或20的機會。  這";
	mes "將是一條好雙手。";
	goto sM_Menu0;
sM_0d:
	mes "[經銷商]";
	mes "“ Ace”卡是黑傑克遊戲中的獨特卡片，因為";
	mes "它可以有2個值。  王牌可以算作11分，";
	mes "或僅1分。  例如，如果您有王牌和4，那將";
	mes "給你15或5分。";
	next;
	mes "[經銷商]";
	mes "如果您決定留下來，王牌將自動算作";
	mes "11分，總共15分。";
	next;
	mes "[經銷商]";
	mes "如果您決定拉並收到9，王牌將";
	mes "自動算作1分，總共14點";
	mes "點。  如果將王牌計為11分，那麼您將有一個";
	mes "總數超過21，將會失去。";
	next;
	mes "[經銷商]";
	mes "正是由於您對“ ACE”的靈活性，這使得";
	mes "它是遊戲中最強大的卡。";
	goto sM_Menu0;
M_End:
	mes "[經銷商]";
	mes "隨時隨時回來";
	close;

//================
L_Play:
	mes "[經銷商]";
	mes "請下注...";
	next;
	menu "2z",M_1a, "10z",M_1b, "20z",M_1c, "100z",M_1d, "Too rich for my blood....",M_End;

M_1a:
	if(Zeny < 2) goto sL_NotEnuf;
	set @bet, 2;
	goto L_Cont0;
M_1b:
	if(Zeny < 10) goto sL_NotEnuf;
	set @bet, 10;
	goto L_Cont0;
M_1c:
	if(Zeny < 20) goto sL_NotEnuf;
	set @bet, 20;
	goto L_Cont0;
M_1d:
	if(Zeny < 100) goto sL_NotEnuf;
	set @bet, 100;
	goto L_Cont0;

sL_NotEnuf:
	mes "[經銷商]";
	mes "對不起，但是您沒有足夠的Zeny來下注。";
	close;

L_Cont0:
	mes "（正在處理卡片）";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- 這是 ^ff5533 -dealer的 ^000000卡：";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "經銷商有： ^ff5533"+@dealTot+"^000000";
	mes " ";
	mes "- 這是 ^5533ffyour ^000000卡：";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "您有:^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "你有:^5533FF" +@playTot+ "^000000, or ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "Hit me(pull)",M_Hit, "Stay",M_Stay, "Double Down",M_Double;
	if(@dealerTurn == 0) menu "Hit me(pull)",M_Hit, "Stay",M_Stay;

M_Stay:
	mes "[經銷商]";
	if(@playTot < 13) goto sL_PlayToLow;
	if(@dealTot > 16) mes "經銷商留下來。";
	if(@dealTot > 16 || @numD == 5) goto L_Check;
	mes "經銷商要拉";
	next;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	set @dealerTurn, 1;
	goto L_Start;

sL_PlayToLow:
	mes "對不起，但是您的總數不足以保持。  你必須拉。";
	next;
	goto M_Hit;

M_Hit:
	if(@numP == 5) goto M_Stay;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;
M_Double:
	mes "[經銷商]";
	mes "玩家選擇加倍。  您當前的賭注是";
	mes "加倍，您將只能額外拉1張卡。";
	next;
	set @dealerTurn, 1;
	set @bet, @bet*2;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

sL_Win:
	mes "[經銷商]";
	mes "恭喜，你贏了！";
	next;
	set Zeny, Zeny + @bet;
	goto L_Play;
sL_Lose:
	mes "[經銷商]";
	mes "對不起，你輸了。";
	set Zeny, Zeny - @bet;
	next;
	goto L_Play;
sL_Push:
	mes "[經銷商]";
	mes "它的推動。  你與經銷商捆綁在一起。";
	next;
	goto L_Play;


//==================================
	// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
	// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

GetDealTot:
	set @dealTot, @dealTot + @dealCard[@i];
	set @i, @i + 1;
	if(@i < @numD) goto GetDealTot;
	if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
	set @i, 0;
GetPlayTot:
	set @playTot, @playTot + @playCard[@i];
	set @i, @i + 1;
	if(@i < @numP) goto GetPlayTot;
	if(@pAce == 1 && @playTot > 21) set @pAce, 2;
	if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
	// Sub function for displaying the Cards
sF_D2cards:
	mes ".-----。 .-----。";
	mes "|"+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "'-----''-----'";
	return;
sF_P2cards:
	mes ".-----。 .-----。";
	mes "|"+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "'-----''-----'";
	return;
sF_D3cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "'-----''-----''------'";
	return;
sF_P3cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "'-----''-----''------'";
	return;
sF_D4cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes "'-----''-----''------'";
	mes ".-----。";
	mes "|"+@dealCard$[3]+" |";
	mes "'------'";
	return;
sF_P4cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes "'-----''-----''------'";
	mes ".-----。";
	mes "|"+@playCard$[3]+" |";
	mes "'------'";
	return;
sF_D5cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes "'-----''-----''------'";
	mes ".-----。 .-----。";
	mes "|"+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "'-----''-----'";
	return;
sF_P5cards:
	mes ".-----。 .-----。 .-----。";
	mes "|"+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes "'-----''-----''------'";
	mes ".-----。 .-----。";
	mes "|"+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "'-----''-----'";
	return;
}
