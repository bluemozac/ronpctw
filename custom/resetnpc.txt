//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
prontera,150,193,4	script	Reset Girl	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[重置女孩]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "抱歉，您只能重置"+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life.";
		close;
	}
	mes "我是重置女孩。";
	mes "重置統計："+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "重置技能："+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "重置兩個："+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "您只能重置"+callfunc("F_InsertPlural",.@Reset[3],"time")+", so use "+((.@Reset[3]>1)?"them":"it")+" wisely.";
	mes "請選擇您想要的服務：";
	next;
	set .@i,(select("^FF3355重置技能:重置統計數據:重置兩者^000000:取消"));
	if(.@i > 3) close;
	mes "[重置女孩]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "抱歉，您沒有足夠的Zeny。";
		close;
	}
	if(.@Reset[3]){
		mes "你只能重置"+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life, are you sure?";
		if(select("讓我想想:這樣就好了") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "你去！";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
