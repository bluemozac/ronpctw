//===== rAthena Script ======================================= 
//= PvP NPCs
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf, Elias (og2)
//===== Current Version: =====================================
//= 2.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= PvP NPCs that are found in the Inns in major cities.
//= Includes Yoyo Mode, Nightmare Mode, and Event Mode.
//===== Additional Comments: ================================= 
//= 2.0 Narrators/GateKeepers,etc. Translated from the Official 2011 [Elias]
//= 2.1 Changed PVP Narrator and GateKeeper to Floating NPC [Elias]
//= 2.2 Some cleaning. [Euphy]
//= 2.3 Fixed grammatical errors. [Joseph]
//= 2.4 Fixed incorrect use of 'close' and some cleaning. [Joseph]
//= 2.5 Renewal/Pre-Renewal split. [Euphy]
//============================================================

// Information
//============================================================ 
-	script	PVP Narrator::pvpe	-1,{
	mes "[PVP敘述者]";
	mes "你好，歡迎！";
	mes "我負責";
	mes "解釋PVP模式。";
	mes "我是PVP敘述者！";
	next;
	switch(select("什麼是 PVP？ :PVP 模式是什麼？ :PVP 規則是什麼？ :保存位置。 :結束對話框。")) {
	case 1:
		mes "[PVP敘述者]";
		mes "簡而言之，PVP意味著";
		mes "“玩家與玩家模式”";
		mes "這是人們的獨特地方";
		mes "互相決鬥。";
		next;
		mes "[PVP敘述者]";
		mes "只是告訴";
		mes "^3355FFGATE門將^000000";
		mes "您想嘗試。他會的";
		mes "讓您進入PVP正方形。";
		next;
		mes "[PVP敘述者]";
		mes "但是，你需要在";
		mes "最少31號。";
		mes "必須支付500 Zeny入口費";
		mes "進入PVP戰鬥廣場。";
		break;
	case 2:
		mes "[PVP敘述者]";
		mes "當您有資格時，您";
		mes "可以選擇兩種模式之一。";
		mes "yoyo模式或噩夢模式。";
		next;
		mes "[PVP敘述者]";
		mes "yoyo模式無風險。";
		mes "您可以體驗PVP";
		mes "沒有任何限製或";
		mes "懲罰。建議使用";
		mes "你練習自己的技能";
		mes "在您繼續前進之前。";
		next;
		mes "[PVP敘述者]";
		mes "噩夢模式非常";
		mes "危險的！請謹慎，";
		mes "你會失去一些";
		mes "當您被擊敗時經驗。和";
		mes "很小的機會";
		mes "您將放一些設備。";
		next;
		mes "[PVP敘述者]";
		mes "但是，如果您獲勝，";
		mes "獎勵可以很棒！";
		mes "避免誤會，";
		mes "你應該三思而後行";
		mes "在您去那里之前...";
		mes "祝你好運！";
		break;
	case 3:
		mes "[PVP敘述者]";
		mes "每個戰鬥正方形";
		mes "有一排敘述者和";
		mes "根據您的選擇";
		mes "資格。";
		next;
		mes "[PVP敘述者]";
		mes "每個敘述者都會問";
		mes "五個PVP地圖中的哪一個";
		mes "你想去。";
		mes "選擇，然後進去！";
		next;
		mes "[PVP敘述者]";
		mes "每張地圖都有限制";
		mes "可以的人數";
		mes "參加。所以你會看到";
		mes "拐角處的數字顯示";
		mes "“與會者/總計”。";
		next;
		mes "[PVP敘述者]";
		mes "另外，還有一個隱藏的經驗";
		mes "在PVP模式下值。這個經驗";
		mes "得分只會適用";
		mes "PVP區域的";
		mes "擔心。";
		next;
		mes "[PVP敘述者]";
		mes "每個玩家的經驗";
		mes "開始通常是5分。";
		mes "如果您贏了，它將";
		mes "增加1分";
		next;
		mes "[PVP敘述者]";
		mes "以同樣的方式，當你";
		mes "失去...您的經驗會";
		mes "下降5分。";
		mes "所以小心！";
		next;
		mes "[PVP敘述者]";
		mes "當你被擊敗時";
		mes "而且您的經驗相等";
		mes "到或小於0，";
		mes "您將從PVP中刪除";
		mes "你的決鬥完成了！";
		next;
		mes "[PVP敘述者]";
		mes "但是，如果您的經驗更多";
		mes "比0。你仍然可以得到幫助";
		mes "通過其他玩家康復...";
		mes "你明白了嗎？";
		next;
		mes "[PVP敘述者]";
		mes "戰鬥命令內部";
		mes "PVP與正常情況相同。";
		mes "所有基本控件是";
		mes "相同的。";
		next;
		mes "[PVP敘述者]";
		mes "但是，有一件事...";
		mes "在PVP戰鬥廣場內";
		mes "和PVP戰區，";
		mes "您無法保存自己的位置。";
		mes "記住好...這些規則";
		mes "可以幫助確保您的勝利。";
		break;
	case 4:
		mes "[PVP敘述者]";
		mes "位置成功保存...";
		mes "非常感謝！";
		mes "我們很快會再次見到你。";
		if(strnpcinfo(4) == "morocc_in") { savepoint "morocc_in",141,139,1,1; }
		if(strnpcinfo(4) == "alberta_in") { savepoint "alberta_in",22,148,1,1; }
		if(strnpcinfo(4) == "prt_in") { savepoint "prt_in",54,137,1,1; }
		if(strnpcinfo(4) == "geffen_in") { savepoint "geffen_in",70,59,1,1; }
		if(strnpcinfo(4) == "payon_in01") { savepoint "payon_in01",142,46,1,1; }
		break;
	case 5:
		mes "[PVP敘述者]";
		mes "怪物之間的戰爭肆虐";
		mes "和人類，這場比賽";
		mes "在人們中-PVP-";
		mes "鼓勵我們所有人";
		mes "更強。再來，";
		mes "我們歡迎您的挑戰！";
		break;
	}
	close;
}
morocc_in,144,138,4	duplicate(pvpe)	PVP Narrator#pe1	84
alberta_in,22,146,4	duplicate(pvpe)	PVP Narrator#pe2	84
prt_in,56,140,4	duplicate(pvpe)	PVP Narrator#pe3	84
geffen_in,67,63,4	duplicate(pvpe)	PVP Narrator#pe4	84
payon_in01,142,50,4	duplicate(pvpe)	PVP Narrator#pe5	84
geffen_in,67,63,4	duplicate(pvpe)	PVP Narrator#pe6	84

// Mode Select
//============================================================ 
-	script	Gate Keeper::gkut	-1,{
	mes "[守門員]";
	mes "很高興能服務。";
	mes "我將打開PVP戰鬥";
	mes "為你平方！如果有";
	mes "有關PVP的任何問題";
	mes "模式或規則，請詢問";
	mes "敘述者...";
	next;
	switch(select("^FF5533'PvP噩夢模式'^000000:^3355FF'PvP溜溜球模式'^000000:^3355FF'PvP事件模式'^000000:退出")) {
		case 1:
			mes "[守門員]";
			mes "很抱歉，目前噩夢模式服務不可用。";
			mes "請改用yoyo模式。不便之處，我們深表歉意。";
			break;
		case 2:
			mes "[守門員]";
			mes "入場費為500 Zeny。";
			mes "你想搬家嗎";
			mes "到Yoyo模式";
			mes "戰鬥廣場？";
			next;
			switch(select("移動:取消")) {
			case 1:
				if ((Zeny > 499) && (BaseLevel > 30)) {
					set Zeny, Zeny - 500;
					callsub L_Warp,"pvp_y_room";
				}
				else {
					mes "[守門員]";
					mes "對不起，但是";
					mes "你沒有做好準備嗎？";
					mes "仔細檢查您是否有";
					mes "500 Zeny入口費，";
					mes "您至少是31級！";
				}
				close;
			case 2:
				mes "[守門員]";
				mes "怪物之間的戰爭肆虐";
				mes "和人類，這場比賽";
				mes "在人們中-PVP-";
				mes "鼓勵我們所有人";
				mes "更強。再來，";
				mes "我們歡迎您的挑戰！";
				close;
			}
		case 3:
			mes "[守門員]";
			mes "歡迎！";
			mes "請仔細檢查";
			mes "您有門票或觀看票。";
			next;
			if ((countitem(7028) == 0) && (countitem(7029) == 0)) {
				mes "[守門員]";
				mes "嗯？你沒有嗎？然後我";
				mes "對不起，這個戰鬥廣場是";
				mes "僅適用於";
				mes "入場或查看門票。";
				mes "沒有它，您就無法進來。";
				close;
			}
			mes "[守門員]";
			mes "是的，謝謝您的參與。玩得開心！";
			if (Zeny >= 500) set Zeny, Zeny - 500;
			callsub L_Warp,"pvp_room";
			break;
		case 4:
			mes "[守門員]";
			mes "怪物之間的戰爭肆虐";
			mes "和人類，這場比賽";
			mes "在人們中-PVP-";
			mes "鼓勵我們所有人";
			mes "更強。再來，";
			mes "我們歡迎您的挑戰！";
			break;
	}
	close;

L_Warp:
	set .@map$,getarg(0);
	if (checkre(0)) {
		switch(rand(1,10)) {
			case 1: warp .@map$,34,38; break;
			case 2: warp .@map$,46,38; break;
			case 3: warp .@map$,58,38; break;
			case 4: warp .@map$,70,38; break;
			case 5: warp .@map$,82,38; break;
			case 6: warp .@map$,82,58; break;
			case 7: warp .@map$,65,58; break;
			case 8: warp .@map$,49,58; break;
			case 9: warp .@map$,33,58; break;
			case 10: warp .@map$,52,24; break;
		}
	} else
		warp .@map$,51,23;
	return;
}
morocc_in,144,142,4	duplicate(gkut)	Gate Keeper#gke1	83
alberta_in,26,146,4	duplicate(gkut)	Gate Keeper#gke2	83
prt_in,52,140,4	duplicate(gkut)	Gate Keeper#gke3	83
geffen_in,63,63,4	duplicate(gkut)	Gate Keeper#gke4	83
payon_in01,140,53,4	duplicate(gkut)	Gate Keeper#gke5	83

// PVP Warper Function
//============================================================ 
// Note: The warper NPCs are in the pre-re/re paths.

//callfunc "F_PVP_FSRS"{,<min_lvl>,<max_lvl>};
function	script	F_PVP_FSRS	{
	if (getargcount()) {
		setarray .@lvl[0], getarg(0), getarg(1);
		if (BaseLevel < .@lvl[0] || BaseLevel > .@lvl[1]) {
			mes "[PVP打架廣場接待人員]";
			mes "抱歉，但是您的基礎水平必須在LV之間"+ .@lvl[0] +" and LV "+ .@lvl[1] +".";
			close;
		}
	}
	if (strnpcinfo(4) == "pvp_y_room") {
		set .@base$, "pvp_y_"+strnpcinfo(2);
		setarray .@Maps$[0],	.@base$+"-1",	.@base$+"-2",	.@base$+"-3",	.@base$+"-4",	.@base$+"-5";
		setarray .@Name$[0],	"Prontera",		"Izlude",		"Payon",		"Alberta",		"Morocc";
		setarray .@Limit[0],	128,			128,			128,			128,			128;
	} else {
		setarray .@Maps$[0],	"pvp_n_8-1",	"pvp_n_8-2",	"pvp_n_8-3",	"pvp_n_8-4",	"pvp_n_8-5";
		setarray .@Name$[0],	"Sandwich",		"Lock on",		"Four Room",	"Under cross",	"Compass Room";
		setarray .@Limit[0],	64,				32,				32,				32,				32;
	}
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		set .@menu$, .@menu$+.@Name$[.@i]+" ["+getmapusers(.@Maps$[.@i])+" / "+.@Limit[.@i]+"]:";
	set .@menu$, .@menu$+"Cancel.";
	set .@i, select(.@menu$)-1;
	if (.@i == 5) close;
	if (getmapusers(.@Maps$[.@i]) >= .@Limit[.@i]) {
		mes "[PVP打架廣場接待人員]";
		mes "該地圖當前已滿。";
		close;
	}
	warp .@Maps$[.@i],0,0;
	end;
}



// Event Contestant Entrance
//============================================================ 
pvp_room,54,85,4	script	Registration Staff#1	105,{
	if (countitem(7028) == 0) {
		mes "[PVP戰鬥平方註冊工作人員]";
		mes "嗯？你是怎麼來這裡的？";
		mes "這僅是球員的入口。";
		mes "有關觀眾的詳細信息，請在您的右邊向登記官提供。";
		close;
	}
	set .@size, getarraysize(.warp_x);
	if (.@size == 0) {
		setarray .warp_x,40,59,20,40;
		setarray .warp_y,59,40,40,20;
	}
	if (select("戰斗方一:取消") == 1) {
		mes "[PVP戰鬥平方註冊工作人員]";
		mes "'"+ strcharinfo(0) +"'";
		mes "你準備好了嗎？";
		next;
		if (select("是！ :不是！") == 1) {
			mes "[PVP戰鬥平方註冊工作人員]";
			mes "好的！我會把你送入裡面";
			mes "祝你好運！";
			close2;
			delitem 7028,1; //Invite_For_Duel
			set .@rand, rand(.@size);
			warp "pvp_2vs2", .warp_x[.@rand], .warp_y[.@rand];
			deletearray .warp_x[.@rand],1;
			deletearray .warp_y[.@rand],1;
			end;
		}
		mes "[PVP戰鬥平方註冊工作人員]";
		mes "隨時準備回來。";
		close;
	}
	close;

OnInit:
	waitingroom "Combat Square players entrance only",0;
	end;
}

// Spectator Entrance
//============================================================ 
-	script	Spectator's Entrance#dum::PVPSpectator	-1,{
	if (countitem(7029) == 0) {
		mes "[PVP競爭廣場登記員]";
		mes "這是觀眾的入口。";
		mes "有關球員入口的詳細信息，請繼續向左側的註冊人員提供。";
		close;
	}
	if (select("競爭方一:取消") == 1) {
		mes "[PVP戰鬥平方註冊工作人員]";
		mes "您明白了，感謝您的參與。玩得開心！";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
			case 1: warp "pvp_2vs2",39,7; end;
			case 2: warp "pvp_2vs2",39,73; end;
			case 3: warp "pvp_2vs2",7,39; end;
			case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "Compete Square viewer's entrance",0;
	end;
}
pvp_room,62,85,4	duplicate(PVPSpectator)	Spectator's Entrance#1	105
pvp_room,70,85,4	duplicate(PVPSpectator)	Spectator's Entrance#2	105
pvp_room,78,85,4	duplicate(PVPSpectator)	Spectator's Entrance#3	105
pvp_room,86,85,4	duplicate(PVPSpectator)	Spectator's Entrance#4	105

// Spectator Warps
//============================================================ 
-	script	Combat Square Staff#dum::PVPSpecWarp	-1,{
OnTouch_:
	mes "[戰鬥廣場員工]";
	mes "我可以幫你嗎？";
	if (select("前往中央觀眾席:離開戰鬥廣場。") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}
pvp_2vs2,5,4,0	duplicate(PVPSpecWarp)	Combat Square Staff#1	45,1,1
pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#2	45,1,1
pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#3	45,1,1
pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	Combat Square Staff#4	45,1,1

pvp_2vs2,40,40,0	script	Combat Square Staff#5	45,1,1,{
	mes "[戰鬥廣場員工]";
	mes "我可以幫你嗎？";
	switch(select("到旁邊的觀眾席:離開戰鬥廣場。")) {
	case 1:
		switch(rand(1,4)) {
			case 1: warp "pvp_2vs2",39,7; end;
			case 2: warp "pvp_2vs2",39,73; end;
			case 3: warp "pvp_2vs2",7,39; end;
			case 4: warp "pvp_2vs2",73,39; end;
		}
	case 2:
		warp "pvp_c_room",84,39;
		end;
	}
}

// PVP Area Exit warp
//============================================================ 
pvp_room,51,19,0	script	out#eventpvp	45,4,2,{
OnTouch_:
	mes "[戰鬥廣場員工]";
	mes "你在戰斗方面玩得開心嗎？";
	mes "請問你想去哪裡？";
	next;
	switch(select("弗龍特拉.:摩洛哥.:格芬.:佩永.:艾伯塔省.:取消。")) {
		case 1: warp "prontera",107,60; end;
		case 2: warp "morocc",157,96; end;
		case 3: warp "geffen",120,36; end;
		case 4: warp "payon",96,100; end;
		case 5: warp "alberta",41,243; end;
		case 6: close;
	}
}
