//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Luoyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Luoyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome) {
	default:// Default message (obsolete)
		mes "[KAFRA員工]";
		mes "歡迎來到";
		mes "Kafra Corporation。";
		mes "Kafra服務";
		mes "總是站在你身邊。";
		mes "我該如何幫助您？";
		break;
	case 1:// Niflheim specific message
		mes "[KAFRA員工]";
		mes "^666666W-WEEELC-COME";
		mes "到Th-t-k-kaaafrrra";
		mes "C-COORPOOR-R-RATIOOOONN ...^000000";
		break;
	case 2:// Guild Castle Kafra message
		mes "[KAFRA員工]";
		mes "歡迎。 ^ff0000" + getguildname( getcharid(2) ) + "^000000 Member.";
		mes "無論您走到哪裡，Kafra的互補都將與您同在。";
		break;
	case 3:// Amatsu specific message (obsolete)
		mes "[KAFRA員工]";
		mes "那麼，您是來自遙遠的土地來研究我們的文化，還是只是在觀光？";
		mes "無論哪種情況，為什麼不待一會兒呢？";
		mes "空氣永遠很重";
		mes "宜人的野花的氣味。";
		break;
	case 4:// Luoyang and Ayothaya specific message (obsolete)
		mes "[KAFRA員工]";
		mes "與我們的許多Kafra";
		mes "服務地點，您從來沒有";
		mes "離家很遠。";
		break;
	case 5:// NPC has it's own welcome message. (Display nothing)
		break;
	}
	next;
	if (.@welcome == 2)// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
		setarray .@K_Menu0$[0], "使用倉庫","使用公會倉庫","租用手推車","使用傳送服務","取消";
	else {
		switch(.@menu_num) {
			// Save and Storage only
			case 1:	setarray .@K_Menu0$[0],"Save","使用倉庫","取消"; break;
			// Storage only
			case 2:	setarray .@K_Menu0$[0],"使用倉庫","取消"; break;
			// No Teleport (Common)
			case 3: setarray .@K_Menu0$[0],"Save","使用倉庫","租用手推車","查看其他資訊","取消"; break;
			// Case 4 is Einbroch no tele message.
			// No save, or teleport. (Common)
			case 5:	setarray .@K_Menu0$[0],"使用倉庫","租用手推車","查看其他資訊","取消"; break;
			// Storage and Check Other Information only.
			case 6:	setarray .@K_Menu0$[0],"使用倉庫","查看其他資訊","取消"; break;
			// Save, Storage, and Pushcart only (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Save","使用倉庫","租用手推車","取消"; break;
			// Save, Storage, Other Check information. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Save","使用倉庫","查看其他資訊","取消"; break;
			// No Save (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"使用倉庫","租用手推車","使用傳送服務","查看其他資訊","取消"; break;
			// Storage, Save, and Pushcart (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"使用倉庫","Save","租用手推車","取消"; break;
			// Default message (obsolete)
			default: setarray .@K_Menu0$[0],"Save","使用倉庫","使用傳送服務","租用手推車","查看其他資訊","取消"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Save")
			return;
		else if (.@K_Menu0$[.@j] == "使用倉庫") {
			if (.@welcome == 2)// Do not charge for Guild Storage
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		}
		else if (.@K_Menu0$[.@j] == "使用傳送服務") {
			if (.@menu_num != 4)// Display Einbroch "No Teleport Service" notice.
				callfunc "F_KafTele",.@welcome;
			else {
				mes "[KAFRA員工]";
				mes "由於 ^ff0000限制";
				mes "運輸協議^000000，";
				mes "Kafra Corporation不能";
				mes "提供傳送服務";
				mes "在施瓦茨瓦爾德共和國。";
				next;
				mes "[KAFRA員工]";
				mes "我們問你";
				mes "使用飛艇服務";
				mes "反而。謝謝你的";
				mes "理解與合作。";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "租用手推車") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "查看其他資訊") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "取消") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "使用公會倉庫") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes "[KAFRA員工]";
			mes "對不起，但另一個公會成員正在使用公會存儲";
			mes "現在。  請等到那個人完成。";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (!callfunc("F_CanOpenStorage")) {
		mes "[KAFRA員工]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-S-SSOOOOORRY，";
			mes "是的，你是a-a-aaaa";
			mes "Nooviiice ... N-Neeeds";
			mes "b-basic SSSSKILL L級別6 ...^000000";
			return;
		}
		mes "對不起，但是你";
		mes "需要新手";
		mes "基本技能6至";
		mes "使用存儲服務。";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (.@type != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes "[KAFRA員工]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^6666666zeeeeeny ...";
					mes "M-MORE Z-ZENY ...！";
					mes "N-Neeed 150 ... Zeny ...";
					mes "恩！ t taking bl-bllood~！ ^000000";
					return;
				}
				// Standard Message
				mes "對不起，但是你沒有";
				mes "有足夠的Zeny使用";
				mes "存儲服務。我們的";
				mes "存儲訪問費是" + .@fee + " zeny.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[KAFRA員工]";
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^6666666thank您..用於...使用...^000000";
		}
		// Normal message
		else {
			mes "[KAFRA員工]";
			mes "在這裡，讓我打開";
			mes "您的存儲空間。";
			mes "謝謝您的使用";
			mes "Kafra服務。";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[KAFRA員工]";
	mes "請選擇";
	mes "您的目的地。";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancel")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[KAFRA員工]";
			mes "對不起，但是你沒有";
			mes "足夠的zeny來傳送";
			mes "服務。傳送費";
			mes "到" + @wrpD$[.@j] + " is " + @wrpP[.@j] + " zeny.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "艾爾帕蘭") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "艾爾貝塔") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "克魔島") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "發樂斯燈塔") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "吉芬") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "伊魯德") {
		if (checkre(0) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "茱諾") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "妙勒尼山脈北區") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "夢羅克") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "獸人村") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "裴揚") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "普隆德拉") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "汶巴拉") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes "[KAFRA員工]";
		mes "對不起，但是";
		mes "手推車租賃服務";
		if (checkre(0) == true)
			mes "僅適用於商人課程。";
		else {
			mes "僅適用於商人";
			mes "鐵匠，史密斯大師，";
			mes "煉金術士和生物化學家。";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if (checkcart() == true) {
		mes "[KAFRA員工]";
		mes "你已經有";
		mes "手推車配備了。";
		mes "不幸的是，我們不能";
		mes "租用多個";
		mes "每個客戶一次。";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes "[KAFRA員工]";
		mes "只有學會\"推車\"技能後才能租車。";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes "[KAFRA員工]";
		mes "手推車出租";
		mes "費用是" + .@rental_fee + " zeny. Would";
		mes "您喜歡租手推車嗎？";
		next;
		if (select("租用手推車:取消") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes "[KAFRA員工]";
			mes "對不起，但是你";
			mes "沒有足夠的東西";
			mes "Zeny付款手推車";
			mes "租金的費用" + .@rental_fee + " zeny.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
// Uncomment next line to block Kafra Storage Protection
//	.@block = 1;
	if (getarg(0) == 2)// Port Malaya, Rune Knight
		setarray .@menu$[0], "Check Special Reserve Points.", "", "", "Cancel";
	else if (.@block)
		setarray .@menu$[0], "Check Special Reserve Points.", "", "Kafra Employee Locations", "Cancel";
	else
		setarray .@menu$[0], "Check Special Reserve Points.", "Storage Password Service", "Kafra Employee Locations", "Cancel";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Check Special Reserve Points.
			mes "[KAFRA員工]";
			mes "讓我們來看看...";
			mes "" + strcharinfo(0) + "...";
			mes "啊，你總共有";
			mes "" + RESRVPTS + " Special Reserve Points.";
			next;
			mes "[KAFRA員工]";
			mes "您可以交換您的";
			mes "特殊儲備點";
			mes "在Al de Baran的Kafra主辦公室獎勵。請使用我們的";
			mes "便捷的服務，以查看我們的獎勵計劃的好處。";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Storage Password Service
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		case 2:// Kafra Employee Locations
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[KAFRA員工]";
	if (getarg(1) == true) {// Save
		mes "您的重生點";
		mes "在這裡保存了";
		mes "" + getarg(2) + ".";
		mes "謝謝您的使用";
		mes "Kafra服務。";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "我們在Kafra Corporation，";
		mes "總是努力為您提供最好的服務。我們希望我們滿足您的冒險需求和卓越標準。";
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^6666664Kafffra n-never";
		mes "Diiiiiiiiiiiiies。在...";
		mes "在y-yooour siiiiide上~^000000";
	} else if (.@type == 2) {// RockRidge Save
		mes "保存。";
		mes "謝謝您的讚助。";
	}
	close3;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "輸入您的存儲密碼：";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Wrong storage password.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + " proudly presents you a new service:";
		mes "使用密碼的其他存儲保護。";
		next;
		if (select( "Set new password -> 5000z", "Cancel" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "您的存儲空間受到密碼的保護。你現在會做什麼？";
	next;
	switch( select( "Change old password -> 5000z", "Remove storage password -> 1000z", "Cancel" ) ) {
	case 1:
		mes .@npc_name$;
		mes "首先，請輸入您的 ^0000flold密碼 ^000000。";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "錯誤的密碼。您無法設置新密碼。";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "請在刪除密碼之前輸入密碼。";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "密碼尚未刪除。";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "您沒有足夠的Zeny。";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "您已經成功清除了存儲密碼。";
			mes "謝謝您的使用" + .@comp_name$ + ".";
			emotion ET_THANKS;
		} else {
			mes "錯誤的密碼。我們不會返回您的1000Z。";
			mes "請下次輸入正確的密碼。";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "現在，輸入您的 ^ff0000new密碼 ^000000，以保護您的存儲器免受小偷的侵害。";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "密碼尚未更改。";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "您沒有足夠的Zeny。";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "您已經通過秘密密碼保護了存儲空間。";
	mes "謝謝您的使用" + .@comp_name$ + ".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "輸入數字1000~10000000：";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Tried to fit storage password.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "您不能使用這樣的大密碼。";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "您不應該使用這樣的簡短密碼。";
		return 0;
	}
	return .@code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
    if (.@map$ == "prontera") {
        setarray @wrpD$[0], "伊魯德", "吉芬", "裴揚", "夢羅克", "獸人村", "艾爾貝塔";
        setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
        setarray @viewpX[0], 151, 29, 282, 152;
        setarray @viewpY[0], 29, 207, 200, 326;
    } else if (.@map$ == "alberta") {
        setarray @wrpP[0], 1200, 1800, 1800;
        setarray @wrpD$[0], "裴揚", "夢羅克", "普隆德拉";
        setarray @viewpX[0], 28, 113, 0, 0;
        setarray @viewpY[0], 229, 60, 0, 0;
    } else if (.@map$ == "aldebaran") {
        setarray @wrpP[0], 1200, 1200, 1800, 1700;
        setarray @wrpD$[0], "吉芬", "茱諾" , "伊魯德", "妙勒尼山脈北區";
    } else if (.@map$ == "comodo" ) {
        setarray @wrpD$[0], "夢羅克", "發樂斯燈塔", "汶巴拉";
        setarray @wrpP[0], 1800, 1200, 1800;
    } else if (.@map$ == "cmd_fild07") {
        setarray @wrpD$[0], "克魔島", "夢羅克";
        setarray @wrpP[0], 1200, 1200;
    } else if (.@map$ == "geffen") {
        setarray @wrpD$[0], "普隆德拉", "艾爾帕蘭", "獸人村", "妙勒尼山脈北區";
        setarray @wrpP[0], 1200, 1200, 1700, 1700;
        setarray @viewpX[0], 120, 203;
        setarray @viewpY[0], 62, 123;
    } else if (.@map$ == "izlude") {
        setarray @wrpD$[0], "吉芬", "裴揚", "夢羅克", "艾爾帕蘭";
        setarray @wrpP[0], 1200, 1200, 1200, 1800;
    } else if (.@map$ == "morocc") {
        setarray @wrpD$[0], "普隆德拉", "裴揚", "艾爾貝塔", "克魔島", "發樂斯燈塔";
        setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
        setarray @viewpX[0], 156, 163, 28, 292;
        setarray @viewpY[0], 97, 260, 167, 211;
    } else if (.@map$ == "umbala") {
        setarray @wrpD$[0], "克魔島";
        setarray @wrpP[0], 1800;
    } else if (.@map$ == "payon") {
        setarray @wrpD$[0], "普隆德拉", "艾爾貝塔", "夢羅克";
        setarray @wrpP[0], 1200, 1200, 1200;
    } else if (.@map$ == "yuno") {
        setarray @wrpD$[0], "艾爾帕蘭";
        setarray @wrpP[0], 1200;
        setarray @viewpX[0], 328, 278, 153, 0;
        setarray @viewpY[0], 108, 221, 187, 0;
    } else if (.@map$ == "job3_rune01") {
        setarray @wrpD$[0], "伊魯德", "吉芬", "裴揚", "夢羅克", "艾爾貝塔";
        setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
    }

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancel";
	return;
}
