//===== rAthena Script ======================================= 
//= RWC 2012 Enchants
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots and enchantments to 2012 RWC Memory accessories.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//============================================================ 

prontera,147,61,3	script	Driller#pron	87,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "庫存中的物品太多。訪問Kafra存儲並重試。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您無法繼續進行，因為您超重。";
		close;
	}
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[鑽機]";
		mes "我的工作是將卡插槽鑽入RWC紀念配件。";
		next;
		mes "[鑽機]";
		mes "對不起，但是您沒有任何配備的物品。";
		close;
	}
	mes "[鑽機]";
	mes "我的工作是將卡插槽鑽入RWC紀念配件。此外，我僅處理 ^ff0000pure項目 ^000000，或那些尚未附魔的項目。";
	next;
	set .@equip_id, getequipid(.@part);
	if (.@equip_id != 2966 && .@equip_id != 2968) {
		mes "[鑽機]";
		mes "但是，我可以看到您在右側穿著的配件無法得到處理。請配備RWC紀念配件。";
		close;
	}
	mes "[鑽機]";
	mes "您還應該知道，將卡插槽製作起來非常危險。 ^FF0000 CHANCES的成功約為50％。 ^000000您想繼續嗎？";
	next;
	if(select("取消:我們走吧！") == 1) {
		mes "[鑽機]";
		mes "然後見。";
		close;
	}
	if (.@equip_id == 2966) {
		set .@slotted, 2967; //RWC_2012_Ring_
		set .@name$,"RWC 2012 Memorial Ring";
		set .@str$,"ring";
	} else if (.@equip_id == 2968) {
		set .@slotted, 2969; //RWC_2012_Pendant_
		set .@name$,"RWC 2012 Memorial Pendant";
		set .@str$,"pendant";
	} else {
		mes "[鑽機]";
		mes "我無法識別您右手穿的配件。我不能努力。";
		close;
	}
	if (getequipcardid(.@part,3) > 0) {
		mes "[鑽機]";
		mes "該項目已經迷住了。我不能違反規則，因此無法做到這一點。";
		close;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id))
		close;

	delequip .@part;
	if (rand(1,10) > 5) {
		getitem .@slotted,1;
		specialeffect2 EF_REPAIRWEAPON;
		mes "[鑽機]";
		mes "耶！成功！你的"+.@name$+" now has a card slot. Check it out!";
		close;
	} else {
		specialeffect2 EF_LORD;
		mes "[鑽機]";
		mes "噢，該死的虛弱"+.@str$+"... It broke during the procedure. I'm sorry.";
		close;
	}
}

prontera,147,59,3	script	Goldberg#pron	878,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶太多物品。組織庫存後，回來。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您無法繼續進行，因為您超重。";
		close;
	}
	mes "[戈德堡]";
	mes "你好！我負責具有一些神秘力量的迷人RWC紀念配件。";
	next;
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[戈德堡]";
		mes "對不起，但是您沒有任何配備的物品。";
		close;
	}
	set .@equip_id, getequipid(.@part);
	if (.@equip_id < 2966 || .@equip_id > 2969) {
		mes "[戈德堡]";
		mes "但是，我可以看到您穿的配件不是我可以使用的東西。請配備RWC紀念配件。";
		close;
	}
	set .@select, select("抱歉，不感興趣。 :請授權我的配件。 :移除附魔。")-1;
	if (.@select == 0) {
		mes "[戈德堡]";
		mes "好吧，然後，下次見...";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	if (.@select == 1) {
		switch(getequipid(.@part)) {
		case 2966:
			setarray .@option[0],2,2,4,4;
			break;
		case 2967:
			setarray .@option[0],0,2,4,4;
			break;
		case 2968:
			setarray .@option[0],1,1,3,3;
			break;
		case 2969:
			setarray .@option[0],0,1,3,3;
			break;
		default:
			mes "[戈德堡]";
			mes "對不起，但是我無法處理您當前穿著的配件。";
			mes "如果您在左側配備了RWC紀念配件，請嘗試將其交換到右側。";
			close;
		}
		for(set .@i,3; .@i>=0; set .@i,.@i-1) {
			if (.@equip_card[.@i] == 0) {
				set .@slot, .@i;
				set .@op_type, .@option[.@i];
				break;
			}
		}
		switch(.@op_type) {
		case 4:
			mes "[戈德堡]";
			mes "您想注入哪種結界？";
			next;
			setarray .@enchant_select[0],1,2,3,4;
			set .@i, select("取消:鬥志:攻擊力(%):最大生命值:生命值")-2;
			break;
		case 3:
			setarray .@enchant_select[0],5,6,7;
			set .@i, select("取消:法術:MATK (%):SP")-2;
			break;
		case 2:
			mes "[戈德堡]";
			mes "^ff0000be小心！魔法大約有25％的機會失敗。如果發生這種情況，該項目將被銷毀。 ^000000您想注入哪個附魔？";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,14;
			set .@i, select("取消:STR:AGI:VIT:INT:DEX:LUK:SP")-2;
			break;
		case 1:
			mes "[戈德堡]";
			mes "^FF0000大約有25％的機會失敗。如果發生這種情況，該項目將被銷毀。 ^000000您想注入哪個附魔？";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,15,16;
			set .@i, select("取消:STR:AGI:VIT:INT:DEX:LUK:MHP:HP")-2;
			break;
		case 0:
			mes "[戈德堡]";
			mes "您的配件已經收到了很多附魔，我幾乎無法處理它。";
			close;
		}
		if (.@i == -1) {
			mes "[戈德堡]";
			mes "好吧，然後，下次見。";
			close;
		}
		mes "[戈德堡]";
		mes "結界的力量將被隨機選擇。 ^ff0000once注入，無法刪除附魔。 ^000000我們可以繼續嗎？";
		next;
		if(select("不，請停止。 :是的，請繼續。") == 1) {
			mes "[戈德堡]";
			mes "好吧，然後，下次見...";
			close;
		}
		set .@enchant_type, .@enchant_select[.@i];
		if (!getequipisequiped(.@part)) {
			mes "[戈德堡]";
			mes "我工作時不要脫下設備，好嗎？";
			close;
		}
		switch(.@enchant_type) {
		case 1:
			setarray .@enc[0],4811,4810,4809; //Fighting_Spirit1,Fighting_Spirit2,Fighting_Spirit3
			break;
		case 2:
			setarray .@enc[0],4819,4766,4767; //Atk1,Atk2,Atk3
			break;
		case 3:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 4:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		case 5:
			setarray .@enc[0],4760,4761,4806; //Matk1,Matk2,Matk3
			break;
		case 6:
			setarray .@enc[0],4815,4814,4813; //Spell1,Spell2,Spell3
			break;
		case 7:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 8:
			setarray .@enc[0],4700,4701,4702; //Strength1,Strength2,Strength3
			break;
		case 9:
			setarray .@enc[0],4730,4731,4732; //Agility1,Agility2,Agility3
			break;
		case 10:
			setarray .@enc[0],4740,4741,4742; //Vitality1,Vitality2,Vitality3
			break;
		case 11:
			setarray .@enc[0],4710,4711,4712; //Inteligence1,Inteligence2,Inteligence3
			break;
		case 12:
			setarray .@enc[0],4720,4721,4722; //Dexterity1,Dexterity2,Dexterity3
			break;
		case 13:
			setarray .@enc[0],4750,4751,4752; //Luck1,Luck2,Luck3
			break;
		case 14:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 15:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 16:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		default:
			mes "[戈德堡]";
			mes "我們有問題，讓我檢查一下。";
			close;
		}

		if (.@enchant_type < 8)
			set .@i, rand(1,300); // 0% break chance.
		else
			set .@i, rand(1,400); // 25% break chance.

		     if (.@i < 151) set .@enchant, .@enc[0];
		else if (.@i < 251) set .@enchant, .@enc[1];
		else if (.@i < 301) set .@enchant, .@enc[2];
		else set .@enchant,9;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		set .@equip_card[.@slot], .@enchant;
		if (.@slot == 2 && .@enchant == 0) {
			set .@equip_card[3],0;
		} else if (.@slot == 1 && .@enchant == 0) {
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		} else if (.@slot == 0 && .@enchant == 0) {
			set .@equip_card[1],0;
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		}

		set .@equip_refine, getequiprefinerycnt(.@part);
		delequip .@part;
		if (.@enchant == 9) {
			mes "[戈德堡]";
			mes "天哪！";
 			mes "該物品不足以承受著附魔，因此被摧毀了。對不起。";
			specialeffect2 EF_LORD;
			close;
		}
		if (.@enchant == 0) { // Should never happen.
			mes "[戈德堡]";
			mes "哦...似乎在所有註入的力量之間存在某種不穩定的能力。這導致所有的魅力消失了。很可惜，但是請重試！";
		} else {
			mes "[戈德堡]";
			mes "偉大的！";
			mes "結界是成功的！它將應用於插座號^990000"+(.@slot+1)+"^000000.";
			specialeffect2 EF_REPAIRWEAPON;
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[戈德堡]";
		mes "我只會初始化Enchant選項，而無需對開槽卡做任何事情。你想繼續嗎？";
		next;
		if(select("我會停下來:是的，當然，繼續。") == 1) {
			mes "[戈德堡]";
			mes "如果您改變主意，請回來。";
			close;
		}
		if (countitem(6665) == 0) {
			mes "[戈德堡]";
			mes "對不起。但是您沒有RWC初始化優惠券。您可以檢查庫存嗎？";
			close;
		}
		if (getiteminfo(.@equip_card[3], ITEMINFO_SUBTYPE) != CARD_ENCHANT) {
			mes "[戈德堡]";
			mes "嗯...此設備很乾淨。如果什麼都沒有，我無法初始化它！再次檢查。";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[戈德堡]";
		mes "您項目中的附魔選項將初始化。";
		delitem 6665,1; //RWC_Inicializer
		delequip .@part;
	
//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for ( .@i = getiteminfo(.@equip_id, ITEMINFO_SLOT); .@i < MAX_SLOTS; .@i++ ) {
			if (getiteminfo(.@equip_card[.@i], ITEMINFO_SUBTYPE) == CARD_ENCHANT)
				.@equip_card[.@i] = 0;// Armor Enchant System
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		
		close;
	}
}
