//===== rAthena Script =======================================
//= Children Week Event
//===== By: ==================================================
//= Samuray22
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Indian Ro: Children Week Event
//===== Additional Comments: =================================
//= 1.1 First Beta. [Samuray22]
//============================================================

prontera,146,91,5	script	Pandit chacha#child07	61,{
	if (BaseLevel > 50) {
		mes "[潘迪特查查]";
		mes "哈哈哈~";
		mes "這是特殊的一天，一年只有一次。";
		mes "如果您看到周圍的小冒險家寄給我~";
		mes "我會給特殊的禮物。";
		close;
	} else if (BaseLevel < 20) {
		mes "[潘迪特查查]";
		mes "哈哈哈~";
		mes "您是一名嬰兒冒險家，不是很小的冒險家。";
		mes "當您更加長大時，再次回來。哈哈哈。";
		close;
	} else if (oversea_event9 < 1) {
		mes "[潘迪特查查]";
		mes "哈哈哈~";
		mes "歡迎，小冒險家！";
		mes "今天是特殊的一天，不是嗎？";
		mes "我想把小禮物送給小冒險家...";
		mes "你呢？你想接受嗎？";
		next;
		if(select("不，我稍後再拿。 :當然，我想要。") == 1) {
			mes "[潘迪特查查]";
			mes "太糟糕了....哼...";
			mes "我收集了一些遠處的世界來實現它。";
			mes "如果您想要的話，每當再次回來...";
			close;
		}
		set oversea_event9,1;
		getitem 11705,10; //Special_White_Potion
		mes "[潘迪特查查]";
		mes "看。這是兒童藥水。";
		mes "重量僅為1，但恢復了很多HP。";
		mes "如果您想獲得更多，請帶上1個婚禮花束和1條無玫瑰玫瑰。";
		close;
	} else if (oversea_event9 == 1) {
		if (countitem(745) > 0 && countitem(748) > 0) {
			mes "[潘迪特查查]";
			mes "啊！";
			mes "你記得我美麗的作品。";
			mes "你做得很好。";
			mes "你能給我1件婚禮花束和1朵雜草玫瑰嗎？";
			next;
			if(select("還沒有:當然，拿著吧。") == 1) {
				mes "[潘迪特查查]";
				mes "如果您還沒有準備好，請在準備好時給我打電話。";
				close;
			}
			mes "[潘迪特查查]";
			mes "哈哈哈~";
			mes "哦~你明白了。";
			mes "在這裡，我將交換50個兒童藥水。";
			mes "一旦您得到了50個兒童藥水，我將不再付出。";
			close2;
			delitem 745,1; //Wedding_Bouquet
			delitem 748,1; //Witherless_Rose
			set oversea_event9,2;
			getitem 11705,50; //Special_White_Potion
			end;
		}
		mes "[潘迪特查查]";
		mes "小冒險家，您應該帶上1個婚禮花束和1條無玫瑰玫瑰。";
		mes "如果您帶上這些東西，我將它們換成兒童藥水。";
		close;
	}
	mes "[潘迪特查查]";
	mes "哈哈哈~";
	mes "您週週享受兒童~嗎？";
	close;
}
