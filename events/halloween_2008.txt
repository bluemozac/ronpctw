//===== rAthena Script =======================================
//= iRO Halloween (2008)
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= iRO Halloween (2008)
//= Run away from the zombies for tickets.
//= Redeem tickets for prizes.
//===== Notes: ===============================================
//= You must enable the event monsters in mob_db2.
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Some small changes. [Kisuka]
//============================================================

payon,162,176,4	script	Halloween Magician#iRO08	704,{
	if (Hallow08 < 1) {
		mes "[萬聖節魔術師]";
		mes "咯咯咯咯！";
		mes "在這個萬聖節，我有一個特殊的活動，可以測試您的運氣和敏捷性。";
		mes "你有興趣嗎？";
		next;
		mes "[萬聖節魔術師]";
		mes "快點！不要成為一個糟糕的！";
		mes "如果您收取足夠的門票，您可以獲得良好的獎品！";
		mes "那你怎麼說？";
		next;
		goto MainMenu;
	}
	if (Hallow08Kill == 1) {
		mes "[萬聖節魔術師]";
		mes "你是'fraidy貓還是什麼？";
		mes "你知道你想再試一次...";
		mes "你知道規則嗎？";
		next;
		set Hallow08Kill,0;
		if(select("是的，我知道。 :不，我不知道。") == 2) {
			goto Rules;
		}
		goto Participate;
	}
	if (Hallow08Kill == 2) {
		mes "[萬聖節魔術師]";
		mes "哦，做得好！你沒事！";
		mes "殭屍不是很有趣嗎？";
		mes "你知道，殭屍也是人！";
		mes "哈!";
		mes "咯咯咯";
		next;
		getitem 7941,1;
		set Hallow08Kill,0;
		mes "[萬聖節魔術師]";
		mes "正如我所承諾的";
		mes "您可以獲得涼爽物品的萬聖節門票。";
		close;
	}
	if (Hallow08 > 0) {
		mes "[萬聖節魔術師]";
		mes "好吧，您想再次聽到規則嗎？";
		next;
		goto MainMenu2;
	}

		Rules:
			mes "[萬聖節魔術師]";
			mes "這個村莊就像一個虛擬的薪資。";
			mes "周圍有殭屍和食屍鬼，還有三個南部出口，但只有一件作品。";
			mes "這取決於您要找出答案。";
			next;
			mes "[萬聖節魔術師]";
			mes "您不能使用任何技能來殺死食屍鬼或殭屍。";
			mes "還有一件事...";
			mes "你不應該忘記...";
			next;
			mes "[萬聖節魔術師]";
			mes "所有參與者都不應該穿。";
			mes "將所有物品放在您的存儲空間中，然後在您的重量為“ 0”時回到這裡。";
			next;
			mes "[萬聖節魔術師]";
			mes "哦，還有一件事！";
			mes "您不能騎pecopeco或有車。";
			mes "如果是，那麼我將在您輸入之前將其刪除。";
			mes "知道了？";
			next;
			mes "[萬聖節魔術師]";
			mes "請記住，有三個出口，但只有一件隨機的作品，周圍漫遊的殭屍和食屍鬼不會被殺死，您不能穿任何東西。";
			next;
			if (Hallow08 > 0) {
				mes "[萬聖節魔術師]";
				mes "嘿...";
				mes "準備好後回來。";
				close;
			} else {
				goto MainMenu;
			}

		Participate:
			mes "[萬聖節魔術師]";
			mes "好的，你準備好了。";
			mes "讓我檢查你的體重。";
			next;
			if (Weight > 0) {
				mes "[萬聖節魔術師]";
				mes "天哪！";
				mes "任何地方總是有一隻黑羊。";
				next;
				mes "[萬聖節魔術師]";
				mes "您認為我不會注意到您的體重高於“ 0”？";
				mes "你超重...";
				close;
			} else {
				mes "[萬聖節魔術師]";
				mes "您看起來不錯，而且體重恰到好處。";
				next;
				mes "[萬聖節魔術師]";
				mes "我的工作很淹沒，所以我精疲力盡。";
				next;
				mes "[萬聖節魔術師]";
				mes "我有時會忘記把你送到那裡...";
				next;
				mes "[萬聖節魔術師]";
				mes "希望你回來好。";
				close2;
				if (Hallow08 < 1) {
					set Hallow08,1;
				}
				set Hallow08Kill,1;
				set @Hallow08Warp, rand(1,3);
				percentheal -98,0;
				setriding 0;
				setcart 0;
				warp "evt_zombie",155,246;
				end;
			}

		TicketExchange:
			mes "[萬聖節魔術師]";
			mes "您想交換獎品嗎？";
			mes "好工作！ kkkkkk！";
			next;
			mes "[萬聖節魔術師]";
			mes "Lemme告訴您可以交換的物品。";
			next;
			mes "[萬聖節魔術師]";
			mes "南瓜派的5張門票。";
			mes "20張南瓜頭的門票。";
			mes "舊藍色盒子的50張門票。";
			mes "70張舊紫色盒子的門票。";
			mes "舊卡專輯的200張門票。";
			next;
			mes "[萬聖節魔術師]";
			mes "您想交換什麼？";
			next;
			switch(select("南瓜派:南瓜頭:舊藍盒:舊紫盒:舊卡冊")) {
			case 1:
				if (countitem(7941) < 5) {
					goto NotEnough;
				} else {
					delitem 7941,5;
					getitem 12192,1;
					goto Enough;
				}
				break;
			case 2:
				if (countitem(7941) < 20) {
					goto NotEnough;
				} else {
					delitem 7941,20;
					getitem 5134,1;
					goto Enough;
				}
				break;
			case 3:
				if (countitem(7941) < 50) {
					goto NotEnough;
				} else {
					delitem 7941,50;
					getitem 603,1;
					goto Enough;
				}
				break;
			case 4:
				if (countitem(7941) < 70) {
					goto NotEnough;
				} else {
					delitem 7941,70;
					getitem 617,1;
					goto Enough;
				}
				break;
			case 5:
				if (countitem(7941) < 200) {
					goto NotEnough;
				} else {
					delitem 7941,200;
					getitem 616,1;
					goto Enough;
				}
			}

		NextTime:
			mes "[萬聖節魔術師]";
			mes "好，然後見。";
			mes "咯咯咯咯。";
			close;

	MainMenu:
		menu "Explain it to me.",Rules,"I want to participate.",Participate,"I want to exchange tickets for prizes.",TicketExchange,"I'll come back next time.",NextTime;

	MainMenu2:
		menu "Get me back there now!",Participate,"Please, tell me the rules",Rules,"I want to exchange tickets for prizes.",TicketExchange,"I'll come back next time.",NextTime;

	NotEnough:
		mes "[萬聖節魔術師]";
		mes "您沒有足夠的門票！";
		mes "你甚至不算嗎？";
		mes "請帶有正確數量的門票來到這裡。";
		close;

	Enough:
		mes "[萬聖節魔術師]";
		mes "這裡是。";
		mes "你需要...";
		mes "還要別的嗎？";
		next;
		if (Hallow08 == 1) {
			goto MainMenu2;
		} else {
			goto MainMenu;
		}
}

evt_zombie,16,142,1	script	zombiewarp001	45,2,2,{
OnTouch:
	if (@Hallow08Warp == 1) {
		set Hallow08Kill,2;
		specialeffect EF_BASH;
		warp "payon",28,142;
	}
	end;
}

evt_zombie,122,27,1	script	zombiewarp002	45,2,2,{
OnTouch:
	if (@Hallow08Warp == 2) {
		set Hallow08Kill,2;
		specialeffect EF_BASH;
		warp "payon",121,40;
	}
	end;
}

evt_zombie,267,89,1	script	zombiewarp003	45,2,2,{
OnTouch:
	if (@Hallow08Warp == 3) {
		set Hallow08Kill,2;
		specialeffect EF_BASH;
		warp "payon",253,95;
	}
	end;
}

evt_zombie,0,0	monster	Zombie	3000,150
evt_zombie,0,0	monster	Ghoul	3001,80
evt_zombie,0,0	monster	Zombie Master	3002,20
