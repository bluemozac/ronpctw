//===== rAthena Script ======================================= 
//= Official Skill Resetter Event in Juno (kRO & idRO)
//===== By: ================================================== 
//= $ephiroth
//===== Current Version: ===================================== 
//= 1.02
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= 2006 - Edition Skill Reset NPC
//= 2006/11/16: 1.0 Release and fully working. [$ephiroth]
//= 1.01 Checked, fixed typos, now uses MISC_QUEST var [Lupus]
//= 1.02 Added missing message,comment,etc. [$ephiroth]
//===== Additional Comments: =================================
//= Skill Reset with payment, minimum BaseLv.60 to reset skill.
//= Each 1 BaseLv need 20.000 Zeny, so be careful to use skill.
//============================================================
yuno,138,187,4	script	Hypnotist Teacher	124,{
	set @npcname$,"[^D5A500Hypnotist^000000]";
	mes @npcname$;

	if(MISC_QUEST & 1024){
		mes "我已經告訴過您，您可能只完成一次。";
		close;
	}
	if(BaseLevel < 60) {
		mes "當您到達60或更高的Baselv時，請返回。";
		close;
	}
	if(SkillPoint != 0) {
		mes "如果您希望我繼續下去，則需要用盡所有技能點。";
		mes "請盡快再來！";
		close;
	}
	if(Weight || checkfalcon() || checkcart() || checkriding()) {
		mes "您想重置技能嗎？";
		mes "對不起，但是...";
		next;
		mes @npcname$;
		if(Weight) {
			mes "你不能重置技能";
			mes "當你保留時";
			mes "任何項目。";
		} else if(checkcart())
			mes "請放下您的購物車，我們將繼續。";
		else if(checkfalcon())
			mes "請釋放您的獵鷹，我們將繼續。";
		else if(checkriding())
			mes "請釋放您的Pecopeco，我們將繼續。";
		next;
		mes @npcname$;
		mes "快來回來！";
		close;
	}
	mes "您好，我是技能重置者。";
	mes "您的名字為 ^0080ff"+strcharinfo(0)+"^000000.";
	mes "我怎麼幫你？";
	next;
	menu "^009500Information about Reset skills.^000000",L_Info,"^00B6FFReset skills.^000000",L_Reset,"^000088Nevermind^000000",-;

	mes @npcname$;
	mes "你知道在哪裡找到我，";
	mes "如果您想重置！ ！"; 
	close;

L_Info:
	mes @npcname$;
	mes "此技能重置不是免費的！ ！";
	mes "技能重置的費用為 ^D5A50020000 Zeny X Baselv ^000000。";
	mes "是的...每個Baselv花費了20000年Zeny重置技能。";
	next;
	mes @npcname$;
	mes "哦，是的，還有一件事！";
	mes "您裝備的任何手推車，獵鷹或山核桃";
	mes "如果您重置自己的技能，將被刪除。";
	next;
	mes @npcname$;
	mes "只有一次又一次地搖動技能點";
	mes "小心您的技能。";
	close;
	
L_Reset:
	mes @npcname$;
	mes "在開始之前重置技能之前。";
	mes "您必須首先告訴我您的基本水平。"; 
	next;
	mes "^D5A500 [" + strcharinfo(0) + "]^000000";
	mes "我的基本水平是 ^aa00aalevel" + BaseLevel + "BaseLv.^000000"; 
	next;
	set @zeny,BaseLevel*20000;
	mes @npcname$;
	mes "總Zeny重置技能金額 ^529DFF" + @zeny + "Zeny^000000  for the skill reset service.";
	next;
	if(Zeny < @zeny) {
		mes @npcname$;
		mes "看來您沒有足夠的錢。";
		mes "此外，我們等待機會。";
		emotion ET_SCRATCH;
		close;
	}
	set Zeny, Zeny-@zeny;
	set MISC_QUEST,MISC_QUEST | 1024;
	resetskill;
	mes @npcname$;
	mes "謝謝。";
	emotion ET_THANKS;
//	logmes "技能重置事件";
	close;
}
