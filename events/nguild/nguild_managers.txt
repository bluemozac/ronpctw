//===== rAthena Script =======================================
//= War of Emperium Managers for N Guild Castles
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project; RO Episode 4+
//===== Description: =========================================
//=
//==============================================
//= Break down of arguments used in the function:
//=   arg(0): name of Castle Manager
//=   arg(1): name of guild castle.
//=   arg(2): x1 coordinate for warp to treasure room
//=   arg(3): y1 coordinate for warp to treasure room
//=   arg(4): guild script suffix for kafra, etc.
//===== Additional Comments: =================================
//= 1.0 Based off existing guild scripts.  Do not know if it is accurate.[kobra_k88]
//= 1.1 Added modified guild manager function. [L0ne_W0lf]
//= 	Removed guardian summoning.
//= 1.2 Corrected function name. (bugreport:1728) [L0ne_W0lf]
//============================================================

//==============================================
function	script	F_GldManager	{
	.@npc_name$ = getarg(0);
	.@map_name$ = getarg(1);
	.@gid = getcastledata(.@map_name$,1);
	.@gm_name$ = getguildmaster(.@gid);

	mes "["+ .@npc_name$ +" ]";
	if (.@gid == 0){
		mes "我正在等待我的主人。  勇敢的冒險家，跟隨你的命運！";
		return;
	}
	if (getcharid(2) != .@gid){
		mes "我在這裡跟隨 ^5533ff" + getguildmaster(.@gid) + "^000000's command! Hey! Your not even a part of the guild!!";
		mes "監護人在哪裡？消滅這些入侵者！";
		return;
	}
	if (is_guild_leader() == false){
		mes "您不是 ^5533ff" + getguildmaster(.@gid) + "^000000! I am here to follow ^5533FF" + getguildmaster(.@gid) + "^000000's command only";
		return 0;
	}

	mes "歡迎大師 ^5533ff" + getguildmaster(.@gid) + "^000000 ! I will assist you in any way I can!";
	next;

	menu "Kafra Staff Employment / Dismissal",M_Kaf, "Enter Treasure Room",M_Treas, "Cancel",M_End;

	//===========================
	M_Kaf:
	//======
		mes "["+ .@npc_name$ +" ]";
		if (GetCastleData(.@map_name$,9) == 1) goto L_Dismiss;
		if (getgdskilllv(.@gid,10001) == 0){
			mes "主人，您沒有與Kafra員工公司簽訂合同。";
			mes "為了僱用Kafra，您必須首先學習公會技能 ^5533ffsflactalt fip kafra ^000000。";
			return;
		}

		L_Hire:
			mes "您想使用Kafra的服務嗎？您將需要 ^5533ff10,000 Zeny ^000000才能做到...";
			next;
			menu "Employ Kafra.",-,"Cancel",sM_KafEnd;

				mes "["+ .@npc_name$ +" ]";
				if (Zeny < 10000){
					mes "主人，您沒有足夠的錢來僱用Kafra。就業已被取消。";
					return;
				}
				set Zeny,Zeny-10000;
				enablenpc "Kafra Staff#"+getarg(4);
				SetCastleData .@map_name$,9,1;
				mes "您已經與Kafra員工公司簽訂了合同。";
				next;
				cutin "kafra_01",2;
				mes "[KAFRA工作人員]";
				mes "你好嗎？我在這里為您提供有用的服務！我會盡力為您服務。";
				next;
				cutin "kafra_01",255;
				mes "["+ .@npc_name$ +" ]";
				//mes "您的僱傭合同期限為^5533FF1個月^000000。此期限結束後，您將必須簽訂新合同。";
				mes "我認為KAFRA員工將使我們的行會成員受益。";
				return;

			sM_KafEnd:
				mes "["+ .@npc_name$ +" ]";
				mes "就像您希望大師一樣。  但是我建議我們盡快獲得KAFRA！";
				return;

		L_Dismiss:
			mes "您想解僱當前的Kafra嗎？";
			next;
			menu "Dismissal",-,"Cancel",sM_KafEnd2;

				cutin "kafra_01",2;
				mes "[KAFRA工作人員]";
				mes "我做錯了什麼嗎？如果我這樣做了，請您原諒我嗎？";
				next;
				menu "Dismiss",-,"Cancel",ssM_KafEnd2;

					mes "[KAFRA工作人員]";
					mes "不幸的是，我將無法再為您的公會服務了。";
					next;
					disablenpc "Kafra Staff#"+getarg(4);
					SetCastleData .@map_name$,9,0;
					cutin "kafra_01",255;
					mes "["+ .@npc_name$ +" ]";
					mes "Kafra已被駁回。  但是...我們真的應該盡快獲得Kafra！";
					return;
				ssM_KafEnd2:
					mes "[KAFRA工作人員]";
					mes "謝謝大師，我會盡力的！ ^^。";
					cutin "kafra_01",255;
					return;
			sM_KafEnd2:
				mes "["+ .@npc_name$ +" ]";
				mes "主人，我認為您應該保留當前的Kafra員工，因為她已經在盡力為我們服務";
				return;

	//=========================
	M_Treas:
	//========
		mes "["+ .@npc_name$ +" ]";
		mes "您要去我們的寶藏室嗎？只有您，公會大師，才能進入這個房間。";
		next;
		menu "Enter Treasure room.",-,"Cancel",sM_TresEnd;

			mes "["+ .@npc_name$ +" ]";
			mes "請關注我以秘密通道方式。";
			mes "您必須下拉秘密開關才能下車。";
			next;
			warp .@map_name$,getarg(2),getarg(3);
			return;
		sM_TresEnd:
			mes "["+ .@npc_name$ +" ]";
			mes "這些商品每天生產。";
			mes "您應該盡可能地得到它們，因為如果您在錯誤的時間接受它們，它們可能會消失。";
			return;


	//==========================
	M_End:
	//=======
		mes "["+ .@npc_name$ +" ]";
		mes "按照您的意願，主人。";
		return;
}

// Castle 1 ==================================================================================
nguild_alde,218,175,0	script	Frolo	55,{
	callfunc "F_GldManager","Frolo","nguild_alde",119,223,"N01";
	close;
}


// Castle 2 ==================================================================================
nguild_gef,40,48,5	script	Leiber	55,{
	callfunc "F_GldManager","Leiber","nguild_gef",155,112,"N02";
	close;
}


// Castle 3 ==================================================================================
nguild_pay,120,58,4	script	Dundar	55,{
	callfunc "F_GldManager","Dundar","nguild_pay",290,7,"N03";
	close;
}


// Castle 4 ==================================================================================
nguild_prt,112,181,0	script	Thefton	55,{
	callfunc "F_GldManager","Thefton","nguild_prt",15,209,"N04";
	close;
}
