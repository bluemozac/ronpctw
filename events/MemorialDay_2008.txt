//===== rAthena Script =======================================
//= In Memory of Heroes
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= iRO Memorial Day (2008)
//===== Additional Comments: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Many Many Fixes. [Kisuka]
//= 1.2 Some changes. [Kisuka]
//============================================================

// Lauds
//============================================================
prontera,182,214,4	script	Lauds#Memorial	58,{
	if (Memorial08 < 1) {
		mes "[瘋狂西米西米傳奇]";
		mes "嘿，喲！";
		mes "你在那裡做什麼！ ？";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "你知道現在是哪一天！ ？";
		mes "這是一個非常重要的日期！";
		mes "要記住的非常重要的日期！";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "這是什麼！ ？";
		emotion ET_SURPRISE;
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "你的毛巾在哪裡！ ？";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "我什麼？";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "您應該如何在沒有毛巾的情況下去旅行！";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "如果您認為這只是另一個假期，我不會浪費呼吸。";
		mes "但是，如果您想向偉大的士兵致敬，我會幫助您自己準備。";
		next;
		if (select("我想是這樣...:你瘋了還是怎麼的？") == 2) {
			mes "[瘋狂西米西米傳奇]";
			mes "很好，沒有準備好！";
			next;
			mes "[瘋狂西米西米傳奇]";
			mes "^ff0000 今天你就在那裡！^000000";
			close;
		}
		mes "[瘋狂西米西米傳奇]";
		mes "偉大的！";
		mes "然後小心地聽我說。";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "現在，在您可以向墮落的士兵致敬之前，您必須有適當的裝備。";
		mes "沒有毛巾，您將迷路！";
		mes "如果您給我帶來材料，我可以讓您成為毛巾。";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "仔細聽。";
		mes "帶我 ^ff000030織物和20個絨毛 ^000000。";
		set Memorial08,1;
		close;
	}

	else if (Memorial08 == 1) {
		if (countitem(1059) < 30 || countitem(914) < 20) {
			mes "[瘋狂西米西米傳奇]";
			mes "這些是什麼？他們還不夠嗎？";
			mes "*嘆*我真的必須再次告訴你嗎？";
			mes "帶我 ^ff000030織物和20個絨毛 ^000000。";
			close;
		} else {
			mes "*** Mad Sago的稱讚似乎在向自己喃喃自語***";
			next;
			mes "[瘋狂西米西米傳奇]";
			mes "那麼，您帶了毛巾材料嗎？";
			next;
			mes "[瘋狂西米西米傳奇]";
			mes "出色的;你把他們全部帶了。";
			mes "然後，我將按照我的承諾使您成為記憶的毛巾。";
			mes "給我一點。";
			next;
			mes "[瘋狂西米西米傳奇]";
			mes "你去！";
			delitem 1059,30;
			delitem 914,20;
			getitem 6025,1;
			getnameditem 6025,strcharinfo(0);
			set Memorial08,2;
			close;
		}
	}
	
	else if (Memorial08 == 2) {
		mes "[瘋狂西米西米傳奇]";
		mes "啊，你現在有蓬鬆的新毛巾，我什至把你的名字放在上面";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "說，您想如何去向陣亡將士紀念日致敬的旅程？";
		next;
		if (select("當然。 :不，我很忙。") == 2) {
			mes "[瘋狂西米西米傳奇]";
			mes "如果有人決定在您的家中建造高速公路，請不要哭泣！";
			close;
		}
		mes "[瘋狂西米西米傳奇]";
		mes "好的。順便說一句，您甚至知道毛巾的用途嗎？";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "看來您只是在不了解其含義的情況下攜帶它。";
		mes "真丟臉！您應該在prontera ^000000中與 ^ff0000grast交談。";
		set Memorial08,3;
		close;
	}

	else if (Memorial08 == 3) {
		mes "[瘋狂西米西米傳奇]";
		mes "你還在這裡做什麼？";
		mes "我告訴你去Prontera的Grast！";
		close;
	}

	else if (Memorial08 == 4 || Memorial08 == 5 || Memorial08 == 6) {
		mes "[瘋狂西米西米傳奇]";
		mes "去幫助刻，然後回來。";
		close;
	}

	else if (Memorial08 == 7) {
		mes "[瘋狂西米西米傳奇]";
		mes "哦，你帶來了所有。";
		mes "哈哈哈！";
		mes "哈哈哈哈哈！咳嗽咳嗽";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "請把他們交給我。順便說一句，感謝您的辛勤工作。";
		mes "說，您從旅程中學到了什麼？";
		mes "現在要去Prontera的12點鐘方向訪問牌匾。";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "你是什麼意思你在那裡？";
		next;
		mes "[瘋狂西米西米傳奇]";
		mes "什麼？你為什麼要給我你的垃圾！";
		mes "這些物品對我根本沒有用。";
		mes "你應該去牌匾！";
		set Memorial08,8;
		close;
	}

	else if (Memorial08 == 8) {
		mes "[瘋狂西米西米傳奇]";
		mes "別試圖給我你的垃圾！";
		mes "前往Prontera的12點鐘方向前往牌匾！";
		close;
	}
	
	else if (Memorial08 >= 9) {
		set .@RandomMsg, rand(1,5);
		if (.@RandomMsg == 1) {
			mes "[瘋狂西米西米傳奇]";
			mes "請勿恐慌!";
			emotion ET_SURPRISE;
			close;
		}
		else if (.@RandomMsg == 2) {
			mes "[瘋狂西米西米傳奇]";
			mes "生活...就像葡萄柚。";
			mes "它是橙色和柔軟的，裡面有一些小書，有些人有一半的早餐。";
			close;
		}
		else if (.@RandomMsg == 3) {
			mes "[瘋狂西米西米傳奇]";
			mes "這個故事有一點，但是它暫時逃脫了編年史家的思想。";
			close;
		}
		else if (.@RandomMsg == 4) {
			mes "[瘋狂西米西米傳奇]";
			mes "42！";
			close;
		}
		else if (.@RandomMsg == 5) {
			mes "[瘋狂西米西米傳奇]";
			mes "認為您可以通過土豆解決任何主要問題是錯誤的。";
			close;
		}
	}
}
		
// Memorial Plaque
//============================================================
prontera,153,286,4	script	Memorial Plaque#Memorial	857,{
	if (Memorial08 < 8) {
		mes "[" + strcharinfo(0) + "]";
		mes "- 這是一個塵土飛揚的舊牌匾。 -";
		close;
	}
	
	else if (Memorial08 == 8) {
		mes "[" + strcharinfo(0) + "]";
		mes "- 這是一個塵土飛揚的舊牌匾。 -";
		mes "“這一定是讚揚在談論的。”";
		mes "“讓我們用毛巾將其除塵。”";
		next;
		set Memorial08,9;
		getexp 93750,43750;
		goto L_CleanPlaque;
	}
	
	else if (Memorial08 >= 9) {
		goto L_CleanPlaque;
	}
	
	L_CleanPlaque:
		mes "- 您會看到清潔牌照中的消息。";
		next;
		mes "- 儘管沒有雕刻的大理石應該上升到他們的記憶， -";
		mes "- 沒有雕刻的石熊記錄， -";
		mes "- 迄今將與他們尊敬的土地一樣持久。 -";
		mes "-丹尼爾·韋伯斯特-";
		if (Memorial08 == 9) {
			getexp 93750,43750;
		}
		next;
		mes "- 還有另一個信息。";
		next;
		mes "- 我可能沒有去我打算去的地方， -";
		mes "- 但是我認為我最終到了我需要去的地方。";
		mes "- 道格拉斯·亞當（Douglas Adams）。";
		if (Memorial08 == 9) { 
			getexp 93750,43750;
		}
		next;
		mes "- 這是最後一條消息。";
		next;
		mes "- 真實的英雄主義非常清醒，非常不像。";
		mes "- 這並不是要以任何成本超越所有其他人的衝動， -";
		mes "- 但是要以任何費用為他人服務的衝動。 -";
		mes "- 亞瑟·阿什（Arthur Ashe） -";
		if (Memorial08 == 9) { 
			getexp 93750,43750;
			set Memorial08,10;
		}
		close;
}

// Grast
//============================================================
prontera,150,270,4	script	Grast#Memorial	900,{
	if (Memorial08 < 3) {
		mes "[痴迷]";
		mes "陣亡將士紀念日是悲傷而光榮的一天。";
		mes "我想知道有多少人記得他們...";
		close;
	}
	
	else if (Memorial08 == 3) {
		mes "[痴迷]";
		mes "哦，那不是記憶的毛巾嗎？";
		mes "我很高興認識一個了解陣亡將士紀念日含義的人。";
		next;
		mes "[痴迷]";
		mes "知道我們今天在慶祝什麼非常重要，您不認為嗎？";
		next;
		mes "[痴迷]";
		mes "如果您想去紀念日致敬，應該給我帶來一些我要求的材料。";
		next;
		mes "[痴迷]";
		mes "請給我帶來 ^ff0000的每個紅水藥水，綠色藥水，覺醒藥水和蝴蝶翼 ^000000。";
		mes "我將等待您的回報。";
		set Memorial08,4;
		close;
	}

	else if (Memorial08 == 4) {
		if (countitem(501) < 1 || countitem(506) < 1 || countitem(656) < 1 || countitem(602) < 1) {
			mes "[痴迷]";
			mes "糟糕，您還沒有帶所有材料。";
			mes "請確保您需要帶我";
			mes "^ff0000每种色藥水，綠色藥水，喚醒藥水和黃蠅翼^000000的ione。";
			next;
			mes "[痴迷]";
			mes "之後您必須帶我更多材料。";
			mes "如果您感到太負擔了，無法收集所有這些，";
			mes "我可以為您提供所有材料。";
			next;
			if (select("我會收集剩下的。 :給我材料。") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		} else {
			mes "[痴迷]";
			mes "哦，你帶來了我問的材料。";
			next;
			mes "[痴迷]";
			mes "嗯，您還需要更多的東西...";
			next;
			mes "[痴迷]";
			mes "請給我帶來每個陷阱，Yggdrasil葉，藍色寶石，水晶鏡，肉和胡蘿蔔的ff0000。 ^000000";
			mes "我將等待您的回報。";
			set Memorial08,5;
			close;
		}
	}
	
	else if (Memorial08 == 5) {
		if (countitem(1065) < 1 || countitem(610) < 1 || countitem(717) < 1 || countitem(747) < 1 || countitem(517) < 1 || countitem(515) < 1) {
			mes "[痴迷]";
			mes "糟糕，您還沒有帶所有材料。";
			mes "請確保你必須帶我";
			mes "^ff0000每個陷阱，Yggdrasil葉，藍色寶石，水晶鏡，肉和胡蘿蔔^000000。";
			next;
			mes "[痴迷]";
			mes "之後您必須帶我更多材料。";
			mes "如果您感到太負擔了，無法收集全部，我可以為您提供所有材料。";
			next;
			if (select("我會收集剩下的。 :給我材料。") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		} else {
			mes "[痴迷]";
			mes "哦，你帶來了我問的一切。";
			mes "嗯，我認為您只需要更多。";
			next;
			mes "[痴迷]";
			mes "請給我帶來每個寵物孵化器，鞭炮，波林娃娃和花束 ^000000的ff0000。";
			set Memorial08,6;
			close;
		}
	}
	
	else if (Memorial08 == 6) {
		if (countitem(643) < 1 || countitem(12018) < 1 || countitem(741) < 1 || countitem(745) < 1) {
			mes "[痴迷]";
			mes "糟糕，您還沒有帶所有材料。";
			mes "請確保您需要帶我";
			mes "^ff0000的每個寵物孵化器，鞭炮，波林娃娃和花束";
			next;
			mes "[痴迷]";
			mes "它們是最後一批材料。";
			mes "如果您感到太負擔了，無法收集全部，我可以為您提供所有材料。";
			next;
			mes "[痴迷]";
			mes "但是，如果我為您提供所有材料，但是，我將不必感謝您的服務。";
			mes "這是你的電話，" + strcharinfo(0) + ".";
			next;
			if (select("我會收集剩下的。 :給我材料。") == 2) {
				goto L_GiveUp;
			}
			goto L_Continue;
		} else {
			mes "[痴迷]";
			mes "你帶了我問的材料嗎？";
			mes "啊，謝謝您所帶來的辛勤工作。";
			next;
			mes "[痴迷]";
			mes "希望您在收集這些材料時學到寶貴的課程。";
			mes "我喜歡給你一個小禮物供您服務。";
			next;
			mes "[痴迷]";
			mes "你看，我每隻手都有兩種不同的禮物。";
			mes "最好的一件事是給你倆，但是...";
			mes "您想如何測試運氣，" + strcharinfo(0) + "?";
			next;
			mes "[痴迷]";
			mes "好吧，您想挑出哪隻手？";
			next;
			if (select("左手:右手。") == 2) {
				mes "[痴迷]";
				mes "您已經選擇了我的右手。";
				mes "這是給你的禮物。";
				mes "現在，請把所有這些材料帶給稱讚。";
				set Memorial08, 7;
				getitem 617,1;
				close;
			}
			mes "[痴迷]";
			mes "您已經選擇了我的左手。";
			mes "這是給你的禮物。";
			mes "現在，請把所有這些材料帶給稱讚。";
			set Memorial08, 7;
			getitem 12109,1;
			close;
		}
	}
	
	else if (Memorial08 == 7) {
		mes "[痴迷]";
		mes "你遇到了讚美嗎？";
		mes "我希望您會記得自己的自由和幸福是建立在戰爭中犧牲的成千上萬生命之上的。";
		close;
	}

	else if (Memorial08 >= 8) {
		mes "[痴迷]";
		mes "毛巾是冒險家可以擁有的最有用的東西。";
		close;
	}

	L_GiveUp:
		mes "[痴迷]";
		mes "我懂了。我想你如今很忙，是嗎？";
		mes "沒問題;我給你所有的用品...";
		next;
		mes "[痴迷]";
		mes "你去。";
		mes "我將所有物品融合在一起，為您更緊湊。";
		mes "稍後您可以感謝我提供額外的服務。";
		mes "請把他們帶到讚美。";
		set Memorial08,7;
		getitem 7126,1;
		next;
		mes "[痴迷]";
		mes "順便說一句，他發生了不幸的事故，並失去了理智。";
		mes "即使他開始bab起來，也請不要感到震驚。";
		close;
	
	L_Continue:
		mes "[痴迷]";
		mes "那是個好主意。";
		mes "那我將等待您的回報。";
		close;
}
