//===== rAthena Script =======================================
//= Old Pharmacist
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trade in items for potions.
//= Breakdown of arguments used:
//= arg(0): Herb required.
//= arg(1): Zeny cost.
//= arg(2): Potion given.
//===== Additional Comments: =================================
//= 1.0 Fully working. [DZeroX]
//= 1.0a Minor fixes [Lupus]
//= 1.1 Added changed the dialogs and name to fit the
//=     the official information. [DZeroX]
//= 1.1a Removed .GATs [Lupus]
//= 1.1b Fixed a bug in Orange Potion creating. Copy&Pasting = bad. [L0ne_W0lf]
//= 1.2 Added checkweight, and input min/max values. [L0ne_W0lf]
//= 1.3 Fixed minor condition check bug. (bugreport:597) [L0ne_W0lf]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta_in,16,28,4	script	Pharmacist	61,{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等！ -";
		mes "- 目前您正在攜帶 -";
		mes "- 與您在一起太多。 -";
		mes "- 請稍後再回來 -";
		mes "- 將一些項目放入KAFRA存儲後。 -";
		close;
	}
	mes "[老藥劑師]";
	mes "嗯...";
	mes "是什麼把你帶到這裡...？";
	next;
	switch(select("製作藥水:對話:混合資訊:取消")) {
	case 1:
		if (MaxWeight - Weight < 5000) {
			mes "[老藥劑師]";
			mes "你為什麼要攜帶這麼多！";
			mes "不要貪婪，只攜帶盡可能多的東西！";
			next;
			mes "[老藥劑師]";
			mes "你太重了，無法從我這裡收到藥水...";
			mes "先將一些物品存儲在您的存儲中！";
			close;
		} else {
			mes "[老藥劑師]";
			mes "您準備好所有的東西，對嗎？您想要哪一個？";
			next;
			switch(select("紅色藥水:橙色藥水:黃色藥水:白色藥水:藍色藥水:綠色藥水:其實，我什麼都不想要。")) {
			case 1:
				callsub L_Making,507,3,501;
			case 2:
				mes "[老藥劑師]";
				if (countitem(507) < countitem(713)) {
					set .@max,countitem(507);
				}
				else if (countitem(508) < countitem(713)) {
					set .@max,countitem(508);
				}
				else {
					set .@max,countitem(713);
				}
				if ((countitem(507) < 1) || (countitem(508) < 1) || (countitem(713) == 0)) {
					mes "你流氓！您期望什麼？一無所有。 tsk！";
					mes "迷路！";
					close;
				}
				if (Zeny < 3) {
					mes "你流氓！您期望什麼？一無所有。 tsk！";
					mes "迷路！";
					close;
				}
				mes "多少？";
				next;
				switch(select("製作盡可能多:我想選擇數量:其實，我什麼都不想要。")) {
				case 1:
					if ((countitem(507) < .@max) || (countitem(508) < .@max) || (countitem(713) < .@max) || (Zeny < .@max*3)) {
						mes "[老藥劑師]";
						mes "你流氓！您甚至都沒有所有的材料，您希望我為您製作藥水嗎？";
						close;
					}
					set Zeny,Zeny-(.@max*5);
					delitem 507,.@max; //Red_Herb
					delitem 508,.@max; //Yellow_Herb
					delitem 713,.@max; //Empty_Bottle
					getitem 502,.@max; //Orange_Potion
					break;
				case 2:
					mes "[老藥劑師]";
					mes "然後選擇低於100的數字。如果您不想要任何數字，只需輸入“ 0”。有了您擁有的材料，您可以做到"+.@max+" potions.";
					next;
					input .@amount,0,101;
					if (.@amount == 0) {
						mes "[老藥劑師]";
						mes "下定決心，對嗎？";
						close;
					}
					if (.@amount > 100) {
						mes "[老藥劑師]";
						mes "你聾了嗎？我說的不到100！";
						close;
					}
					if ((countitem(507) < .@amount) || (countitem(508) < .@amount) || (countitem(713) < .@amount) || (Zeny < .@amount*3)) {
						mes "[老藥劑師]";
						mes "你流氓！您甚至都沒有所有的材料，您希望我為您製作藥水嗎？";
						close;
					}
					next;
					set Zeny,Zeny-(.@amount*5);
					delitem 507,.@amount; //Red_Herb
					delitem 508,.@amount; //Yellow_Herb
					delitem 713,.@amount; //Empty_Bottle
					getitem 502,.@amount; //Orange_Potion
					break;
				case 3:
					mes "[老藥劑師]";
					mes "什麼？ ！";
					mes "咕嚕……";
					mes "！";
					mes "迷路！";
					close;
				}
				mes "[老藥劑師]";
				mes "幹得好。一切都這樣做了，您可以接受。但是請記住！濫用藥不好。";
				close;
			case 3:
				callsub L_Making,508,10,503;
			case 4:
				callsub L_Making,509,20,504;
			case 5:
				callsub L_Making,510,30,505;
			case 6:
				callsub L_Making,511,3,506;
			case 7:
				mes "[老藥劑師]";
				mes "什麼？ ！";
				mes "咕嚕……";
				mes "！";
				mes "迷路！";
				close;
			}
		}
	case 2:
		mes "[老藥劑師]";
		mes "使用藥物，您可以提高一個人的再生能力。但是，他們只有一點點才能。 *嘆氣*我開始想到我小時候的日子。我一定要老了。";
		next;
		mes "[老藥劑師]";
		mes "無論如何，藥水只是一種藥水。僅此而已。";
		close;
	case 3:
		mes "[老藥劑師]";
		mes "嗯嗯...";
		mes "您的年輕人可能很煩人。但是，自從您問，我會解釋的。";
		next;
		mes "[老藥劑師]";
		mes "草藥自己工作得很好，但是如果您使用我的特殊技巧和技巧來製作藥水，那麼效果要大得多。";
		next;
		mes "[老藥劑師]";
		mes "如果您熱切有禮貌地問，我會為您做。但是，不是免費的...不用擔心，我只收取少量費用，所以這並不是那麼貴。";
		next;
		mes "[老藥劑師]";
		mes "紅色藥水 -  ^0098E52紅色草藥，1個空瓶，2 Zeny Fee。 ^000000";
		mes "橙色藥水 -  ^0098E51紅色草藥，1個黃色草藥，1個空瓶，5 Zeny Fee。 ^000000";
		mes "黃色藥水 -  ^0098E52黃色草藥，1個空瓶，10 Zeny。 ^000000";
		next;
		mes "[老藥劑師]";
		mes "白藥水 -  ^0098E52白色草藥，1個空瓶，20 Zeny Fee。 ^000000";
		mes "藍色藥水 -  ^0098E52藍色草藥，1個空瓶，30 Zeny Fee。 ^000000";
		mes "綠色藥水 -  ^0098E52綠色草藥，1個空瓶，3 Zeny Fee。 ^000000";
		close;
	case 4:
		mes "[老藥劑師]";
		mes "多麼無聊的人。如果您有話要說，為什麼不說呢？";
		close;
	}
	
L_Making:
	.@item_req = getarg(0);
	.@req_amount = getarg(1);

	mes "[老藥劑師]";
	if ((countitem(.@item_req)/2) < countitem(713)) {
		.@max = countitem(.@item_req) /2;
	} else {
		.@max = countitem(713);
	}
	if (countitem(.@item_req) < 2 || countitem(713) == 0) {
		mes "你流氓！您期望什麼？一無所有。 tsk！";
		mes "迷路！";
		close;
	}
	if (Zeny < 3) {
		mes "你流氓！您期望什麼？一無所有。 tsk！";
		mes "迷路！";
		close;
	}
	mes "多少？";
	next;
	switch(select("製作盡可能多:我想選擇數量:其實，我什麼都不想要。")) {
	case 1:
		if ((countitem(.@item_req) < .@max*2) || countitem(713) < .@max || (Zeny < .@max*.@req_amount)) {
			mes "[老藥劑師]";
			mes "你流氓！您甚至都沒有所有的材料，您希望我為您製作藥水嗎？";
			close;
		}
		Zeny = Zeny - (.@max*.@req_amount);
		delitem .@item_req,.@max*2;
		delitem 713,.@max; //Empty_Bottle
		getitem getarg(2),.@max; 
		break;
	case 2:
		mes "[老藥劑師]";
		mes "然後選擇低於100的數字。如果您不想要任何數字，只需輸入“ 0”。有了您擁有的材料，您可以做到"+.@max+" potions.";
		next;
		input .@amount;
		if (.@amount == 0) {
			mes "[老藥劑師]";
			mes "下定決心，對嗎？";
			close;
		}
		if (.@amount > 100) {
			mes "[老藥劑師]";
			mes "你聾了嗎？我說的不到100！";
			close;
		}
		if ((countitem(.@item_req) < .@amount*2) || countitem(713) < .@amount || (Zeny < .@amount*.@req_amount)) {
			mes "[老藥劑師]";
			mes "你流氓！您甚至都沒有所有的材料，您希望我為您製作藥水嗎？";
			close;
		}
		Zeny = Zeny - (.@max*.@req_amount);
		delitem .@item_req,.@amount*2;
		delitem 713,.@amount; //Empty_Bottle
		getitem getarg(2),.@amount; 
		break;
	case 3:
		mes "[老藥劑師]";
		mes "什麼？ ！";
		mes "咕嚕……";
		mes "！";
		mes "迷路！";
		close;
	}
	mes "[老藥劑師]";
	mes "幹得好。一切都這樣做了，您可以接受。但是請記住！濫用藥不好。";
	close;
}
