//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morocc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morocc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Inn Employee#Sammy	53,{

	callfunc "F_InnMaid","[Employee Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Inn Employee#Ahlma	53,{

	callfunc "F_InnMaid","[Employee Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	Inn Employee#Jennie	53,{

	callfunc "F_InnMaid","[Employee Jennie]","'Fisherman Inn'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Inn Employee#Cena	53,{

	callfunc "F_InnMaid","[Employee Cena]","'Ifrit,' the only Inn in the city of Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Inn Employee#Ahee	53,{

	callfunc "F_InnMaid","[Employee Ahee]","Payon Inn","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Inn Employee#Hasna	53,{

	callfunc "F_InnMaid","[Employee Hasna]","Morocc Inn","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Inn Employee#Manar	53,{

	callfunc "F_InnMaid","[Employee Manar]","Morocc Inn","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Inn Maid#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Al De Baran Inn","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Inn Keeper#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Rachel Inn","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Hotel Employee#01	86,{
	mes "[酒店員工]";
	mes "歡迎來";
	mes "皇家龍，";
	mes "在哪裡可以找到";
	mes "最好的住宿";
	mes "以及最好的服務。";
	next;
	switch(select("儲存點:休息 - 5,000 zeny:取消")) {
	case 1:
		savepoint "lhz_in02",209,275,1,1;
		mes "[酒店員工]";
		mes "謝謝你";
		mes "重生點具有";
		mes "被保存在這裡";
		mes "皇家龍。";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[酒店員工]";
			mes "對不起，但是";
			mes "您需要5,000 Zeny";
			mes "為了入住。";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[酒店員工]";
		mes "謝謝~";
		mes "我希望你喜歡";
		mes "你留在";
		mes "皇家龍。";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[酒店員工]";
		mes "謝謝，";
		mes "祝你今天過得愉快。";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Inn Maid#Receptionist	53,{

	callfunc "F_InnMaid","[Receptionist]","Hugel Inn","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Inn Master#Receptionist	709,{
	mes "[旅館大師]";
	mes "美好的一天~";
	mes "歡迎來到";
	mes "最舒適";
	mes "靜脈裡的旅館~";
	next;
	switch(select("儲存:休息 -> 5000 zeny:離開")) {
	case 1:
		mes "[旅館大師]";
		mes "您的重生點";
		mes "已被保存在靜脈中。";
		mes "享受你在城裡住宿~";
		savepoint "ve_in",157,209,1,1;
		close;
	case 2:
		mes "[旅館大師]";
		if(Zeny < 5000){
			mes "對不起，但我沒有";
			mes "認為你有足夠的";
			mes "簽到的錢。";
			mes "服務費是5,000 Zeny。";
			close;
		}
		mes "享受你的住宿~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[旅館大師]";
		mes "請再來。";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "歡迎來";
	mes getarg(1) + "。";
	mes "我該如何幫助您？";
	next;
	switch(select("儲存:休息 -> 5000 zeny:取消")) {
	case 1:
		mes .@npc_name$;
		mes "您的重生點";
		mes "已保存。";
		mes "謝謝你，";
		mes "請再來。";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "很抱歉，服務費是5,000 Zeny。請確保您有足夠的錢可以下次檢查，好嗎？";
			close;
		}
		mes "謝謝。";
		mes "我希望你";
		mes "享受休息~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
