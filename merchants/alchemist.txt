//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	Guild Dealer	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 等一下！ -";
		mes "- 目前您正在攜帶 -";
		mes "- 與您在一起太多。 -";
		mes "- 請再次回來 -";
		mes "- 將一些物品存儲到Kafra存儲中後。 -";
		close;
	}
	mes "[給予夏普]";
	mes "歡迎來到";
	mes "煉金術士聯盟。";
	mes "我今天如何為您提供幫助？";
	next;
	switch(select("購買材料:購買生產手冊:離開")) {
	case 1:
		mes "[給予夏普]";
		mes "你要什麼？";
		next;
		if (select("藥碗 - 8 Zeny:取消") == 1) {
			mes "[給予夏普]";
			mes "你想要幾個？";
			mes "如果要退出，請輸入“ 0”。";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[給予夏普]";
					mes "交易被取消。";
					mes "下次再來。";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[給予夏普]";
					mes "必須";
					mes "小於2000。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[給予夏普]";
				mes "你沒有";
				mes "有足夠的zeny。";
				mes "檢查多少zeny";
				mes "你有先行。";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[給予夏普]";
				mes "似乎沒有";
				mes "你可以攜帶一切。";
				mes "請檢查空間";
				mes "在您的庫存中。";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[給予夏普]";
			mes "謝謝。";
			mes "再來。";
			close;
		}
		mes "[給予夏普]";
		mes "好吧，那，";
		mes "何時再來";
		mes "您需要購買";
		mes "與";
		mes "煉金術，好嗎？";
		close;
	case 2:
		mes "[給予夏普]";
		mes "你需要什麼？";
		mes "手冊通常為100,000 Zeny。但是，有幾本特殊的手冊將花費更多。";
		next;
		switch(select("藥水製作指南:酒精製作指南:瓶子手榴彈製作指南:酸瓶製作指南:植物瓶製作指南:海洋球瓶製作指南:閃亮塗層製作指南:濃縮藥水製作指南:取消交易")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[給予夏普]";
			mes "好吧...";
			mes "如果你回來";
			mes "曾經需要購買";
			mes "生產手冊。";
			close;
		}
	case 3:
		mes "[給予夏普]";
		mes "好吧，那，";
		mes "祝你有美好的一天。";
		close;
	}

S_SellManual:
	.@item_id = getarg(0);
	.@zeny_req = getarg(1);

	if (Zeny < .@zeny_req) {
		mes "[給予夏普]";
		mes "你沒有";
		mes "有足夠的zeny。";
		mes "檢查多少zeny";
		mes "你有先行。";
		close;
	}
	mes "[給予夏普]";
	mes getitemname(.@item_id) +"？";
	mes "那會"+ .@zeny_req +" zeny.";
	next;
	if (select("購買:離開") == 1) {
		Zeny = Zeny - .@zeny_req;
		getitem .@item_id,1;
		mes "[給予夏普]";
		mes "謝謝你";
		mes "您的光顧。";
		close;
	}
	mes "[給予夏普]";
	mes "再來";
	mes "下次。";
	close;
}
