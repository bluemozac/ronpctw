//===== rAthena Script =======================================
//= Ice Cream Maker
//===== By: ==================================================
//= KOOK SWU, Kisuka
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A man makes you ice-cream
//===== Additional Comments: =================================
//= 1.1 Optimized, changed overlapping coords in Alberta [Lupus]
//= 1.2 According to the patch, IceCream seller has been removed from Morocc [Lupus]
//= 1.3 No more label menu and updated to match AEGIS script. [Kisuka]
//= 1.4 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.5 Fixed small checkweight issue. [Kisuka]
//============================================================

-	script	Ice Cream Maker::IceCreamer	-1,{
	mes "[冰淇淋製造商]";
	mes "新鮮的冰淇淋是用路易（Lutie）雪製成的！";
	mes "現在享受它，它不會長時間出售！";
	mes "^3355ff100 Zeny^000000冰淇淋，";
	mes "冰淇淋！";
	next;
	if(select("給我冰淇淋!:取消交易") == 2) {
		mes "您確定您不想要嗎？";
		mes "我不會賣掉它，";
		mes "一旦我用完了，";
		mes "不會再有什麼！ ！ ！";
		close;
	}
	mes "[冰淇淋製造商]";
	mes "新鮮的冰淇淋是用路易（Lutie）雪製成的！";
	mes "每個人都想要我們的美味冰淇淋，";
	mes "但是我們的數量有限，";
	mes "因此，您一次只能購買5個！ ！";
	next;
	while(true) {
		input .@input;
		if(.@input < 1) {
			mes "[冰淇淋製造商]";
			mes "如果您不想購買，";
			mes "你能讓下一個客戶嗎";
			mes "購買？";
			mes "謝謝。";
			close;
		}
		if(.@input > 5) {
			mes "[冰淇淋製造商]";
			mes "哎喲";
			mes "您期望太多。";
			mes "尊敬的顧客，";
			mes "如果您吃了5種以上的冰淇淋，";
			mes "如果您可能會進行很多旅行";
			mes "今晚去洗手間。";
			next;
			continue;
		}
		break;
	}
	if(Zeny<.@input*100) {
		mes "[冰淇淋製造商]";
		mes "親愛的客戶，您的錢包似乎很輕。";
		mes "價格為 ^3355ff100 Zeny ^000000每冰淇淋。";
		close;
	}
	if(checkweight(536,.@input) == 0) {
		mes "[冰淇淋製造商]";
		mes "親愛的顧客，您看起來好像攜帶了很多。";
		mes "冰淇淋很好，";
		mes "但是你必須考慮自己的體重";
		mes "在購買之前。";
		close;
	}
	set Zeny,Zeny-100*.@input;
	getitem 536,.@input;	// Ice_Cream
	close;
}

// Alberta
alberta,120,45,2	duplicate(IceCreamer)	Ice Cream Maker#1	85

// Morocc Field
moc_fild16,88,304,4	duplicate(IceCreamer)	Ice Cream Maker#2	85

// Morocc
//morocc,160,144,4	duplicate(IceCreamer)	Ice Cream Maker#3	85
