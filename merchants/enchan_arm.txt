//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

prontera,165,60,2	script	Apprentice Craftsman	73,{
	disable_items;
	mes "[學徒工匠]";
	if (Zeny >= 400000) {
		mes "我一直在研究增強裝甲以最大化其能力的方法。";
		next;
		mes "[學徒工匠]";
		mes "迷人是一項很棒的技能，它注入了神秘的地位力量，將其插入了盔甲的隱藏插座中。";
		next;
		mes "[學徒工匠]";
		mes "但是，您必須記住，如果您擁有的同類裝甲有兩種裝甲，將以將它們放入庫存中的順序應用。";
		next;
		mes "[學徒工匠]";
		mes "在這種情況下，附魔可能應用於您並不意味著附魔的項目。因此，只需帶上 ^5555ffone Armor ^000000，您想要附魔是安全的...";
		next;
		mes "[學徒工匠]";
		mes "如果您在庫存中有多種多類情況，我將不負責會發生什麼。";
		next;
		switch(select("無插槽防具:有插槽防具:高級防具:下次再說")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[學徒工匠]";
			mes "當您對迷住盔甲有興趣時，請回來。";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "我負責迷人的裝甲。簡而言之，我一直在研究加電裝甲的方法。";
	next;
	mes "[學徒工匠]";
	mes "如果有機會，您想讓自己的盔甲迷住，帶我40萬Zeny和想要著迷的盔甲，您都準備就緒。";
	close;

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[學徒工匠]";
	if (countitem(.@itemid) == 1) {
		mes "插座附魔將花費您40萬Zeny。並且會有一個隨機的選項著迷。當然，有可能打破盔甲。";
		next;
		mes "[學徒工匠]";
		mes "首先也是最重要的。";
		mes "^ff5555持有盔甲的精煉水平";
		mes "卡將消失。 ^000000";
		mes "您還想嘗試一個附魔嗎？";
		next;
		if(select("嗯...讓我再考慮一下:繼續進行") == 1) {
			mes "[學徒工匠]";
			mes "好吧，我不能怪你。安全首先，嗯？";
			mes "現在您過得愉快。";
			close;
		}
		mes "[學徒工匠]";
		mes "一個冒險家吧？好吧，我們吧？";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[學徒工匠]";
			mes "抱歉，但是您沒有足夠的Zeny。";
			close;
		}
		progressbar "ffff00",7;
		set Zeny, Zeny-400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1: set .@addpart,4702;break;
			case 2: set .@addpart,4712;break;
			case 3: set .@addpart,4722;break;
			case 4: set .@addpart,4732;break;
			case 5: set .@addpart,4742;break;
			case 6: set .@addpart,4752;break;
			case 7:
			case 8: set .@addpart,4701;break;
			case 9:
			case 10: set .@addpart,4711;break;
			case 11:
			case 12: set .@addpart,4721;break;
			case 13:
			case 14: set .@addpart,4731;break;
			case 15:
			case 16: set .@addpart,4741;break;
			case 17:
			case 18: set .@addpart,4751;break;
			case 19:
			case 20:
			case 21: set .@addpart,4700;break;
			case 22:
			case 23:
			case 24: set .@addpart,4710;break;
			case 25:
			case 26:
			case 27: set .@addpart,4720;break;
			case 28:
			case 29:
			case 30: set .@addpart,4730;break;
			case 31:
			case 32:
			case 33: set .@addpart,4740;break;
			case 34:
			case 35:
			case 36: set .@addpart,4750;break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[學徒工匠]";
				mes "好吧，太糟糕了。";
				mes "請求的設備未能吸引人。";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "唔？沒有什麼可以迷人了！";
		mes "請僅帶一個誘人的設備回來。";
		close;
	}
}
