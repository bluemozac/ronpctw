//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[骨]";
		mes "我是分銷商喀什";
		mes "Kunai的忍者。拿";
		mes "環顧四周，讓我";
		mes "知道你是否有興趣";
		mes "在我的任何商品中。";
		next;
		switch(select("10 劇毒苦無:10 冰柱苦無:10 烈風苦無:10 黑土苦無:10 熱浪苦無:取消")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[骨]";
			mes "好吧，謝謝你";
			mes "去參觀我的商店。";
			mes "請當我來找我";
			mes "您需要購買一些";
			mes "庫奈。現在再見~";
			close;
		}
	}
	mes "[骨]";
	mes "我是分銷商喀什";
	mes "Kunai的忍者。如果你";
	mes "有任何朋友";
	mes "忍者，那你可能";
	mes "想告訴他們關於我的事。";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[骨]";
	mes "您可以交換";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" and";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" for every";
	mes "一組10"+ getitemname(.@item_id) +".";
	next;
	mes "[骨]";
	mes "我只能一次最多給您500套Kunais。";
	mes "如果您想取消交易，請輸入'0.''";
	mes "您想要的Kunai套裝嗎？";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[骨]";
		mes "嗯？對不起，但是";
		mes "我不能給你";
		mes "許多Kunai套裝。請";
		mes "輸入小於500的值。";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[骨]";
		mes "嗯，你沒有";
		mes "足夠的項目";
		mes "Kunai Exchange。請";
		mes "再次檢查您的物品。";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[骨]";
		mes "嗯，好像你";
		mes "庫存沒有";
		mes "足夠存儲的空間";
		mes "更多項目。你更好";
		mes "首先釋放一些空間。";
		close;
	}
	mes "[骨]";
	mes "太好了，一切都在";
	mes "命令，讓我們繼續前進";
	mes "並完成此交易。";
	mes "我確定你會";
	mes "很滿意";
	mes "這些Kunais。";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
