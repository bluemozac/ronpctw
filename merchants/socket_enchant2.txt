//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 等一下 ！ ！ -";
		mes "- 目前您正在攜帶 -";
		mes "- 與您在一起太多。 -";
		mes "- 請重試 -";
		mes "- 減肥後。 -";
		close;
	}
	mes "[萊布勒姆]";
	mes "再會。我的名字是Leablem，";
	mes "我是一位專門研究的技術人員";
	mes "在武器和裝甲中添加插槽。";
	mes "有時可能很難，但是我喜歡";
	mes "我做什麼，並為此感到自豪。";
	next;
	mes "[萊布勒姆]";
	mes "我從兄弟Seyablem那裡學到了大多數事情，";
	mes "但是我認為我還有很多東西要學習。";
	mes "因此，請了解我的插槽服務有限，";
	mes "與我的兄弟Seyablem不同。";
	next;
	mes "[萊布勒姆]";
	mes "我的兄弟不太高興教我";
	mes "他的技能是因為他擔心自己的技能";
	mes "不再是唯一的。但是，他做到了最好的";
	mes "教我";
	next;
	mes "[萊布勒姆]";
	mes "多虧了他，我學到了很多東西。";
	mes "我不太自信";
	mes "在嘗試許多不同的事情時";
	mes "但是我會做的";
	mes "我現在可以做。";
	next;
	mes "[萊布勒姆]";
	mes "實際上，有一件事";
	mes "我哥哥不想我做。";
	mes "我不想讓他不高興，所以...讓我們跳過那部分。";
	mes "我不會再說了！";
	next;
	mes "[萊布勒姆]";
	mes "無論如何，你有任何設備嗎";
	mes "您想添加到插槽中？";
	mes "我的服務費，材料，";
	mes "成功的機會都取決於";
	mes "我正在從事的具體項目。";
	next;
	switch(select("武器:盔甲:關於你跳過的那件事:更多資訊:離開"))
	{
		case 1:
			mes "[萊布勒姆]";
			mes "武器，你說？當然，沒問題。";
			mes "武器分為4個不同的班級";
			mes "取決於工作困難。";
			mes "C班是最簡單的課程，S級是最難的課程。";
			mes "您想嘗試哪個課？";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[萊布勒姆]";
					mes "C課，我明白了。";
					mes "那麼，您有什麼樣的武器？";
					next;
					switch(select("大地之母之書:巨浪之書:狂風之書:烈日之書"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[萊布勒姆]";
					mes "您選擇了平均B類。";
					mes "那麼，您有什麼樣的武器？";
					next;
					switch(select("獸人斧:彎刀:尖刺"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[萊布勒姆]";
					mes "課？您似乎想要太多。但是，沒問題。";
					mes "那麼，您有什麼樣的武器？";
					next;
					switch(select("屠龍者:震動拳刃:狂焰拳刃:冰晶拳刃:刺風拳刃:黃金權杖:東方琵琶:女王之鞭:幽靈長矛:蓋伯爾格:瑞士軍刀"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[萊布勒姆]";
					mes "呵呵，S班？ ！哦，天哪，你是認真的嗎？ *顫抖*";
					mes "所以...您想製作哪個班級武器？";
					next;
					switch(select("西風:破甲短劍:屠龍之劍:破劍短劍:刺客短劍:大十字:處刑者"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[萊布勒姆]";
			mes "盔甲，你說嗎？當然，沒問題。";
			mes "裝甲分為4個不同的班級";
			mes "取決於工作困難。";
			mes "C班是最簡單的課程，S級是最難的課程。";
			mes "您想嘗試哪個課？";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[萊布勒姆]";
					mes "C課，我明白了。";
					mes "那麼，您有哪種盔甲？";
					next;
					switch(select("向日葵:博士帽:大蝴蝶結:男孩帽"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[萊布勒姆]";
					mes "您選擇了平均B類。";
					mes "那麼，您有哪種盔甲？";
					next;
					switch(select("骷髏戒指:高跟鞋"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[萊布勒姆]";
					mes "課？您似乎想要太多。但是，沒問題。";
					mes "那麼，您有哪種盔甲？";
					next;
					switch(select("海盜頭巾:黑色皮靴"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[萊布勒姆]";
					mes "呵呵，S班？ ！哦，天哪，你是認真的嗎？ *顫抖*";
					mes "所以...您想製作哪個S級裝甲？";
					next;
					switch(select("魔法師外袍:聖袍:神聖使命:內衣:內褲"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[萊布勒姆]";
			mes "哦，沒什麼特別的，但他認為";
			mes "這不是人類混亂的東西。";
			mes "我不想讓我的兄弟不高興";
			mes "所以我最好忘記它。";
			next;
			mes "[萊布勒姆]";
			mes "...該服務本身俱有很高的成功機會，";
			mes "但是需要大量材料和高速服務費。";
			mes "我非常懷疑任何人實際上都想使用";
			mes "即使我確實打開了服務。";
			next;
			mes "[萊布勒姆]";
			mes "...嗯...";
			next;
			mes "[萊布勒姆]";
			mes "......................";
			next;
			mes "[萊布勒姆]";
			mes "......................";
			next;
			mes "[萊布勒姆]";
			mes "您似乎有興趣使用該服務。";
			mes "如果願意，我至少可以告訴您。";
			mes "但是您必須保證將其保密。";
			mes "如果我的兄弟發現我要告訴你什麼，";
			mes "他會給我一個毆打！";
			next;
			mes "[萊布勒姆]";
			mes "這項特殊服務的成功機會90％，";
			mes "但是需要2個黃金和2億Zeny：";
			mes "我可以在太陽神的帽子上添加插槽。";
			next;
			mes "[萊布勒姆]";
			mes "我的兄弟說，即使自古以來，也禁止修改那頂帽子。";
			mes "但是我相信人類太好奇了";
			mes "實驗，尤其是在禁止時。";
			mes "我只是不想讓我的兄弟對我大喊大叫。就這樣。";
			next;
			switch(select("使用服務","離開"))
			{
				case 1:
					mes "[萊布勒姆]";
					mes "……………………";
					next;
					mes "[萊布勒姆]";
					mes "……………………";
					next;
					mes "[萊布勒姆]";
					mes "... WH ...什麼？不！";
					mes "給我那骯髒的外觀";
					mes "不會讓我這樣做！";
					mes "不，我說不！不，不！不！";
					next;
					mes "[萊布勒姆]";
					mes "不，我不會！不！";
					next;
					mes "[萊布勒姆]";
					mes "我做不到...";
					next;
					mes "[萊布勒姆]";
					mes "我做不到...";
					next;
					mes "[萊布勒姆]";
					mes "……………………";
					next;
					mes "[萊布勒姆]";
					mes "嗯...";
					next;
					mes "[萊布勒姆]";
					mes "你是如此堅持。好吧，我會讓你一個例外。";
					mes "只是不要告訴任何人，";
					mes "特別是我的兄弟。你保證嗎？";
					next;
					mes "[萊布勒姆]";
					mes "好吧，請帶我";
					mes "^ff00002黃金，2億Zeny-服務費 - ，";
					mes "^ff0000和太陽神的帽子。";
					mes "^ff000000您知道您的成功機會是90％，不是嗎？ ^000000";
					mes "我們沒有時間浪費，";
					mes "因此，讓我們立即開始業務。";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[萊布勒姆]";
						mes "我們現在開始嗎？";
						mes "向您的眾神祈禱，祝你好運。";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[萊布勒姆]";
							mes "出色的！哇，我想90％的成功機會是真的！";
							mes "看，插槽是在完美的地方添加的。";
							mes "恭喜。";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[萊布勒姆]";
							mes "這是我第一次也是最後一次為您做。";
							mes "永遠不要告訴任何人";
							mes "關於這個。好的？";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[萊布勒姆]";
							mes "哇！哦，天哪，我們失敗了！";
							mes "我的天哪，我想我們的運氣最糟糕。";
							mes "我們該怎麼辦？ ...這就是原因";
							mes "我不想首先做！";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[萊布勒姆]";
							mes "不，永遠不要讓我再次做這種冒險的事情！";
							mes "呸~";
							close;
						}
					}
					else
					{
						mes "[萊布勒姆]";
						mes "嗯...";
						next;
						mes "[萊布勒姆]";
						mes "您沒有足夠的材料。";
						mes "伙計，你為什麼還要求我嘗試？";
						mes "我不會再為你做這個，";
						mes "即使您帶來了足夠的材料。 bah~";
						close;
					}
				case 2:
					mes "[萊布勒姆]";
					mes "永遠不要告訴任何人";
					mes "關於我告訴你的。好的？";
					close;
			}
		case 4:
			mes "[萊布勒姆]";
			mes "您想知道更多信息嗎？";
			mes "好吧，我認為沒有什麼";
			mes "您需要特別知道。";
			mes "但是我至少可以給你一個小費。";
			next;
			mes "[萊布勒姆]";
			mes "每個城鎮的老虎機技術人員";
			mes "提供不同的成功機會";
			mes "取決於設備的升級水平。";
			mes "我不知道那是真的，";
			mes "但是我可以看到每個城鎮的區別。";
			next;
			mes "[萊布勒姆]";
			mes "例如，一個城鎮的老虎機技術員";
			mes "提供更高的成功機會";
			mes "設備項目的升級水平較高，";
			mes "雖然另一個城鎮的技術人員更高";
			mes "在較低的升級水平上成功。";
			next;
			mes "[萊布勒姆]";
			mes "當您無法添加插槽時，設備";
			mes "會破碎並消失。";
			mes "我盡力成功，但是";
			mes "插槽是複雜的工作。";
			next;
			mes "[萊布勒姆]";
			mes "哦，您需要注意的是一件重要的事情。";
			mes "^ff0000bry不要攜帶兩個相同的設備項目";
			mes "^ff0000與您添加插槽時。";
			mes "^ff0000，可以將插槽添加到一個隨機選擇的項目之一中，";
			mes "^ff0000和在最壞的情況下，您可能會失去對您更有價值的物品。 ^000000";
			next;
			mes "[萊布勒姆]";
			mes "希望這些信息對您有所幫助。";
			mes "謝謝。";
			close;
		case 5:
			mes "[萊布勒姆]";
			mes "好吧，那麼。";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[萊布勒姆]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"...好吧，那你需要帶我";
		mes "^ff0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny as service charge,";
		mes "^ff0000"+ .@req_amount[0] +"ea "+ getitemname(.@item_req[0]) +" and "+ .@req_amount[1] +"ea "+ getitemname(.@item_req[1]) +".";
		mes "^ff0000 of課程，您需要一個"+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"？好吧，那你需要帶我";
		mes "^ff0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny as service charge and "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" as the requirement.";
		mes "^ff0000 of課程，別忘了帶我"+ getitemname(.@item_id) +".";
		mes "^ff0000 you應該有所有項目。 ^000000";
	}
	next;
	mes "[萊布勒姆]";
	mes "你已經把所有人都帶了嗎？";
	mes "對於您的信息，如果您無法創建插槽，";
	mes "您將失去所有項目的要求以及目標"+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "weapon" : "armor") +".";
	mes "還請記住，如果"+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "weapon" : "armor") +" has been upgraded, and has been inserted with a card,";
	mes "即使您成功創建了一個插槽，您也會失去它們。";
	next;
	switch(select("請求插槽創建:下次再試"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[萊布勒姆]";
				mes "好吧，然後開始工作！";
				mes "您最好為成功的結果祈禱。";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[萊布勒姆]";
					mes "太好了，它似乎是成功的。";
					mes "看起來做得很好。恭喜！";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[萊布勒姆]";
					mes "再見，哥們！";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[萊布勒姆]";
					mes "哇！ ...我很抱歉，它失敗了。";
					mes "但是，我完全無辜。";
					mes "這是您的運氣，它是上帝注定的，好嗎？";
					mes "不要那麼失望，";
					mes "下次嘗試。";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[萊布勒姆]";
					mes "再見，哥們！";
					close;
				}
			}
			else
			{
				mes "[萊布勒姆]";
				mes "你愚蠢還是什麼？您沒有帶所有必需的物品！";
				mes "快速帶他們！";
				close;
			}
		case 2:
			mes "[萊布勒姆]";
			mes "下次見。";
			close;
	}
}
