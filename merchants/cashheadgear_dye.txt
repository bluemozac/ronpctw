//===== rAthena Script ======================================= 
//= Headgear Dyer (Cash)
//===== By: ==================================================
//= Xantara
//= Maud_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Change headgear's color. Cost is a Mysterious Dyestuff
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//============================================================

prt_in,253,168,4	script	Alora	862,{

// Mysterious Dyestuff
	set .@dyeID, 6220;
// Headgear Choices
	setarray .@hgn$[1], "魔法師帽","毛線帽","垂耳貓","惡魔侍者帽","假髮","緞帶","雄偉山羊";
// Mage Hats - Normal, Yellow, Blue, Brown, Gray, Red
	setarray .@hg_1[1], 5027, 5242, 5241, 5240, 5239, 5238;
// Beanies - Normal, Pink, Blue, Brown
	setarray .@hg_2[1], 5076, 5237, 5236, 5235;
// Drooping Cats - Normal, Blue, Brown, Gray, Pink, Yellow
	setarray .@hg_3[1], 5058, 5233, 5231, 5230, 5232, 5234;
// Deviruchi Hats - Normal, Red, Gray, Brown
	setarray .@hg_4[1], 5038, 5227, 5228, 5229;
// Wigs - Happy, Shiny, Marvelous, Fantastic
	setarray .@hg_5[1], 5273, 5274, 5275, 5276;
// Ribbons - Normal, Black, Yellow, Green, Pink, Red, Orange, White
	setarray .@hg_6[1], 2208, 5191, 5192, 5193, 5194, 5195, 5196, 5197;
// Magestic Goats - Normal, Evolved
	setarray .@hg_7[1], 2256, 5217;

	mes "[然後]";
	mes "您好，如果您帶給我神秘的染料，我可以更改頭飾的顏色。";
	next;
	mes "[然後]";
	mes "您有要染的頭飾嗎？";
	next;
	for(set .@i,1; .@i < getarraysize(.@hgn$); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@hgn$[.@i];
		else            set .@menu$, .@menu$ +":"+ .@hgn$[.@i];
	}
	set .@menu$, .@menu$ +":取消";
	set .@t, select(.@menu$);
	if(.@t == getarraysize(.@hgn$)) {
		mes "[然後]";
		mes "有一個很好的旅程冒險家！";
		mes "如果您曾經想過在Kafra頭飾上嘗試一種新顏色，或者您在冒險中發現的顏色，請來找我！";
		close;
	}
	mes "[然後]";
	mes "好的，您想將其更改為什麼顏色？";
	next;
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1 && .@t != 5)
			set .@menu$, "Normal";
		else if(.@i == 1 && .@t == 5)
			set .@menu$, getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else {
			sscanf(getitemname(getd(".@hg_"+.@t+"["+.@i+"]")),"%s "+ .@hgn$[.@t],.@colour$);
			set .@menu$, .@menu$ +":"+ .@colour$;
		}
	}
	set .@menu$, .@menu$ +":取消";
	set .@c, select(.@menu$);
	if(.@c == getarraysize(getd(".@hg_"+.@t+""))) {
		mes "[然後]";
		mes "哦，好的，沒問題！";
		close;
	}
	set .@dyHG, getd(".@hg_"+.@t+"["+.@c+"]");
	mes "[然後]";
	mes "哦，我很興奮，不是嗎？";
	mes "只是為了確保，什麼顏色是"+ .@hgn$[.@t] +" 你想要我使用?";
	next;
	mes "[然後]";
	mes "請了解我將使用 ^ff00001st"+ .@hgn$[.@t] +" 該顏色在你的庫存中!^000000";
	next;
	mes "[然後]";
	mes "任何升級和卡都將被神秘的染料解散，因此請確保您可以擁有 ^0000ff+0"+ getitemname(.@dyHG) +" 沒有任何卡片.^000000";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "好，謝謝你的警告，我想我會給你我的";
	next;
	set .@menu$, "沒關係";
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@menu$ +":Normal "+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else            set .@menu$, .@menu$ +":"+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
	}
	set .@mine, select(.@menu$) - 1;
	if(!.@mine) {
		mes "[然後]";
		mes "最好是非常確定，有一個安全的旅程。";
		close;
	}
	set .@myHG, getd(".@hg_"+.@t+"["+.@mine+"]");
	if(.@dyHG == .@myHG) {
		mes "[然後]";
		mes "哇，發生了什麼事？";
		close;
	}
	if(!countitem(.@dyeID) || !countitem(.@myHG)) {
		mes "[然後]";
		mes "哦，天哪，你似乎缺少一些東西。";
		close;
	}
	mes "[然後]";
	mes "看起來很棒嗎？";
	mes "希望您能回到染上更漂亮的頭飾！";
	delitem .@dyeID,1; // Dyestuff
	delitem .@myHG,1;  // Your headgear
	getitem .@dyHG,1;  // Dyed headgear
	close;
}
