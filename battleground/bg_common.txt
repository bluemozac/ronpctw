//===== rAthena Script ======================================= 
//= BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features and created a reward function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Croix王子]";
	mes "明智的冒險家，你為什麼不借給我們勝利的力量？";
	next;
	switch(select("這場戰役的原因是什麼？ :告訴我關於紀堯姆將軍的事")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Croix王子]";
		mes "Maroll的偉大國王Marlel Marollo VII最近病得很重。";
		mes "Ma下宣布，他將將Maroll的未來留給我或第三王子Guillaume將軍。";
		next;
		mes "[Croix王子]";
		mes "Guillaume將軍在這場戰鬥中可能有優勢，因為他是Maroll的偉大將軍，但這並不意味著他會獲勝。";
		mes "我想贏得這場戰鬥，以便我可以為ro徒帶來繁榮。他們遭受了戰爭的痛苦...";
		next;
		switch(select("是的，我想加入你們。 :結束對話")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Croix王子]";
			mes "太感謝了。我覺得我可以在像您這樣的冒險家的幫助下獲勝。";
			mes "現在，請下樓，加入您的同志，以提高他們與敵人作鬥爭的技能！";
			break;
		case 2:
			mes "[Croix王子]";
			mes "天氣！";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Croix王子]";
		mes "第三王吉拉姆（Guillaume）是瑪洛爾（Maroll）的偉大將軍。";
		mes "向您解釋他的領導人或軍閥是浪費時間，因為他指揮了巨大的海軍軍事力量。";
		next;
		mes "[Croix王子]";
		mes "不幸的是，他和他的追隨者沒有意識到：";
		mes "海洋人民真的希望他們在軍事力量上花費這麼多錢嗎？";
		mes "我們遭受了戰爭的痛苦。";
		mes "我認為武器不是將繁榮帶入一個國家的最佳方法。";
		next;
		mes "[Croix王子]";
		mes "我不想流血，但我別無選擇，只能為和平和為了我的人民而戰。";
		next;
		switch(select("是的，我想加入你們。 :結束對話")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Croix王子]";
			mes "太感謝了。我覺得我可以在像您這樣的冒險家的幫助下獲勝。";
			mes "現在，請下樓，加入您的同志，以提高他們與敵人作鬥爭的技能！";
			break;
		case 2:
			mes "[Croix王子]";
			mes "天氣！";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[Guillaume將軍]";
	mes "熱血冒險家，我們需要您贏得這場戰鬥的能力。";
	next;
	switch(select("這場戰鬥的原因是什麼？ :告訴我關於克羅伊王子的事")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[Guillaume將軍]";
		mes "我們的偉大國王Marcel Marollo VII最近病得很重。";
		mes "Ma下宣布，他選擇了我或Croix王子作為他的9個兒子中的下一個國王。";
		next;
		mes "[Guillaume將軍]";
		mes "兩個國王不能分享一個國家！他je下指定的戰鬥中只有一場胜利才能登上。";
		next;
		mes "[Guillaume將軍]";
		mes "但是，這不僅是我們之間的戰鬥。這場戰鬥將決定這個國家的未來。";
		mes "我保證我的榮幸證明我是可以保護這種母馬免受外部威脅的人。";
		next;
		switch(select("是的，我想加入你們。 :結束對話")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[Guillaume將軍]";
			mes "歡迎來到我的軍隊，同志。";
			mes "你的眼睛告訴我你是我可以信任的士兵。";
			next;
			mes "[Guillaume將軍]";
			mes "現在，上樓，與同志申請戰鬥。";
			mes "我確定他們會全心全意歡迎您！";
			break;
		case 2:
			mes "[Guillaume將軍]";
			mes "我將成為捕獲國旗的人！";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[Guillaume將軍]";
		mes "第五王子Croix目前的標題為Maroll總理。";
		mes "他認為，一個國家的所有國家事務都可以在桌子上討論和確定，";
		mes "並相信與其他國家的和平共存。";
		next;
		mes "[Guillaume將軍]";
		mes "他太無知了，無法承認所謂的和平是建立在戰爭中犧牲的無數生命之上的，而正常的公民和上層階級可以活下去，這不見允許他們這樣生活的恐怖。";
		next;
		mes "[Guillaume將軍]";
		mes "他太天真了，無法理解現實。";
		mes "我不能把天鼠留給像他這樣生活在夢中的人！";
		next;
		mes "[Guillaume將軍]";
		mes "他不切實際的信念將使這個國家陷入貧困，並使人民虛弱。如果他成為國王，那麼馬羅將永遠不會從其他國家的猛擊中休息。";
		mes "我想教他這個小國如此強大和繁榮的原因。這是軍事力量！";
		next;
		switch(select("我想加入你們的軍隊！ :結束對話")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[Guillaume將軍]";
			mes "歡迎來到我的軍隊，同志。";
			mes "你的眼睛告訴我你是我可以信任的士兵。";
			next;
			mes "[Guillaume將軍]";
			mes "現在，上樓，申請您的同志們的戰鬥。";
			mes "我確定他們會全心全意歡迎您！";
			break;
		case 2:
			mes "[Guillaume將軍]";
			mes "我將成為捕獲國旗的人！";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[傳送器]";
	mes "您想離開戰場嗎？使用我的服務返回城鎮。";
	next;
	switch(select("離開:不離開")) {
	case 1:
		mes "[傳送器]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morocc Ruins.","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		case 8:
			setarray .@mapname$[0],"Morocc.","morocc";
			setarray .@xy[0],156,46;
			break;
		}
		mes "您將被送回"+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[傳送器]";
		mes "每當您需要我的服務時，我都會在這裡。";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[馬羅爾戰鬥招募員]";
	mes "美好的一天，冒險家。";
	mes "我是一個來自一個遙遠國家的騎士，叫做Maroll王國。";
	next;
	mes "[馬羅爾戰鬥招募員]";
	mes "王國的兩位王子現在正在爭奪海洋寶座，需要像您這樣的經驗豐富的士兵。";
	mes "您想如何將自己的力量借給海洋王國的一位王子？";
	next;
	switch(select("加入:不加入")) {
	case 1:
		mes "[馬羅爾戰鬥招募員]";
		mes "願戰爭上帝保佑你。";
		close2;
		getmapxy(.@mapname$,.@x,.@y,BL_NPC);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else if (.@mapname$ == "morocc")
			set bat_return,8;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[馬羅爾戰鬥招募員]";
		mes "我將永遠駐紮在這裡尋找更多的士兵。只要您有興趣，就可以隨時回來。";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "該命令已取消。";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "我可以幫你嗎？";
		next;
		switch(select("關閉戰場:打開戰場:重置 a01:重置 b01:重置 a02:重置 b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "完全的";
		close;
	}
}

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- 等一下 ！ ！ -";
		mes "- 目前您正在攜帶 -";
		mes "- 與您在一起太多。 -";
		mes "- 請重試 -";
		mes "- 減肥後。 -";
		close;
	}
	mes "[第十輪]";
	mes "你有戰場徽章嗎？";
	mes "我可以將勇敢的徽章和英勇徽章換成獎勵物品。";
	next;
	switch(select("交換徽章:查看目錄")) {
	case 1:
		mes "[第十輪]";
		mes "您想交換哪種類型的物品？";
		mes "要檢查有關獎勵項目的更多信息，請使用我們的 ^3131ffcatalog ^000000。";
		next;
		switch(select("武器:防具:配件:消耗品")) {
		case 1:
			mes "[第十輪]";
			mes "您選擇 ^3131FFWEAPON ^000000。";
			mes "以下武器可與戰場徽章交換。";
			mes "請注意， ^3131ffbraver徽章的項目表示為（bb） ^000000， ^3131ffvalor徽章為（vb） ^000000。";
			next;
			switch(select("匕首/單劍/雙劍/雙矛:法杖/狼牙棒/雙斧/手裡劍:弓/拳頭/音樂/鞭子:書/指節:左輪手槍/步槍/加特林/霰彈槍/發射器")) {
			case 1:
				mes "[第十輪]";
				mes "以下項目可在 ^3131ffdagger，單手劍，雙手劍和雙手長矛 ^000000類別中找到。";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[第十輪]";
				mes "以下項目可在 ^3131FFSTAFF / MACE /雙手AX / Huuma Shuriken ^000000類別中找到。";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[第十輪]";
				mes "以下武器可在 ^3131ffbow / katar / katar /樂器 /鞭子 ^000000類別中獲得。";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[第十輪]";
				mes "以下武器可在 ^3131FFBOOK / KNUCKLE ^000000類別中獲得。";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[第十輪]";
				mes "以下武器可在 ^3131ffrevolver /步槍 /蓋特林槍 / shot彈槍 /手榴彈發射器 ^000000類別中獲得。";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[第十輪]";
			mes "您選擇了 ^3131ff"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
			mes "您可以與 ^ff0000100交換此商品"+getitemname(.@Weapons[.@i+1])+"^000000.";
			mes "您想交換嗎？";
			next;
			switch(select("不交換:交換")) {
			case 1:
				break;
			case 2:
				mes "[第十輪]";
				mes "您想花 ^ff0000100嗎"+getitemname(.@Weapons[.@i+1])+"^000000 and receive a ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next;
				mes "[第十輪]";
				mes "請記住，戰場獎勵項目是 ^ff0000Character綁定 ^000000。您確定要這個項目嗎？";
				next;
				switch(select("是:否")) {
				case 1:
					mes "[第十輪]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "謝謝您的交流。";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "抱歉，您沒有足夠的徽章可供兌換。";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[第十輪]";
			mes "您需要更多時間檢查項目嗎？";
			close;
		case 2:
			mes "[第十輪]";
			mes "您選擇 ^3131ffarmor ^000000。";
			mes "以下裝甲可與戰場徽章交換。";
			next;
			switch(select("服裝/鞋子:盔甲")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[第十輪]";
			mes "您選擇 ^3131ffacCessory ^000000。";
			mes "您可以根據工作課程將榮譽勳章與徽章交換，如下所示：";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[第十輪]";
			mes "您選擇了 ^3131ffconsumable ^000000。";
			mes "以下可消耗品可與戰場徽章交換：";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[第十輪]";
		mes "我們有很多物品，所以請刻意查看併購買。";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[第十輪]";
	mes "您選擇了 ^3131ff"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "此項目僅適用於劍客和跆拳道大師班。"; break;
		case 2721: mes "該物品僅適用於盜賊等級。"; break;
		case 2722: mes "該物品僅適用於侍僧等級。"; break;
		case 2723: mes "此物品僅適用於魔術師級別。"; break;
		case 2724: mes "該物品僅適用於弓箭手級別。"; break;
		case 2725: mes "該商品僅適用於商戶類別。"; break;
		case 2733: mes "該物品僅適用於槍手。"; break;
		default: break;
	}
	mes "您可以與 ^ff0000交換此商品"+.@Items[.@i+1]+" "+getitemname(7828)+" or "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "您想交換嗎？";
	next;
	switch(select("不交換:交換")) {
	case 1:
		mes "[第十輪]";
		mes "您需要更多時間檢查項目嗎？";
		break;
	case 2:
		mes "[第十輪]";
		mes "您想交換哪個徽章？";
		mes "您需要 ^3131ff"+.@Items[.@i+1]+" Badges^000000 to exchange.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[第十輪]";
			mes "請記住，戰場獎勵項目是 ^ff0000Character綁定 ^000000。您確定要這個項目嗎？";
			next;
		}
		set .@j, select("勇敢徽章:勇敢徽章:取消");
		mes "[第十輪]";
		if (.@j == 3) {
			mes "您取消了交換。";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "謝謝您的交流。";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "你還不夠"+getitemname(.@cost)+"s.";
		break;
	}
	close;
}

// Badge Reward Function
//============================================================
function	script	F_BG_Badge	{
	.@team$ = getarg(1);
	.@bg_type$ = getarg(2);

	if (.@bg_type$ == "Tierra") {
		set .@badge,7828; //BF_Badge1
		set .@amount_win,3;
		set .@amount_lose,1;
	} else if (.@bg_type$ == "Flavius") {
		set .@badge,7829; //BF_Badge2
		set .@amount_win,9;
		set .@amount_lose,3;
	} else
		return;

	if (getarg(0) == 1) {
		set .@amount, .@amount_win;
		if (.@team$ == "Guillaume") {
			mes "[埃克索·羅斯]";
			mes "祝福的紀念品！";
		} else if (.@team$ == "Croix") {
			mes "[揮霍]";
			mes "祝福的克魯瓦！";
		}
		mes "讓我們享受我們光榮的勝利！";
		mes strcharinfo(0)+"，這是反映勝利的標誌。";
	} else {
		set .@amount, .@amount_lose;
		if (.@team$ == "Guillaume") {
			mes "[埃克索·羅斯]";
			mes "您迷路了，但您致力於這場戰鬥。";
			mes "這是對您的Guillaume Marollo偉大奉獻精神的獎勵！";
			mes "只要將這次失敗作為課程，下次您一定會贏。";
		} else if (.@team$ == "Croix") {
			mes "[揮霍]";
			mes "哦，"+strcharinfo(0)+" Don't be sad.";
			mes "即使我們沒有獲勝，我們也盡了最大的努力。";
			mes "這是Croix的皇家禮物，請不要忘記這場戰鬥。我們將贏得下一個。";
		}
	}
	close2;

	// If the VIP system is enabled, VIP players receive an extra 2 badges.
	if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
		set .@amount, .@amount+2;

	set .@medal_gap, 500 - countitem(.@badge);
	if (.@medal_gap >= .@amount)
		getitem .@badge,.@amount;
	else
		getitem .@badge,.@medal_gap;
	return;
}
