//===== rAthena Script =======================================
//= Alberta Town
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Town-specific Alberta NPCs
//===== Additional Comments: =================================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Bug fixes and syntax. [Capuche]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[法邊]";
	mes "伙計...當您在世界各地旅行時，您會聽到一些瘋狂的事情。";
	next;
	mes "[法邊]";
	mes "有一次，我聽說有包含怪物力量的卡片。如果有人碰巧可以使用卡片，他們將能夠使用該怪物的力量。";
	next;
	mes "[法邊]";
	mes "我猜這是某種時尚或騙局，它們會讓您收集所有卡片或其他。我的意思是，卡片如何真正擁有怪物的力量？";
	next;
	mes "[法邊]";
	mes "嚴重地...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[斯坦納]";
	mes "哦...！";
	mes "歡迎來到艾伯塔省，";
	mes "年輕的冒險家！";
	next;
	mes "[斯坦納]";
	mes "如果我似乎分心，請原諒我。我正在努力製定計劃。您會看到，我聽說Geffen的一家商店出售對魔術具有抵抗力的裝甲。";
	next;
	mes "[斯坦納]";
	mes "如果我批量購買很多，然後以更高的價格將它們轉售在這裡...";
	close;
}

alberta_in,20,183,0	script	Chad	1_M_03,{
	mes "[查德]";
	mes "人們說，傳奇的武器Gungnir永遠不會錯過其目標。我想知道這是否可能是真的...";
	next;
	mes "[查德]";
	mes "人們還說，嬰兒在分娩前被鸛組裝而來，女孩的行為像混蛋一樣，而聖誕老人則存在！但只有在路易（Lutie）。";
	next;
	mes "[查德]";
	mes "我想知道...";
	mes "如果有的話";
	mes "可能是";
	mes "真的...";
	close;
}

alberta,131,139,2	script	Drunken Old Man	1_M_JOBGUIDER,{
	mes "[迪格爾]";
	mes "^666666*打嗝*^000000";
	mes "你是什麼";
	mes "盯著？迷路！ ！";
	next;
	switch(select("什麼都不說。","別管他。")) {
	case 1:
		mes "[迪格爾]";
		mes "Hahahaha ^666666*hiccup* ^000000 ...您有些神經。我現在看起來一文不值，但是我曾經是“瑪麗”的水手。";
		next;
		switch(select("從未聽說過。","真的嗎？沒開玩笑！")) {
		case 1:
			mes "[Deagle]";
			mes "從來沒有聽說過嗎？每個人都知道這艘海盜船“去瑪麗！” ^666666*hiccup~*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~ol'days。如果只是...如果我們沒有遇到那場風暴...^666666*hiccup*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~上尉。我想念我們的帽子比什麼都重要...在Cap'n's劍前沒有敵人倖存下來。";
			close;
		case 2:
			mes "[迪格爾]";
			mes "這是正確的！沒有人與“瑪麗·瑪麗”的船員融為一體！而且沒有人能在劍戰中擊敗Cap'n！";
			next;
			mes "[迪格爾]";
			mes "隊長~!!! ^666666*hiccup~*^000000他會像這樣擺動劍，然後...";
			next;
			mes "[迪格爾]";
			mes "船長正在戰鬥的混蛋，他附近的任何朋友都被火焰包圍！";
			next;
			mes "[迪格爾]";
			mes "伙計，那把劍一定有某種神秘的力量，或者船長真是太好了……！";
			next;
			mes "[迪格爾]";
			mes "phew ~~ ^666666*sob**sob ...* ^000000上帝，我想念大家！現在我很沮喪！拜託，現在走開。";
			close;
		}
	case 2:
		mes "[迪格爾]";
		mes "這是正確的！";
		mes "走開~";
		close;
	}
}

alberta,58,80,2	script	Shakir	99,{
	mes "[沙基爾]";
	if (rand(2)) {
		mes "當我們出售商品時，我們的商人有自己的談判技巧。這項技能比其他人出售商品時可以獲得更多的錢。";
		next;
		mes "[沙基爾]";
		mes "這不僅僅是大喊“您必須給更多的錢！”您需要具有魅力和大師修辭！";
		next;
		mes "[沙基爾]";
		mes "通過這種令人難以置信的技能，我們最多可以增加24％的Zeny。但是記住要努力訓練以獲取它！";
	} else {
		mes "我們的商人可以";
		mes "開放的路邊攤";
		mes "做生意。";
		next;
		mes "[沙基爾]";
		mes "有了折扣技巧，我們可以從城鎮的商店購買真正便宜的商品，並將其裝入我們租用的購物車中。";
		next;
		mes "[沙基爾]";
		mes "然後，我們可以在任何地方旅行，並出售我們的商品以獲利！";
		next;
		mes "[沙基爾]";
		mes "這樣，業務更加方便和安全。不要入睡，儘管這樣做太容易了。";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[索尼婭]";
	switch(rand(3)){
	case 0:
		mes "嘿，你知道，這一次我走過森林，我看到這個小綠色的莖在四處走動。";
		next;
		mes "[索尼婭]";
		mes "我去看看那是什麼，什麼時候去觸摸它。莖實際上拍了我的手！";
		next;
		mes "[索尼婭]";
		mes "這讓我震驚，所以我跳了一點，然後我意識到那不是莖，而是一個很小的動物。";
		next;
		mes "[索尼婭]";
		mes "我很幸運，我沒有讓它不高興。如果生氣，即使是最小的動物也可能是危險的。";
		close;
	case 1:
		mes "您知道那些懶惰的熊在去佩恩的路上生活在森林中嗎？";
		next;
		mes "[索尼婭]";
		mes "只是為了好玩，我向它扔了一塊岩石，突然它沖向我！我很害怕，我開始逃跑，然後bam ！ ！ ！";
		next;
		mes "[索尼婭]";
		mes "它碰到了一個低矮的樹枝，將自己撞倒了！我發誓，我再也不會為動物而興趣！";
		close;
	case 2:
		mes "我曾經看到一包狼在扮演那些巨大的懶惰熊之一！";
		next;
		mes "[索尼婭]";
		mes "狼比看起來更合作。如果其中一個受到攻擊，那麼附近的任何狼都會奔跑以提供幫助。";
		next;
		mes "[索尼婭]";
		mes "如果您想在同類中的其他人在周圍時，我會三思而後行。小心：不要被幫忙！";
		close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[祖母阿爾瑪]";
	mes "不久前，";
	mes "一艘廢棄的船";
	mes "漸漸進入";
	mes "艾伯塔省港。";
	next;
	mes "[祖母阿爾瑪]";
	mes "希望能節省任何倖存者，一些城鎮居民冒險進入船上。但是，他們所有人都毫不害怕，說屍體在船上走來走去。";
	next;
	mes "[祖母阿爾瑪]";
	mes "這艘船還充滿了危險的海洋生物，即使他們願意，它們也無法進入。";
	next;
	mes "[祖母阿爾瑪]";
	mes "我們對那隻不祥的船無能為力，只是把它留下來。如今，探索團隊試圖進入該船並消滅其怪物。";
	next;
	mes "[祖母阿爾瑪]";
	mes "因此，像您這樣的年輕人成為新兵可能是一次很好的經歷。但是，如果您不夠強大，仍然不值得冒著生命的風險。";
	close;
}

alberta,189,151,5	script	Fisk	4W_SAILOR,{
	mes "[菲斯克]";
	mes "嘿伙計，";
	mes "你在哪裡";
	mes "想去嗎？";
	next;
	switch(select("沉船 -> 250 zeny.","伊斯拉德碼頭 -> 500 zeny.","算了。")) {
	case 1:
		if (Zeny < 250) {
			mes "[菲斯克]";
			mes "嘿，現在，不要試圖欺騙我！我說250 Zeny！";
			close;
		}
		Zeny = Zeny - 250;
		warp "alb2trea",43,53;
		end;
	case 2:
		if (Zeny < 500) {
			mes "[菲斯克]";
			mes "沒有500 Zeny首先，您絕對不會到達那裡！";
			close;
		}
		Zeny = Zeny - 500;
		if (checkre(0))
			warp "izlude",195,212;
		else
			warp "izlude",176,182;
		end;
	case 3:
		mes "[菲斯克]";
		mes "好吧...";
		mes "陸地。";
		close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	4W_SAILOR,{
	mes "[菲斯克]";
	mes "所以你想回到艾伯塔省的大陸，是嗎？";
	next;
	if (select("是的，請。","我改變主意了。") == 1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	4_M_04,{
	mes "[保羅]";
	mes "美好的一天~";
	mes "你願意";
	mes "加入";
	mes "勘探團隊";
	mes "沉沒的船？";
	next;
	mes "[保羅]";
	mes "哦！在您加入之前，我必須警告您。如果您不那麼強壯，那麼您可能不想去。";
	next;
	mes "[保羅]";
	mes "所以，想要";
	mes "註冊？";
	mes "入學";
	mes "費用只是";
	mes "200名女性。";
	next;
	switch(select("幫我報名！","呃，不用了，謝謝。")) {
	case 1:
		if (Zeny < 200) {
			mes "[保羅]";
			mes "看來你沒有錢，我的朋友。但是，當您能夠付款時，請回來。";
			close;
		}
		Zeny = Zeny - 200;
		warp "alb2trea",62,69;
		close;
	case 2:
		mes "[保羅]";
		mes "好吧，好吧...";
		mes "我會在附近";
		mes "如果您更改";
		mes "你的想法。";
		close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	mes "[菲利克斯]";
	if ((MaxWeight - Weight) < 10000) {
		mes "稍等！ ！";
		mes "您帶來了太多的東西！";
		mes "您不能再接受任何項目！";
		mes "請減少物品的數量，";
		mes "然後來看看我。";
		close;
	}
	if (@event_zelopy == 0) {
		mes "你到底在這裡嗎？";
		mes "如果您想要的話，您可以在這艘船上免費獲得任何免費的東西！";
		next;
		mes "[菲利克斯]";
		mes "嗯，那你為什麼不帶我10果凍，我會給我1個藥水。那聲音怎麼樣？";
		mes "或者，如果您的三色屁股太難了，則有3個果凍，可用於1個胡蘿蔔。";
		next;
		mes "[菲利克斯]";
		mes "如果您對我的報價感興趣，請給我我提到的東西。";
		@event_zelopy = 1;
		close;
	}
	mes "嗯..您想將果凍換成紅色藥水或一些胡蘿蔔嗎？好..哪一個？";
	next;
	switch(select("請給我紅色藥水。","請給我胡蘿蔔。")) {
	case 1:
		mes "[菲利克斯]";
		mes "好吧...";
		mes "讓我們來看看";
		mes "什麼是...";
		next;
		mes "[菲利克斯]";
		if (countitem(909) < 10) {
			mes "嘿！你不聽嗎？我說10個紅水藥的果凍。你是聾子嗎？";
			close;
		} else {
			.@max = countitem(909)/10;
			mes "嗯，還不錯...";
			mes "多少藥水";
			mes "你想得到嗎？";
			next;
			switch(select("請給我盡可能多的。","我想要這麼多。","算了，我喜歡我的果凍。")) {
			case 1:
				delitem 909,.@max*10;// Jellopy
				getitem 501,.@max;// Red_Potion
				break;
			case 2:
				mes "[菲利克斯]";
				mes "我一次沒有給您100多個以上，所以不要打擾，好嗎？如果您不想要任何東西，只需說“ 0”。";
				mes "現在，您能得到的最多是" + .@max + " but remember, 100 at most, you want to break my back?.";
				input .@amount;
				next;
				mes "[菲利克斯]";
				if (.@amount <= 0) {
					mes "很多義務，隨時再次來。";
					close;
				}
				if (.@amount > 100) {
					mes "嘿，我說什麼？最多有100個，您想殺了我不是嗎！";
					close;
				}
				if (countitem(909) < .@amount*10) {
					mes "嗯，看來您沒有足夠的時間。如果您想要我的其他任何東西，去獲取更多果凍。";
					close;
				}
				delitem 909,.@amount*10;// Jellopy
				getitem 501,.@amount;// Red_Potion
				break;
			case 3:
				mes "[菲利克斯]";
				mes "沒問題，";
				mes "下次見。";
				close;
			}
			mes "[菲利克斯]";
			mes "你去！正如我所承諾的。不要一次偷走它們。";
			close;
		}
	case 2:
		mes "[菲利克斯]";
		mes "好吧，讓我們看看你有什麼...";
		next;
		mes "[菲利克斯]";
		if (countitem(909) < 3) {
			mes "嗯，看褲子的屁股，我說3個果凍的1胡蘿蔔..得到了嗎？";
			close;
		}
		.@max = countitem(909)/3;
		mes "不糟糕的是Pansy ...";
		mes "你想要幾個？";
		next;
		switch(select("請給我盡可能多的","我想要這麼多。","算了，我喜歡我的果凍。")) {
		case 1:
			delitem 909,.@max*3;// Jellopy
			getitem 515,.@max;// Carrot
			break;
		case 2:
			mes "[菲利克斯]";
			mes "是的，我一次沒有給您100多個以上，所以不要打擾，好嗎？如果您不想要任何東西，只需說“ 0”。";
			input .@amount;
			next;
			mes "[菲利克斯]";
			if (.@amount == 0) {
				mes "好吧，下次見。";
				close;
			}
			if (.@amount > 100) {
				mes "嘿，Pansy的屁股，我最多只說100個，不超過這一點！我不會為您喜歡的人打破我的支持！";
				close;
			}
			if (countitem(909) < .@amount*3) {
				mes "看來您沒有足夠的時間。如果您想要其他任何東西，請再獲得更多。";
				close;
			}
			delitem 909,.@amount*3;// Jellopy
			getitem 515,.@amount;// Carrot
			break;
		case 3:
			mes "[菲利克斯]";
			mes "後來catch'ya。";
			close;
		}
		mes "[菲利克斯]";
		mes "你去~！正如我所承諾的。盡量不要塞滿臉。";
		close;
	}
}
