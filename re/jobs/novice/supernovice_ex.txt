//===== rAthena Script ======================================= 
//= Expanded Super Novice Job Quest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change quest for Expanded Super Novice class.
//===== Additional Comments: ================================= 
//= 1.0 All dialogue is custom, contains some incorrect behaviors.
//= 1.1 Switched to a translated copy of the official script
//=     by Gennosuke Kouga, but vastly edited its content. [Euphy]
//= 1.1a Replaced effect numerics with constants. [Euphy]
//============================================================ 

function	script	Esseray_Ex	{
	if (BaseLevel > 98 && JobLevel > 98) {
		if (EXSUPNOV_Q > 0 && EXSUPNOV_Q < 6) {
			mes "[散文]";
			mes "新手世界中的傳奇已經消失了，可以在科莫多做某事。";
			mes "你應該找到他。";
			next;
			mes "[散文]";
			mes "他可以指導你";
			mes "正確的路徑...";
			close;
		}
		mes "[散文]";
		mes "哦？您是我們新手社會之一嗎？";
		mes "你看起來不錯。";
		next;
		mes "[散文]";
		mes "你看起來充滿活力...";
		mes "您似乎足夠強大以忍受測試。";
		next;
		mes "[散文]";
		mes "我知道一種變得更強壯的方法。您願意嘗試嗎？";
		next;
		if(select("不，我現在很好:我想變得更強！") == 1) {
			mes "[散文]";
			mes "如果您對平庸感到滿意，我將無能為力。";
			mes "那時過著正常的生活。";
			close;
		}
		mes "[散文]";
		mes "好的！";
		mes "新手世界中的傳奇已經消失了，可以在科莫多做某事。";
		mes "你應該找到他。";
		next;
		setquest 5092;
		set EXSUPNOV_Q,1;
		mes "[散文]";
		mes "他可以指導你";
		mes "正確的路徑...";
		close;
	}
	else return;
}

beach_dun2,119,92,4	script	Fishing Novice#sp2	567,{
	if (checkquest(5092) >= 0) {
		mes "[釣魚新手]";
		mes "噓--------------！";
		mes "請安靜。";
		mes "你會嚇到魚。";
		next;
		select("嗯……抱歉……");
		mes "[釣魚新手]";
		mes "啊啊啊啊！";
		mes "你在幹什麼？";
		mes "我告訴你安靜！";
		mes "他們正在游泳噪音~";
		next;
		mes "[釣魚新手]";
		mes "（ -  sh--創作 - ）";
		mes "誰...你是？";
		mes "你為什麼要破壞我的釣魚？";
		next;
		select("我收到了來自埃瑟雷的快遞...");
		emotion ET_SCRATCH;
		mes "[釣魚新手]";
		mes "什麼？ Esseray？ Esseray？";
		mes "唔！我認識他...";
		mes "Esseray ... Esseray ...嗯。";
		next;
		emotion ET_THINK, playerattached();
		emotion ET_THINK;
		mes "................................................";
		next;
		emotion ET_SURPRISE;
		mes "[釣魚新手]";
		mes "啊-----！ ------啊-------！";
		mes "知道了！ - - 知道了！";
		mes "^0000FFA Aldebaran新手學會成員。 ^000000";
		mes "Esseray，對嗎？錯誤的？";
		next;
		if(select("不，他不是:是的，對") == 2) {
			mes "[釣魚新手]";
			mes "hu hu~我的感覺還活著...";
			next;
			mes "[釣魚新手]";
			mes "那你想從我那裡得到什麼？";
		}
		else {
			mes "[釣魚新手]";
			mes "什麼？ ！他不是嗎？";
			mes "那Esseray來自哪裡？";
			next;
			emotion ET_THINK, playerattached();
			emotion ET_THINK;
			mes "……………………………………";
			next;
			mes "[釣魚新手]";
			mes "什麼  -  -  -  -  ！ ！";
			mes "那並不重要，對嗎？";
			next;
			mes "[釣魚新手]";
			mes "然後  -  -  -  -  ！ ！";
			mes "你為什麼來這裡？";
		}
		next;
		mes "- 您解釋了為什麼在這裡。 -";
		next;
		emotion ET_SCRATCH;
		mes "[釣魚新手]";
		mes "嗯...這是一個輕鬆而棘手的問題。";
		next;
		mes "- 您會聽到一個聲音抱怨某事。 -";
		next;
		emotion ET_FRET;
		mes "[釣魚新手]";
		mes "esseray！這樣的兒子是％@＆$＃破壞我的假期...";
		next;
		mes "[釣魚新手]";
		mes "哈哈哈~";
		mes "我的糟糕生活~";
		mes "啊~這麼多問題。";
		next;
		emotion ET_QUESTION;
		mes "[釣魚新手]";
		mes "太難了。嘿，小孩子！";
		mes "你叫什麼名字？";
		next;
		select(strcharinfo(0)+", how about you?");
		mes "[罕見]";
		mes "我叫Nodor！ Nodor。";
		next;
		mes "[罕見]";
		mes "…………………………";
		next;
		mes "[罕見]";
		mes "嘿！我可以看到你在臉上在想什麼~";
		next;
		mes "[罕見]";
		mes "由於我的名字叫Nodor，我一生都在取笑。";
		mes "禿鷹和氣味...";
		mes "不要試圖欺騙我。";
		next;
		changequest 5092,5093;
		mes "[罕見]";
		mes "不管是什麼原因...您在這裡！";
		mes "讓我們聚在一起走得更遠。";
		mes "一會兒再來~";
		next;
		mes "- 再次與他交談。 -";
		close;
	}
	else if (checkquest(5093) >= 0) {
		mes "[罕見]";
		mes "嗯嗯------！";
		next;
		mes "-  Nodor給您帶來可疑的外觀。 -";
		next;
		mes "[罕見]";
		mes "您不知道 ^0000ffsuper新手 ^000000很棒嗎？";
		next;
		mes "-  Nodor充滿信心地問。 -";
		next;
		mes "[罕見]";
		mes "嘿！超級新手~";
		mes "你知道我是誰嗎？";
		next;
		select("你在說什麼？ ！");
		mes "[罕見]";
		mes "嘿...";
		mes "你有很多神經，不是嗎？";
		next;
		select("…………");
		mes "- 這個傢伙是個怪人。 -";
		next;
		mes "[罕見]";
		mes "嘿！超級新手~";
		mes "我會讓你變得更強壯！";
		mes "那你相信我告訴你的嗎？";
		next;
		if(select("我不會跟著你:為什麼不呢？咳咳~") == 1) {
			mes "[罕見]";
			mes "拒絕？你不想堅強嗎？";
			mes "我也想做什麼也不做。";
			mes "如果你想去的話，讓我釣魚。";
			close;
		}
		mes "[罕見]";
		mes "emmhmm~我聽起來很刺耳嗎？";
		mes "嗯，我不禁用這樣的話。";
		next;
		mes "[罕見]";
		mes "不要流汗。";
		mes "我要教你變得更強壯是沒有問題的。";
		next;
		mes "[罕見]";
		mes "太好了~！";
		mes "您的問題是什麼？";
		mes "您不知道如何變得更強壯。正確的？";
		next;
		select("是的，我無法變得更強。");
		emotion ET_SCRATCH;
		mes "[罕見]";
		mes "嗯嗯~！";
		mes "有一種方法...";
		mes "這很危險，但我可以";
		mes "告訴你如何。";
		next;
		mes "[罕見]";
		mes "我只在一個條件下這樣做。您什麼都沒問，您會做我告訴您的一切！";
		mes "怎麼樣？";
		mes "您願意這樣做嗎？";
		next;
		if(select("不，我不會！ :讓我們這樣做吧！") == 1) {
			mes "[罕見]";
			mes "堅強是唯一的生活途徑！";
			mes "您想永遠成為一個弱者嗎？";
			next;
			mes "[罕見]";
			mes "我會給你一個小費。";
			mes "你不能總是正確的。";
			mes "有時您需要檢查自己的自我並聽別人。";
			emotion ET_FRET;
			close;
		}
		mes "[罕見]";
		mes "好的  -  -  -  - - ！ ！";
		mes "您的熱情會使您變得更強壯。";
		mes "你冷靜嗎？";
		next;
		mes "[罕見]";
		mes "我要你做的是...";
		mes "狩獵1,000個怪物。";
		next;
		mes "[罕見]";
		mes "你知道這是用來的嗎？";
		next;
		setarray .@Monsters$[0],"Poring","Lunatic","Fabre","Picky";
		set .@i, rand(4);
		changequest 5093,5094+.@i;
		mes "[罕見]";
		mes "你知道什麼 ^ff0000"+.@Monsters$[.@i]+"^000000 monster is, right?";
		next;
		mes "[罕見]";
		mes "狩獵1,000！";
		mes "不要說你不能這樣做！";
		mes "我知道那是很多怪物，但是它們是如此虛弱！";
		next;
		select("你在開玩笑吧？");
		mes "[罕見]";
		mes "呵呵~！";
		mes "Rembember，沒問題！";
		mes "直到您狩獵1,000才回來。";
		close;
	}
	else if (checkquest(5094) >= 0 || checkquest(5095) >= 0 || checkquest(5096) >= 0 || checkquest(5097) >= 0) {
		mes "["+strcharinfo(0)+"]";
		mes "嘿，Nodor！";
		next;
		emotion ET_HUK;
		mes "[罕見]";
		mes "我的天啊！";
		mes "如此害怕...讓我看看嗎？";
		next;
		if (checkquest(5094,HUNTING) != 2 && checkquest(5095,HUNTING) != 2 && checkquest(5096,HUNTING) != 2 && checkquest(5097,HUNTING) != 2) {
			mes "[罕見]";
			mes "嘿！超級新手！";
			mes "我不是對1,000人說亨特嗎？";
			mes "直到有才能回來。";
			close;
		}
		mes "[罕見]";
		mes "您已經狩獵了所有1000人嗎？";
		next;
		mes "[罕見]";
		mes "你比我想的要好。";
		mes "無論如何，我叫你打獵的怪物是什麼？";
		next;
		set .@i, select("波利:瘋子:法布爾:挑剔:巴風特")-1;
		if (.@i == 4) {
			mes "[罕見]";
			mes "呵呵？您能獨自認真狩獵Baphomet嗎？";
			mes "不要說謊！我會打你！";
			close;
		}
		if (checkquest(5094+.@i,HUNTING) != 2) {
			mes "[罕見]";
			mes "不要說謊！";
			mes "我沒有告訴你狩獵這個！";
			close;
		}
		changequest 5094+.@i,5098;
		emotion ET_SMILE;
		mes "[罕見]";
		mes "咳咳咳~";
		next;
		mes "[罕見]";
		mes "你讓我煩惱，所以我讓你做了這個任務...";
		next;
		mes "[罕見]";
		mes "哇！您實際上狩獵了全部1000人嗎？";
		mes "我想我不能質疑您變得更強大的熱情。";
		next;
		mes "[罕見]";
		mes "好的！到目前為止，一切都很好。";
		mes "我會讓您知道如何克服力量不足。";
		close;
	}
	else if (checkquest(5098) >= 0) {
		if (rand(10) != 1) {
			mes "[罕見]";
			mes "哦！你已經來到這裡了嗎？";
			mes "你不是說你有要處理的東西嗎？";
			mes "你有足夠的激情，但是...";
			mes "您需要學習耐心。";
			close;
		}
		emotion ET_KIK;
		mes "[罕見]";
		mes "哈哈哈哈~";
		next;
		mes "[罕見]";
		mes "哦，對不起！對不起！";
		mes "很久以前，我收到了哥哥的來信，您會讀我的來信以獲取您的信息嗎？";
		next;
		if(select("是的，我會:不，我不會") == 2) {
			mes "[罕見]";
			mes "您應該閱讀更多！";
			next;
		}
		else {
			mes "["+strcharinfo(0)+"]";
			mes "tsk*...好...";
			next;
		}
		mes "- 他向你扔了一封捆綁的信。 -";
		next;
		mes "親愛的兄弟。 Nodor，";
		mes " ";
		mes "嘿，兄弟，這是你的兄弟努多。你最近怎麼樣？我寫這封信的原因是...";
		mes "我需要錢來吃東西。你有很多錢，所以請幫助我。";
		next;
		mes "如果您不...記住上次您％$＆*@*＆％$ ...";
		mes "- 這封信皺巴巴，所以您無法閱讀更多。 -";
		mes "我仍在施瓦茨瓦爾德共和國。";
		mes "～努多爾";
		next;
		mes "- 通過閱讀這封信，您會感到不安。 -";
		next;
		mes "[罕見]";
		mes "呼~呼~";
		mes "嗯，我只有一個兄弟，他總是要錢。";
		mes "這一定是我的名聲... T_T";
		next;
		mes "- 現在您真的有一種不安的感覺。 -";
		next;
		changequest 5098,5099;
		mes "[罕見]";
		mes "嘿 - 超級新手"+strcharinfo(0)+"!";
		next;
		mes "[罕見]";
		mes "你可以給我的兄弟。他在飛艇上。";
		close;
	}
	else if (checkquest(5099) >= 0) {
		mes "[罕見]";
		mes "你已經去過了嗎？";
		next;
		select("我應該給多少錢？");
		mes "[罕見]";
		mes "哦，你還沒去那裡嗎？";
		mes "給他一點，是的";
		next;
		mes "[罕見]";
		mes "100,000 Zeny還可以~";
		mes "1,000,000 Zeny還可以~";
		mes "10,000,000 Zeny還可以~";
		close;
	}
	else if (checkquest(5100) >= 0) {
		if (EXSUPNOV_Q_ == 3) set .@n,300000;
		else if (EXSUPNOV_Q_ == 4) set .@n,1000000;
		else set .@n,100000;
		mes "[罕見]";
		mes "嘿，你去那裡了嗎？";
		mes "我從兄弟那裡收到了另一個消息。";
		next;
		mes "兄弟，有人給我...";
		mes callfunc("f_insertComma",.@n)+" Zeny.";
		mes "你確實在乎我...";
		mes "～努多爾";
		next;
		select("他看起來好餓……");
		mes "[罕見]";
		mes "順便一提...";
		mes callfunc("f_insertComma",.@n)+" Zeny is so little considering my celebrity status, don't you think?";
		next;
		select("呃……");
		mes "[罕見]";
		mes "無論如何~";
		mes callfunc("f_insertComma",.@n)+" Zeny...";
		mes "你不認為你給他太少了嗎？我的聲譽現在將受到打擊。";
		next;
		select("嘿...");
		mes "[罕見]";
		mes "哇，你有我的意思了嗎？";
		next;
		mes "[罕見]";
		mes "我認為你是對的。";
		mes "該測試是關於您打開別人同情的能力。";
		next;
		mes "[罕見]";
		mes "我的意思是您的消費習慣和腸子...";
		mes "哈哈哈！";
		next;
		select("嘿，這不是我的...");
		mes "[罕見]";
		mes "唔？啊...";
		mes "我幾乎忘了告訴";
		mes "您如何超出極限，";
		mes "不是嗎？";
		next;
		mes "[罕見]";
		mes "嗯！我寄給你後，我不僅在殺死時間！";
		next;
		emotion ET_HUK;
		mes "[罕見]";
		mes "什麼？ ---啊？ ----！";
		next;
		select("????");
		emotion ET_SWEAT;
		mes "[罕見]";
		mes "當心！ ！一堆魔像的人正在追隨我們！";
		mes "我們...必須跑... ！ ！ ！";
		next;
		emotion ET_HUK, playerattached();
		if(select("什麼？ ！ :嗯？") == 2) {
			mes "- 您環顧四周， -";
			mes "- 你身後沒有什麼。 -";
			mes "- 當您要懷疑...-";
		}
		else {
			mes "- 您一次問Nodor-";
			mes "- 再次驚恐...-";
		}
		next;
		if (SkillPoint) {
			mes "[罕見]";
			mes "您仍然有一些沒有用完的技能點。";
			mes "在您使用這些觀點之前，我不能使您更強壯。";
			close;
		}
		specialeffect2 EF_LORD;
		specialeffect2 EF_SIGHTRASHER;
		specialeffect2 EF_STORMGUST;
		erasequest 5100;
		mes "[罕見]";
		mes "哈哈----------------------！";
		mes "-----烏哈哈哈哈------！";
		mes "-  -  -  -  -  -  耶  -  ！ ！";
		next;
		nude;
		callfunc "F_ClearJobVar"; // -- Clears all job variables for the current player
		jobchange roclass(eaclass()|EAJL_THIRD);
		set EXSUPNOV_Q,5;	
		emotion ET_SMILE;
		mes "[罕見]";
		mes "普哈哈哈哈哈哈！";
		mes "好的！你害怕了嗎？";
		next;
		select("…………");
		emotion ET_THINK, playerattached();
		emotion ET_SORRY;
		mes "[罕見]";
		mes "抱歉嚇到你！";
		mes "我認為我們作為新手的職責和使命是使日子快樂。";
		next;
		mes "[罕見]";
		mes "那麼，您不覺得有些變化了嗎？";
		mes "感覺自己的身體比以前更輕...？";
		mes "還是更強大？";
		next;
		select("...？ ...？ ...？");
		emotion ET_QUESTION, playerattached();
		mes "[罕見]";
		mes "哦---我的---（tut，tut，tut）！";
		next;
		emotion ET_SCRATCH;
		mes "[罕見]";
		mes "Sieu Ngu先生 -";
		mes "不是卑鄙的，但是";
		mes "你有點沉悶...";
		mes "你現在不感到與眾不同嗎？";
		next;
		if(select("你在說什麼？ :我明白了！") == 1) {
			mes "[罕見]";
			mes "嗯---對，你很沉悶，不是嗎？";
			mes "您已經超出了限制。";
			next;
			mes "[罕見]";
			mes "您可以比以前更強大。";
			mes "不是你的夢想嗎？";
			next;
		}
		mes "[罕見]";
		mes "好~！";
		mes "現在前往世界，作為新手生活，享受您自由，幸福的生活！";
		mes "就是這樣！";
		close;
	}
	else if (EXSUPNOV_Q == 5) {
		mes "[罕見]";
		mes "（p嘴）------------------------------------------";
		mes "所以你終於回來了。";
		next;
		mes "[罕見]";
		mes "為了慶祝這個新的新手同志，我已經準備了一些設備來給您 -";
		next;
		mes "[罕見]";
		mes "不要忘記確保您有足夠的 ^ff0000inventory ^000000從我這裡拿走這些物品。";
		mes "而且不要說我也沒有警告你。";
		next;
		if(select("是的，讓我檢查一下我的庫存。 :我很好，給我看看。") == 1) {
			mes "[罕見]";
			mes "好吧，！";
			mes "你知道！沒有後悔。";
			close;
		}
		mes "[罕見]";
		mes "我年輕的時候我曾經穿過的衣服，";
		mes "但是他們仍然很好。";
		if (EXSUPNOV_Q_ == 3) getitem 2522,1; // Undershirt
		else if (EXSUPNOV_Q_ == 4) {
			getitem 2628,1; // Novice Armlet [1]
			getitem 2113,1; // Novice Shield [1]
			getitem 2416,1; // Novice Shoes [1]
			getitem 5119,1; // Super Novice Hat [1]
			getitem 2512,1; // Novice Manteau [1]
			getitem 2340,1; // Novice Breastplate [1]
		}
		else getitem 2340,1; // Novice Breastplate [1]
		next;
		select("二手貨吧？");
		mes "[罕見]";
		mes "把他們還給我。";
		mes "當我向您提供財產時，您該如何反應？";
		next;
		mes "[罕見]";
		mes "我...";
		mes "我無法收回我已經贈送的東西。";
		next;
		mes "[罕見]";
		mes "現在，我要再次說問候。";
		next;
		specialeffect2 EF_RECOVERY;
		mes "[罕見]";
		mes "讓超級新手"+strcharinfo(0)+" be blessed with the Super Novice Guardian!";
		set EXSUPNOV_Q,6;
		close;
	}
	else if (EXSUPNOV_Q > 5) {
		mes "[罕見]";
		mes "別忘了你是新手。";
		mes "永遠成為新手！";
		mes "生活的目的是度過一個愉快的時間！";
		close;
	}
	mes "[釣魚新手]";
	mes "噓！";
	mes "請安靜，因為你會";
	mes "嚇the魚。";
	close;
}

airplane,33,47,5	script	Nudor#sp2	729,{
	if (checkquest(5099) >= 0) {
		mes "[努多爾]";
		mes "啊~我沒有錢買任何食物。";
		mes "我餓了，但我不知道兄弟何時來...";
		next;
		select("嘿，你是……努多爾先生嗎？");
		mes "[努多爾]";
		mes "eeeeeh？ ！誰想知道？";
		mes "我知道你？";
		next;
		mes "- 您向Nudor解釋了為什麼您來見他。 -";
		next;
		mes "[努多爾]";
		mes "啊！你來給我一些零用錢代替我的兄弟~";
		next;
		mes "[努多爾]";
		mes "哇~~他給你多少？";
		next;
		mes "-Nudor眨著眼睛 -";
		mes "- 眼睛，伸出雙手。 -";
		mes "- 您想給多少？ -";
		next;
		set .@i, select("我沒有錢:給100,000 Zeny:給300,000 Zeny:給1,000,000 Zeny:給你所有的錢");
		mes "[努多爾]";
		if (.@i == 1) {
			mes "- 哭 -";
			mes "我討厭Nodor ---！";
			mes "我也恨你---！";
			close2;
			warp "airplane",70,93;
			end;
		}
		else if (.@i < 5) {
			switch(.@i) {
				case 2: set .@n,100000; break;
				case 3: set .@n,300000; break;
				case 4: set .@n,1000000; break;
			}
			mes "可愛~~他給你了"+callfunc("F_InsertComma",.@n)+" Zeny? That's pretty much!";
			next;
			if (Zeny < .@n) {
				mes "[努多爾]";
				mes "什麼？但是在哪裡"+callfunc("F_InsertComma",.@n)+" Zeny?";
				close;
			}
		}
		else {
			mes "哦...哦，天哪，你真的給我所有的zeny嗎？真的嗎？";
			next;
			if(select("不，不是真的...:全力以赴") == 1) {
				mes "[努多爾]";
				mes "PSHAW！多麼令人失望！";
				close;
			}
			if (!Zeny) {
				mes "[努多爾]";
				mes "嗯？但是你沒有錢。你真可憐，是嗎？";
				close;
			}
			set .@n,Zeny;
		}
		mes "[努多爾]";
		mes "哇~!!"+.@n+" Zeny! This helped me so much~ Tehee~";
		mes "我不敢相信...";
		changequest 5099,5100;
		set Zeny, Zeny - .@n;
		if (.@i < 5) set EXSUPNOV_Q_, .@i;
		else {
			if (.@n > 1000000) set EXSUPNOV_Q_,4;
			else if (.@n > 300000) set EX_SUPNOV_Q_,3;
			else set EXSUPNOV_Q_,2;
		}
		next;
		mes "[努多爾]";
		if (Sex == SEX_MALE) mes "你可知道？你真的好帥啊~";
		else mes "我要告訴你，你真的…真的~美麗。";
		next;
		mes "[努多爾]";
		mes "- 你覺得自己在 -";
		mes "- 被欺騙，所以回到Nodor。 -";
		close;
	}
	else if (checkquest(5100) >= 0) {
		mes "[努多爾]";
		mes "我不敢相信我的兄弟寄給我錢...我喜歡它~";
		next;
		mes "[努多爾]";
		mes "哦！";
		mes strcharinfo(0)+"，噓----！";
		next;
		mes "[努多爾]";
		mes "不要告訴Nodor這個~~";
		close;
	}
	mes "[努多爾]";
	mes "啊~我沒有錢買任何食物。";
	mes "我餓了，但我不知道兄弟何時來...";
	close;
}
