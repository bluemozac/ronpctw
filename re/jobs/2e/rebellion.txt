//===== rAthena Script =======================================
//= Rebellion Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Gunslinger -> Rebellion.
//===== Changelogs: ==========================================
//= 1.0 First version. [exneval]
//= 1.1 Syntax change. [Capuche]
//============================================================

moc_fild12,261,318,3	script	Suspicious Man	4_M_MOCASS2,2,2,{
	end;
OnTouch:
	if (Class == Job_Gunslinger && BaseLevel >= 99 && JobLevel >= 70) {
		if (isbegin_quest(12340) == 0) {
			mes "[可疑男人]";
			mes "終於我找到了您，Vagabond Spike！你的脖子是我的！";
			next;
			select("說什麼？");
			mes "[可疑男人]";
			mes "* Snort*不要假裝您不知道。看看這個海報！你在裡面！";
			next;
			select("我看到了相似之處，但這不是我。");
			mes "["+strcharinfo(0)+"]";
			mes "忘記相似之處。你從哪裡得到那張海報的？我從未犯罪。";
			next;
			mes "[可疑男人]";
			mes "我知道你會嘗試撒謊。";
			next;
			mes "[可疑男人]";
			mes "^0000ffeinbroch私人酒吧 ^000000發布了想要海報的。您的腳後跟有許多像我這樣的賞金獵人。";
			next;
			mes "[可疑男人]";
			mes "我不在乎你是對的，只要你看它，我就可以把你死了或活著。";
		}
		else {
			mes "[可疑男人]";
			mes "又是你！你有膽量回到這裡！";
		}
		next;
		if (select("逃跑吧。 :跟他戰鬥。") == 1)
			mes "^0000ffi現在應該運行，以後再考慮一下。 ^000000";
		else
			mes "^0000ffhe並不是我唯一的賞金獵人，如果他說實話。我需要時間來製定策略。 ^000000";
		close2;
		if (isbegin_quest(12340) == 0)
			setquest 12340;// Rebellion Job Change Quest
		warp "moc_fild12",186,83;
	}
	end;
}

einbroch,54,97,3	script	Bouncer	4_M_REBELLION,{
	if (BaseClass != Job_Gunslinger) {
		mes "[保鏢]";
		mes "場地周圍沒有游泳。";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[保鏢]";
		mes "正在建設中，無法進入。";
		close;
	case 1:
		mes "[保鏢]";
		mes "*喘著粗氣*你！我以為你已經被俘虜了。哦，現在沒關係。您是我的免費卡！";
		next;
		select("我不是你想像的那個罪犯！");
		mes "[保鏢]";
		mes "你不是嗎？";
		next;
		mes "^0000ffthe Bouncer檢查您的臉。 ^000000";
		next;
		mes "[保鏢]";
		mes "嗯，您的相似之處很明顯，儘管您看起來不像犯罪尖峰那樣險惡";
		next;
		select("我想和海報出版商談談。");
		mes "[保鏢]";
		mes "您的問題有點不合時宜。進入。如果我發現您一直是真正的交易，我會發現您再也看不到外面了。";
		close2;
		warp "job_gun",81,29;
		end;
	case 2:
		mes "[保鏢]";
		mes "你回來了。為什麼？";
		next;
		if (select("我想進來:我路過。") == 2) {
			mes "[保鏢]";
			mes "你很傻。";
			close;
		}
		mes "[保鏢]";
		mes "前進。避免麻煩。";
		close2;
		warp "job_gun",70,28;
		end;
	}
}

job_gun,68,33,3	script	Ruthless Rebellion	4_F_REBELLION,{
	mes "[殘酷的叛亂]";
	mes "你知道如何使用槍嗎？";
	close;
}

job_gun,78,33,3	script	Suspicious Gunslinger	4_M_JOB_BLACKSMITH,{
	mes "[可疑的槍手]";
	mes "...";
	next;
	mes "[安靜的槍手]";
	mes "不要說我看起來像是你知道的鐵匠。";
	close;
}

job_gun,74,24,6	script	Drunken Man	4_M_LIEMAN,{
	mes "[醉漢]";
	mes "*打ic*哦，我是這裡的所有者。";
	mes "哦，等等，我在酒吧還是其他地方？ zzz ...";
	close;
}

job_gun,64,33,6	script	Anxious Man	4_M_ALCHE_A,{
	mes "[焦慮的人]";
	mes "也許我在錯誤的地方。這個地方看起來不像酒吧。";
	close;
}

job_gun,86,29,3	script	Kulbertinov	4_M_REBELLION,{
	if (Class == Job_Rebellion) {
		mes "[庫爾伯季諾夫]";
		mes "你想見老闆嗎？";
		next;
		if (select("是的。 :沒有。") == 1) {
			close2;
			warp "job_gun",220,130;
			end;
		}
		end;
	}
	if (Class != Job_Gunslinger) {
		mes "[庫爾伯季諾夫]";
		mes "你在找我嗎？";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[庫爾伯季諾夫]";
		mes "我可以幫你嗎？";
		close;
	case 1:
		mes "[庫爾伯季諾夫]";
		mes "你想要什麼？";
		next;
		select("我被誤認為是罪犯！");
		mes "[庫爾伯季諾夫]";
		mes "等一下... MWAH哈哈！";
		next;
		mes "^0000ffhe掃描您的臉，然後擦拭。 ^000000";
		next;
		mes "[庫爾伯季諾夫]";
		mes "對不起。有一會兒，我以為Spike重生了。三天前他被發現死亡。";
		next;
		select("他已經被抓了？");
		mes "[庫爾伯季諾夫]";
		mes "是的，賞金獵人已經得到了付款。這個消息沒有傳播，您是他的吐痰形象。去數字。";
		next;
		select("你必須做點什麼！");
		mes "[庫爾伯季諾夫]";
		mes "對不起，但是我不能為您和每個賞金獵人交談。當然，我願意彌補您的不便。";
		next;
		mes "[庫爾伯季諾夫]";
		mes "嘿，您加入我們的隊伍呢？最後，您不會被槍擊。";
		next;
		select("你想讓我成為賞金獵人嗎？");
		mes "[庫爾伯季諾夫]";
		mes "不，賞金狩獵只是一種消遣。我們是世界上所有槍支的專家。";
		next;
		mes "[庫爾伯季諾夫]";
		mes "無憂無慮的精神！";
		mes "神奇的噴火！";
		mes "我們稱自己為 ^0000frebellion ^000000。";
		next;
		mes "[庫爾伯季諾夫]";
		mes "您看起來好像知道如何使用槍。您想如何加入我們的隊伍？您不必再擔心其他賞金獵人了。";
		next;
		if (select("讓我想一想:我想我別無選擇。") == 1) {
			mes "[庫爾伯季諾夫]";
			mes "您可以一遍又一遍地考慮一下，但是答案不會改變。只需報價即可。";
		}
		else {
			mes "[庫爾伯季諾夫]";
			mes "好思考。現在也可以做到這一點，然後再解決。";
		}
		next;
		mes "[庫爾伯季諾夫]";
		mes "與Enwin Conick交談。她引導像您這樣的烈酒流動到叛亂之路。我會向她發言。啊，不要把槍指向她。";
		completequest 12340;
		setquest 12341;// Rebellion Job Change Quest
		close2;
		warp "job_gun",19,15;
		end;
	case 2:
		mes "[庫爾伯季諾夫]";
		mes "您與Elwin Conick有生意嗎？";
		next;
		if (select("否:請派我去找埃爾文·科尼克。") == 1) {
			mes "[庫爾伯季諾夫]";
			mes "好的。照顧伏特加酒的照片嗎？";
			close;
		}
		mes "[庫爾伯季諾夫]";
		mes "好吧，我會讓她知道。";
		close2;
		warp "job_gun",19,15;
		end;
	}
}

job_gun,19,24,3	script	Elwin Conick#1	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[艾爾文·科尼克]";
		mes "如果您不想加入叛亂，那就走出去。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12341) == 1) {
		mes "[艾爾文·科尼克]";
		mes "您要加入我們的叛亂嗎？";
		next;
		mes "^0000ff，眼睛敏銳的矮個女人凝視著你的眼睛。 ^000000";
		next;
		mes "[艾爾文·科尼克]";
		mes "在這裡簽名。";
		next;
		mes "^0000ff，即使在測試期間可能發生的不可預見的事故中，他或她遭受了傷害，肢解或死亡，簽名的政黨也不會說該測試。 ^000000";
		next;
		select("什麼？ ！");
		mes "[艾爾文·科尼克]";
		mes "你想加入我們嗎？";
		next;
		if (select("我放棄.:...我會簽名。") == 1) {
			mes "[艾爾文·科尼克]";
			mes "您的選擇。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[艾爾文·科尼克]";
		mes "好的。現在，跟著我。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		mes "[艾爾文·科尼克]";
		mes "你想再試一次嗎？";
		next;
		if (select("我放棄了:做吧。") == 1) {
			mes "[艾爾文·科尼克]";
			mes "您的選擇。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[艾爾文·科尼克]";
		mes "跟我來。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12343) > 0) {
		mes "[艾爾文·科尼克]";
		if (isbegin_quest(12343) == 1)
			mes "您還有伊万的射擊測試，您想再次嘗試嗎？";
		else
			mes "伊万的射擊測試尚未完成，您想重試嗎？";
		next;
		if (select("我放棄了:做吧。") == 1) {
			mes "[艾爾文·科尼克]";
			mes "不值得放棄，現在出去放鬆。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[艾爾文·科尼克]";
		mes "記住要盡力而為。";
		close2;
		warp "job_gun",120,130;
		end;
	}
	if (isbegin_quest(12345) == 1) {
		mes "[艾爾文·科尼克]";
		mes "伊万在等你，去找他。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[艾爾文·科尼克]";
	mes "您無需參加測試。你為什麼不出去喝酒？";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,117,46,3	script	Elwin Conick#2	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[艾爾文·科尼克]";
		mes "如果您不想加入叛亂，那就走出去。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
	.@quest_12341 = isbegin_quest(12341);
	.@quest_12342 = isbegin_quest(12342);
	if (.@emt_chk == 3169) {
		mes "^0000ffyou已經處於測試形式。如果您完成了所有測試，請在彙編室^000000向Elwin Conick報告。";
		close;
	}
	if (.@emt_chk) {
		mes "[艾爾文·科尼克]";
		mes "看著自己，您真的想測試嗎？";
		next;
		mes "^0000FFELWIN看到您變成另一種形式，她很生氣。等待您當前的表格結束，然後再次找到她。 ^000000";
		next;
		if (select("等等:走開。") == 2) {
			mes "[艾爾文·科尼克]";
			mes "當您沒有任何形式的時候回來。";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[艾爾文·科尼克]";
		mes "不值得放棄，現在出去放鬆。";
		close;
	}
	if (.@quest_12341 == 1 || .@quest_12342 == 1) {
		if (countitem(6746)) {
			mes "[艾爾文·科尼克]";
			mes "測試後，必須退回任何剩餘的鋼鐵工件。";
			delitem 6746,countitem(6746);// Steel_Article
			close;
		}
		if (countitem(6747)) {
			mes "[艾爾文·科尼克]";
			mes "測試後，必須退回任何剩餘的鋼鐵工件。";
			delitem 6747,countitem(6747);// Steel_Article_
			close;
		}
		if ((countitem(13118) - isequipped(13118)) > 0) {
			mes "[艾爾文·科尼克]";
			mes "時間結束。您未能通過測試。讓我服用您所有剩下的測試材料。";
			delitem 13118,1;// Tiny_Flame
			close;
		}
		if (.@quest_12342 == 1)
			.@menu$ = "Got it.";
		else {
			.@menu$ = "Okay.";
			setquest 12342;// Rebellion Job Change Quest
			completequest 12341;
		}
		mes "[艾爾文·科尼克]";
		mes "聽。我不會竭盡全力向您解釋測試。";
		next;
		select(.@menu$ );
		mes "[艾爾文·科尼克]";
		if (.@quest_12341 == 1) {
			mes "這是施放槍支模具的地方。您可以在任何地方看到鋼鐵工件。";
			next;
			mes "[艾爾文·科尼克]";
			mes "我們的叛亂是內心的武器專家。在這裡，您將學習創建和組裝槍支。";
			next;
			mes "[艾爾文·科尼克]";
			mes "您會被要求控制入口附近的爐控制器，並選擇5個標準鋼製工件。";
			next;
			mes "[艾爾文·科尼克]";
			mes "然後，您將將它們帶到下一個彙編室，並將它們組裝為槍支。您將有3分鐘的時間來完成所有這些。您最好快速而專注，否則您的測試會失敗。";
			next;
			mes "[艾爾文·科尼克]";
			mes "我將在隔壁的房間裡，等待您帶給我一支完整的槍支。要檢查經過的時間，您將轉變為另一個工作。 ^0000ffif您經過3分鐘，然後回到正常狀態，回到我身邊，然後重新開始測試。 ^000000。";
		}
		else {
			mes "您會被要求控制機器上的開關，並選擇5個標準的鋼製工件。";
			next;
			mes "[艾爾文·科尼克]";
			mes "然後，您將將它們帶到下一個組裝室，然後將它們組裝成完整的槍支。您將獲得3分鐘的時間來完成所有這些。 ^0000ffto檢查經過的時間，您將轉變為其他工作^000000。";
			next;
			mes "[艾爾文·科尼克]";
			mes "我將在隔壁的房間裡，等待您帶給我一支完整的槍支。請記住， ^0000ffyou在3分鐘後轉換過期時未能通過測試。";
		}
		transform 3169,180000;
		close;
	}
	mes "[艾爾文·科尼克]";
	mes "啊？你迷路了嗎？您需要先註冊，然後回到這裡。";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,198,39,3	script	Elwin Conick#3	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[艾爾文·科尼克]";
		mes "如果您不想加入叛亂，那就走出去。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@emt_chk == 3169) {
			if ((countitem(13118) - isequipped(13118)) < 1) {// don't count if equipped
				mes "[艾爾文·科尼克]";
				mes "你完成了嗎？我在您的庫存中沒有看到任何完整的槍支。";
				close;
			}
			mes "[艾爾文·科尼克]";
			mes "鑑於時間，這是卑鄙的，但還不錯。你通過了。";
			delitem 13118,1;// Tiny_Flame
			next;
			mes "[艾爾文·科尼克]";
			mes "讓我服用您所有剩下的測試材料。";
			delitem 6746,countitem(6746);// Iron_Artifact
			delitem 6747,countitem(6747);// Steel_Artifact
			next;
			mes "[艾爾文·科尼克]";
			mes "現在，您可以繼續下一課程。 Ivan Sidorenko將監督您的測試。";
			completequest 12342;
			close2;
			warp "job_gun",120,133;
			end;
		}
		if (.@emt_chk) {
			mes "[艾爾文·科尼克]";
			mes "看著自己，您真的想測試嗎？";
			next;
			mes "^0000FFELWIN看到您變成另一種形式，她很生氣。等待您當前的表格結束，然後再次找到她。 ^000000";
			next;
			if (select("等等:走開。") == 2) {
				mes "[艾爾文·科尼克]";
				mes "當您沒有任何形式的時候回來。";
				close2;
				warp "job_gun",81,29;
				end;
			}
			mes "[艾爾文·科尼克]";
			mes "不值得放棄，現在出去放鬆。";
			close;
		}
		mes "[艾爾文·科尼克]";
		mes "時間結束。您未能通過測試。讓我服用您所有剩下的測試材料。";
		delitem 6746,countitem(6746);// Iron_Artifact
		delitem 6747,countitem(6747);// Steel_Artifact
		delitem 13118,(countitem(13118) - isequipped(13118));// Tiny_Flame
		next;
		mes "[艾爾文·科尼克]";
		mes "讓我們重新開始。";
		close2;
		warp "job_gun",117,41;
		end;
	}
	mes "[艾爾文·科尼克]";
	mes "啊？你迷路了嗎？您需要先註冊，然後回到這裡。";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,165,18,0	script	Furnace Controller#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "只有授權用戶才能執行操作。";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {// J_REB_SHECIL1
		if (isbegin_quest(12342) == 1) {
			mes "您想從爐子上挖出黴菌嗎？";
			next;
			if (select("退出:開始。") == 1) {
				mes "切換到待機模式。";
				close;
			}
			mes "初始化爐控制器ZX-3100 ...";
			disablenpc "Furnace Controller#1";
			donpcevent "Furnace Controller#2::OnStart";
			close;
		}
	}
	mes "不是授權用戶。中止操作。";
	close;
}

job_gun,214,36,0	script	Auto Anvil#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "只有授權用戶才能執行操作。";
		close;
	}
	if ((countitem(13118) - isequipped(13118)) > 0) {// Tiny_Flame
		mes "已經發現未經授權的槍支創建。向經理Elwin報告。";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {
		if (countitem(6747) < 5) {// Steel_Article_
			mes "您沒有標準的鋼鐵工件。";
			close;
		}
		if (isbegin_quest(12342) == 1) {
			mes "您是否要使用自動砧並將標準的鋼鐵工件塑造成槍支？";
			next;
			if (select("退出:開始。") == 1) {
				mes "為了安全起見，請遠離自動砧。";
				close;
			}
			if (rand(1,10) > 6) {
				mes "您已經成功地使用了自動砧創建了槍支。 ^ff0000please報告在您的轉換到期之前^000000。";
				specialeffect EF_REPAIRWEAPON;
				getitem 13118,1;// Tiny_Flame
			}
			else {
				mes "未能創建槍支。";
				specialeffect EF_SUI_EXPLOSION;
			}
			delitem 6747,5;// Steel_Article_
			close;
		}
	}
	mes "不是授權用戶。中止操作。";
	close;
}

job_gun,165,18,0	script	Furnace Controller#2	CLEAR_NPC,{
	mes "該系統正忙於從爐子挖出完整的模具。";
	close;

OnStart:
	enablenpc "Furnace Controller#2";
	sleep 3000;
	mapannounce "job_gun","Furnace Controller: Dredging a mold up from the furnace... Workers, stand by at the production line.",bc_map,"0x00ff44";
	sleep 500;
	for ( .@i = 0; .@i < 30; ++.@i ) {
		.@eff_rnd = rand(1,100);
		if (.@eff_rnd < 33)
			donpcevent "Heating Furnace#3::OnStart";
		else if (.@eff_rnd > 66)
			donpcevent "Heating Furnace#4::OnStart";
		else
			donpcevent "Heating Furnace#5::OnStart";
		sleep 200;
		.@d_itemx = rand(127,143);
		.@d_itemy = rand(26,29);
		makeitem 6746,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article
		if (rand(1,10) > 7)
			makeitem 6747,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article_
		sleep 500;
	}
	sleep 2000;
	mapannounce "job_gun","Furnace Controller: Finishing the operation... Entering Standby mode in 15 seconds.",bc_map,"0x00ff44";
	sleep 15000;
	disablenpc "Furnace Controller#2";
	enablenpc "Furnace Controller#1";
	end;
OnInit:
	disablenpc "Furnace Controller#2";
	end;
}

job_gun,130,31,0	script	Heating Furnace#3	CLEAR_NPC,{
	end;
OnStart:
	specialeffect EF_FIREPILLAR;
	sleep 500;
	specialeffect EF_FIREPILLARBOMB;
	end;
}
job_gun,136,31,0	duplicate(Heating Furnace#3)	Heating Furnace#4	CLEAR_NPC
job_gun,141,31,0	duplicate(Heating Furnace#3)	Heating Furnace#5	CLEAR_NPC

job_gun,120,138,3	script	Ivan Sidorenko#1	4_M_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[伊万·西多連科]";
		mes "這是射擊測試區，您不能按照自己的意願來這裡。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) < 2) {
		mes "[伊万·西多連科]";
		mes "似乎有一個錯誤，您不能留在這裡。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12343) == 0) {
		mes "[伊万·西多連科]";
		mes "嘿，保持專注 - 我們在這裡使用Live彈藥，您可以隨時被槍擊。";
		next;
		mes "[伊万·西多連科]";
		mes "讓我快速。基本上，您將在此射擊範圍內運行，包括5個部分。";
		next;
		mes "[伊万·西多連科]";
		mes "目的是在人群中快速而失敗地找到敵人。";
		next;
		mes "[伊万·西多連科]";
		mes "在所有目標中，您必須僅拍攝 ^0000FFSTANDARD-ISSUE目標 ^000000。如果您犯了一個錯誤，您將失去積分。";
		next;
		mes "[伊万·西多連科]";
		mes "讓我們嘗試一下。 ^0000ffstand-issue目標 ^000000並不多，因此請保持眼睛剝落。";
		next;
		mes "[伊万·西多連科]";
		mes "^0000ffyou有3分鐘的時間來完成此測試。如果在轉換到期之前無法完成^000000，您將失敗。如果發生這種情況，只需沿著回到我身邊的道路即可。";
		next;
		mes "[伊万·西多連科]";
		mes "^0000ffyou'如果錯過了標準問題目標，或者射擊3不正確的目標^000000。讓我們開始。";
		setquest 12343;// Rebellion Job Change Quest
		setquest 12344;// Rebellion Job Change Quest
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	if (isbegin_quest(12343) == 1) {
		if (checkquest(12343,HUNTING) == 2)
			.@j_r_s = 10;
		if (checkquest(12344,HUNTING) == 2)
			.@j_r_f = 2;
		if (getstatus(SC_MONSTER_TRANSFORM,1) != 3169)
			.@j_r_t = 5;
		.@j_r_c = .@j_r_s - .@j_r_f - .@j_r_t;
		if (.@j_r_c > 9) {
			mes "[伊万·西多連科]";
			mes "好工作。我認為您不會這麼快。";
			completequest 12343;
			completequest 12344;
			setquest 12345;// Rebellion Job Change Quest
			next;
			mes "[伊万·西多連科]";
			mes "您可能會離開並在外面等。我將帶其他講師的評估報告來見。";
			close2;
			warp "job_gun",210,132;
			end;
		}
		if (.@j_r_c == 8) {
			mes "[伊万·西多連科]";
			mes "您足夠快，但是您拍攝了太多錯誤的目標。你失敗了。";
		}
		else if (.@j_r_c == 5) {
			mes "[伊万·西多連科]";
			mes "您足夠準確，但是您花了太多時間。緩慢的叛亂無法生存足夠長的時間。";
		}
		else if (.@j_r_c < 5) {
			mes "[伊万·西多連科]";
			mes "您以速度和準確性失敗。你是怎么生存這麼長時間的？";
		}
		else {
			mes "[伊万·西多連科]";
			mes "有一個問題，統計數據似乎是錯誤的。好吧...這有時會發生。";
		}
		erasequest 12343;
		erasequest 12344;
		next;
		mes "[伊万·西多連科]";
		mes "讓我們再試一次。這次要更加小心。";
		setquest 12343;
		setquest 12344;
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	mes "[伊万·西多連科]";
	mes "你怎麼不離開？";
	next;
	mes "[伊万·西多連科]";
	mes "您可能會離開並在外面等。我將帶其他講師的評估報告來見。";
	close2;
	warp "job_gun",210,132;
	end;
}

job_gun,1,4,0	script	#target	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#target::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#target";
	while( mobcount("job_gun","#target::OnMyMobDead") < 16 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
	}
	end;
}

job_gun,1,5,0	script	#nontarget	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#nontarget::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#nontarget";
	while( mobcount("job_gun","#nontarget::OnMyMobDead") < 56 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
	}
	end;
}

job_gun,220,132,3	script	Ivan Sidorenko#2	10062,{
	if (!checkweight(1201,1)) {
		mes "您有太多的物品，請減少它們繼續。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "庫存中的物品使您重視。首先減輕體重。";
		close;
	}
	if (isbegin_quest(12345) == 0) {
		mes "[伊万·西多連科]";
		mes "一定有一個錯誤。您不應該留在這裡。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Gunslinger) {
		if (BaseLevel < 99 || JobLevel < 70) {
			mes "[伊万·西多連科]";
			mes "嗯？你看起來很虛弱嗎？我無話可說。";
			close;
		}
		if (SkillPoint) {
			mes "[伊万·西多連科]";
			mes "哦，不~您仍然有技能點。";
			close;
		}
		mes "[伊万·西多連科]";
		mes "Kulbertinov，Elwin，我已經評估了您的叛亂測試結果。";
		next;
		mes "[伊万·西多連科]";
		mes "和...";
		next;
		mes "[伊万·西多連科]";
		mes "您已經滿足了我們所有的要求，包括態度和考試表現。";
		next;
		mes "[伊万·西多連科]";
		mes "沒錯，您現在是叛亂的成員。準備好哇。";
		next;
		mes "[伊万·西多連科]";
		mes "Elwin希望您擁有這個。照顧好它。他為你做了。";
		jobchange Job_Rebellion;
		completequest 12345;
		getitem 13119,1;// Freedom_Flame
		next;
		mes "[伊万·西多連科]";
		mes "稍後再見。";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Rebellion) {
		mes "[伊万·西多連科]";
		mes "這一生應該很有趣，而不是無聊。";
		next;
		mes "[伊万·西多連科]";
		mes "通過成為叛亂，您的生活應該得到升級。因此，去創造一個偉大的旅程，讓我們感到自豪！";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[伊万·西多連科]";
	mes "你有話要說，不是嗎？";
	close;
}

job_gun,220,138,4	shop	Prop Vending Machine	2_VENDING_MACHINE1,13210:-1,13211:-1,13212:-1,13213:-1,13214:-1,7663:-1,7664:-1,7665:-1

job_gun,216,138,4	script	Butler Karlex	1_M_01,{
	mes "[管家卡萊克斯]";
	mes "在進行進步測試時，請使用此臨時存儲來確保您的物品安全。";
	next;
	.@s = select("退出:打開存儲。");
	mes "[管家卡萊克斯]";
	mes "感謝您使用該服務。";
	close2;
	if (.@s == 2)
		openstorage;
	end;
}

job_gun,56,26,0	warp	rebelroom#1	2,2,einbroch,49,97
job_gun,18,10,0	warp	rebelroom#2	2,2,job_gun,81,29
job_gun,165,22,0	warp	rebelroom#3	2,2,job_gun,196,35
job_gun,197,49,0	warp	rebelroom#4	2,2,job_gun,157,21
job_gun,88,144,0	warp	shootingrange#1	3,3,job_gun,93,123
job_gun,105,97,0	warp	shootingrange#2	3,3,job_gun,125,98
job_gun,156,115,0	warp	shootingrange#3	3,3,job_gun,157,137
job_gun,152,154,0	warp	shootingrange#4	3,3,job_gun,135,154
job_gun,130,146,0	warp	shootingrange#5	3,3,job_gun,120,130


job_gun,1,1,0	script	Admin1	CLEAR_NPC,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) != 1)
		end;
	mes "[時間經理]";
	mes "您想什麼時候回來？";
	next;
	switch( select("取消:測試怪物生成:從頭開始:熔爐區:射擊區:完成測試") ) {
	case 1:
		end;
	case 2:
		donpcevent "#target::OnStart";
		donpcevent "#nontarget::OnStart";
		end;
	case 3:
		callsub S_Quest,0,0;
	case 4:
		callsub S_Quest,12341,12341;
	case 5:
		callsub S_Quest,12342,12342;
	case 6:
		callsub S_Quest,12345,12344;
	}
	end;

S_Quest:
	setarray .@arguments[0], getarg(0), getarg(1);
	for ( .@quest = 12340; .@quest <= 12345; ++.@quest ) {
		if (isbegin_quest(.@quest) > 0)
			erasequest .@quest;
		if (.@quest <= .@arguments[0])
			setquest .@quest;
		if (.@quest <= .@arguments[1])
			completequest .@quest;
	}
	end;
}
