//===== rAthena Script ======================================= 
//= Sura Job Quest
//===== By: ================================================== 
//= Muad_Dib, Gepard
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Monk / Champion -> Sura.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Removed disablenpc which causes players stucked in waitingroom. [Joseph]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added GM management function. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//= 1.5 Fixed players stuck in waiting room, event triggers properly [Luciar]
//============================================================

ve_in,237,125,0	script	King Crab#job_shu	107,{
	if (job_shu == 0) {
		if (BaseJob == Job_Monk && BaseLevel >= 99 && JobLevel >= 50) {
			set .@job_check,1;
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_shu,1;
				setquest 11155;
				next;
			}
		}
		mes "[國王螃蟹]";
		mes "乙:哈哈哈哈哈哈。";
		mes "那個小新秀被我的打擊擊倒了！";
		next;
		mes "[污水蠕蟲]";
		mes "那些傲慢的研究員應該得到一個痛苦的教訓。";
		mes "老闆，你怎麼敢又有新秀攻擊你！";
		next;
		if (!.@job_check) {
			mes "[國王螃蟹]";
			mes "khha ha ha ha ha ha哈哈!!!!";
			mes "再來一杯啤酒，這裡！ ！ ！";
			mes "我今天要喝！ ！ ！";
			next;
			mes "[污水蠕蟲]";
			mes "國王螃蟹！ ！ ！我將永遠跟隨你！";
			close;
		}
		mes "[國王螃蟹]";
		mes "哈哈哈哈哈哈哈哈!!!!";
		next;
		mes "[污水蠕蟲]";
		mes "嘿！就在這裡！ ！ ！再來一杯啤酒！ ！ ！";
		mes "你在幹什麼？我老闆的杯子已經空了！快來！";
		next;
		mes "[女服務員]";
		mes "是的，我會立即將其帶走。";
		next;
		mes "[污水蠕蟲]";
		mes "太慢了！ ！ ！你認為我的老闆是誰？嗯！ ？";
		next;
		mes "[女服務員]";
		mes "我...我真的很抱歉，先生...";
		next;
		mes "[國王螃蟹]";
		mes "k k k k k。就足夠了。";
		mes "那個可愛的女孩似乎害怕你！";
		mes "嘿，女士~我們不是恐怖的人~~";
		next;
		mes "[女服務員]";
		mes "aaaaaaaaaaak！不要這樣做！";
		next;
		mes "- 爆炸 -";
		donpcevent "???#bcmd::OnEnable";
		next;
		mes "[？ ？ ？ ]";
		mes "嘿。";
		mes "我一直在看著你，像你擁有這個地方一樣做一個騷動。";
		next;
		mes "[？ ？ ？ ]";
		mes "太嘈雜了。因此，如果您想再次胡說八道，請回家。";
		next;
		mes "[污水蠕蟲]";
		mes "什麼？ ！";
		mes "我不知道你是誰，但你一定已經失去了理智！";
		mes "你知道我的老闆是誰嗎？";
		next;
		mes "[污水蠕蟲]";
		mes "這是著名的國王螃蟹！ ！ ！";
		mes "誰能用拳頭淘汰整個軍隊！ ！ ！";
		next;
		mes "[？ ？ ？ ]";
		mes "您可以做所有骯髒的事情，例如骯髒的臉。";
		mes "您只是一個暴徒滿足您的慾望。";
		next;
		mes "[國王螃蟹]";
		mes "什麼？";
		mes "你怎麼敢說這些話？";
		mes "僅僅因為你是一個女孩，並不意味著我會放手。";
		next;
		mes "[污水蠕蟲]";
		mes "嗯！老闆真的很生氣！";
		mes "我可以一擊毆打你...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿！你怎麼敢對女士使用暴力！ ？";
		next;
		mes "[？ ？ ？ ]";
		mes "向後移，你小事...";
		next;
		mes "[？ ？ ？ ]";
		mes "哦，我很高興聽到這個消息。";
		mes "我不會首先進攻，因為我正在訓練中。";
		next;
		mes "[國王螃蟹和污垢蠕蟲]";
		mes "什麼？";
		next;
		mes "[國王螃蟹和污垢蠕蟲]";
		mes "嗯？ ！";
		next;
		mes "[國王螃蟹和污垢蠕蟲]";
		mes "啊啊啊啊啊！ ！ ！ ！ ！ ！";
		specialeffect EF_FIRESPLASHHIT,AREA,"King Crab#job_shu";
		specialeffect EF_FIRESPLASHHIT,AREA,"Sludge Worm#job_shu";
		next;
		mes "- 這是什麼驚人的力量？ -";
		mes "- 我從未見過 -";
		mes "- 在...  -";
		next;
		mes "[？ ？ ？ ]";
		mes "好吧，心情被寵壞了...";
		next;
		mes "[女服務員]";
		mes "tha ...非常感謝...";
		mes "我該如何償還你？";
		next;
		mes "[？ ？ ？ ]";
		mes "這沒什麼大不了的。沒關係。";
		next;
		mes "[女服務員]";
		mes "您要回到住宿嗎？";
		mes "如果您還可以，我可以給您用餐嗎？";
		next;
		mes "[？ ？ ？ ]";
		mes "哦，您真的不需要。";
		mes "只有一片麵包和溫暖的湯就足夠了。";
		mes "然後，我必須說再見。再見。";
		donpcevent "???#bcmd::OnDisable";
		next;
		mes "[女服務員]";
		mes "再見...";
		mes "...";
		mes "…………";
		mes "........（臉頰發光）";
		set job_shu,1;
		setquest 11155;
		close;
	} else {
		if (strnpcinfo(0) == "King Crab#job_shu") {
			mes "[國王螃蟹]";
			mes "一個新秀怎麼把我淘汰！我！大螃蟹...";
		} else {
			mes "[污水蠕蟲]";
			mes "老闆...我們現在真的超越了我們的素數嗎？";
			mes "抽泣...哭泣...";
		}
		close;
	}
}
ve_in,241,128,4	duplicate(King Crab#job_shu)	Sludge Worm#job_shu	110

ve_in,244,126,3	script	Waitress#job_shu	69,{
	mes "[女服務員]";
	if (job_shu == 0) {
		mes "哦，不~";
		mes "我討厭那些嘈雜和不禮貌的客戶！";
		mes "我希望有人可以責罵我。";
		close;
	} else if (job_shu == 1) {
		if (BaseJob == Job_Monk) {
			mes "哦，親愛的~";
			mes "我真的很感謝你"+jobname(Class)+", for helping me a while ago.";
			next;
		}
		mes "啊，我們以前見過的人？";
		mes "她已經在這個村莊住了幾天，";
		mes "她不是那麼好嗎？";
		next;
		mes "[女服務員]";
		mes "儘管她是像我這樣的女人，但她太酷了！";
		mes "aaahh~那個美麗而氣勢壯大的人物...";
		next;
		mes "[女服務員]";
		mes "哦，我的！";
		mes "我忘了做點什麼！";
		mes "我應該馬上做飯，然後將飯菜帶到旅館。 aaahh~";
		next;
		mes "[女服務員]";
		mes "如果您仍然不知道該留在哪裡，請使用該建築物上側的 ^f57d7dthe Inn ^000000。";
		close;
	}
	mes "現在，客戶變得有些安靜。";
	mes "啊，我現在感覺好多了！";
	close;
}

ve_in,240,131,0	script	???#bcmd	484,{
	end;
OnInit:
	disablenpc "???#bcmd";
	end;
OnEnable:
	enablenpc "???#bcmd";
	initnpctimer;
	end;
OnDisable:
	disablenpc "???#bcmd";
	stopnpctimer;
	end;
OnTimer600000:
	donpcevent "???#bcmd::OnDisable";
	stopnpctimer;
	end;
}

ve_in,97,149,0	script	#Suraryokan	139,3,3,{
OnTouch:
	if (job_shu == 1) {
		set .@n$, "["+strcharinfo(0)+"]";
		mes "[？ ？ ？ ]";
		mes "是誰？ ！";
		next;
		mes .@n$;
		mes "我是不久前在酒吧的人。";
		mes "我有事要問。";
		next;
		mes "[？ ？ ？ ]";
		mes "不要打擾我，就走開！";
		next;
		mes .@n$;
		mes "拜託，我只想說話。";
		next;
		mes "[？ ？ ？ ]";
		mes "...";
		next;
		mes .@n$;
		mes "不要忽略我，只是跟我說話。";
		next;
		mes .@n$;
		mes "只是一會兒，還可以嗎？";
		next;
		mes .@n$;
		mes "我不是在這裡傷害你。";
		next;
		mes .@n$;
		mes "你怎麼能對那樣嚴重地來到你家門口？";
		next;
		mes .@n$;
		mes "你走得太遠。";
		next;
		mes .@n$;
		mes "我看到你打架...";
		next;
		mes .@n$;
		mes "我以為你一定已經練習武術，我來這裡見你。";
		next;
		mes .@n$;
		mes "但是看到你像這樣把我站在外面...";
		next;
		mes .@n$;
		mes "您必須在武術中進步...";
		next;
		mes "[？ ？ ？ ]";
		mes "多麼持久...";
		next;
		mes "[？ ？ ？ ]";
		mes "你想要什麼？";
		set job_shu,2;
		close;
	}
	end;
}

ve_in,98,159,0	script	Fighter#job_shu	484,{
	if (job_shu < 2) {
		mes "[戰鬥機]";
		mes "啊~我很累。";
		mes "訓練是如此艱難，我還有很長的路要走！";
		close;
	} else if (job_shu == 2) {
		mes "["+strcharinfo(0)+"]";
		mes "我想知道您前一段時間使用的技能。";
		next;
		mes "[？ ？ ？ ]";
		mes "...";
		mes "......";
		mes "…………";
		mes "............一個像您不需要知道的新秀。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "新秀？ ！";
		mes "我穩定地練習武術。";
		mes "我相信我永遠不會輸。";
		next;
		mes "[？ ？ ？ ]";
		mes "所以你也可以擊敗我嗎？";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "不...";
		mes "我想那太多了...";
		mes "坦率地說，我很驚訝你。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我以為自己足夠堅強，但是見到你戰鬥後，我意識到自己充滿了自負。";
		next;
		mes "[？ ？ ？ ]";
		mes "huuuh~無論你有多堅強，";
		mes "您無法避免達到真正的限制。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你只是我期望的人。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我想你會知道";
		mes "如何克服這一限制。";
		next;
		mes "[？ ？ ？ ]";
		mes "停止它。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "什麼？為什麼？";
		next;
		mes "[？ ？ ？ ]";
		mes "你是人嗎？";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "什麼？";
		mes "當然，我是一個人。";
		next;
		mes "[？ ？ ？ ]";
		mes "變得更強壯對於僅僅是人來說太難了。";
		mes "如果需要，只需購買更好的設備即可。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "那你是什麼？";
		mes "你的意思是你不是人嗎？";
		next;
		mes "[？ ？ ？ ]";
		mes "超越極限...";
		mes "對於人來說是不可能的。";
		mes "這是鬼的方式...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "鬼？";
		next;
		mes "[？ ？ ？ ]";
		mes "選擇這種方式後，我放棄了成為人類。";
		mes "成為蘇拉放棄神聖保護的方式。";
		next;
		mes "[？ ？ ？ ]";
		mes "如果您選擇這種方式，您甚至沒有時間休息。";
		mes "您將成為戰爭的木偶...";
		next;
		switch(select("但我還想了解更多。 :好吧，我就退出了。")) {
		case 1:
			mes "[？ ？ ？ ]";
			mes "你不是白痴嗎？";
			mes "您為什麼對成為鬼魂感到很高興？";
			next;
			mes "[？ ？ ？ ]";
			mes "您會後悔的，因此只要對您的生活感到滿意。";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "好吧，既然我遇見了你，我將不再以這種方式過自己的生活！";
			next;
			mes "[？ ？ ？ ]";
			mes "哈...你真的讓我累了。";
			next;
			mes "[？ ？ ？ ]";
			mes "好的，然後^f57d7d去殺死100狼。 ^000000";
			mes "之後，我將再次考慮。";
			set job_shu,3;
			changequest 11155,11156;
			close;
		case 2:
			mes "[？ ？ ？ ]";
			mes "好吧，好的選擇。";
			mes "您無需尋找麻煩。";
			close;
		}
	} else if (job_shu == 3) {
		if (checkquest(11156,HUNTING) == 2) {
			mes "[？ ？ ？ ]";
			mes "哇~";
			mes "你真的殺死了所有這些狼嗎？";
			mes "我想你會放棄。";
			next;
			mes "[？ ？ ？ ]";
			mes "女人的話是她的紐帶！";
			mes "承諾是一個承諾。";
			next;
			mes "[？ ？ ？ ]";
			mes "轉到Juno ^000000東南部的 ^f57d7d7d7del Mes峽谷。";
			mes "我的同事 ^f57d7dbuddy ^000000將在該地區中心附近訓練自己。";
			next;
			mes "[布魯諾]";
			mes "告訴他，“布魯諾”把你送到那裡，然後才適合自己。";
			set job_shu,4;
			changequest 11156,11157;
			close;
		}
		mes "[？ ？ ？ ]";
		mes "哈...你真的讓我累了。";
		next;
		mes "[？ ？ ？ ]";
		mes "好的，然後^f57d7d去殺死100狼。 ^000000";
		mes "然後，我將再次考慮。";
		close;
	} else if (job_shu == 4) {
		mes "[布魯諾]";
		mes "轉到Juno ^000000東南部的 ^f57d7d7d7del Mes峽谷。";
		mes "我的同事 ^f57d7dbuddy ^000000將在該地區中心附近訓練自己。";
		next;
		mes "[布魯諾]";
		mes "告訴他，“布魯諾”把你送到那裡，然後才適合自己。";
		close;
	} else if (job_shu == 100) {
		mes "[布魯諾]";
		mes "嘿~！";
		mes "你好嗎？";
		next;
		mes "[布魯諾]";
		mes "你沒有禮物給你的老師嗎？";
		close;
	}
	mes "[布魯諾]";
	mes "我建議您重新考慮成為蘇拉語。";
	mes "蘇拉強壯又酷，但這並不是全部。";
	next;
	mes "[布魯諾]";
	mes "我？";
	mes "如果我需要再次選擇工作，我一定會選擇蘇拉。";
	close;
}

yuno_fild07,254,176,7	script	Buddy#job_shu	753,{
	if (job_shu < 4) {
		mes "[夥伴]";
		mes "haaap ！ ！ ！ haaaap ！ ！ ！哇！ haahhp ！ ！ ！";
		next;
		mes "[夥伴]";
		mes "我不知道你為什麼在這裡，但是除非你想死，否則請不要站在我身後。";
		close;
	} else if (job_shu == 4) {
		mes "[夥伴]";
		mes "haaap ！ ！ ！ haaaap ！ ！ ！哇！ haahhp ！ ！ ！";
		next;
		mes "[夥伴]";
		mes "我不知道你為什麼在這裡，但是除非你想死，否則請不要站在我身後。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "“布魯諾”把我送到這裡。";
		next;
		mes "[夥伴]";
		mes "啊!我的同事向我介紹了您！";
		mes "無論如何，你為什麼在這裡見我？";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我問布魯諾關於她的力量，她向你推薦我。";
		next;
		mes "[夥伴]";
		mes "那個困難的女孩...不，只是開玩笑。";
		mes "您必須是一個特殊的人，讓她寄給我。";
		next;
		mes "[夥伴]";
		mes "好吧，我無法回答您的問題，因為我正在自我訓練的過程中，所以我會帶您去我們的主人。";
		next;
		mes "[夥伴]";
		mes "我的主人不喜歡噪音，所以請 ^f57d7d7dwait在客廳默默地。 ^000000";
		next;
		mes "[夥伴]";
		mes "這邊走...";
		set job_shu,5;
		changequest 11157,11158;
		close2;
		warp "sword_1-1",215,244;
		end;
	} else if (job_shu == 100) {
		mes "[夥伴]";
		mes "啊，很久沒看見，"+strcharinfo(0)+".";
		mes "你在這裡見我的主人嗎？";
		next;
		switch(select("是:否")) {
		case 1:
			mes "[夥伴]";
			mes "我馬上帶你。";
			close2;
			warp "sword_1-1",216,168;
			end;
		case 2:
			mes "[夥伴]";
			mes "那你為什麼在這裡？";
			mes "你不是在這裡見我，不是嗎？";
			close;
		}
	}
	mes "[夥伴]";
	mes strcharinfo(0)+"，你還沒有見過大師嗎？";
	next;
	mes "[夥伴]";
	mes "我的主人不喜歡噪音，所以請 ^f57d7d7dwait在客廳默默地。 ^000000";
	next;
	mes "[夥伴]";
	mes "這邊走...";
	close2;
	warp "sword_1-1",215,244;
	end;
}

sword_1-1,223,243,4	script	Drawing Room	753,{
	end;
OnInit:
	waitingroom "Drawing Room",20,"Drawing Room::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "sword_2-1",223,205;
	donpcevent "Buddy#Sura_Salon::OnEnable";
	disablewaitingroomevent;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

sword_1-1,223,243,4	script	Buddy#job_shu_Salong	753,{
	mes "[夥伴]";
	mes "我的主人不喜歡噪音，所以請 ^f57d7d7dwait在客廳默默地。 ^000000";
	next;
	switch(select("到客廳去:到外面去。")) {
	case 1:
		mes "[夥伴]";
		mes "請在上側的客廳裡等待。";
		close;
	case 2:
		mes "[夥伴]";
		mes "然後，再次見到你。";
		close2;
		warp "yuno_fild07",255,178;
		end;
	}
}

sword_2-1,1,1,0	script	Buddy#Sura_Salon	66,{
	end;
OnInit:
	disablenpc "Buddy#Sura_Salon";
	end;
OnEnable:
	enablenpc "Buddy#Sura_Salon";
	initnpctimer;
	end;
OnReset:
	killmonster "sword_2-1","Buddy#Sura_Salon::OnMyMobDead";
	end;
OnDisable:
	disablenpc "Buddy#Sura_Salon";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("sword_2-1","Buddy#Sura_Salon::OnMyMobDead") < 1) {
		monster "sword_2-1",219,210,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	}
	end;
OnTimer3000:
	mapannounce "sword_2-1","The master is now in the middle of training, and you need to wait in the living room around 5 minutes.",bc_map;
	end;
OnTimer4000:
	mapannounce "sword_2-1","Ah! You may have hallucinations in the living room, so please be careful.",bc_map;
	end;
OnTimer5000:
	mapannounce "sword_2-1","This is the policy of the master, so please don't feel unpleasant.",bc_map;
	end;
OnTimer33000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"It can't be true",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Illusion",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer93000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Just Imagination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer153000:
	mapannounce "sword_2-1","If you're bored, may I bring you some magazines?",bc_map;
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer213000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Never mind",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Can you see me?",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer273000:
	monster "sword_2-1",219,210,"",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer300000:
	mapannounce "sword_2-1","The master has arrived. Soon I'll show you into the master's room.",bc_map;
	end;
OnTimer303000:
	donpcevent "Buddy#job_shuaneh::OnEnable";
	end;
OnTimer305000:
	donpcevent "Buddy#Sura_Salon::OnReset";
	end;
OnTimer315000:
	mapannounce "sword_2-1","Come on here.",bc_map;
	donpcevent "Buddy#job_shuaneh::OnDisable";
	donpcevent "#Sura_garajjom::OnEnable";
	end;
OnTimer320000:
	mapannounce "sword_2-1","I guess you're not ready to meet the master yet...",bc_map;
	mapwarp "sword_2-1","yuno_fild07",255,178;
	donpcevent "#Sura_garajjom::OnDisable";
	donpcevent "Buddy#Sura_Salon::OnDisable";
	donpcevent "Drawing Room::OnEnable";
	end;
}

sword_2-1,223,205,7	script	Buddy#job_shuaneh	753,{
	if (job_shu > 4) {
		mes "我會告訴你路。這邊走。";
		close2;
		warp "sword_1-1",216,168;
		donpcevent "Buddy#job_shuaneh::OnDisable";
		end;
	}
	mes "你為什麼在這裡？";
	close2;
	warp "yuno_fild07",248,179;
	end;
OnInit:
OnDisable:
	disablenpc "Buddy#job_shuaneh";
	end;
OnEnable:
	enablenpc "Buddy#job_shuaneh";
	end;
}

sword_2-1,223,205,0	script	#Sura_garajjom	139,10,10,{
OnTouch_:
	warp "sword_1-1",216,168;
	end;
OnInit:
OnDisable:
	disablenpc "#Sura_garajjom";
	end;
OnEnable:
	enablenpc "#Sura_garajjom";
	end;
}

sword_1-1,222,169,5	script	Bruno#job_shu	484,{
	if (job_shu > 4) {
		mes "[布魯諾]";
		mes "嘿~"+strcharinfo(0)+", what's up?";
		next;
		switch(select("我們來談談吧:出去吧。")) {
		case 1:
			if (job_shu == 100) {
				mes "[布魯諾]";
				mes "我的主人？";
				mes "他說，他已經把世界擺脫了腦海，他是一個幽靈，放棄了人類，但他似乎對世界有持久的渴望。";
				next;
				mes "[布魯諾]";
				mes "好吧，我也是蘇拉（Sura），但我仍然喜歡旅行和結識許多人~";
				next;
				mes "[布魯諾]";
				mes "在過去，我的主人有一個密友，但是在成為蘇拉後，";
				next;
				mes "[布魯諾]";
				mes "我聽說他寫了一封他去世的朋友的信。";
				mes "然後他有時秘密拜訪“記憶的位置”。";
				next;
				mes "[布魯諾]";
				mes "最好不要說他死了，只是繼續與他的朋友見面。";
				mes "他有點像木術。";
				next;
				mes "[布魯諾]";
				mes "但是他真的很酷，很棒。";
				mes "何";
				next;
				mes "[布魯諾]";
				mes "我繼續旅行的原因是要找到主人的“寶貴朋友”，而不是為了訓練自己。";
				next;
				mes "[布魯諾]";
				mes "當我看到主人想念他的朋友時，我會感到沉重的...";
				mes "不是...不僅是我...大師！";
				close;
			}
			mes "[布魯諾]";
			mes "哇~我以為你會在見到我的主人之前被淘汰，但你終於在這裡了嗎？";
			next;
			mes "[布魯諾]";
			mes "無論如何，您現在在這裡，所以我是您的直接上級。";
			next;
			mes "[布魯諾]";
			mes "我們不在乎您在世界上的年齡！";
			close;
		case 2:
			mes "[布魯諾]";
			mes "每當你想~";
			mes "我將向您展示死者的培訓課程~";
			close2;
			warp "yuno_fild07",255,178;
			end;
		}
	}
	mes "[布魯諾]";
	mes "你是做什麼的？";
	mes "您是怎麼找到這個地方的！";
	mes "離開！ ！ ！";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_1-1,223,167,2	script	Master#job_shu	483,{
	mes "[掌握]";
	if (job_shu == 5) {
		mes "您必須有明亮的眼睛，才能看到您終於來到了這個房間。";
		next;
		mes "[掌握]";
		if (SkillPoint != 0) {
			mes "除非您使用所有技能點，否則您將無法成為蘇拉。";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "您似乎負擔沉重。請刪除其中的一些。";
			close;
		}
		if (BaseLevel < 99 || JobLevel < 50) {
			mes "您似乎沒有達到人類的極限，為了成為蘇拉，您還有很多事情要走。";
			mes "請去練習更多。";
			close;
		}
		if (BaseJob != Job_Monk) {
			mes "唔？";
			close;
		}
		mes "當然~";
		mes "我敏銳的眼睛。";
		next;
		mes "[布魯諾]";
		mes "掌握！這是一件狼毛皮外套。";
		mes "寒冷時穿上它。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "啊！那是狼我...被抓了...呃。";
		next;
		mes "- 鞭打 -";
		next;
		mes "[布魯諾]";
		mes "嗬嗬~";
		mes "你有話要說嗎"+strcharinfo(0)+"?";
		mes "您成為我們家庭的成員，讓我們在一起~";
		next;
		mes "[掌握]";
		mes "是的，我現在也接受你作為我的學生。";
		next;
		mes "[掌握]";
		mes "我認為您已經知道了，但是作為蘇拉人生活並不那麼容易。";
		next;
		mes "[掌握]";
		mes "我也把自己拒之門外。如果有一天我們再次去世界，那一定是世界需要戰爭鬼魂的一天。";
		next;
		mes "[掌握]";
		mes "我希望那些日子不會來，但是……我願意成為幽靈並殺死所有敵人。";
		next;
		if (ismounting()) {
			mes "[掌握]";
			mes "所以...請從騎寵物那裡下車！";
			mes "在這種情況下，這是失去禮節。";
			close;
		}
		mes "[掌握]";
		mes "您應該穩步訓練自己，直到那天。";
		set job_shu,100;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11158;
		getitem 5754,1; //Blazing_Soul
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_shu > 5) {
		mes "有一次我也是一個普通的年輕人，他經常旅行並認識了許多朋友。";
		next;
		mes "[掌握]";
		mes "但是，正如您可能認為的那樣，我終於面臨著自己的局限性。";
		next;
		mes "[掌握]";
		mes "有時我想知道“我是否應該放棄一切才能變得更強大……？”";
		next;
		mes "[掌握]";
		mes "但是做了什麼...";
		close;
	}
	mes "這不是您可以留下來的地方。";
	mes "請回去。";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_2-1,1,2,0	script	Button Girl#sura	66,{
	callfunc "F_GM_NPC";
	switch(select("打開競技場.:關閉競技場.:打開客廳.:打開Buddy.:打開Gara.:關閉全部.:關閉")) {
	case 1:
		mes "打開競技場。";
		donpcevent "Drawing Room::OnEnable";
		close;
	case 2:
		mes "關閉競技場。";
		donpcevent "Drawing Room::OnDisable";
		close;
	case 3:
		mes "打開客廳。";
		donpcevent "Buddy#Sura_Salon::OnEnable";
		close;
	case 4:
		mes "打開好友。";
		donpcevent "Buddy#job_shuaneh::OnEnable";
		close;
	case 5:
		mes "打開加拉。";
		donpcevent "#Sura_garajjom::OnEnable";
		close;
	case 6:
		mes "關閉所有。";
		donpcevent "Drawing Room::OnDisable";
		donpcevent "Buddy#Sura_Salon::OnReset";
		donpcevent "Buddy#Sura_Salon::OnDisable";
		donpcevent "Buddy#job_shuaneh::OnDisable";
		donpcevent "#Sura_garajjom::OnDisable";
		close;
	case 7:
		close;
	}
}
