//===== rAthena Script ======================================= 
//= Mechanic Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Jobchange Quest from Blacksmith / Whitesmith -> Mechanic.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Fixed the Door NPC [JayPee].
//= 1.2 Updated script. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//============================================================ 

yuno,129,156,3	script	Chainheart	923,{
	mes "[鏈心]";
	if (eaclass() & EAJL_THIRD) {
		mes "作為機械師生活很艱難，";
		mes "但是我很高興我能";
		mes "總是做我想做的。";
		close;
	}
	if (job__mechanic == 12) {
		mes "關於力學或魔術機械的無話可說。";
		mes "您會比現在任何人都了解更多。";
		next;
		mes "[鏈心]";
		mes "通過體驗世界所能提供的東西，繼續增進知識。";
		close;
	} else if (job__mechanic == 11) {
		if (BaseJob == Job_Blacksmith && BaseLevel > 98 && JobLevel > 49 && SkillPoint == 0) {
			if (ismounting()) {
				mes "你在騎寵物上，";
				mes "因此，您無法改變工作。";
				mes "請取消騎行寵物，然後重試！";
				close;
			}
			mes "這是什麼？那是你嗎？";
			mes "哦，等等，您看起來與我上次見到你的情況完全不同。";
			mes "發生了什麼事嗎？";
			next;
			mes "[鏈心]";
			mes "啊！你去了少年";
			mes "就像我說的。就像弗蘭克森一樣。";
			next;
			mes "[鏈心]";
			mes "似乎沒有必要";
			mes "更多詢問。";
			mes "我敢肯定，您已經獲得了真正的技工知識。";
			next;
			mes "[鏈心]";
			mes "我無話可說。";
			mes "您已經是一個很棒的機械師。";
			mes "不要因";
			mes "標題。最重要的是您有知識。";
			next;
			mes "[鏈心]";
			mes "您似乎不是將鎖定在實驗室或庫中的類型。";
			mes "你是一種學習的類型";
			mes "通過積極參與。";
			next;
			mes "[鏈心]";
			mes "我喜歡你是誰，我認為我們需要這種新的血液來為我們的機械師。";
			next;
			mes "[鏈心]";
			mes "科學正在尋找和研究可以在邏輯上證明的事物。";
			mes "具有諷刺意味的是，科學通常從不確定的理論開始，就像魔術一樣。";
			next;
			mes "[鏈心]";
			mes "正如我之前說的，如果激情和才華是真實的，那將沒有問題！";
			mes "恭喜您穿越新的道路作為機械師！";
			next;
			set job__mechanic,12;
			completequest 10101;
			if (Sex == SEX_MALE)
				getitem 5749,1; //Driver_Band
			else
				getitem 5760,1; //Driver_Band_
			getitem 2795,1; //Green_Apple_Ring
			jobchange roclass(eaclass()|EAJL_THIRD);
			mes "[鏈心]";
			mes "這是我給你的禮物。";
			mes "如果你保持健康，有一天";
			mes "你會感謝我的。";
			next;
			mes "[鏈心]";
			mes "好吧，去。這個廣闊的世界是";
			mes "您的實驗室，一切";
			mes "這個世界將成為你的";
			mes "教科書，所以讓機械師的未來有一線希望！";
			close;
		}
		mes "似乎還不夠嗎？";
		close;
	} else if (job__mechanic >= 3) {
		mes "順便說一句，弗蘭克森失了。";
		mes "他說他想學習";
		mes "然後消失。";
		mes "他可以去哪裡？";
		next;
		mes "[鏈心]";
		mes "如果您繼續走向機械師，您可能會撞到他。";
		mes "命運以怪異的方式工作。";
		next;
		mes "[鏈心]";
		mes "無論如何，告別。";
		close;
	} else if (job__mechanic == 2) {
		mes "我認為我已經做了所有的解釋，您還有什麼想知道的嗎？";
		next;
		switch(select("我想了解更多。 :沒什麼。")) {
		case 1:
			mes "[鏈心]";
			mes "嗯..？您想進一步了解魔術機械。是嗎？";
			mes "嗯...我告訴你你很有才華，這很困難。";
			next;
			mes "[鏈心]";
			mes "好吧，您似乎很有才華，所以如果您可以成為一名機械師，對我們來說還不錯。";
			mes "但不幸的是不是";
			mes "這樣的工作。";
			next;
			mes "[鏈心]";
			mes "機械學者不僅可以輕鬆地承認您，並在不首先展示真正的努力的情況下將知識交給您。";
			next;
			mes "[鏈心]";
			mes "當然，我是那些學者之一，所以我想我可以嘗試考慮您。";
			next;
			mes "[鏈心]";
			mes "無論如何，我只是認為排除沒有任何考驗的人才華橫溢的人是不公平的。";
			next;
			mes "[鏈心]";
			mes "唔...";
			next;
			mes "[鏈心]";
			mes "實際上，如果有人不能教您，您可以自己學習。";
			mes "如果有人可以做到，那麼任何人都可以為自己而弄清楚嗎？";
			next;
			mes "[鏈心]";
			mes "這並不容易，但是如果您的激情很強烈並且您的才華是真實的，那麼就不會有任何問題。";
			next;
			mes "[鏈心]";
			mes "想到它...";
			mes "弗蘭克森（Franklson）開發了";
			mes "魔術機械自己。";
			mes "那你為什麼不去";
			mes "少年的廢墟？";
			next;
			mes "[鏈心]";
			mes "正如我以前告訴您的那樣，如果您的熱情和才華是真實的，那麼您一定會找到一些東西。";
			next;
			mes "[鏈心]";
			mes "找出知識";
			mes "像我們一樣自己。";
			mes "不要只等到有人向您教它。展示您聲稱擁有的激情。";
			next;
			set job__mechanic,3;
			changequest 10091,10092;
			mes "[鏈心]";
			mes "無論如何，無論您選擇什麼，我都希望您盡力而為。";
			close;
		case 2:
			mes "[鏈心]";
			mes "多麼奇怪的人。";
			mes "我以為您有真正的機械師所需要的。";
			close;
		}
	} else if (job__mechanic == 1) {
		mes "好的，讓我屏住呼吸。";
		mes "你一定對";
		mes "魔術機械和";
		mes "力學世界。";
		mes "好的，讓我解釋一下。";
		next;
		mes "[鏈心]";
		mes "魔術機械是由機器和魔術之間的融合而誕生的。";
		mes "到目前為止，這兩個尚未真正連接在一起。";
		next;
		mes "[鏈心]";
		mes "如果您使用魔術機器，則確實必須熟悉機器的內部工作。";
		next;
		mes "[鏈心]";
		mes "它可以有效使用，";
		mes "而且，您不需要依靠不確定和不穩定的魔術的東西，所以比這更好。";
		next;
		mes "[鏈心]";
		mes "發明魔術機械的人是施瓦茲瓦爾德共和國的學者富蘭克森。";
		next;
		mes "[鏈心]";
		mes "弗蘭克森（Franklson）在Juperos ^000000的 ^ff0000ruins中發現了一台古老的機器，並將其與魔術的力量融合在一起，這就是開始。";
		next;
		mes "[鏈心]";
		mes "自然而然地，弗蘭克森（Franklson）命名了新生機器魔術機械。";
		next;
		mes "[鏈心]";
		mes "無論如何，由於朱諾以外的其他地方並不為人所知，因此對魔術機械的研究一直在進步。";
		next;
		mes "[鏈心]";
		mes "因為我們已經研究了";
		mes "魔術機械無休止";
		mes "人們開始打電話給我們 ^ff0000 mechanics ^000000。";
		mes "這個名字是不言自明的";
		mes "所以它只是卡住了。";
		next;
		mes "[鏈心]";
		mes "唔。好吧，我想我已經解釋了魔術機械。";
		mes "我想解釋更多，但這太技術了。";
		mes "我不想讓你更加厭煩。";
		next;
		set job__mechanic,2;
		changequest 10090,10091;
		mes "[鏈心]";
		mes "無論如何，如果您成為我們中的一員，您可以自己形成自己的定義和解釋。";
		close;
	}
	if (BaseJob == Job_Blacksmith) {
		if (BaseLevel > 98 && JobLevel > 49 && SkillPoint == 0) {
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job__mechanic,1;
				setquest 10090;
				next;
			}
			mes "乍一看，您似乎是一個非常多功能的人，正在處理各種事情。";
			mes "你看起來很熟練。";
			mes "你可以是那個。";
			next;
			mes "[鏈心]";
			mes "啊，我沒有自我介紹。";
			mes "我是著名的連鎖之心。";
			mes "我是機械師。";
			mes "好吧，實際上我不是那麼出名。";
			next;
			mes "[鏈心]";
			mes "我不認為機械師這個詞";
			mes "你很熟悉。";
			mes "實際上，當它到來時";
			mes "描述，";
			mes "這是一項可以";
			mes "簡單地解釋。";
			next;
			mes "[鏈心]";
			mes "一個研究和使用魔術機械的人，這是一種機械師。";
			next;
			mes "[鏈心]";
			mes "那麼，機械研究和使用的魔術機械是什麼？";
			mes "這並不那麼簡單，但我會嘗試一下。";
			next;
			setquest 10090;
			set job__mechanic,1;
			mes "[鏈心]";
			mes "抱歉，我開始了";
			mes "談論太多。";
			mes "讓我抓住我的";
			mes "呼吸一段時間。";
			mes "如果您有興趣";
			mes "請再次與我交談。";
			close;
		}
		mes "您看起來像一個對處理東西有很大意識的人。";
		next;
		mes "[鏈心]";
		mes "該力量已在";
		mes "只是處理簡單的事情。";
		mes "那不會給";
		mes "你真實的享受。";
		next;
		mes "[鏈心]";
		mes "對...如果您想享受";
		mes "真正的交易和";
		mes "深刻的深刻";
		mes "你需要處理";
		mes "^ff0000magic機械^000000！";
		next;
		mes "[鏈心]";
		mes "魔術機械還沒有";
		mes "在世界上很長一段時間，因此認識到它並不常見。";
		mes "它非常精確，與您在世界上經歷的任何事物完全不同。";
		next;
		mes "[鏈心]";
		mes "是的，處理魔術機械的人稱為 ^ff0000mechanics ^000000。";
		mes "我也是那些光榮的機制之一。";
		next;
		mes "[鏈心]";
		mes "看來您有一個好機械師需要什麼。";
		mes "這是基本要求。";
		next;
		mes "[鏈心]";
		mes "如果您在達到 ^ff0000base級別99 ^000000和 ^ff0000job級別50 ^000000之後來找我，";
		mes "我將指導您進入機械師世界。";
		close;
	}
	mes "作為機械師生活很艱難，";
	mes "但是我很高興我能";
	mes "總是做我想做的。";
	next;
	mes "[鏈心]";
	mes "您還不想成為機械師並享受真正的幸福嗎？";
	close;
}

jupe_cave,37,55,5	script	Scholar#Mechanic	883,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 300) {
		mes "- 您的背包太重了。 -";
		mes "- 首先減輕身體 -";
		mes "- 請重試。 -";
		close;
	}
	mes "[學者]";
	if (job__mechanic > 10) {
		mes "少年怎麼樣？";
		mes "我很害怕，我仍然";
		mes "不能去那裡。";
		close;
	} else if (job__mechanic > 3) {
		mes "^ff0000您無法通過步行到達目的地。";
		mes "^ff0000 strong敵人不是這裡的問題，您只是無法到達目的地。";
		mes "請讓自己在家。 ^000000";
		next;
		mes "[學者]";
		mes "多麼舒適，可能";
		mes "我無意去。";
		next;
		mes "[學者]";
		mes "好吧，你準備好了嗎";
		mes "去中途";
		mes "在少年？";
		next;
		switch(select("我準備好了！ :還沒有")) {
		case 1:
			mes "[學者]";
			mes "呃...你的舉止如此堅定，以至於我有點後悔，以至於我還沒有去過那裡。";
			mes "我應該去那裡嗎？呃...在溢出的牛奶上哭泣是沒有用的！";
			next;
			mes "[學者]";
			mes "好吧，我會寄給你。";
			mes "我希望你得到";
			mes "知識";
			mes "你想要。";
			close2;
			warp "jupe_core2",149,288;
			end;
		case 2:
			mes "[學者]";
			mes "嗯...對。當然，您很猶豫也就不足為奇了。";
			next;
			mes "[學者]";
			mes "但是，如果您不為您打開的扭曲，那麼您將無法到達目的地。";
			mes "請不要浪費您的精力。";
			next;
			mes "[學者]";
			mes "當然，無論您多麼舒適，我都不會去那裡。";
			close;
		}
	}
	mes "我是一個學者，來這裡研究少年的廢墟。";
	next;
	mes "[學者]";
	mes "但是...實際上，當我嘗試進一步走時";
	mes "我很害怕。我幾乎無法進去。";
	mes "我能做些什麼？";
	next;
	mes "[學者]";
	mes "我聽到有傳言說人類不受歡迎。";
	mes "但是研究其中的奇妙機器應該非常有用。";
	if (job__mechanic != 3) close;
	next;
	mes "[學者]";
	mes "嗯？您還要去少年少吃東西嗎？";
	mes "我沒有勇氣去那裡。";
	mes "祝你好運。";
	next;
	mes "[學者]";
	mes "我知道去的方式";
	mes "中間點，我也有";
	mes "我需要的所有東西。但是我太害怕了。";
	next;
	mes "[學者]";
	mes "所以如果你願意，我會寄給你";
	mes "到少年。先生學者先生";
	mes "已經告訴我如何扭曲";
	mes "淡少將的中間點。";
	next;
	set job__mechanic,4;
	mes "[學者]";
	mes "的確，它變得容易";
	mes "但是你不禁恐懼";
	mes "太糟糕了。無論如何";
	mes "準備去，請告訴我。";
	close;
}

jupe_core2,149,273,3	script	Ghostfire#1	802,{
	mes "[？ ]";
	if (job__mechanic == 11) {
		mes "我失去了身體，剩下的就是我在這個地方的精神。";
		mes "我將永遠在少年少年生生活和死亡...";
		close;
	} else if (job__mechanic == 10) {
		mes "那個外觀...是嗎？您還獲得了知識。";
		mes "當未來充滿可能性時...";
		mes "純孩子的態度";
		mes "就像你可以做任何事情...";
		next;
		mes "[？ ]";
		mes "現在想到現在，現在最珍貴的事情可能是獲得自信而不是知識本身...";
		next;
		mes "[？ ]";
		mes "請不要做任何";
		mes "從現在開始的缺點。";
		mes "我希望你有知識";
		mes "有一天自己。";
		next;
		mes "[？ ]";
		mes "好吧，然後回去，找到一個知道魔術機械的人，並顯示出您獲得了知識的證據...";
		next;
		mes "[？ ]";
		mes "我是失去身體的人";
		mes "我的精神被破壞了";
		mes "我被限制在這個地方，少年...我會和少年一起生活和死亡";
		mes "永遠...";
		next;
		mes "[？ ]";
		mes "我的名字...名字是... fr ...";
		close2;
		set job__mechanic,11;
		changequest 10100,10101;
		warp "yuno",157,83;
		end;
	} else if (job__mechanic > 4) {
		mes "我失去了身體，剩下的就是我在這個地方的精神。";
		mes "我將永遠在少年少年生生活和死亡...";
		close;
	} else if (job__mechanic > 2) {
		mes "才華橫溢。";
		mes "確保有";
		mes "周圍沒有敵人。";
		mes "如果您專注於我，";
		mes "您可能會受到攻擊。";
		next;
		if(select("那麼，稍後。 :集中註意力。") == 1) close;
		mes "[？ ]";
		mes "我曾經是人類學者。";
		mes "我想知道人類不知道的科學局限性，所以我學到了很多東西，研究了，";
		mes "並蒐索...";
		next;
		mes "[？ ]";
		mes "那是我發現的不確定的魔力。";
		next;
		mes "[？ ]";
		mes "不確定的魔術是它是高度不穩定和不可預測的。";
		mes "在學習期間，我遇到了一個高度揮發性的魔法。";
		next;
		mes "[？ ]";
		mes "我唯一的回憶是從那天開始。";
		mes "從那以後我一直被困在這裡。";
		next;
		mes "[？ ]";
		mes "我現在知道的是少年。";
		mes "我要在這裡死。";
		next;
		mes "[？ ]";
		mes "我不知道你是誰...";
		mes "我不會理會你是否";
		mes "想獲得一些知識";
		mes "找到一個在乎的人。";
		next;
		if (job__mechanic == 3 || job__mechanic == 4)
			changequest 10092,10094;
		set job__mechanic,5;
		mes "[？ ]";
		mes "知識不是從他人那裡獲得的。通過自己的努力找到它。";
		mes "找到自己的知識路徑。";
		close;
	}
	end;
}

jupe_core2,53,75,3	script	Ghostfire#2	802,{
	mes "[？ ]";
	if (job__mechanic > 5) {
		mes "juperos ...我...";
		mes "少年...我想要什麼";
		mes "存在於這裡...";
		mes "科學的力量...機器的天堂...";
		close;
	} else if (job__mechanic == 5) {
		mes "才華橫溢。";
		mes "確保有";
		mes "周圍沒有敵人。";
		mes "如果您專注於我，";
		mes "您可能會受到攻擊。";
		next;
		if(select("那麼，稍後:焦點") == 1) close;
		mes "[？ ]";
		mes "當我身體";
		mes "我想證明";
		mes "卓越科學";
		mes "對於那些只相信的人";
		mes "這樣的神秘事物...";
		next;
		mes "[？ ]";
		mes "我想利用";
		mes "魔術作為合理的形式";
		mes "這是可見的。有更強的";
		mes "結果，我想控制";
		mes "魔術的力量";
		mes "使用科學...";
		next;
		mes "[？ ]";
		mes "我想證明科學";
		mes "很好地控制魔術";
		mes "喜歡神秘。我研究了很多";
		mes "事物和痛苦";
		mes "製作科學和魔術";
		mes "共存。";
		next;
		mes "[？ ]";
		mes "我的研究把我帶到了這裡，";
		mes "少年。在這個地方";
		mes "有許多科學產品";
		mes "這可能會刺激學者的";
		mes "腦...";
		next;
		mes "[？ ]";
		mes "我發現有可能將魔術的力量與科學一起加入。";
		mes "即使它不是完美的，它也足以吸引其他學者...";
		next;
		mes "[？ ]";
		mes "世界最近的事件引起了混亂，因此我的研究尚未與世界其他地區分享。";
		next;
		set job__mechanic,6;
		changequest 10094,10095;
		mes "[？ ]";
		mes "所以...我回到Juperos繼續對科學和魔術的調查。";
		close;
	}
	end;
}

jupe_core2,242,62,3	script	Ghostfire#3	802,{
	mes "[？ ]";
	if (job__mechanic > 6) {
		mes "我想進一步了解知識...";
		mes "人類的特徵...";
		mes "首先要做的事情...";
		mes "真理之門後來...";
		close;
	} else if (job__mechanic == 6) {
		mes "才華橫溢。";
		mes "確保有";
		mes "周圍沒有敵人。";
		mes "如果您專注於我，";
		mes "您可能會受到攻擊。";
		next;
		if(select("那麼，稍後:焦點") == 1) close;
		mes "[？ ]";
		mes "我對自己的成功不滿意，所以我一次又一次地回到這裡。";
		next;
		mes "[？ ]";
		mes "當我踩到這個雕像的立足點時，我收集和學到的能量被吸收了。";
		next;
		mes "[？ ]";
		mes "但是，隨著這種能量被吸收，我開始獲得我以前從未知道的知識。";
		next;
		mes "[？ ]";
		mes "但這是有代價的。有了知識，我開始失去人性。";
		mes "我失去了身體，唯一擁有的就是我的精神。";
		next;
		mes "[？ ]";
		mes "我不後悔獲得知識。我希望的是我可以讓自己的身體恢復原狀。";
		next;
		set job__mechanic,7;
		changequest 10095,10096;
		mes "[？ ]";
		mes "在將精力倒入雕像中後，我認為我可以將能量移至不同的雕像。";
		close;
	}
	end;
}

jupe_core2,29,150,0	script	#Foothold	139,1,1,{
OnTouch_:
	if (job__mechanic == 8) {
		percentheal 0,-30;
		mes "- 我覺得自己的力量是 -";
		mes "- 驅逐我的整個身體 -";
		mes "-怎麼了？ -";
		next;
		mes "- 找到真相的門 -";
		mes "- 這是 -";
		mes "- 功率排水。 -";
		close;
	} else if (job__mechanic == 7) {
		percentheal -20,0;
		mes "- 不明智的人 -";
		mes "- 聲明在我的頭上響了 -";
		mes "- 是什麼？";
		next;
		mes "- 裝有機器 -";
		mes "- 消除魔術力量 -";
		mes "- 犧牲奉獻 -";
		mes "- 人類的雕像。";
		next;
		set job__mechanic,8;
		changequest 10096,10097;
		mes "- 最確定的是 -";
		mes "- 我開始感到渴望 -";
		mes "- 去真理之門。 -";
		close;
	} else if (job__mechanic > 2) {
		percentheal 0,-30;
		mes "- 我覺得自己的力量是 -";
		mes "- 從我的身體中騰出";
		mes "-怎麼了？ -";
		close;
	}
	end;
}

jupe_core2,272,149,0	script	#Foothold2	139,1,1,{
OnTouch_:
	if (job__mechanic == 9) {
		mes "- 我有一種力量 -";
		mes "- 一直在排水， -";
		mes "- 但是現在，我不 -";
		mes "- 任何東西。 -";
		next;
		mes "- 到達 -";
		mes "- 立足很快。 -";
		close;
	} else if (job__mechanic == 8) {
		percentheal -20,-30;
		mes "- 我覺得自己的力量是 -";
		mes "- 從我的身體中騰出";
		mes "-怎麼了？ -";
		next;
		mes "- 我感覺到我的感覺 -";
		mes "- 回到我身邊。 -";
		next;
		donpcevent "#Door::OnEnable";
		changequest 10097,10098;
		mes "- 我認為我正在";
		mes "- 犧牲。 -";
		close;
	} else if (job__mechanic > 2) {
		percentheal 0,-30;
		mes "- 我覺得自己的力量是 -";
		mes "- 從我的身體中騰出";
		mes "-怎麼了？ -";
		close;
	}
	end;
}

jupe_core2,288,142,3	script	#Door	844,1,1,{
	end;
OnInit:
OnDisable:
	disablenpc "#Door";
	end;
OnEnable:
	enablenpc "#Door";
	specialeffect EF_MAPPILLAR;
	initnpctimer;
	end;
OnTimer180000:
	donpcevent "#Door::OnDisable";
	end;
OnTouch_:
	mes "- 再次，一個聲音響起 -";
	mes "- 我的頭。這次是 -";
	mes "- 比以前不同，是 -";
	mes "- 令人印象深刻的人的聲音。 -";
	next;
	mes "我知道您走了這麼遠，因為您想要知識。";
	mes "人類是薄弱的動物";
	mes "任何犧牲都是艱難的";
	mes "他們做出的決定。";
	next;
	mes "但是您缺乏經過真理之門。";
	mes "通過這扇門，最重要的是人類思維。";
	next;
	mes "如果沒有超過您的大腦容量，它不會損害人的思想。";
	next;
	mes "嘗試將您的身體放在南部的知識立足點上。";
	next;
	mes "如果知識適合您，您將能夠獲得它而不會成為犧牲。";
	mes "如果您不合格，您將失去身體作為犧牲。";
	next;
	mes "當您有資格時，您將能夠穿過真理的門。";
	mes "直到那天，不要失去自己。";
	next;
	mes "…………";
	next;
	mes "- 我聽不到任何聲音 -";
	mes "-  Anymore和我感覺不到 -";
	mes "- 在這個地方有什麼東西。 -";
	next;
	mes "- 我的聲音";
	mes "- 指示，讓我們去 -";
	mes "- 南部的立足點。 -";
	close2;
	set job__mechanic,9;
	changequest 10098,10099;
	donpcevent "#Door::OnDisable";
	end;
}

jupe_core2,149,34,0	script	#Acquiring Knowledge	139,1,0,{
OnTouch_:
	if (job__mechanic == 9) {
		mes "- 我踩到的那一刻";
		mes "- 立足聲音 -";
		mes "- 沒有進入我的大腦 -";
		mes "- 新知識 -";
		mes "- 自然中的組件。 -";
		next;
		mes "- 幾乎瞬間 -";
		mes "- 您獲得了所有的";
		mes "- 知道您需要 -";
		mes "- 了解力學 -";
		mes "- 和魔術機械。 -";
		next;
		set job__mechanic,10;
		changequest 10099,10100;
		specialeffect EF_POTION_CON;
		mes "- 這是知識的數量 -";
		mes "- 足夠了。讓我們完成 -";
		mes "- 找到出路。 -";
		close;
	}
	end;
}
jupe_core2,150,33,0	duplicate(#Acquiring Knowledge)	#Acquiring Knowledge2	139,1,0

jupe_core2,0,0	monster	Dimik	1669,100
