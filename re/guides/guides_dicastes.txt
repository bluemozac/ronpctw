//===== rAthena Script ======================================= 
//= El Dicastes Guide
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guide for the city of El Dicastes.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Joseph]
//= 1.1 Fixed viewpoint syntax. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

dicastes01,189,191,5	script	Traveler Ichack#info	481,2,2,{
	mes "[旅行者Ichack]";
	mes "哦！ ！你是一個陌生人！";
	mes "我是Ichack，這是第一個以El Dicastes到達這裡的冒險家！";
	next;
	mes "[旅行者Ichack]";
	mes "探險隊中有人聲稱他是第一個，但這是我。這是正確的。";
	next;
	mes "[旅行者Ichack]";
	mes "這就是為什麼我將提供一項服務來指導來自Midgard的冒險家的原因。";
	mes "您想知道什麼？";
	F_Navi("[Traveler Ichack]");
	next;
	switch(select("迪卡斯特斯迪爾:住宅區:訓練室:工廠:酒吧:貓爪商隊")) {
	case 1:
		mes "[旅行者Ichack]";
		mes "啊~那是一棟美麗的建築。";
		mes "據說他們討論了重要的事情...";
		next;
		mes "[旅行者Ichack]";
		mes "請確保您在"+F_Navi("迪爾","dicastes01,198,351")+"，否則您會遇到麻煩。";
		mes "從這裡直接向北走，您不會錯過它。";
		viewpoint 1,198,351,0,0x00FF00;
		close;
	case 2:
		mes "[旅行者Ichack]";
		mes "Sapha部落是如此奇怪。他們怎麼能在那裡睡覺？";
		next;
		mes "[旅行者Ichack]";
		mes "你不明白嗎？";
		mes "直到看到它，您才相信我！自己去看看！";
		next;
		mes "[旅行者Ichack]";
		mes "住宅區分為兩個區域。";
		mes "這"+F_Navi("下層住宅區","dicastes01,136,106")+" 大約在7點鐘方向，而 ";
		mes F_Navi("上區域","dicastes01,280,283")+" 大約在1點鐘方向。";
		viewpoint 1,280,283,1,0x01FF01;
		viewpoint 1,136,106,2,0x0066FF;
		close;
	case 3:
		mes "[旅行者Ichack]";
		mes "由於這裡的危險，我們需要更好的安全性...";
		mes "你去過";
		mes F_Navi("培訓室","dicastes01,255,172")+"？訓練守衛似乎從不睡覺！";
		viewpoint 1,255,172,3,0xFFCCCC;
		close;
	case 4:
		mes "[旅行者Ichack]";
		mes "這是他們住在這裡的最重要原因之一。";
		mes "如果你去"+F_Navi("工廠","dicastes01,97,256")+"，你可以看到最近精煉的布拉迪姆。";
		viewpoint 1,97,256,4,0x0066FF;
		close;
	case 5:
		mes "[旅行者Ichack]";
		mes "儘管我是第一位冒險家，但酒吧的所有者是這裡的第一個人。";
		mes F_Navi("布門弗洛琳的酒吧","dicastes01,136,106")+" 位於下層住宅區的地下。";
		mes "他可能會為您提供一些有價值的信息。";
		viewpoint 1,136,106,5,0xFF3300;
		close;
	case 6:
		mes "[旅行者Ichack]";
		mes "這"+F_Navi("貓","dicastes01,199,39")+" 似乎不像我們一樣與Sapha有良好的關係。";
		mes "也許是因為涉及交易。";
		mes "可憐的貓。他們必須從那裡的寒冷中發抖。";
		next;
		mes "[旅行者Ichack]";
		mes "您可以在大門外找到他們的大篷車。";
		viewpoint 1,199,39,6,0x9933FF;
		close;
	}
OnTouch:
	if (ep13_3_invite == 6) 
		emotion ET_SURPRISE;
	end;
}
