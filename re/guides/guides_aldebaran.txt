//===== rAthena Script ======================================= 
//= Al De Baran Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Al De Baran.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Navigation system update. [Euphy]
//=     Optimized.
//============================================================ 

aldebaran,139,63,4	script	Guide#01aldebaran::GuideAldearan	105,{
	cutin "prt_soldier",2;
	mes "[Al de Baran指南]";
	mes "歡迎來到 ^8b4513al de baran ^000000，";
	mes "邊境城市。";
	mes "您需要幫助城市嗎？";
	F_Navi("[Al De Baran Guide]");
	next;
	set .@str$,"Would you like to check any other locations?";
	while (1) {
		switch(select("[ 主要設施 ]:[ 商人與助手 ]:從迷你地圖移除標記:取消")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ KAFRA總部 ]:[ 煉金術士公會 ]:[ 鐘樓 ]:[ 旅館與工具店 ]:[ 維修與購買裝備 ]:[ 酒館 ]:上一頁")) {
				case 1:
					callsub L_Mark, F_Navi("KAFRA總部","aldebaran,61,229","^B9062F");
					mes .@str$;
					viewpoint 1,61,229,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("煉金術士公會","aldebaran,60,60","^0000FF");
					mes .@str$;
					viewpoint 1,60,60,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("鐘樓","aldebaran,139,133","^FF5400");
					mes .@str$;
					viewpoint 1,139,133,2,0xFFB400;
					next;
					break;
				case 4:
					mes "[Al de Baran指南]";
					mes "讓我標記";
					mes F_Navi("旅館和工具店","aldebaran,197,70","^006400")+" building";
					mes "在您的迷你圖上。";
					mes .@str$;
					viewpoint 1,197,70,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[Al de Baran指南]";
					mes "如果你想";
					mes F_Navi("購買或維修裝備","aldebaran,197,70","^7F3300")+",";
					mes "然後，這是您要參觀的地方。";
					mes .@str$;
					viewpoint 1,72,197,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("酒館","aldebaran,231,106","^800080");
					viewpoint 1,231,106,5,0xDA70D6;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 伊甸園傳送官員 ]:[ KAFRA員工 ]:[ 克勞斯先生 ]:[ 廚師助理 ]:上一頁")) {
				case 1:
					callsub L_Mark, F_Navi("伊甸園傳送官員","aldebaran,133,119","^B9062F");
					mes .@str$;
					viewpoint 1,133,119,6,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, "^0000FFKAFRA員工^000000";
					viewpoint 1,143,119,7,0x0A82FF;
					next;
					break;
				case 3:
					mes "[Al de Baran指南]";
					mes "克勞斯先生可以傳送";
					mes "你去"+F_Navi("盧堤","aldebaran,168,168","^006400")+".";
					mes "讓我標記他的位置";
					mes "在您的迷你圖上。";
					mes .@str$;
					viewpoint 1,168,168,8,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("廚師助理","aldebaran,165,107","^7F3300");
					mes .@str$;
					viewpoint 1,165,107,9,0xD2691E;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Al de Baran指南]";
			mes "當然，我將從您的迷你圖中刪除所有標記。";
			mes "我還有什麼可以為您做的嗎？";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Al de Baran指南]";
			mes "好吧，那，";
			mes "享受您的住宿！";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Al de Baran指南]";
	mes "讓我標記";
	mes getarg(0);
	mes "在您的迷你圖上。";
	return;
}
aldebaran,243,143,2	duplicate(GuideAldearan)	Guide#02aldebaran	105
aldebaran,135,243,4	duplicate(GuideAldearan)	Guide#03aldebaran	105
aldebaran,36,135,6	duplicate(GuideAldearan)	Guide#04aldebaran	105
