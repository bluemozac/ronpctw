//===== rAthena Script ======================================= 
//= Izlude Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Izlude.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Updated to match the new Izlude Map. [Masao]
//= 1.2 Added Renewal Izlude viewpoint coordinates.
//= 1.3 Navigation system update. [Euphy]
//=     Optimized.
//= 1.4 Added Izlude duplicates. [Euphy]
//============================================================ 

-	script	::GuideIzlude	-1,{
	cutin "prt_soldier",2;
	mes "[izlude指南]";
	mes "歡迎來到 ^8b4513伊斯魯德 ^000000，";
	mes "普隆德拉的衛星小鎮。";
	mes "您需要幫助瀏覽城鎮嗎？";
	F_Navi("[Izlude Guide]");
	next;
	set .@map$, strnpcinfo(4);
	while (1) {
	switch(select("[ 主要設施 ]:[ 商人與助手 ]:從迷你地圖移除標記:取消")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 克魯普拉學院 ]:[ 碼頭 ]:[ 國際飛艇 ]:[ 競技場 ]:[ 工具商店 ]:[ 劍士公會 ]:[ 附魔協會 ]:[ 鍛造廠 ]:上一頁")) {
				case 1:
					mes "[izlude指南]";
					mes "新手冒險家應該去"+F_Navi("克魯普拉學院","iz_ac01,100,39")+" 接受更多資訊和訓練！";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[izlude指南]";
					mes "您可以使用"+F_Navi("碼頭",.@map$+",197,205","^B9062F");
					mes "轉到 ^8B4513艾爾貝塔 ^000000或 ^8B4513拜揚島 ^000000。";
					mes "當心 ^8b4513伊斯魯德地下城 ^000000";
					mes "在拜揚島上，因為它是";
					mes "充滿可怕的怪物。";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,197,205,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[izlude指南]";
					mes "這"+F_Navi("國際飛艇",.@map$+",202,75","^0000FF");
					mes "轉到 ^8B4513朱諾 ^000000和 ^8B4513拉赫 ^000000。";
					mes "讓我標記它的位置";
					mes "在您的迷你圖上。";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,210,73,2,0x0A82FF;
					next;
					break;
				case 4:
					mes "[izlude指南]";
					mes "這"+F_Navi("競技場",.@map$+",213,161","^FF5400");
					mes "是一個理想的地方";
					mes "測試您的戰鬥能力。";
					mes "讓我標記它的位置";
					mes "在您的迷你圖上。";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,213,161,3,0xFF5400;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("工具商店",.@map$+",110,182","^006400");
					viewpoint 1,108,179,4,0xAAFF00;
					next;
					break;
				case 6:
					mes "[izlude指南]";
					mes "這"+F_Navi("劍士公會",.@map$+",51,172")+" 是您可以 ^800080轉職為劍士^000000 的地方。";
					mes "讓我標記它的位置";
					mes "在您的迷你圖上。";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("附魔協會",.@map$+",97,125","^B9062F");
					viewpoint 1,97,125,6,0xDA70D6;
					next;
					break;
				case 8:
					callsub L_Mark, F_Navi("鍛造廠",.@map$+",153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 9:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 伊甸園傳送官員 ]:[ 卡普拉員工 ]:[ 催眠師 ]:[ 新婚助手 ]:[ 傭兵公會成員 ]:[ 寵物美容師 ]:上一頁")) {
				case 1:
					mes "[izlude指南]";
					mes "這"+F_Navi("伊甸園傳送官員",.@map$+",131,148","^0000FF");
					mes "目前呆在裡面";
					mes "劍客公會大樓。";
					mes "讓我在您的迷你圖上標記他的位置。";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,131,148,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("卡普拉員工",.@map$+",128,148","^FF5400"),1;
					viewpoint 1,128,148,9,0xFF5400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("催眠師",.@map$+",127,175"),1;
					viewpoint 1,127,175,10,0x006400;
					next;
					break;
				case 4:
					mes "[izlude指南]";
					mes "如果你想去度蜜月";
					mes "^8b4513爪哇伊^000000，新婚夫婦的天堂，";
					mes "請與";
					mes F_Navi("新婚助手",.@map$+",180,224","^B9062F")+".";
					mes "您想檢查其他任何位置嗎？";
					viewpoint 1,180,224,11,0xFF0000;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("傭兵公會成員",.@map$+",47,170","^0000FF");
					viewpoint 1,47,170,12,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("寵物美容師","izlude_in,72,98"),1;
					viewpoint 1,108,179,13,0x006400;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[izlude指南]";
			mes "當然，我將從您的迷你圖中刪除所有標記。";
			mes "我還有什麼可以為您做的嗎？";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 4:
			mes "[izlude指南]";
	mes "享受你的住宿。";
	mes "再見~！";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[izlude指南]";
	if (getarg(1,0)) {
		mes "讓我標記";
		mes getarg(0);
	} else
		mes "讓我標記"+getarg(0);
	mes "在您的迷你圖上。";
	mes "您想檢查其他任何位置嗎？";
	return;
}

izlude,129,175,4	duplicate(GuideIzlude)	Guide#01izlude	105
izlude_a,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_a	105
izlude_b,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_b	105
izlude_c,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_c	105
izlude_d,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_d	105

izlude,133,113,4	duplicate(GuideIzlude)	Guide#02izlude	105
izlude_a,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_a	105
izlude_b,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_b	105
izlude_c,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_c	105
izlude_d,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_d	105

//izlude,120,206,4	duplicate(GuideIzlude)	Guide#03izlude	105
