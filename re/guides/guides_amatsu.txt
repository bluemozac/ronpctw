//===== rAthena Script ======================================= 
//= Amatsu Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guide for the city of Amatsu.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

amatsu,202,91,3	script	Amatsu Guide#amatsu	758,{
	mes "[阿瑪昌]";
	mes "歡迎來到 ^8B4513AMATSU ^000000，";
	mes "善良的鎮";
	mes "和美麗的櫻花。";
	next;
	mes "[阿瑪昌]";
	mes "我是Amachang，";
	mes "第13小姐阿瑪蘇小姐。";
	mes "我會指導您有關城鎮的信息";
	mes "作為Amatsu小姐。";
	mes "請告訴我";
	mes "如果您想知道一些東西。";
	F_Navi("[Amachang]");
	while (1) {
		next;
		switch(select("[ KAFRA員工 ]:[ 酒吧 ]:[ 武器商人 ]:[ 工具商人 ]:[ 天城宮殿 ]:[ 廚師助理 ]:[ 忍者公會大樓 ]:[ 船長 ]:從迷你地圖移除標記:取消")) {
		case 1:
			callsub L_Mark, "^0000FFKAFRA員工^000000";
			viewpoint 1,102,149,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark, F_Navi("酒吧","amatsu,215,116","^006400");
			viewpoint 1,215,116,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("武器商人","amatsu,129,117","^008080");
			viewpoint 1,129,117,2,0x008080;
			break;
		case 4:
			callsub L_Mark, F_Navi("工具商人","amatsu,97,117","^FF1493");
			viewpoint 1,97,117,3,0xFF1493;
			break;
		case 5:
			callsub L_Mark, F_Navi("天城宮殿","amatsu,87,235","^8B4513");
			viewpoint 1,87,235,4,0x8B4513;
			break;
		case 6:
			callsub L_Mark, F_Navi("廚師助理","amatsu,206,150","^8A2BE2");
			viewpoint 1,206,150,5,0x8A2BE2;
			break;
		case 7:
			callsub L_Mark, F_Navi("忍者公會大樓","amatsu,148,137","^4B0082");
			viewpoint 1,148,137,6,0x4B0082;
			break;
		case 8:
			callsub L_Mark, F_Navi("船長","amatsu,195,79","^00BFFF");
			viewpoint 1,195,79,7,0x00BFFF;
			break;
		case 9:
			mes "[阿瑪昌]";
			mes "我將從您的迷你圖中刪除所有標記。";
			mes "我還有什麼可以為您做的嗎？";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			viewpoint 2,1,1,4,0xFFFFFF;
			viewpoint 2,1,1,5,0xFFFFFF;
			viewpoint 2,1,1,6,0xFFFFFF;
			viewpoint 2,1,1,7,0xFFFFFF;
			break;
		case 10:
			mes "[阿瑪昌]";
			mes "享受您的旅行~!!";
			close;
		}
	}
	end;
L_Mark:
	mes "[阿瑪昌]";
	mes "這"+getarg(0)+" is";
	mes "在您的迷你圖上標記。";
	mes "我還有什麼可以為您做的嗎？";
	return;
}
