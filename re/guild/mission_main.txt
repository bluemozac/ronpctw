//===== rAthena Script =======================================
//= Guild Mission - Core Script
//===== Description: =========================================
//= Template for guild mission added in episode 14.3
//===== Changelogs: ==========================================
//= [Official Conversion]
//= 1.0 First version [Secretdataz]
//= 1.1 Fixed potential exploit from original Aegis script. [Secretdataz]
//============================================================

-	script	gld_mission#core	-1,{
function brief;
function mob_brief;
function calcexp;
function calcjobexp;

	if (!checkweight(1201,3)){
		mes "您有太多的項目可以進行任務。";
		mes "請減輕您的負載，然後重試。";
		close;
	}

	.@zone$ = strnpcinfo(2);
	if(.@zone$ == "gqaldeg"){
		.@zoneid = 3;
		.@edition = 1;
	} else if(.@zone$ == "gqaru"){
		.@zoneid = 4;
		.@edition = 2;
	} else if(.@zone$ == "gqgefg"){
		.@zoneid = 2;
		.@edition = 1;
	} else if(.@zone$ == "gqpayg"){
		.@zoneid = 1;
		.@edition = 1;
	} else if(.@zone$ == "gqprtg"){
		.@zoneid = 0;
		.@edition = 1;
	} else if(.@zone$ == "gqsch"){
		.@zoneid = 5;
		.@edition = 2;
	} else if(.@zone$ == "gqtealde"){
		.@zoneid = 6;
		.@edition = 3;
	} else if(.@zone$ == "gqteprt"){
		.@zoneid = 7;
		.@edition = 3;
	} else {
		// Custom error message.
		debugmes "從未知的激動區域調用的公會任務腳本[" + .@zone$ + "].";
		end;
	}
	.@npcname$ = .npcname$[.@zoneid];

	mes .@npcname$;
	mes "我們提供您可以在該地區完成的任務和每日任務" + .region$[.@zoneid] + ".";
	mes "您只需要在攻城時間內完成簡單的日常任務和任務。所以，考慮一下~";
	next;
	switch(select("征服" + .region$[.@zoneid], "公會地下城每日任務", "關於任務", "我不需要它。")){
		case 1: // Conquering.
			.@gid = getcharid(2);
			if(.@gid && is_guild_leader() == true){
				.@time_check1 = checkquest(.conquer_delay_questid[.@zoneid], PLAYTIME);
				if(.@time_check1 == 0){
					mes .@npcname$;
					mes "自我們上次檢查以來已經不到一個小時了！";
					mes "等待時間結束後回來。";
					next;
					mes .@npcname$;
					mes "如果您在自己的身邊很幸運，您會取得良好的成績。";
					close;
				} else if(.@time_check1 == 2){
					erasequest .conquer_delay_questid[.@zoneid];
					mes .@npcname$;
					mes "我已經確認征服任務的等待時間已經結束。";
					mes "我將為您刪除不必要的記錄。";
					close;
				} else {
					.@siege_check = (.@edition == 1 ? agitcheck() : .@edition == 2 ? agitcheck2() : agitcheck3());
					.@time = atoi(gettimestr("%H%M",4));
					.@after_time = ((.weekday[.@zoneid] == gettime(4)) && ((.@time > .start_time[.@zoneid]) && (.@time < .end_time[.@zoneid])));

					if(.@siege_check && .@after_time){
						mes .@npcname$;
						mes "根據 ^4d4dff，您的行會在這一刻征服的要塞數量， ^000000我會給您一個獎勵。";
						mes "您現在要檢查嗎？您知道 ^4d4dff即使我們說話時數字也會改變， ^000000對嗎？";
						next;
						if(select("我以後會回來的。","我們現在就來檢查一下吧！") == 1){
							mes .@npcname$;
							mes "正確的。生活就是關於時機的。";
							mes "當它發光最大的時候回來。";
							close;
						} else {
							.@total = 0;
							for(.@i = 0; .@i < 5; ++.@i){
								.@total += (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID));
							}

							if(!.@total){
								mes .@npcname$;
								mes "不幸的是，您的公會沒有征服的要塞。";
								mes "再試一次！";
								close;
							} else {
								mes .@npcname$;
								if(.@total == 5)
									mes "極好的。";
								else
									mes "目前，您的公會征服了要塞總數";
								for(.@i = 0; .@i < 5; ++.@i){
									mes getcastlename(.castle$[.@i+.@zoneid*5]) + (.@gid == getcastledata(.castle$[.@i+.@zoneid*5],CD_GUILD_ID) ? "： ^4d4dffocupied ^000000" : "");
								}
								if(.@total == 5)
									mes "你的公會征服了" + .region$[.@zoneid] + "!";
								else
									mes "是" + .@total + "!";
								next;
								mes .@npcname$;
								mes "我會給你一個獎勵。希望您能很好地利用它。";
								mes "您有一個小時的等待時間，直到下一個報告。請記住這一點，然後重試。";
								setquest .conquer_delay_questid[.@zoneid];
								getitem 6489,.@total;
								getitem 6615,.@total;
								getitem 12888,.@total;
								close;
							}
						}
					} else {
						mes .@npcname$;
						mes "對不起，但是您只能在攻城時間完成此任務。恐怕在這裡我什麼都不做。";
						close;
					}
				}
			} else {
				mes .@npcname$;
				mes "對不起，但是征服任務僅針對公會大師。";
				mes "成員應改用公會地牢每日任務。";
				close;
			}
			break;
		case 2: // Daily Quests
			mes .@npcname$;
			mes "這些是您可以在行會地牢中完成的任務" + .region$[.@zoneid] + " area.";
			mes "如果您願意，可以取消它們。隨意挑戰自己。";
			next;
			.@cancel_index = (.@edition == 1 ? 3 : 2);
			for(.@i = 0; .@i < 3; ++.@i){
				if(.daily_hunting_questid[.@zoneid*3 + .@i])
					.@menu$ = .@menu$ + .region$[.@zoneid] + " Mission " + (.@i+1) + (isbegin_quest(.daily_hunting_questid[.@zoneid*3 + .@i]) ? " ^4d4dffCheck the result^000000" : "") + ":";
			}
			.@sel = select(.@menu$) - 1;
			.@index = .@zoneid*3 + .@sel;
			if(.@sel == .@cancel_index){
				mes .@npcname$;
				mes "您不需要著急，因為這些任務是在公會地牢中進行的。";
				mes "隨時隨時嘗試一下。";
				close;
			}
			if(BaseLevel < .level_req[.@index]){
				mes .@npcname$;
				mes "嗯，不幸的是，此任務僅限於 ^4d4dfflevel的用戶" + .level_req[.@index] + "^000000 or higher only.";
				mes "您的水平不夠高。";
				close;
			}
			.@hunting = checkquest(.daily_hunting_questid[.@index], HUNTING);
			.@playtime = checkquest(.daily_delay_questid[.@index], PLAYTIME);
			if(.@hunting > -1){
				if(.@hunting == 2){
					mes .@npcname$;
					mes "哦，你做得很好。";
					mes "這並不多，但希望它會有所幫助。";
					setquest .daily_delay_questid[.@index];
					erasequest .daily_hunting_questid[.@index];
					getexp calcexp(.@zoneid,.@index),calcjobexp(.@edition);
					getitem 6615,1;
				} else {
					mes .@npcname$;
					mes "唔？似乎您還沒有完成任務。";
					mes "有問題嗎？";
					next;
					if(select("不。","放棄任務。") == 1){
						mes .@npcname$;
						mes "我不確定您可以使用行會地牢多久，但是祝您好運。";
						close;
					} else {
						mes .@npcname$;
						mes "好吧，沒有人強迫你。";
						mes "做您想做的事。";
						mes "那我將取消任務。";
						erasequest .daily_hunting_questid[.@index];
						close;
					}
				}
			} else {
				if(.@playtime == 0 || .@playtime == 1){
					mes .@npcname$;
					mes "該規則在此處設置，以便您每天只能完成一個任務。";
					mes "我希望您在等待時間結束時回來。";
					close;
				} else if(.@playtime == 2){
					erasequest .daily_delay_questid[.@index];
					mes .@npcname$;
					mes "我已經確認該任務的等待時間已經結束。";
					mes "我將刪除用於確認的記錄。";
					mes "現在，嘗試完成任務！";
					close;
				} else {
					mes .@npcname$;
					mes "這是" + .ordinal$[.@sel] + " mission in " + .region$[.@zoneid] + ".";
					brief(.daily_hunting_questid[.@index]);
					next;
					mes .@npcname$;
					if(mob_brief(.daily_hunting_questid[.@index]))
						mes "完成此任務後，我會相應地獎勵您。";
					else
						mes "我會給你一些回報。";
					next;
					if(select("接受它。","拒絕它。") == 1){
						mes .@npcname$;
						mes "公會地牢的入口受到限制。但這也不是您也找不到去那裡的方法。";
						mes "我為您的成功與這些怪物作鬥爭。";
						setquest .daily_hunting_questid[.@index];
						close;
					} else {
						mes .@npcname$;
						mes "好吧，按照您的意願做。";
						close;
					}
				}
			}
			break;
		case 3: // About quests
			mes .@npcname$;
			mes "一點都不困難。";
			mes "我對您征服了多少要塞給予獎勵 ^4D4DFFDEPEDEND。";
			mes "^4d4dff獎勵的類型和質量取決於我檢查那一刻被征服的要塞的數量。 ^000000";
			next;
			mes .@npcname$;
			mes "當然，我僅在攻城時間檢查。";
			mes "^4d4dffreceiving獎勵之後是一個小時的等待時間。因此，您最好也找出正確的時機。 ^000000";
			next;
			mes .@npcname$;
			mes "如果您是當前參加圍困戰的公會大師，則應該嘗試一下。";
			next;
			mes .@npcname$;
			mes "公會地牢每日任務包括您可以在公會地牢中完成的戰鬥任務。";
			mes "很簡單。";
			close;
		case 4: // I don't need it.
			mes .@npcname$;
			mes "你真的是其他~！";
			close;
	}
	end;

OnInit:
	setarray .castle$[0],
		"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
		"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
		"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
		"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
		"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
		"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05",
		"te_aldecas1","te_aldecas2","te_aldecas3","te_aldecas4","te_aldecas5",
		"te_prtcas01","te_prtcas02","te_prtcas03","te_prtcas04","te_prtcas05";
	setarray .npcname$[0],
		"[Altir]","[Alshine]","[Denev]","[Tarazed]","[Mirah]","[Almark]","[Becrux]","[Acrux]";
	setarray .region$[0],
		"Valkyrie Realm","Greenwood Lake","Britoria","Luina","Valfreyja","Nidhoggur","Kafragarten","Gloria";
	setarray .weekday[0], // Day of the week. 0 = Sunday, 6 = Saturday.
		0,0,0,0,0,0,6,6;
	setarray .start_time[0],
		2200,2200,2200,2200,2200,2200,2200,2200;
	setarray .end_time[0],
		2230,2230,2230,2230,2230,2230,2230,2230;
	setarray .conquer_delay_questid[0], // Quest ID for cooldowns
		7517,7520,7519,7518,7522,7521,7524,7523;
	setarray .level_req[0], // Level requirement for quests. There are 2-3 quests per region
		100,120,120,	//prt
		100,120,120,	//pay
		100,120,120,	//gef
		100,120,120,	//alde
		90,90,0,		//aru
		90,90,0,		//sch
		70,70,0,		//tealde
		70,70,0;		//teprt
	setarray .max_level[0], // Exp increase cap for quests. 0 for unlimited/not exist
		130,140,0,	//prt
		130,140,0,	//pay
		130,140,0,	//gef
		130,140,0,	//alde
		120,120,0,	//aru
		120,120,0,	//sch
		99,99,0,	//tealde
		99,99,0;	//teprt
	setarray .daily_hunting_questid[0], //Quest ID for daily quests.
		7525,7526,7527,	//prt
		7534,7535,7536,	//pay
		7531,7532,7533,	//gef
		7528,7529,7530,	//alde
		7539,7540,0,	//aru
		7537,7538,0,	//sch
		7561,7562,0,	//tealde
		7541,7542,0;	//teprt
	setarray .daily_delay_questid[0],	//Quest ID for mob hunting check.
		7543,7544,7545,	//prt
		7552,7553,7554,	//pay
		7549,7550,7551,	//gef
		7546,7547,7548,	//alde
		7557,7558,0,	//aru
		7555,7556,0,	//sch
		7563,7564,0,	//tealde
		7559,7560,0;	//teprt
	setarray .ordinal$[0],"first","second","third";
	end;

	function brief {
		switch(getarg(0)){
			case 7527:
			case 7530:
			case 7533:
			case 7536:
				mes "它是處理出現在深淵走廊中的強大生物。";
				break;
			default:
				mes "它是消除出現在公會地牢中的怪物。";
				break;
		}
		return;
	}

	function mob_brief {
		switch(getarg(0)){
			case 7525:
				mes "您應該狩獵50或更多的毛毛蟲和奶油恐懼怪物。";
				break;
			case 7526:
				mes "在所有3種類型的深淵Kobolds中尋找30個或更多。";
				break;
			case 7527:
				mes "^4d4dffyou應該殺死憤怒的'pyuriel'和戰士'lora'。 ^000000";
				return 1;
			case 7528:
				mes "^4d4dffhunt分別為50或更多的巨型黃蜂和古老的蠕蟲。 ^000000";
				break;
			case 7529:
				mes "^4d4dfffhunt 40或更多的殺手和50個或更多的angra mantises。 ^000000";
				break;
			case 7530:
				mes "它們是 ^4d4dfff'elvira'和'gioia'，這是英雄眼淚的奇怪機械生物。 ^000000";
				return 1;
			case 7531:
				mes "^4d4dfffhunt分別為50或更多的殭屍大師和幽靈死亡。 ^000000";
				break;
			case 7532:
				mes "^4d4dfffhunt 50 gld_dark_shadow，20 gld_dark_frame和20 gld_dark_priest怪物。 ^000000";
				break;
			case 7533:
				mes "他們是 ^4d4dfff'kades'和'rudo'，在死者的山上徘徊。 ^000000";
				return 1;
			case 7534:
				mes "^4d4dfffhunt分別為50或更多Gullinbursti和Leib Olmai。 ^000000";
				break;
			case 7535:
				mes "^4d4dffhunt 45或更多骨架將軍，20或更多AM MUT和25或更多Gajomart。 ^000000";
				break;
			case 7536:
				mes "^4d4dffthey是舊帝國的一般“ daehyon”和守衛“ soheon”。 ^000000";
				return 1;
			case 7537:
				mes "^4d4dfffhunt 50或更多地獄啟示錄和Zakudam。 ^000000";
				break;
			case 7538:
				mes "^4d4dffhunt分別為35或更多重金屬和鈷礦物。 ^000000";
				break;
			case 7539:
				mes "^4d4dffhunt超過50個女妖大師和30個旁觀者大師。 ^000000";
				break;
			case 7540:
				mes "^4d4dffhunt 50 aunoes和20個粉絲。 ^000000";
				break;
			case 7541:
				mes "^4d4dfffhunt 30或更多騎士，鐵匠，刺客。 ^000000";
				break;
			case 7542:
				mes "^4d4dfffhunt 30或更多嚮導，牧師，獵人。 ^000000";
				break;
			case 7561:
				mes "^4d4dffhunt 30或更多，十字軍，流氓，煉金術士。 ^000000";
				break;
			case 7562:
				mes "^4d4dfffhunt 30或更多Sage，Monk，Bard。 ^000000";
				break;
		}
		return 0;
	}
	
	function calcexp {
		.@zoneid = getarg(0);
		.@index = getarg(1);
		.@maxlevel = .max_level[.@index];
		.@isboss = (.@zoneid < 4 && !((.@index+1)%3));
		
		if(!.@isboss && BaseLevel > .@maxlevel){
			return 1000 * (100 + ((.@maxlevel - 100) * 3));
		} else {
			return 1000 * (100 + ((BaseLevel - 100) * 3)) * (1 + .@isboss);
		}
	}
	
	function calcjobexp {
		.@edition = getarg(0);
		
		if(.@edition == 3){
			if(jobcanentermap("te_prtcas01")){
				if (JobLevel > 50)
					return 1000 * (50 + ((JobLevel - 50) * 3));
				else
					return 5000;
			} else {
				debugmes "工作意外情況:" + Class + "(" + jobname(Class) + ") for gld_mission#core::calcjobexp";
				return 0;
			}
		} else if(.@edition == 2 ) {
			if(JobLevel > 50)
				return 1000 * (50 + ((JobLevel - 50) * 3));
			else
				return 3000;
		} else {
			return 5000 * JobLevel;
		}
		return 0;
	}
}
