//===== rAthena Script =======================================
//= Cute Pet Manager
//===== By: ==================================================
//= Z3R0
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Exchanges Hunted Items for Tames
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//= 1.1 Little clean-up & added a delitem. [Capuche]
//============================================================

// 1st NPC
-	script	CPM1	-1,{
	
	set .@npc$, "[Cute Pet Manager]";
	
	setarray .@tame_id[0],619,620,622,623,624,627,628,629,630;
	setarray .@tame_amount[0],3,3,3,3,3,3,3,3,3;
	setarray .@hunt_id[0],909,909,705,916,935,919,919,940,921;
	setarray .@hunt_amount[0],500,600,500,500,500,500,600,500,500;
	setarray .@mob_id[0],1002,1113,1063,1049,1011,1167,1107,1052,1014;
	set .@tame_gets, 3;

	if (cpm_one == .@tame_gets) {
		mes "[可愛的寵物經理]";
		mes "你已經交換了";
		mes .@tame_amount + "馴服物品的時間。";
		mes "您不能再交換了。";
		next;
		
		mes "[可愛的寵物經理]";
		mes "除了我，還有其他";
		mes "可愛的寵物經理。如果你想";
		mes "我建議馴服其他寵物";
		mes "你去看他們。";
		close;
	}
	
	callfunc "cute_pet_manager",.@tame_id,.@tame_amount,.@hunt_id,.@hunt_amount,.@mob_id,cpm_one,.@tame_gets,.@hunt_id2,.@hunt_amount2;

}

// 2nd NPC
-	script	CPM2	-1,{
	
	set .@npc$, "[Cute Pet Manager]";
	
	setarray .@tame_id[0],621,625,632,631,633,634,635,636,659,637,639,640;
	setarray .@tame_amount[0],2,2,2,2,2,2,2,2,2,2,2,2;
	setarray .@hunt_id[0],938,935,925,7033,945,942,931,901,1094,1021,936,1035;
	setarray .@hunt_amount[0],500,600,500,300,500,500,500,500,500,500,500,500;
	setarray .@mob_id[0],1031,1042,1019,1077,1056,1057,1023,1026,1188,1110,1029;
	set .@tame_gets, 2;

	if (cpm_two == .@tame_gets) {
		mes "[可愛的寵物經理]";
		mes "你已經交換了";
		mes .@tame_amount + "馴服物品的時間。";
		mes "您不能再交換了。";
		next;
		
		mes "[可愛的寵物經理]";
		mes "除了我，還有其他";
		mes "可愛的寵物經理。如果你想";
		mes "我建議馴服其他寵物";
		mes "你去看他們。";
		close;
	}
	
	callfunc "cute_pet_manager",.@tame_id,.@tame_amount,.@hunt_id,.@hunt_amount,.@mob_id,cpm_two,.@tame_gets,.@hunt_id2,.@hunt_amount2;

}

// 3rd NPC
-	script	CPM3	-1,{
	
	set .@npc$, "[Cute Pet Manager]";
	
	setarray .@tame_id[0],638,626,641,661,660,642;
	setarray .@tame_amount[0],1,1,1,1,1,1;
	setarray .@hunt_id[0],1020,943,1038,7047,1970,923;
	setarray .@hunt_amount[0],600,600,600,500,1,100;
	setarray .@hunt_id2[0],0,0,0,0,7017,0;
	setarray .@hunt_amount2[0],0,0,0,0,20,0;
	setarray .@mob_id[0],1170,1035,1109,1275,1200,1101;
	set .@tame_gets, 1;

	if (cpm_three == .@tame_gets) {
		mes "[可愛的寵物經理]";
		mes "你已經交換了";
		mes .@tame_amount + "馴服物品的時間。";
		mes "您不能再交換了。";
		next;
		
		mes "[可愛的寵物經理]";
		mes "除了我，還有其他";
		mes "可愛的寵物經理。如果你想";
		mes "我建議馴服其他寵物";
		mes "你去看他們。";
		close;
	}
	
	callfunc "cute_pet_manager",.@tame_id,.@tame_amount,.@hunt_id,.@hunt_amount,.@mob_id,cpm_three,.@tame_gets,.@hunt_id2,.@hunt_amount2;

}

function	script	cute_pet_manager	{
	// getarg(0) -> .@tame_id Array
	// getarg(1) -> .@tame_amount Array
	// getarg(2) -> .@hunt_id Array
	// getarg(3) -> .@hunt_amount Array
	// getarg(4) -> .@mob_id Array
	// getarg(5) ->  Attempt Variable
	// getarg(6) -> .@tame_gets Variable
	// getarg(7) -> .@hunt_id2 Array
	// getarg(8) -> .@hunt_amount2 Array

	mes "[可愛的寵物經理]";
	mes "你好~！我是一個可愛的寵物經理";
	mes "負責公共關係的人";
	mes "對於新升級的可愛寵物系統。";
	next;
	
	mes "[可愛的寵物經理]";
	mes "如果您對";
	mes "我在這裡指導您的可愛寵物系統。";
	mes "讓我知道哪個馴服";
	mes "您想要的項目，";
	mes "那我會告訴你怪物";
	mes "您可以馴服該項目。";
	next;
	
	mes "[可愛的寵物經理]";
	mes "我還將解釋什麼材料";
	mes "您需要帶來";
	mes "獲取馴服物品。";
	next;
	
	mes "[可愛的寵物經理]";
	mes "如果您已經帶來了材料";
	mes "為了換取馴服物品，";
	mes "我可以立即將其換成馴服物品。";
	next;
	
	mes "[可愛的寵物經理]";
	mes "但是，有一個極限";
	mes "用於交換馴服物品。";
	mes "你只能得到馴服物品";
	mes getarg(6) + "時代。所以請確保";
	mes "您交換了多少次。";
	next;
	
	mes "[可愛的寵物經理]";
	mes "那麼，您想要哪個馴服物品？";
	mes "請從列表中選擇一個。";
	next;
	
	// Create Menu System
	set .@size, getarraysize(getarg(0));
	for (set .@a, 0; .@a < .@size; set .@a, .@a + 1)
		set .@menu$, .@menu$ + (.@menu$ == "" ? "" : ":") + getitemname(getelementofarray(getarg(0), .@a));
	
	// Query Player Choice
	set .@choice, select(.@menu$) - 1;

	// Store Variables (Less Lookup)
	set .@tame_id, getelementofarray(getarg(0), .@choice);
	set .@tame_amount, getelementofarray(getarg(1), .@choice);
	set .@hunt_id, getelementofarray(getarg(2), .@choice);
	set .@hunt_amount, getelementofarray(getarg(3), .@choice);
	set .@mob_id, getelementofarray(getarg(4), .@choice);
	set .@hunt_id2, getelementofarray(getarg(7), .@choice);
	set .@hunt_amount2, getelementofarray(getarg(8), .@choice);

	// dispbottom "Tame ID: " + .@tame_id;
	// dispbottom "Tame Amount: " + .@tame_amount;
	// dispbottom "Hunt ID: " + .@hunt_id;
	// dispbottom "Hunt Name: " + getitemname(.@hunt_id);
	// dispbottom "Hunt Amount: " + .@hunt_amount;
	// dispbottom "Mob ID: " + .@mob_id;
	if (.@hunt_id2) { 
		// dispbottom "Hunt ID2: " + .@hunt_id2;
		// dispbottom "Hunt Amount2: " + .@hunt_amount2;
		set .@hunt2_count, countitem(.@hunt_id2);
	}
	if (countitem(.@hunt_id) >= .@hunt_amount && countitem(6083) > 0 && .@hunt2_count >= .@hunt_amount2) {
		mes "[可愛的寵物經理]";
		mes "啊，你收集了所有物品";
		mes "和你的朋友。您現在可以";
		mes "交換" + getitemname(.@tame_id) + ".";
		mes "你想和我交流嗎？";
		next;
		if  (select("交換:不交換") == 2) { 
			mes "[可愛的寵物經理]";
			mes "你不是嗎？你會再次回來。";
			close;
		}
		delitem .@hunt_id, .@hunt_amount;
		delitem 6083, 1;
		if (.@hunt_id2)
			delitem .@hunt_id2, .@hunt_amount2;
		set getarg(5), getarg(5) + 1;
		getitem .@tame_id, .@tame_amount;
		
		mes "[可愛的寵物經理]";
		mes "明智的選擇。";
		mes "希望您和您的寵物相處。";
		close;
	} else {
		mes "[可愛的寵物經理]";
		mes "你選擇了" + getitemname(.@tame_id) + ".";
		mes "您可以將其用於馴服";
		mes "^ff0000" + strmobinfo(1,.@mob_id) + "^000000 monsters.";
		next;
		mes "[可愛的寵物經理]";
		mes "帶上 ^ff0000" + .@hunt_amount + " " + getitemname(.@hunt_id) + "^000000" + (.@hunt_id2 ? ", ^FF0000" + .@hunt_amount2 + " " + getitemname(.@hunt_id2) : "") + " ^000000 and ^FF00001 Dolly Capsule^000000";
		mes "那你可以交換";
		mes "對於一個" + getitemname(.@tame_id) + ".";
		close;
	}
}

// NPC Duplicates
prontera,67,212,5	duplicate(CPM1)	Cute Pet Manager#1	4_M_ALCHE_C
prontera,242,92,3	duplicate(CPM2)	Cute Pet Manager#2	4_M_ALCHE_C
prontera,179,92,3	duplicate(CPM3)	Cute Pet Manager#3	4_M_ALCHE_C

geffen,180,125,5	duplicate(CPM1)	Cute Pet Manager#4	4_M_ALCHE_C
geffen,152,66,3	duplicate(CPM2)	Cute Pet Manager#5	4_M_ALCHE_C
geffen,197,95,3	duplicate(CPM3)	Cute Pet Manager#6	4_M_ALCHE_C

morocc,115,83,5	duplicate(CPM1)	Cute Pet Manager#7	4_M_ALCHE_C
morocc,218,130,3	duplicate(CPM2)	Cute Pet Manager#8	4_M_ALCHE_C
morocc,236,225,3	duplicate(CPM3)	Cute Pet Manager#9	4_M_ALCHE_C

payon,109,278,5	duplicate(CPM1)	Cute Pet Manager#10	4_M_ALCHE_C
payon,157,124,3	duplicate(CPM2)	Cute Pet Manager#11	4_M_ALCHE_C
payon,85,237,3	duplicate(CPM3)	Cute Pet Manager#12	4_M_ALCHE_C
