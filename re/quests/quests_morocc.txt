//===== rAthena Script =======================================
//= Morocc Quest NPCs
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Quest NPCs related to Morocc.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.0a Gathered remaining dialogues. [Euphy]
//= 1.1 Added new warpers for Satan Morocc maps. [Euphy]
//============================================================

// Stop Post Quest
// ============================================================
morocc,152,167,4	duplicate(William)	William#moc	89

// Binoculars Quest
// ============================================================
morocc,169,168,4	duplicate(Alchemist)	Alchemist#moc	64

// Resurrection of Satan Morocc
// ============================================================
-	script	Continental Guard#man	-1,{
	mes "[大陸警衛]";
	mes "由於已經檢測到了不尋常的時空現象，因此我們已經收到了總部的訂單以阻止對該區域的訪問。";
	next;
	mes "[大陸警衛]";
	mes "如果您真的想去下一個區域，您將為自己的安全負責。";
	next;
	switch(select("沒關係。 :進入領域。")) {
	case 1:
		mes "[大陸警衛]";
		mes "明智的選擇。";
		mes "有一個安全的旅程。";
		close;
	case 2:
		mes "[大陸警衛]";
		mes "我懂了。";
		mes "我很快就會將您送到田野。";
		close2;
		warp "moc_fild20",208,207;
		end;
	}
}
moc_fild07,380,202,1	duplicate(Continental Guard#man)	Continental Guard#man1	852
moc_fild11,189,360,6	duplicate(Continental Guard#man)	Continental Guard#man2	852
prt_fild10,263,23,1	duplicate(Continental Guard#man)	Continental Guard#man3	852
prt_fild09,95,19,1	duplicate(Continental Guard#man)	Continental Guard#man4	852
prt_fild09,246,17,1	duplicate(Continental Guard#man)	Continental Guard#man5	852
moc_fild13,32,171,6	duplicate(Continental Guard#man)	Continental Guard#man6	852
moc_fild16,124,381,4	duplicate(Continental Guard#man)	Continental Guard#man7	852
moc_fild16,333,380,4	duplicate(Continental Guard#man)	Continental Guard#man8	852
moc_fild01,84,19,1	duplicate(Continental Guard#man)	Continental Guard#man9	852

morocc,176,101,3	duplicate(Continental Official)	Continental Official#mor	754

// Pyramids Nightmare Mode :: nightmare_pry
//============================================================
moc_prydb1,103,54,3	script	Suspicious Cat#night	547,{
	mes "[可疑貓]";
	mes "我不敢相信媽媽可以對我這樣做。哦，伙計，很痛。沒有人應該發現這個。";
	next;
	if(select("木乃伊？發生了什麼事？ :假裝什麼都不知道。") == 2)
		close;
	mes "[可疑貓]";
	mes "你抓住了我！該死的...我沒想到我作為古墓麗影的聲譽會受到這樣的損害...";
	next;
	mes "[可疑貓]";
	mes "但是來吧，他們真奇怪！他們是木乃伊！只是木乃伊，你知道嗎？他們不應該那麼堅強！";
	next;
	if(select("堅強的木乃伊？他們在哪兒？ :結束對話。") == 2) {
		mes "[可疑貓]";
		mes "whaaat~你不想听我的故事嗎？美好的！您最好不要在其他任何地方提及我的名字！";
		close;
	}
	mes "[可疑貓]";
	mes "哈哈，這是一個秘密。";
	mes "我在偷竊時發現了……不，我的意思是，只是穿過金字塔。";
	next;
	mes "[可疑貓]";
	mes "當我看到一個木乃伊朝我走來時，我就像是“任何東西”，不在乎。但是後來他用一巴掌擊倒了我！他怎麼能對像我這樣可愛的貓做到這一點！你包紮的怪胎...你只是等待！";
	next;
	if(select("我也想去:結束談話。") == 2)
		close;
	mes "[可疑貓]";
	mes "決不！我花了很多時間找到那條路！";
	next;
	mes "[可疑貓]";
	mes "嗯...但是如果您堅持，請給我5000 Zeny。我帶領你。";
	mes "什麼~？不要給我那個外觀！您是否知道貓在Midgard生存有多麼困難！";
	next;
	mes "[可疑貓]";
	mes "^ff0000但是想到了，像您在那裡無法生存的弱者。如果您幸運的話，也許一秒鐘？ ^000000";
	mes "好吧，這與我無關。但是我還是警告過你！";
	next;
	switch(select("支付 5,000 zeny 並出發。 :算了。")) {
	case 1:
		if (Zeny < 5000) {
			mes "[可疑貓]";
			mes "您認為您要去哪裡！你是如此破產！";
			close;
		}
		set Zeny, Zeny-5000;
		mes "[可疑貓]";
		mes "啊，這筆舊錢怎麼了……您沒有任何閃閃發光或閃亮的東西？";
		mes "啊，跟我！";
		close2;
		warp "moc_prydn1",93,96;
		end;
	case 2:
		mes "[可疑貓]";
		mes "天哪，您甚至沒有5000 Zeny，對嗎？";
		close;
	}
}

moc_prydn1,94,98,3	script	Suspicious Cat#night2	547,{
	mes "[可疑貓]";
	mes "當您移開視線時，那個愚蠢的木乃伊再次拍了拍我！嗅探嗅...讓我們回去，喵！";
	next;
	switch(select("返回盜賊公會:消滅真理:消滅遠古木乃伊:沒什麼")) {
	case 1:
		mes "[可疑貓]";
		mes "對了...讓我們回去。";
		close2;
		warp "moc_prydb1",100,57;
		end;
	case 2:
		set .@playtime, checkquest(2290,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[可疑貓]";
			mes "看看那個Verit，他害怕我們！ kyaahaha！我們很安全，至少有一段時間！";
			close;
		} else if (.@playtime == 2)
			erasequest 2290;
		set .@hunting, checkquest(2289,HUNTING);
		if (.@hunting == -1) {
			mes "[可疑貓]";
			mes "您知道貓和狗不相處嗎？";
			next;
			mes "[可疑貓]";
			mes "看看那個醜陋而醜陋的verit。他皺著眉頭，搖著尾巴，試圖靠近。";
			mes "哦，你沒有尾巴，對嗎？他認為我是他身邊的刺，他現在正試圖開始戰鬥！";
			next;
			mes "[可疑貓]";
			mes "看著他，如此貪婪和流口水...我也不喜歡他呼吸的方式。我無法忍受他如何使那些吞噬的聲音...";
			mes "很明顯他正在等待從後面攻擊我。";
			next;
			mes "[可疑貓]";
			mes "天哪，如果不是我的胃痛，我本來可以撓他的臉！";
			mes "嘿，這不是因為我害怕Maparuros！";
			next;
			mes "[可疑貓]";
			mes "如果您想回家，最好毆打那個醜陋的小狗！否則，他會咬你！";
			next;
			switch(select("幫助他:遠離")) {
			case 1:
				mes "[可疑貓]";
				mes "好，目標編號為20！祝你好運！";
				setquest 2289;
				close;
			case 2:
				mes "[可疑貓]";
				mes "帕恩！你不在乎我嗎？很好，你叛徒！";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[可疑貓]";
			mes "Verit的訓練進展順利嗎？";
			next;
			switch(select("當然。 :我想停下來。")) {
			case 1:
				mes "[可疑貓]";
				mes "好的。保持良好的工作！";
				close;
			case 2:
				mes "[可疑貓]";
				mes "什麼？你是如此不負責任！";
				erasequest 2289;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[可疑貓]";
			mes "嘿，您實際上很有用！好工作！";
			mes "那個Verit不敢一會兒來到這裡，對嗎？穆哈哈！";
			changequest 2289,2290;
			getexp 300000,100000;
			close;
		}
	case 3:
		set .@playtime, checkquest(2291,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[可疑貓]";
			mes "很好，我會很好地利用您獲得的時間！但是我要先就此胃痛做些事情.....";
			close;
		} else if (.@playtime == 2)
			erasequest 2291;
		set .@hunting, checkquest(2292,HUNTING);
		if (.@hunting == -1) {
			mes "[可疑貓]";
			mes "珍貴的寶藏應該隱藏在這裡的深層秘密地方！";
			mes "我敢肯定，木乃伊的珍貴寶藏隱藏在第二個地下室。";
			next;
			mes "[可疑貓]";
			mes "但是古老的木乃伊讓我措手不及，在我不知不覺中撞了我的頭！";
			next;
			mes "[可疑貓]";
			mes "我敢肯定，他們試圖通過減少我的上級腦細胞來阻止我找到寶藏！ ！";
			next;
			mes "[可疑貓]";
			mes "他們不能阻止我！讓我們去擺脫古老的木乃伊！ ！";
			next;
			mes "[可疑貓]";
			mes "不要問為什麼！我敢肯定，您也不希望那些美麗的寶藏被隱藏在黑暗中！";
			next;
			switch(select("幫助他:遠離")) {
			case 1:
				mes "[可疑貓]";
				mes "好吧，目標編號為20！祝你好運！";
				setquest 2292;
				close;
			case 2:
				mes "[可疑貓]";
				mes "啊，你真冷心。";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[可疑貓]";
			mes "戰鬥古老的木乃伊進展順利嗎？";
			next;
			switch(select("當然。 :我想停下來。")) {
			case 1:
				mes "[可疑貓]";
				mes "好的。保持良好的工作！";
				close;
			case 2:
				mes "[可疑貓]";
				mes "什麼？你是如此不負責任！";
				erasequest 2292;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[可疑貓]";
			mes "嘿，您實際上很有用！好工作！";
			mes "那個古老的木乃伊不敢一會兒來到這裡，對嗎？穆哈哈！";
			changequest 2292,2291;
			getexp 600000,200000;
			close;
		}
	case 4:
		close;
	}
}
