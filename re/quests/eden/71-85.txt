//===== rAthena Script =======================================
//= Eden Group Quests - Mission [71 - 85]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 71 - 85.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official scripts. [Euphy]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,38,3	script	Mission [71 - 85]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- 只有 -";
		mes "- 伊甸園可以閱讀 -";
		mes "- 這個公告板。 -";
		close;
	}

	// Clear the original quest data (no longer used).
	if (slv_quest) set slv_quest,0;
	if (checkquest(10102) > -1 || checkquest(10103) > -1 || checkquest(10104) > -1 || checkquest(10105) > -1 || checkquest(10106) > -1) {
		for(set .@quest,10102; .@quest<=10106; set .@quest,.@quest+1) {
			if (checkquest(.@quest) > -1)
				erasequest .@quest;
		}
		mes "- 由於客戶已移動 -";
		mes "- 到另一個地方， -";
		mes "- 申請表已被刪除。 -";
		next;
	}

	if (BaseLevel < 71) {
		mes "- 只有 -";
		mes "- 正確的級別可以讀取 -";
		mes "- 這個公告板。 -";
		mes "- 嘗試找到板 -";
		mes "- 適合您的水平。 -";
		close;
	} else if (BaseLevel > 85) {
		// Quest IDs: 10107-10123, 5055-5057
		setarray .@quests[0],
			10107,10108,10109,
			10110,10111,10112,10113,
			10114,10115,10116,10117,
			10118,10119,10120,
			10121,10122,10123,5055,5056;
		for(set .@i,0; .@i<getarraysize(.@quests); set .@i,.@i+1) {
			if (checkquest(.@quests[.@i],HUNTING) == 2)
				set .@complete[getarraysize(.@complete)], .@quests[.@i];
		}
		if (checkquest(5057) > -1 && countitem(7187) >= 30)
			set .@complete[getarraysize(.@complete)],5057;
		if (getarraysize(.@complete)) {
			mes "您正在進行任務。";
			mes "你想把它們上交嗎？";
			next;
			if(select("當然。 :不。") == 2)
				close;
			for(set .@i,0; .@i<getarraysize(.@complete); set .@i,.@i+1)
				callsub L_Quest,.@complete[.@i];
			close;
		}
		mes "- 只有 -";
		mes "- 正確的級別可以讀取 -";
		mes "- 這個公告板。 -";
		mes "- 嘗試找到板 -";
		mes "- 適合您的水平。 -";
		close;
	}
	mes "- 選擇您的位置 -";
	mes "- 想打獵。 -";
	next;
	switch(select("獅身人面像地下城:格拉斯特·海姆:朱諾區域:鐘樓:局部島嶼")) {
	case 1:
		mes "- 有幾個請求 -";
		mes "- 與獅身人面達地牢有關 -";
		mes "- 在這個任務委員會中。 -";
		next;
		set .@str1$,"The villagers of Morocc are worried by the increasing number of monsters in the Sphinx Dungeon. ";
		set .@str2$,"- Meidi from Morocc -";
		switch(select("狩獵安魂曲:狩獵馬杜克:狩獵帕薩納")) {
			case 1: callsub L_Quest,10107,.@str1$+"Hunt 10 Requiems.",.@str2$;
			case 2: callsub L_Quest,10108,.@str1$+"Hunt 10 Marduks.",.@str2$;
			case 3: callsub L_Quest,10109,.@str1$+"Hunt 10 Pasanas.",.@str2$;
		}
	case 2:
		mes "- 有幾個請求 -";
		mes "- 與Glast Heim有關 -";
		mes "- 在這個任務委員會中。 -";
		next;
		set .@str1$,"I am very afraid that the monsters from Glast Heim will attack us. ";
		set .@str2$,"- Ancellia from Geffen -";
		switch(select("狩獵黑暗框架:狩獵邪惡德魯伊:狩獵幽靈:狩獵雷德里克弓箭手")) {
			case 1: callsub L_Quest,10110,.@str1$+"Hunt 10 Dark Frames.",.@str2$;
			case 2: callsub L_Quest,10111,.@str1$+"Hunt 10 Evil Druids.",.@str2$;
			case 3: callsub L_Quest,10112,.@str1$+"Hunt 10 Wraiths.",.@str2$;
			case 4: callsub L_Quest,10113,.@str1$+"Hunt 10 Raydric Archers.",.@str2$;
		}
	case 3:
		mes "- 有幾個請求 -";
		mes "- 與該區域有關 -";
		mes "- 在朱諾附近。 -";
		next;
		set .@str1$,"Those who can't fight, like me, find it much more difficult to travel due to the large number of monsters. ";
		set .@str2$,"- Jeanbai, traveler -";
		switch(select("狩獵大佩科:狩獵沉睡者:狩獵山羊:狩獵鷹身女妖")) {
			case 1: callsub L_Quest,10114,.@str1$+"Hunt 20 Grand Pecos.",.@str2$;
			case 2: callsub L_Quest,10115,.@str1$+"Hunt 20 Sleepers.",.@str2$;
			case 3: callsub L_Quest,10116,.@str1$+"Hunt 20 Goats.",.@str2$;
			case 4: callsub L_Quest,10117,.@str1$+"Hunt 20 Harpies.",.@str2$;
		}
	case 4:
		mes "- 有幾個請求 -";
		mes "- 與鍾樓有關 -";
		mes "- 在這個任務委員會中。 -";
		next;
		set .@str1$,"I used to love listening to the sweet melody of the Al De Baran Clock Tower but I can't hear anything anymore because of all the noises the monsters are making. ";
		set .@str2$,"- Rizingsetter, President of the Love Clock community -";
		switch(select("狩獵時鐘:狩獵朋克:狩獵騎術")) {
			case 1: callsub L_Quest,10118,.@str1$+"Hunt 15 Clocks before it breaks.",.@str2$;
			case 2: callsub L_Quest,10119,.@str1$+"Hunt 15 Punks before it breaks.",.@str2$;
			case 3: callsub L_Quest,10120,.@str1$+"Hunt 15 Ridewords before it breaks.",.@str2$;
		}
	case 5:
		mes "- 有幾個請求 -";
		mes "- 與本地島有關 -";
		mes "- 在這個任務委員會中。 -";
		next;
		set .@str1$,"I want to go on an adventure but there are so many things I have to deal with. If I can't, then others can't either! ";
		set .@str2$,"- Funfy, who wants to go on an adventure -";
		switch(select("狩獵奇奇莫拉:狩獵雅娃娃:狩獵米高:狩獵無頭騾子:狩獵塔姆魯安:收集節日面具")) {
			case 1: callsub L_Quest,10121,.@str1$+"Hunt 15 Kikimoras in Moscovia!",.@str2$;
			case 2: callsub L_Quest,10122,.@str1$+"Hunt 15 Miyabi Dolls in Amatsu!",.@str2$;
			case 3: callsub L_Quest,10123,.@str1$+"Hunt 15 Mi Gaos in Luoyang!",.@str2$;
			case 4: callsub L_Quest,5055,"I know there are many terrifying monsters outside the city, but what terrifies me are these Headless Mules lurking around. I hope they will never venture into town. Hunt 15 Headless Mules!","- Mariana from Brasilis -";
			case 5: callsub L_Quest,5056,"Tamruans make a lot of noise. I don't mind in the morning but they keep me from falling asleep at night. I want to sleep. Hunt 15 Tamruans!","- Shaman Pumo from Ayothaya -";
			case 6: callsub L_Quest,5057,"The Dumpling Childs stole all the masks that I had prepared for the festival. Try to get a hold of 30 masks! I'll owe you~","- SaYumMoon Chief of Kunlun -";
		}
	}
	end;

//callsub L_Quest,<quest ID>{,"<description1>","<description2>"};
L_Quest:
	set .@quest, getarg(0);
	switch(.@quest) {
		case 10107: set .@name$,"Hunt Requiem"; set .@exp,15000; break;
		case 10108: set .@name$,"Hunt Marduk"; set .@exp,15000; break;
		case 10109: set .@name$,"Hunt Pasana"; set .@exp,15000; break;
		case 10110: set .@name$,"Hunt Dark Frame"; set .@exp,20000; break;
		case 10111: set .@name$,"Hunt Evil Druid"; set .@exp,20000; break;
		case 10112: set .@name$,"Hunt Wraith"; set .@exp,20000; break;
		case 10113: set .@name$,"Hunt Raydric Archer"; set .@exp,20000; break;
		case 10114: set .@name$,"Hunt Grand Peco"; set .@exp,30000; break;
		case 10115: set .@name$,"Hunt Sleeper"; set .@exp,30000; break;
		case 10116: set .@name$,"Hunt Goat"; set .@exp,30000; break;
		case 10117: set .@name$,"Hunt Harpy"; set .@exp,30000; break;
		case 10118: set .@name$,"Hunt Clock"; set .@exp,25000; break;
		case 10119: set .@name$,"Hunt Punk"; set .@exp,25000; break;
		case 10120: set .@name$,"Hunt Rideword"; set .@exp,25000; break;
		case 10121: set .@name$,"Hunt Kikimora"; set .@exp,30000; break;
		case 10122: set .@name$,"Hunt Miyabi Doll"; set .@exp,30000; break;
		case 10123: set .@name$,"Hunt Mi Gao"; set .@exp,30000; break;
		case 5055: set .@name$,"Hunt Headless Mule"; set .@exp,30000; break;
		case 5056: set .@name$,"Hunt Tamruan"; set .@exp,30000; break;
		case 5057: set .@name$,"Gather Festival Masks for the Festival"; set .@exp,30000; break;
	}
	if (getargcount() == 1) {
		mes "'"+.@name$+"' completed. Do you want to turn it in and receive your reward?";
		next;
		if(select("交任務:取消。") == 1) {
			if (.@quest == 5057) delitem 7187,30; //Festival_Mask
			erasequest .@quest;
			getexp .@exp,0;
			mes "您已經收到了EXP完成任務。";
			next;
			return;
		}
		close;
	} else {
		if (.@quest == 5057) {
			// Quest 5057 is the only collection quest, so separate conditions are unnecessary.
			if (checkquest(5057) == -1)
				set .@hunting,-1;
			else if (countitem(7187) >= 30)
				set .@hunting,2;
		} else
			set .@hunting, checkquest(.@quest,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "完成任務後返回這裡。";
		} else if (.@hunting == 2) {
			mes "您已經完成了任務。";
			next;
			if(select("交任務:取消。") == 1) {
				if (.@quest == 5057) delitem 7187,30; //Festival_Mask
				erasequest .@quest;
				getexp .@exp,0;
				mes "您已經收到了EXP完成任務。";
			}
		}/* else if (.@hunting == 3) {
//			recall_completequest .@quest
			erasequest .@quest;
			mes "您已經重新接受了任務'"+.@name$+"'.";
		}*/ else {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "您想接受任務嗎？";
			next;
			if(select("是的。 :沒有。") == 1) {
				mes "您已經收到了任務'"+.@name$+"'. View your quest info for more details.";
				setquest .@quest;
			}
		}
		close;
	}
}
