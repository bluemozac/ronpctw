//===== rAthena Script =======================================
//= Eden Group Quests - Mission [86 - 90]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 86 - 90.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official script. [Euphy]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,48,175,3	script	86-90 Mission Board	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "等一下！";
		mes "- 你需要一個";
		mes "- ^4d4dfff'eden組標記'^000000-";
		mes "- 接收這些任務。 -";
		next;
		switch(select("走開吧。 :^0000FFEden Group^000000 個任務？ :我怎樣才能成為 ^0000FFEden Group^000000 的成員？")) {
		case 1:
			close;
		case 2:
			mes "::伊甸園團體任務";
			mes "::基本上是任務";
			mes "::請求";
			mes "::來自王國周圍的人們。";
			mes "::我們只是對它們進行分類。";
			next;
			mes "::在伊甸園，";
			mes "::我們整理了任務";
			mes "::根據難度的水平。";
			mes "::根據這個級別，";
			mes "::您可以找到";
			mes "::任務";
			mes "::適合你。";
			next;
			mes "::完成後，";
			mes "::你可以回來";
			mes "::致任務委員會。";
			next;
			mes "::那麼你將";
			mes "::獎勵！ ！";
			mes "::簡單，不是！";
			mes "::無需佣金！";
			close;
		case 3:
			mes "::您可以成為會員";
			mes ":: 伊登集團";
			mes ":: 至^0000flaime甚至^000000，";
			mes "::誰在一樓等待。";
			next;
			mes "::如果您成為會員，";
			mes "::您會得到一個";
			mes "::\"^006400Eden Group Mark^000000\".";
			mes "::那麼您將能夠";
			mes "::看任務委員會。";
			close;
		}
	}
	if (BaseLevel < 86  || BaseLevel > 90) {
		// Quest IDs: 4167-4180
		// Quest cooldown IDs: 4198-4211
		setarray .@names$[0],
			"Dragon Tail","Spring Rabbit","Pest",
			"Bathory","Alarm",
			"Baba Yaga","Yao Jun","Firelock Soldier","Zipper Bear",
			"Ground Petite","Rafflesia","Venomous","Pitman","Yellow Novus";
		setarray .@exp[0],
			76000,82000,82000,
			72000,76000,
			72000,78000,82000,80000,
			72000,72000,72000,86000,80000;
		mes "您必須在86-90級內接受這些任務。";
		for(set .@quest,4167; .@quest<=4180; set .@quest,.@quest+1) {
			set .@hunting, checkquest(.@quest,HUNTING);
			if (.@hunting == 0 || .@hunting == 1) {
				next;
				mes "::您無法繼續";
				mes ": ^0000ff"+.@names$[.@quest-4167]+" Hunting^000000.";
				mes "::任務註冊";
				mes "::被取消。";
				erasequest .@quest;
				close;
			} else if (.@hunting == 2) {
				next;
				mes "::您添加了信息";
				mes "::關於任務";
				mes ": ^0000ff"+.@names$[.@quest-4167]+" Hunting^000000";
				mes "::在任務委員會上。";
				mes "::你有一個";
				mes "::有益的經驗。";
				erasequest .@quest;
				getexp .@exp[.@quest-4167],0;
				close;
			}
		}
		close;
	}
	mes "[86-90任務委員會]";
	mes "該董事會上有幾個請求。";
	next;
	switch(select("^8B4513海龜島^000000:^8B4513鐘樓地下城^000000:^8B4513擴展城鎮^000000:^8B4513其他地區^000000")) {
	case 1:
		mes "^8B4513海龜島^000000。";
		mes "您將狩獵哪個怪物或完成狩獵？";
		next;
		switch(select("^0000FF龍尾^000000:^0000FF春兔^000000:^0000FFPest^000000")) {
			case 1: callsub L_Quest,4167,"Dragon Tail",76000,"","There is a monster that only can be found on ^8B4513Turtle Island^000000.","s";
			case 2: callsub L_Quest,4168,"Spring Rabbit",82000,"","There is a monster that only can be found on ^8B4513Turtle Island^000000.","s";
			case 3: callsub L_Quest,4169,"Pest",82000,"","There is a monster that only can be found on ^8B4513Turtle Island^000000.","s";
		}
	case 2:
		mes "^8B4513鐘樓地下城^000000。";
		mes "您將狩獵哪個怪物或完成狩獵？";
		next;
		switch(select("^0000FFBathory^000000:^0000FFAlarm^000000")) {
			case 1: callsub L_Quest,4170,"Bathory",72000,"","The Clock Tower Dungeon is full of dangerous monsters.";
			case 2: callsub L_Quest,4171,"Alarm",76000,"","The Clock Tower Dungeon is full of dangerous monsters.";
		}
	case 3:
		mes "^8B4513擴展城市^000000";
		mes "您將狩獵哪個怪物或完成狩獵？";
		next;
		switch(select("^0000FFBaba Yaga^000000:^0000FF姚軍^000000:^0000FF火槍士兵^000000:^0000FF拉鍊熊^000000")) {
			case 1: callsub L_Quest,4172,"Baba Yaga",72000,"","The witch Baba Yaga can be found in the outskirts of Moscovia.";
			case 2: callsub L_Quest,4173,"Yao Jun",117000,"","There are human-like monsters that roam around the dungeon in Luoyang.";
			case 3: callsub L_Quest,4174,"Firelock Soldier",82000,"","There are human-like monsters that roam around the dungeon in Amatsu.","s";
			case 4: callsub L_Quest,4175,"Zipper Bear",120000,"","There are bear monsters swarming in the Kunlun dungeon.";
		}
	case 4:
		mes "[86-90任務委員會]";
		mes "這是在Midgard其他地區狩獵怪物的隨機請求列表。";
		mes "您將狩獵哪個怪物或完成狩獵？";
		next;
		switch(select("^0000FF地面嬌小^000000:^0000FFRafflesia^000000:^0000FFVenomous^000000:^0000FFPitman^000000:^0000FFYellow Novus^000000")) {
			case 1: callsub L_Quest,4176,"Ground Petite",72000,"","There are dragon type monsters swarming the western region of Geffen.";
			case 2: callsub L_Quest,4177,"Rafflesia",72000,"","There are plant type monsters infesting the fields of Lighthalzen.";
			case 3: callsub L_Quest,4178,"Venomous",72000,"","There are formless monsters floating around the fields of Einbroch.";
			case 4: callsub L_Quest,4179,"Pitman",86000,"","There are undead monsters pushing a cart around the fields and dungeons of Einbroch.";
			case 5: callsub L_Quest,4180,"Yellow Novus",80000,"","There are yellow dragon type monsters terrorizing the fields of Hugel.";
		}
	}
	end;

//callsub L_Quest,<quest ID>,"<monster name>",<reward EXP>,"<cutin image>","<description>"{,"s"};
L_Quest:
	set .@quest1, getarg(0);
	set .@quest2, .@quest1+31;
	.@cutin$ = getarg(3);
	set .@playtime, checkquest(.@quest2,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[86-90任務委員會]";
		mes "您需要等待3個小時才能再次執行此任務。";
		close;
	}
	set .@hunting, checkquest(.@quest1,HUNTING);
	if (.@hunting == 0 || .@hunting == 1) {
		mes "[86-90任務委員會]";
		mes "你完成了30次狩獵嗎"+getarg(1)+getarg(5,"")+" yet?";
		close;
	} else if (.@hunting == 2) {
		mes "[86-90任務委員會]";
		mes "您已經完成了狩獵。";
		mes "請接受此獎勵作為補償。";
		erasequest .@quest1;
		if (.@playtime > -1) erasequest .@quest2;
		setquest .@quest2;
		getexp getarg(2),0;
		close;
	}/* else if (.@hunting == 3) {
//		recall_completequest .@quest1
		erasequest .@quest1;
		mes "[86-90任務委員會]";
		mes "您已經重新接受了"+getarg(1)+" Hunting mission.";
		close;
	}*/ else {
		mes "[86-90任務委員會]";
		mes getarg(4);
		next;
		if (.@cutin$ != "") cutin .@cutin$,3;
		mes "[86-90任務委員會]";
		mes "亨特30"+getarg(1)+" monsters there and post your success on this board.";
		next;
		if (.@cutin$ != "") cutin .@cutin$,255;
		if(select("我會追捕他們。 :不，謝謝。") == 1) {
			mes "[86-90任務委員會]";
			mes "完成任務後，將其發佈在該板上並收集獎勵。";
			if (.@playtime > -1) erasequest .@quest2;
			setquest .@quest1;
		}
		close;
	}
}
