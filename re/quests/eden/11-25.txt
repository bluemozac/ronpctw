//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,36,38,3	script	Mission [11 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- 您需要一個 -";
		mes "-  ^4d4dfff'eden組標記' ^000000-";
		mes "- 接收這些任務。 -";
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes "- 您必須收集您的 -";
			mes "- 開始之前獎勵 -";
			mes "- 新任務。 -";
			mes " ";
			mes "- 經理：Spike-";
			close;
		}
		else if (.@j >= 0) {
			mes "- 您只能請求 -";
			mes "- 一次任務。 -";
			mes " ";
			mes "- 經理：Spike-";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes "- 沒有任務 -";
		mes "- 現在為您開放。 -";
		close;
	}
	if (BaseLevel > 25) {
		mes "- 這些任務也是 -";
		mes "- 容易適合您的口徑 -";
		mes "- 搜索更高的水平 -";
		mes "- 適當的任務。 -";
		close;
	}
	mes "- 任務公告板 -";
	mes "- 對於初學者。 -";
	mes " ";
	mes "- 經理：Spike-";
	next;
	switch(select("黃蜂狩獵:禿鷹狩獵:蚱蜢的腿:蟲尾狩獵:孢子狩獵:害蟲防治:木卡狩獵:熟悉狩獵:收集羽毛:收集毒孢子:取消")) {
		case 1: callsub L_Quest,11114,"Hornet Hunting"," - Hunt 10 ^4d4dff'Hornets'^000000 - "," - northwest of Prontera. - ";
		case 2: callsub L_Quest,11115,"Condor Hunting"," - Hunt 20 ^4d4dff'Condors'^000000 - "," - south of Morocc. - ";
		case 3: callsub L_Quest,11116,"Grasshopper's Leg"," - Hunt 10 ^4d4dff'Rockers'^000000 - "," - southwest of Prontera and - "," - bring 10 ^4d4dffGrasshopper's Legs^000000. - ";
		case 4: callsub L_Quest,11117,"Worm Tail Hunting"," - Hunt 20 ^4d4dff'Worm Tails'^000000 - "," - southeast of Payon.- ";
		case 5: callsub L_Quest,11118,"Spore Hunting"," - Hunt 30 ^4d4dff'Spores'^000000 - "," - around Payon. - ";
		case 6: callsub L_Quest,11119,"Pest Control"," - Hunt 20 ^4d4dffThief Bug Eggs^000000 - "," - in the Prontera Culvert - "," - and bring 10 ^4d4dffChrysalis^000000. - ";
		case 7: callsub L_Quest,11120,"Muka Hunting"," - Hunt 20 ^4d4dff'Muka'^000000 - "," - south of Prontera. - ";
		case 8: callsub L_Quest,11121,"Familiar Hunting"," - Hunt 20 ^4d4dffFamiliar^000000 - "," - in the Culvert Sewers - "," - west of Prontera. - ";
		case 9: callsub L_Quest,11122,"Collect Feathers"," - Hunt 30 ^4d4dff'Peco Pecos'^000000 - "," - south of Prontera. - ";
		case 10: callsub L_Quest,11123,"Collect Poison Spores"," - Hunt 30 ^4d4dffPoison Spores^000000 - "," - inside the Geffen Tower and - "," - bring 5 ^4d4dffPoison Spores^000000. - ";
		case 11:
			mes "- 停止閱讀公告板。 -";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes "- 任務 ^4D4DFF"+ getarg(1) +"^000000 - ";
		mes "- 今天已經完成。 -";
		mes " ";
		mes "- 經理：Spike-";
		close;
	}
	mes "- 使命 '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes "- 經理：Spike-";
	next;
	if (select("我會執行這個任務。 :尋找其他任務。") == 2) {
		mes "- 讓我們搜索 -";
		mes "- 其他任務。 -";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Hornet Hunting",900;
				case 11115: callsub L_Quest,.@i,"Condor Hunting",1800;
				case 11116: callsub L_Quest,.@i,"Grasshopper's Leg",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Worm Tail Hunting",2600;
				case 11118: callsub L_Quest,.@i,"Spore Hunting",3900;
				case 11119: callsub L_Quest,.@i,"Extirpate Insects",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Muka Hunting",3200;
				case 11121: callsub L_Quest,.@i,"Familiar Hunting",3200;
				case 11122: callsub L_Quest,.@i,"Collect Feathers",5400;
				case 11123: callsub L_Quest,.@i,"Collect Poison Spores",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[長釘]";
			mes "您還沒有完成任務。";
			mes "保持良好的工作。你快完成了。";
			close;
		}
	}
	mes "[長釘]";
	mes "您在尋找工作嗎？";
	mes "我們總是有很多工作要做。";
	next;
	mes "[長釘]";
	mes "您為什麼不環顧四周並找到一些有趣的任務？";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[長釘]";
				mes "你沒有足夠的"+ getitemname(.@item_req) +".";
				mes "你需要帶"+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[長釘]";
		mes "嗯...讓我看看~";
		mes "您已經完成了'"+ getarg(1) +"' mission.";
		next;
		mes "[長釘]";
		mes "好工作~!!";
		mes "我們的客戶對您的工作感到非常滿意。他會感謝您所做的一切。";
		mes "請再次幫助我們。謝謝！";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
