//===== rAthena Script =======================================
//= Merchants from new dungeons for Level 175
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- Merchants related to new dungeons.
//===== Changelog: ===========================================
//= 1.0 Initial release of Einbech Dungeon 3 merchants. [Capuche]
//============================================================

einbech,149,257,4	script	Site manager Elmen#extdq0	4_TOWER_16,{
	if (checkweight(25814,100) == 0 || checkweight(1201,1) == 0 || (MaxWeight - Weight) < 10000) {
		mes "- 您無法繼續進行，因為您的項目太多了。 -";
		close;
	}
	mes "[埃爾門]";
	mes "你好。冒險家。";
	mes "如果您是在這裡購買Dynite採礦設備或將礦石改進到Dynite中，那麼您來了。";
	next;
	switch( select("達尼特精煉", "達尼特商店", "達尼特裝備交換" ) ) {
	case 1:
		npctalk "Elmen：你一次最多可以精煉 100 顆 Dynite。機器很小。", "", bc_self;
		mes "[埃爾門]";
		mes "" + mesitemlink(25814) + " Refined Ore.";
		mes "" + mesitemlink(25809) + " ^777777(currently " + countitem(25809) + ")^000000";
		mes "" + mesitemlink(25810) + " ^777777(currently " + countitem(25810) + ")^000000";
		mes "" + mesitemlink(25811) + " ^777777(currently " + countitem(25811) + ")^000000";
		mes "" + mesitemlink(25812) + " ^777777(currently " + countitem(25812) + ")^000000";
		mes "" + mesitemlink(25813) + " ^777777(currently " + countitem(25813) + ")^000000";
		mes "您需要五種礦石中的每種。";
		next;
		input .@val;
		if (.@val > 100) {
			mes "[埃爾門]";
			mes "對不起。冒險家。";
			mes "Dynite精煉只能一次完善100個？機器很小，因此沒有幫助。請理解。";
			close;
		}
		if (.@val == 0) {
			mes "[埃爾門]";
			mes "取消？";
			mes "如果您需要對Dynite進行改進，請隨時找到它。沒有我，沒有人能做到。";
			close;
		}
		if (countitem(25809) < .@val || countitem(25810) < .@val || countitem(25811) < .@val || countitem(25812) < .@val || countitem(25813) < .@val) {
			mes "[埃爾門]";
			mes "嘿！這使得彼此很難，您難道不需要精煉所需的材料嗎？材料！";
			close;
		}
		specialeffect EF_REPAIRWEAPON;
		progressbar_npc "FFFF00",1;
		delitem 25809,.@val;	// Ein_REDIUM
		delitem 25810,.@val;	// Ein_RINDIUM
		delitem 25811,.@val;	// Ein_ODIUM
		delitem 25812,.@val;	// Ein_PURDIUM
		delitem 25813,.@val;	// Ein_WHIDIUM
		getitem 25814,.@val;	// Ein_DYNITE
		mes "[埃爾門]";
		mes "" + .@val + " Dynite have been refined. It's perfect today!";
		close;
	case 2:
		mes "[埃爾門]";
		mes "有多種配件和修改許可。在購買前仔細看看您想要的東西。";
		close2;
		callshop "barter_ein_dun03";
		end;
	case 3:
		mes "[埃爾門]";
		mes "我可以將設備與大分工交換，但是它已經放在一個密封的盒子中。";
		mes "給我50 ^4d4dffdynites ^000000，我將為您打開一個 ^4d4dfffweapon box ^000000。";
		next;
		mes "[埃爾門]";
		mes "我不知道會發生什麼，但是質量很好。您想打開它嗎？";
		next;
		if (select("閉上眼睛，交流。", "請勿交換。" ) == 2) {
			mes "[埃爾門]";
			mes "Kee，這是浪費，這是一筆不錯的。";
			close;
		}
		if (countitem(25814) < 50) {
			mes "[埃爾門]";
			mes "您沒有足夠的分子朋友。";
			mes "我不知道會發生什麼，所以很難以這個價格去任何地方購買這種設備。";
			close;
		}
		delitem 25814,50;
		// unknown text
		getgroupitem(IG_EIN_1HITEMBOX);
		end;
	}
}
