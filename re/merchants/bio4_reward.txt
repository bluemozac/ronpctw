//===== rAthena Script =======================================
//= Lighthalzen rewards NPCs
//===== Description: =========================================
//= Enchant and reward NPCs from Bio instance/dungeon item drop.
//===== Changelogs: ==========================================
//= 1.0 Moved the NPCs from WolfchevLaboratory.txt. [Capuche]
//============================================================

// Weird Old Man (Rewards)
//============================================================
lighthalzen,342,291,4	script	Weird old man#Bio4Reward	4_M_EINOLD,{
	if ((MaxWeight - Weight) < 2800 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	mes "[怪異的老人]";
	mes "如今，那些年輕人是如此粗魯，沒有舉止。";
	next;
	if (select("不理他", "與他交談") == 1) {
		mes "[怪異的老人]";
		mes "你，非常糟糕的年輕人！";
		close;
	}
	mes "["+ strcharinfo(0) +"]";
	mes "先生，你還好嗎？";
	next;
	mes "[怪異的老人]";
	mes ".... ??你不像其他年輕人。你的舉止很好！你知道我發生了什麼事。";
	next;
	switch(rand(1,2)) {
	case 1:
		mes "[怪異的老人]";
		mes "我正在去拜訪住在符文米加特人的兒子的路上。真的很遙遠，所以我坐了飛機，..";
		next;
		mes "[怪異的老人]";
		mes "那些年輕人...太嘈雜了。 。 。我站著，但他們沒有一個給我一個座位。 .他們只是大聲說話，這困擾著所有人。";
		next;
		mes "[怪異的老人]";
		mes "如果我小10歲，我可以講授他們。";
		next;
		mes "[怪異的老人]";
		mes "pffff ..從不喜歡時間的流逝。";
		next;
		mes "[怪異的老人]";
		mes "所以，你...你會怎麼做？";
		next;
		mes "[怪異的老人]";
		mes "在一個公開的公共場所，您應該保持安靜到目的地，並將座位供應老人和體弱者。";
		next;
		break;
	case 2:
		mes "[怪異的老人]";
		mes "我正在去拜訪住在符文米加特人的兒子的路上。真的很遙遠，所以我坐了飛機，..";
		next;
		mes "[怪異的老人]";
		mes "那些年輕人...太嘈雜了。 。 。我站著，但他們沒有一個給我一個座位。 .他們只是大聲說話，這困擾著所有人。";
		next;
		mes "[怪異的老人]";
		mes "如果我小10歲，我可以講授他們。";
		next;
		mes "[怪異的老人]";
		mes "pffff ..從不喜歡時間的流逝。";
		next;
		mes "[怪異的老人]";
		mes "所以，你...你會怎麼做？";
		next;
		mes "[怪異的老人]";
		mes "在一個公開的公共場所，您應該保持安靜到目的地，並將座位供應老人和體弱者。正確的？";
		next;
		mes "[怪異的老人]";
		mes "那不是嗎？";
		next;
		switch(rand(1,2)) {
		case 1:
			mes "[怪異的老人]";
			mes "哦，然後你知道什麼...？";
			next;
			mes "[怪異的老人]";
			mes "一旦我在鎮上行走，一些男孩在街上吐口水，正在打擾那些經過的女孩...圖特（Tut Tut）！";
			next;
			mes "[怪異的老人]";
			mes "世界要去狗。甚至是他們這樣做的一天。";
			next;
			switch(rand(1,2)) {
			case 1:
				mes "[怪異的老人]";
				mes "哦，然後你知道什麼...？";
				next;
				break;
			case 2:
				mes "[怪異的老人]";
				mes "哦，然後你知道什麼...？";
				next;
				mes "[怪異的老人]";
				mes "最近，年輕人不那麼耐心！ ！ ！";
				next;
				mes "[怪異的老人]";
				mes "因此，他們經常說他們會辭職..他們說他們做不到。他們將如何以這種心態過著生活。";
				next;
				mes "[怪異的老人]";
				mes "找到工作並不容易。被錄用是非常困難的，但是他們甚至無法在那里站立一周並離開工作場所。";
				next;
				mes "[怪異的老人]";
				mes "多麼不耐煩和不負責任！";
				next;
				mes "[怪異的老人]";
				mes "哦，然後你知道什麼...？";
				next;
				break;
			}
		case 2:
			mes "[怪異的老人]";
			mes "哦，然後你知道什麼...？";
			next;
			break;
		}
	}
	mes "^e7a43d老人一遍又一遍地交談。 ^000000";
	next;
	mes "[怪異的老人]";
	mes "嗯……你不喜歡普通的年輕人，你似乎也很耐心。我喜歡！ ！ ！";
	next;
	mes "[怪異的老人]";
	mes "現在我只是一個虛弱的老人，但是我年輕的時候我是鎮上的著名商人。";
	next;
	mes "[怪異的老人]";
	mes "自從我停止業務以來已經有一段時間了";
	mes "但是我的前客戶與我聯繫並問我";
	next;
	mes "[怪異的老人]";
	mes "如果我能給他一些未知的物體，稱為“戰士意志”，“對鮮血的口渴”和“死者寒意”。";
	next;
	mes "[怪異的老人]";
	mes "我喜歡大多數年輕人沒有的禮貌，所以如果您得到我，我會給您一些我認為是我的寶藏的東西。";
	next;
	while(1) {
		switch(select("當我收集它們時，我會回來", "讓我看看盔甲", "給我看看武器")) {
		case 1:
			mes "[怪異的老人]";
			mes "好，年輕人。稍後再見。";
			close;
		case 2:
			mes "[怪異的老人]";
			mes "因此，您對盔甲感興趣。";
			next;
			mes "[怪異的老人]";
			mes "這些是我擁有的裝甲。";
			next;
			for(.@i = 1; .@i < 8; .@i++) {
				if (((.@i == 1 || .@i == 5) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 35)) || 
				    ((.@i == 2 || .@i == 3) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 22)) ||
				    ((.@i == 4 || .@i == 7) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400)) ||
				    ((.@i == 6) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 30))) {
					.@armor$[.@i] = "^0000FF";
				} else {
					.@armor$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"Show me other items.",
					""+.@armor$[1]+"Giant Shield^000000",
					""+.@armor$[2]+"Ancient Book of Geffenia-Water^000000",
					""+.@armor$[3]+"Bible of Promise(2nd Vol.)^000000",
					""+.@armor$[4]+"Salvage Cape^000000",
					""+.@armor$[5]+"Assassin's Handcuffs^000000",
					""+.@armor$[6]+"Green Operating Gown^000000",
					""+.@armor$[7]+"Ancient Gold Decoration^000000"
				)) {
				case 1:
					mes "[怪異的老人]";
					mes "看看。";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<巨型盾牌[1] >>";
					mes "減少大型怪物損害的5％，如果精煉的LV為9，則減少了5％的損害。";
					mes "盾牌 / def 130 /重量280 /必需的lv 100 /套接字1 /3級劍客班";
					callsub L_Reward, 2160, 100, 50, 35; //Giant_Shield
					break;
				case 3:
					mes "<< Geffenia-Water的古代書[1] >>";
					mes "int + 1，mdef + 2。如果原始int點為120，matk + 10，MHP + 800。";
					mes "在配備Lacryma Stick時，來自Storm Gust，Frost Misty和Jack Frost的鑄造％將使相當於4倍精緻的LV打折。";
					mes "盾牌 / def 30 /重量100 /必需的LV 100 /插座1 /用於術士";
					callsub L_Reward, 2161, 100, 50, 22; //Geffenia_Book_Water
					break;
				case 4:
					mes "<<諾言聖經（第二卷）[1] >>";
					mes "MDEF + 5，允許使用“ Odin的功率”（LV 2）。增加5％的治療技能。";
					mes "盾牌 / def 20 /重量50 /必需的LV 140 /插座1 /用於大主教";
					callsub L_Reward, 2162, 100, 50, 22; //Bible_Of_Promise2
					break;
				case 5:
					mes "<<打撈斗篷[0] >>";
					mes "允許使用“ Cicada皮膚脫落”（LV 1）。這項技能的涼爽時間將為30秒。在裝備它時，使用任何技能的SP消耗將加倍，當它脫掉時，SP -1200。";
					mes "服裝 / def 1 /重量40 /必需的lv 130 /插座0 /大主教";
					callsub L_Reward, 2582, 300, 300, 400; //Salvage_Cape
					break;
				case 6:
					mes "<<刺客的手銬[1] >>";
					mes "MSP + 20，CRI + 3。與Krishna裝備時，ATK + 50，ATK功率增加了50％，逃跑了-30。當配備Cakram CRI + 4時，臨界ATK功率增加了40％，MHP -10％-10％。";
					mes "附件 / DEF 3 /重量40 /必需的LV 100 /插座1 /用於斷頭台十字架";
					callsub L_Reward, 2892, 100, 50, 35; //Assassin_Handcuffs
					break;
				case 7:
					mes "<<綠色操作禮服[1] >>";
					mes "DEX + 1，MSP + 30。裝備手術刀將放下“不朽的心臟”，“酒精”在狩獵人類，動物型怪物時具有一定的機會。精煉LV會影響下降速率。";
					mes "裝甲 / def 66 /重量66 /必需的lv 100 /插座1 / for通用";
					callsub L_Reward, 15044, 100, 50, 30; //Green_Operation_Coat
					break;
				case 8:
					mes "<<古代金裝飾[1] >>";
					mes "如果基本LV為150，則所有狀態 + 2。對於Swordman，Merchant，Thief類ATK + 8％，對於魔術師，Acolyte類Matk + 8％，Heal Power + 7％，用於Archer Classs dex + 3，長距離ATK Power + 10％。";
					mes "Helm / def 7 /位置 - 上 /重量40 /必需的lv 100 / socket 1 / for 3rd obs類";
					callsub L_Reward, 18570, 300, 300, 400; //Ancient_Gold_Deco
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
			break;
		case 3:
			mes "[怪異的老人]";
			mes "因此，您對武器感興趣。";
			next;
			mes "[怪異的老人]";
			mes "這些是我擁有的武器。";
			next;
			for(.@i = 1; .@i < 15; .@i++) {
				if (((.@i == 1) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 10)) ||
				((.@i == 2) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 85)) ||
				((.@i == 3 || .@i == 4 || (.@i >= 8 && .@i < 11) || .@i >= 12) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 35)) ||
				((.@i == 5 || .@i == 7 || .@i == 11) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 40)) ||
				((.@i == 6) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400))) {
					.@weapon$[.@i] = "^0000FF";
				} else {
					.@weapon$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"Show me other items.",
					""+.@weapon$[1]+"Agent Katar^000000",
					""+.@weapon$[2]+"Guillotine Katar^000000",
					""+.@weapon$[3]+"Ygnus Stale^000000",
					""+.@weapon$[4]+"End Sektura^000000",
					""+.@weapon$[5]+"Cannon Spear^000000",
					""+.@weapon$[6]+"Giant Lance^000000",
					""+.@weapon$[7]+"Chilly Spell Book^000000",
					""+.@weapon$[8]+"Recovery Light^000000",
					""+.@weapon$[9]+"Aztoe Nail^000000",
					""+.@weapon$[10]+"Scarletto Nail^000000",
					""+.@weapon$[11]+"Bloody Cross^000000",
					""+.@weapon$[12]+"Catapult^000000",
					""+.@weapon$[13]+"Big CrossBow^000000",
					""+.@weapon$[14]+"Creeper Bow^000000"
				)) {
				case 1:
					mes "[怪異的老人]";
					mes "看看。";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<代理卡塔爾[1] >>";
					mes "每2個原始盧克點獲得額外的1個命中點";
					mes "Katar / ATK 170 /重量120 /武器LV 3 /必需的LV 100 /插座1 / for Guillotine Cross";
					callsub L_Reward, 1290, 50, 100, 10; //Agent_Katar
					break;
				case 3:
					mes "<< 斷頭刀拳頭 [1] >>";
					mes "DEX +2。 FELE -30。對人類怪物的傷害增加了50％。為交叉衝擊技能增加30％的傷害。";
					mes "Katar / ATK 200 /重量150 /武器LV 4 /必需的LV 140 /插座1 / for Guillotine Cross";
					callsub L_Reward, 1291, 50, 100, 85; //Guillotine_Katar
					break;
				case 4:
					mes "<< ygnus 不斷 [1] >>";
					mes "在進行短距離攻擊時，會有一定的機會將燒傷狀態施放給玩家和敵人。";
					mes "雙手AX / ATK 250 /重量190 / FIRE屬性 /武器LV 4 /必需LV 95 /插座1 /用於高劍手 /商人班";
					callsub L_Reward, 1392, 50, 100, 35; //Ygnus_Stale
					break;
				case 5:
					mes "<<結束sektura [1]>>";
					mes "在進行短距離攻擊時，會有一定的機會向玩家和敵人進行凍結。";
					mes "雙手斧 / ATK 250 /重量190 /水財產 /武器lv 4 /必需的LV 95 /插座1 /用於高劍手 /商人班";
					callsub L_Reward, 1393, 50, 100, 35; //End_Sektura
					break;
				case 6:
					mes "<<大砲長矛[1] >>";
					mes "MSP -100，增加了受大砲矛影響的損傷的10％，加農砲長矛的額外ATK功率相當於3倍。";
					mes "單手矛 / ATK 180 /重量160 /武器LV 4 /必需的LV 100 /插座1 / for Royal Guard";
					callsub L_Reward, 1435, 50, 100, 40; //Cannon_Spear
					break;
				case 7:
					mes "<<巨型長矛[0] >>";
					mes "ASPD -10，當它被取下時，角色將獲得SP -600。在使用後背衝突螺旋後，還存在20秒的延遲時間。如果原始STR點為120，則 + ATK 300。";
					mes "雙手長矛 / ATK 20 /重量2000 /武器LV 4 /必需LV 140 /插座0 /用於符文騎士";
					callsub L_Reward, 1490, 300, 300, 400; //Giant_Lance
					break;
				case 8:
					mes "<<寒冷的咒語書[2] >>";
					mes "MATK + 160，DEX + 1。將冷螺栓的ATK％增加，鑽石粉塵相當於3倍，從這些技能中增加了相當於5倍的lv倍。無法吃到。";
					mes "圖書 / ATK 100 /重量100 /水財產 /武器LV 4 /必需的LV 100 / socket 2 / for Sorcerer";
					callsub L_Reward, 1584, 50, 100, 40; //Chilly_Spell_Book
					break;
				case 9:
					mes "<<恢復光[1] >>";
					mes "MATK + 160。取決於其精緻的LV，治療能力和SP消耗以提高技能。堅不可摧。";
					mes "單手工作人員 / ATK 30 /重量40 / Saint Property / Saint Property /武器LV 4 /必需的LV 110 /插座1 /用於大主教。";
					callsub L_Reward, 1659, 50, 100, 35; //Recovery_Light
					break;
				case 10:
					mes "<< 阿茲托美甲 [0] >>";
					mes "MATK + 80。對於物理近戰攻擊，將冰凍拋向目標，有一定的機會。機會受到精煉LV的影響。";
					mes "匕首 / ATK 160 /重量50 /風能 /武器LV 4 /必需LV 110 /插座0 /用於高劍客 /商人 /小偷課程";
					callsub L_Reward, 13069, 50, 100, 35; //Aztoe_Nail
					break;
				case 11:
					mes "<< 斯卡萊托指甲 [0] >>";
					mes "MATK + 80。對於物理近戰攻擊，將石頭詛咒施加到目標上。機會受到精煉LV的影響。";
					mes "匕首 / ATK 160 /重量50 / FIRE屬性 /武器LV 4 /必需LV 110 /插座0 /用於高劍客 /商人 /小偷課程";
					callsub L_Reward, 13070, 50, 100, 35; //Scarletto_Nail
					break;
				case 12:
					mes "<<血腥十字架[0] >>";
					mes "對於身體近戰攻擊，有一定的機會自動播種“地獄地獄”（LV 1）。機會受到精煉LV的影響。";
					mes "MACE / ATK 170 /重量150 / Darkness屬性 /武器LV 4 /必需的LV 100 /插座0 /用於高侍酒體類別";
					callsub L_Reward, 16017, 50, 100, 40; //Bloody_Cross
					break;
				case 13:
					mes "<<彈射器[2] >>";
					mes "“三角形射擊”的ATK％等於2倍，而技能也增加了2倍。";
					mes "弓 / ATK 150 /重量110 /武器LV 4 /必需的LV 100 /插座2 / for Shadow Chaser";
					callsub L_Reward, 18109, 50, 100, 35; //Catapult
					break;
				case 14:
					mes "<<大cross [2] >>";
					mes "將“箭頭風暴”的ATK％增加到5倍完善的LV。如果原始AGI點為120，ASPD + 1，則技能的消耗也會提高。";
					mes "弓 / ATK 160 /重量90 /武器LV 4 /必需的LV 110 /插座2 / for Ranger";
					callsub L_Reward, 18110, 50, 100, 35; //Big_CrossBow
					break;
				case 15:
					mes "<<爬行弓[2] >>";
					mes "DEX + 1，用於物理攻擊，具有一定機會的自動鑄造“光纖鎖”（LV 1）。";
					mes "弓 / ATK 150 /重量150 /武器LV 3 /必需的LV 120 /插座2 /3級弓箭手班";
					callsub L_Reward, 18111, 50, 100, 35; //Creeper_Bow
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
		}
	}
	close;

L_Reward:
	setarray .@amount[0], getarg(1), getarg(2), getarg(3);
	next;
	mes ""+ .@amount[0] +" Will of Warrior, "+ .@amount[1] +" Thirst for Blood, "+ .@amount[2] +" Chill of the Dead are needed for the exchange.";
	next;
	if (countitem(6469) >= .@amount[0] && countitem(6470) >= .@amount[1] && countitem(6471) >= .@amount[2]) {
		if (select("不交換", "交換") == 1) {
			mes "[怪異的老人]";
			mes "好的..";
			close;
		}
		emotion ET_PROFUSELY_SWEAT;
		progressbar "FF00FF", 2;
		delitem 6469, .@amount[0]; //Will_Of_Warrior
		delitem 6470, .@amount[1]; //Blood_Thirst
		delitem 6471, .@amount[2]; //Goast_Chill
		getitem getarg(0), 1;
		mes "[怪異的老人]";
		mes "你去。有時拜訪我，我們可以說話。";
		close;
	}
	return;
}

// Sorcerer (Rewards)
//============================================================
lhz_cube,233,24,4	script	Sorcerer#Bio4Reward	4_M_UMDANCEKID,{
	if ((MaxWeight - Weight) < 1200 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	disable_items;
	mes "[布丁]";
	mes "......................................";
	next;
	switch(select("我挖我挖", "你在這裡做什麼？", "目前發現了哪些裝備？", "在這裡，先生。")) {
	case 1:
		emotion ET_FRET;
		mes "[布丁]";
		mes "嘿，對不起，但我不是來自Umbala！你真無禮。";
		close;
	case 2:
		mes "[布丁]";
		mes "如您所見，我是一名訓練巫師，我的名字叫布丁。";
		next;
		select("....？");
		emotion ET_FRET;
		mes "[布丁]";
		mes "...那張臉是什麼？我開始感到不愉快。";
		next;
		emotion ET_OK;
		mes "[布丁]";
		mes "幾天前，當我經歷紀律時，我感到一種奇怪的，奇怪的色調。所以我遵循了這一點，它把我帶到了這裡。";
		next;
		select("我懂了。");
		mes "[布丁]";
		mes "好，讓我繼續。如果我在說話時打擾我，我會很困擾。";
		next;
		mes "[布丁]";
		mes "然後我進入了這裡的實驗室，以調查和天哪！我發現那些像“ Geffen Dungeon”的Doppelgangers一樣的怪物。那充滿了他們。";
		next;
		mes "[布丁]";
		mes "從這些怪物中，我可以找到稀有的物品，例如戰士的'^f2766will^000000'，'^952420 for for Blood^000000'和'^88c6f7chill of Dead^000000'，您在任何其他地方都找不到。";
		next;
		emotion ET_QUESTION;
		mes "[布丁]";
		mes "經過幾個小時的研究，我終於找到了一種使用這些物品的方法。你想知道嗎？";
		next;
		switch(select("哦...請原諒？ ！", "當然，我真的很想知道。")) {
		case 1:
			emotion ET_HUK, playerattached();
			emotion ET_FRET;
			mes "[布丁]";
			mes "哦，我！你問我什麼後你睡著了嗎？好吧，無論如何，我覺得自己懶得再次解釋，所以我會繼續。";
			next;
			break;
		case 2:
			emotion ET_SMILE;
			mes "[布丁]";
			mes "很好，我很滿意您正在仔細聽我的故事。那我將繼續解釋。";
			next;
			break;
		}
		mes "[布丁]";
		mes "那些'^f276666 ewill of warrior^000000'和'^952420 thirt for Blood^000000'.....是侵略性但溫暖的材料。";
		next;
		mes "[布丁]";
		mes "因此";
		next;
		mes "[布丁]";
		mes "'^952420第一個是血液^000000'可以為第三插槽提供特殊的力量。";
		next;
		mes "[布丁]";
		mes "然而！ ！ ！";
		mes "'^88c6f7chill of the Dead^000000'是一種冷型材料";
		mes "這允許從戰士的'^f2766666will^000000'或'^952420 thir thir for Blood^000000'返回電源，因此使設備不再附魔了。";
		next;
		mes "[布丁]";
		mes "你怎麼說？聽起來不有趣嗎？但是，有一個問題...";
		next;
		mes "[布丁]";
		mes "由於這些材料只能從實驗室的怪物中找到，因此特殊功率也可以居住到可以從實驗室獲得的某些設備。";
		next;
		select("那麼結論是什麼呢？");
		emotion ET_QUESTION, playerattached();
		emotion ET_FRET;
		mes "[布丁]";
		mes "哎呀..我告訴你我說話時不要削減，不是嗎？好吧，我會回答的，因為這很重要。";
		next;
		mes "[布丁]";
		mes "如果您給我帶來10'^f27666666 ewill of Warrior^000000'或10'^952420 for Blood^000000'，我會為您在實驗室中找到的某些設備提供一些電源。";
		next;
		mes "[布丁]";
		mes "而且，如果您對獲得的力量不滿意，請給我帶10'^88c6f7chill of the Dead^000000'，我將脫下這種力量。";
		next;
		mes "[布丁]";
		mes "你怎麼說？有兩個選擇.....很好，是嗎？";
		next;
		mes "[布丁]";
		mes "如果您獲得了戰士的'^f276666will^000000'或'^952420 for Blood^000000'，還可以獲得能夠接受其力量的設備，請將其帶給我。我會等。";
		close;
	case 3:
		mes "[布丁]";
		mes "hm ....到目前為止發現的設備是....";
		next;
		mes "- 檢查 -";
		mes "布丁從口袋裡取出一些紙。";
		next;
		mes "[布丁]";
		mes "讓我看看..到目前為止發現的設備是...。";
		mes "7架裝甲，14個武器。";
		next;
		while(1) {
			switch(select("好吧，我不需要知道更多。", "7 盔甲", "14種武器")) {
			case 1:
				mes "[布丁]";
				mes "如果您獲得了戰士的'^f276666will^000000'或'^952420 for Blood^000000'，還可以獲得能夠接受其力量的設備，請將其帶給我。我會等。";
				close;
			case 2:
				mes "<裝甲清單>";
				mes "巨型盾牌[1]";
				mes "Geffenia-Water的古代書[1]";
				mes "承諾聖經（第二卷）[1]";
				mes "打撈斗篷[0]";
				mes "刺客的手銬[1]";
				mes "綠色操作禮服[1]";
				mes "古代金裝飾[1]";
				next;
				mes "[布丁]";
				mes "到目前為止，這些是發現的裝甲。你想看其他東西嗎？";
				next;
				break;
			case 3:
				mes "<武器清單>";
				mes "特工卡塔爾[1]";
				mes "斷頭台卡塔爾[1]";
				mes "伊格納斯·陳舊 [1]";
				mes "結束Sektura [1]";
				mes "大砲長矛[1]";
				mes "巨型蘭斯[0]";
				mes "寒冷的咒語書[2]";
				mes "恢復光[1]";
				mes "阿茲托美甲 [0]";
				mes "斯卡萊托美甲 [0]";
				mes "血腥十字架[0]";
				mes "彈射器[2]";
				mes "大cross [2]";
				mes "爬行者弓[2]";
				next;
				mes "[布丁]";
				mes "到目前為止，這些是發現的武器。你想看其他東西嗎？";
				next;
				break;
			}
		}
		close;
	case 4:
		emotion ET_SPARK;
		mes "[布丁]";
		mes "哦，你帶了他們嗎？";
		next;
		break;
	}
	mes "[布丁]";
	mes "好的！您想在哪種設備上駐留電源？";
	next;
	setarray .@parts[0], EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_TOP;
	for(.@i = 0; .@i < 7; .@i++) {
		if (!getequipisequiped(.@parts[.@i])) {
			switch(.@i) {
				case 0:
					.@equipped$[.@i] = "^999999Armor-[Not equipped]^000000";
					break;
				case 1:
					.@equipped$[.@i] = "^999999Left Hand-[Not equipped]^000000";
					break;
				case 2:
					.@equipped$[.@i] = "^999999Right Hand-[Not equipped]^000000";
					break;
				case 3:
					.@equipped$[.@i] = "^999999Garment-[Not equipped]^000000";
					break;
				case 4:
					.@equipped$[.@i] = "^999999Accessory-[Not equipped]^000000";
					break;
				case 5:
					.@equipped$[.@i] = "^999999Accessory-[Not equipped]^000000";
					break;
				case 6:
					.@equipped$[.@i] = "^999999Helm-[Not equipped]^000000";
					break;
			}
		} else {
			.@equipped$[.@i] = getequipname(.@parts[.@i]);
		}
	}
	switch(select("I changed my mind.", .@equipped$[0], .@equipped$[1], .@equipped$[2], .@equipped$[3], .@equipped$[4], 
	.@equipped$[5], .@equipped$[6])) {
	case 1:
		mes "[布丁]";
		mes "好吧，我期待著這樣做，但那時我無能為力。";
		close;
	case 2:
		if (!getequipisequiped(EQI_ARMOR)) {
			mes "[布丁]";
			mes "嘿..盔甲在哪裡？";
			close;
		}
		.@part = EQI_ARMOR;
		break;
	case 3:
		if (!getequipisequiped(EQI_HAND_L)) {
			mes "[布丁]";
			mes "好吧..您的左手沒有任何裝備。";
			close;
		}
		.@part = EQI_HAND_L;
		break;
	case 4:
		if (!getequipisequiped(EQI_HAND_R)) {
			mes "[布丁]";
			mes "好吧..您的右手沒有任何裝備。";
			close;
		}
		.@part = EQI_HAND_R;
		break;
	case 5:
		if (!getequipisequiped(EQI_GARMENT)) {
			mes "[布丁]";
			mes "嗯....你的長袍在哪裡？";
			close;
		}
		.@part = EQI_GARMENT;
		break;
	case 6:
		if (!getequipisequiped(EQI_ACC_L)) {
			mes "[布丁]";
			mes ".....附件在哪裡？";
			close;
		}
		.@part = EQI_ACC_L;
		break;
	case 7:
		if (!getequipisequiped(EQI_ACC_R)) {
			mes "[布丁]";
			mes ".....附件在哪裡？";
			close;
		}
		.@part = EQI_ACC_R;
		break;
	case 8:
		if (!getequipisequiped(EQI_HEAD_TOP)) {
			mes "[布丁]";
			mes "嘿..你沒有戴任何舵機嗎？";
			close;
		}
		.@part = EQI_HEAD_TOP;
		break;
	}
	.@refine_count = getequiprefinerycnt(.@part);
	.@equip_item = getequipid(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@lhz_max_num = 4000;
	if (.@equip_item == 13069 || .@equip_item == 1291 || .@equip_item == 1392 || 
	    .@equip_item == 1393 || .@equip_item == 1435 || .@equip_item == 1490 || 
	    .@equip_item == 13069 || .@equip_item == 13070 || .@equip_item == 16017) {
		.@type = 1;
		if (.@equip_item == 1490)
			.@lhz_max_num = 4200;
	} else if (.@equip_item == 18109 || .@equip_item == 18110 || .@equip_item == 18111) {
		.@type = 2;
	} else if (.@equip_item == 1584 || .@equip_item == 1659) {
		.@type = 3;
	} else if (.@equip_item == 2160 || .@equip_item == 2161 || .@equip_item == 2162 ||
	           .@equip_item == 2582 || .@equip_item == 2892 || .@equip_item == 15044 || 
	           .@equip_item == 18570) {
		.@type = 4;
		if (.@equip_item == 2582 || .@equip_item == 18570)
			.@lhz_max_num = 4200;
	} else {
		mes "[布丁]";
		mes "第一的！我需要檢查該設備是否可以接受電源。";
		next;
		specialeffect2 EF_SPHERE;
		progressbar "ffff00", 2;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion ET_CRY;
		mes "[布丁]";
		mes "hm ....此設備無法接受戰士的'^f2766WILL^000000'或'^952420 thir for Blood^000000'。";
		close;
	}
	emotion ET_DELIGHT;
	mes "[布丁]";
	mes "哦，此設備可以接受戰士的'^f2766will^000000'或'^952420 thir for Blood^000000'。";
	next;
	if (countitem(6469) >= 10)
		.@menu_in$[1] = "Dwell the power of '^F2766EWill of Warrior^000000'";
	else
		.@menu_in$[1] = "Not enough ^999999'Will of Warrior'^000000";

	if (countitem(6470) >= 10)
		.@menu_in$[2] = "Dwell the power of '^952420Thirst for Blood^000000'";
	else
		.@menu_in$[2] = "Not enough ^999999'Thirst for Blood'^000000";

	if (countitem(6471) >= 10)
		.@menu_in$[3] = "Consume the '^88C6F7Chill of the Dead^000000'";
	else
		.@menu_in$[3] = "Not enough ^999999'Chill of the Dead'^000000";
	mes "[布丁]";
	mes "好的！您想駐留在設備中哪種動力？";
	next;
	switch(select("我說不！我不需要那個！ ！", .@menu_in$[1], .@menu_in$[2], .@menu_in$[3])) {
	case 1:
		mes "[布丁]";
		mes "哦，我很期待這樣做，但這全部取決於你……";
		close;
	case 2:
		if (countitem(6469) < 10) {
			mes "[布丁]";
			mes "戰士^000000'的'^f27666666。您需要攜帶10'^f27666666 ewill of Warrior^000000'";
			close;
		}
		else if (.@equip_card[3] > 0) {
			mes "[布丁]";
			mes "該設備已經具有戰士^000000'的'^f2766666666。";
			close;
		} else {
			.@socket_type = 4;
		}
		break;
	case 3:
		if (countitem(6470) < 10) {
			mes "[布丁]";
			mes "不夠'^952420第一個血液^000000'。您需要攜帶10'^952420 thir for Blood^000000'";
			close;
		}
		else if (.@equip_card[2] > 0) {
			mes "[布丁]";
			mes "該設備已經具有'^952420 thir的功率，用於血液^000000'。";
			close;
		} else {
			.@socket_type = 3;
		}
		break;
	case 4:
		// Enchant Removal
		if (countitem(6471) < 10) {
			.@menu_clear$[1] = "^999999Not enough 'Chill of the Dead'^000000";
			.@menu_clear$[2] = "^999999Not enough 'Chill of the Dead'^000000";
			.@4thzptsodyd = 1;
			.@3thzptsodyd = 1;
		} else {
			if (.@equip_card[3] > 0) {
				.@menu_clear$[1] = "Remove the power of '^F2766EWill of Warrior^000000'";
			} else {
				.@menu_clear$[1] = "^999999There's no power to remove^000000";
				.@4thzptsodyd = 1;
			}
			if (.@equip_card[2] > 0) {
				.@menu_clear$[2] = "Remove the power of '^952420Thirst for Blood^000000'";
			} else {
				.@menu_clear$[2] = "^999999There's no power to remove^000000";
				.@3thzptsodyd = 1;
			}
		}
		mes "[布丁]";
		mes "好吧，您想刪除哪種功能？";
		next;
		.@menu = select("不，我不想刪除任何內容。", .@menu_clear$[1], .@menu_clear$[2]);
		switch(.@menu){
		case 1:
			mes "[布丁]";
			mes "真的嗎？好的。我得到了它..";
			close;
		case 2:
		case 3:
			if (countitem(6471) < 10) {
				mes "[布丁]";
				mes "不夠的'^88c6f7chill的死亡^000000'。您需要攜帶10'^88c6f7chill of the Dead^000000'";
				close;
			}
			if ( (.@menu==3 && .@3thzptsodyd == 1) || (.@menu==2 && .@4thzptsodyd == 1) ) {
				mes "[布丁]";
				mes "該設備沒有任何特殊的電源。";
				close;
			}
			specialeffect EF_BEGINSPELL2;
			progressbar "ffff00", 2;
			specialeffect2 EF_FROSTWEAPON;
			delitem 6471, 10; //Goast_Chill

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_item) || callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@refine_count))
				close;

			if (.@menu == 2)
				.@equip_card[3] = 0;
			else
				.@equip_card[2] = 0;
			delequip .@part;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			mes "[布丁]";
			if (.@menu == 3)
				mes "'^952420 thir for for Blood^000000'的功能已被完美地刪除。";
			else
				mes "戰士^000000'的'^f27666666will的功率已被完美刪除。";
			close;
		}
	}
	mes "[布丁]";
	mes "在我居住之前，讓我警告你。";
	next;
	mes "[布丁]";
	mes "雖然我將戰士的“^f27666666666666666”的功率^000000'或'^952420 thir for Blood^000000'，但設備可能無法接受它，並將因此被銷毀。";
	next;
	mes "[布丁]";
	mes "^9d2628如果設備被銷毀，您還將丟失您插入的卡片，並且精煉lv也將被刪除。 ^000000";
	next;
	mes "[布丁]";
	mes "好吧。您真的希望將電源放入設備中嗎？";
	next;
	if (select("等等，我再想想。", "當然，繼續。") == 1) {
		mes "[布丁]";
		mes "好的。我希望再次見到你。";
		close;
	}
	mes "[布丁]";
	mes "希望您做出正確的決定...";
	next;
	if (.@socket_type >= 3) {
		switch(.@type){
			case 1:	// Short Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 401)  .@enchant = 4700; //Strength1
				else if (.@r < 441)  .@enchant = 4701; //Strength2
				else if (.@r < 445)  .@enchant = 4702; //Strength3
				else if (.@r < 845)  .@enchant = 4730; //Agility1
				else if (.@r < 885)  .@enchant = 4731; //Agility2
				else if (.@r < 889)  .@enchant = 4732; //Agility3
				else if (.@r < 1289) .@enchant = 4740; //Vitality1
				else if (.@r < 1329) .@enchant = 4741; //Vitality2
				else if (.@r < 1333) .@enchant = 4742; //Vitality3
				else if (.@r < 1523) .@enchant = 4710; //Inteligence1
				else if (.@r < 1542) .@enchant = 4711; //Inteligence2
				else if (.@r < 1544) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1874) .@enchant = 4721; //Dexterity2
				else if (.@r < 1877) .@enchant = 4722; //Dexterity3
				else if (.@r < 2067) .@enchant = 4750; //Luck1
				else if (.@r < 2086) .@enchant = 4751; //Luck2
				else if (.@r < 2088) .@enchant = 4752; //Luck3
				else if (.@r < 2491) .@enchant = 0;
				else if (.@r < 2891) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2931) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2935) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3125) .@enchant = 4815; //Spell1
				else if (.@r < 3144) .@enchant = 4814; //Spell2
				else if (.@r < 3146) .@enchant = 4813; //Spell3
				else if (.@r < 3148) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3348) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 2:	// Long Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 191)  .@enchant = 4700; //Strength1
				else if (.@r < 210)  .@enchant = 4701; //Strength2
				else if (.@r < 212)  .@enchant = 4702; //Strength3
				else if (.@r < 612)  .@enchant = 4730; //Agility1
				else if (.@r < 652)  .@enchant = 4731; //Agility2
				else if (.@r < 656)  .@enchant = 4732; //Agility3
				else if (.@r < 846)  .@enchant = 4740; //Vitality1
				else if (.@r < 865)  .@enchant = 4741; //Vitality2
				else if (.@r < 867)  .@enchant = 4742; //Vitality3
				else if (.@r < 1167) .@enchant = 4710; //Inteligence1
				else if (.@r < 1197) .@enchant = 4711; //Inteligence2
				else if (.@r < 1200) .@enchant = 4712; //Inteligence3
				else if (.@r < 1600) .@enchant = 4720; //Dexterity1
				else if (.@r < 1640) .@enchant = 4721; //Dexterity2
				else if (.@r < 1644) .@enchant = 4722; //Dexterity3
				else if (.@r < 1834) .@enchant = 4750; //Luck1
				else if (.@r < 1853) .@enchant = 4751; //Luck2
				else if (.@r < 1855) .@enchant = 4752; //Luck3
				else if (.@r < 2258) .@enchant = 0;
				else if (.@r < 2448) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2467) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2469) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 2669) .@enchant = 4815; //Spell1
				else if (.@r < 2689) .@enchant = 4814; //Spell2
				else if (.@r < 2691) .@enchant = 4813; //Spell3
				else if (.@r < 2693) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3093) .@enchant = 4818; //Sharp1
				else if (.@r < 3133) .@enchant = 4817; //Sharp2
				else if (.@r < 3137) .@enchant = 4816; //Sharp3
				else if (.@r < 3537) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3577) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 3:
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 534)  .@enchant = 4730; //Agility1
				else if (.@r < 554)  .@enchant = 4731; //Agility2
				else if (.@r < 556)  .@enchant = 4732; //Agility3
				else if (.@r < 956)  .@enchant = 4740; //Vitality1
				else if (.@r < 996)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1400) .@enchant = 4710; //Inteligence1
				else if (.@r < 1440) .@enchant = 4711; //Inteligence2
				else if (.@r < 1444) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1884) .@enchant = 4721; //Dexterity2
				else if (.@r < 1888) .@enchant = 4722; //Dexterity3
				else if (.@r < 2078) .@enchant = 4750; //Luck1
				else if (.@r < 2097) .@enchant = 4751; //Luck2
				else if (.@r < 2099) .@enchant = 4752; //Luck3
				else if (.@r < 2502) .@enchant = 0;
				else if (.@r < 2692) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2711) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2713) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3113) .@enchant = 4815; //Spell1
				else if (.@r < 3153) .@enchant = 4814; //Spell2
				else if (.@r < 3157) .@enchant = 4813; //Spell3
				else if (.@r < 3159) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3349) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 4:	// Armor
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 634)  .@enchant = 4730; //Agility1
				else if (.@r < 664)  .@enchant = 4731; //Agility2
				else if (.@r < 667)  .@enchant = 4732; //Agility3
				else if (.@r < 967)  .@enchant = 4740; //Vitality1
				else if (.@r < 997)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1300) .@enchant = 4710; //Inteligence1
				else if (.@r < 1330) .@enchant = 4711; //Inteligence2
				else if (.@r < 1333) .@enchant = 4712; //Inteligence3
				else if (.@r < 1633) .@enchant = 4720; //Dexterity1
				else if (.@r < 1663) .@enchant = 4721; //Dexterity2
				else if (.@r < 1666) .@enchant = 4722; //Dexterity3
				else if (.@r < 1966) .@enchant = 4750; //Luck1
				else if (.@r < 1996) .@enchant = 4751; //Luck2
				else if (.@r < 1999) .@enchant = 4752; //Luck3
				else if (.@r < 2399) .@enchant = 0;
				else if (.@r < 2549) .@enchant = 4786; //Mdef2
				else if (.@r < 2599) .@enchant = 4787; //Mdef4
				else if (.@r < 2609) .@enchant = 4788; //Mdef6
				else if (.@r < 2614) .@enchant = 4789; //Mdef8
				else if (.@r < 2616) .@enchant = 4790; //Mdef10
				else if (.@r < 2766) .@enchant = 4791; //Def3
				else if (.@r < 2866) .@enchant = 4792; //Def6
				else if (.@r < 2876) .@enchant = 4793; //Def9
				else if (.@r < 2878) .@enchant = 4794; //Def12
				else if (.@r < 3278) .@enchant = 0;
				else if (.@r < 3428) .@enchant = 4859; //Evasion1
				else if (.@r < 3478) .@enchant = 4860; //Evasion3
				else if (.@r < 3488) .@enchant = 4762; //Evasion6
				else if (.@r < 3498) .@enchant = 4800; //SP50
				else if (.@r < 3734) .@enchant = 4795; //HP100
				else if (.@r < 3834) .@enchant = 4796; //HP200
				else if (.@r < 3884) .@enchant = 4797; //HP300
				else                 .@enchant = 0;
			break;
		}
		specialeffect EF_BEGINSPELL3;
		progressbar "ffff00", 2;
		if (.@socket_type == 3)
			delitem 6470, 10; //Blood_Thirst
		else
			delitem 6469, 10; //Will_Of_Warrior

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_item) || callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@refine_count))
			close;

		.@equip_card[(.@socket_type-1)] = .@enchant;
		delequip .@part;
		
		if (.@enchant == 0){
			specialeffect2 EF_SUI_EXPLOSION;
			emotion ET_OTL;
			mes "[布丁]";
			mes "...好....設備無法接受"+((.@socket_type==3)?"'^952420Thirst for Blood^000000'":"'^F2766EWill of Warrior^000000'")+" and it got destroyed.";
			close;
		} else {
			specialeffect2 EF_FIREHIT;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			emotion ET_DELIGHT;
			mes "[布丁]";
			mes "幸運的是，"+((.@socket_type==3)?"'^952420Thirst for Blood^000000'":"'^F2766EWill of Warrior^000000'")+" has been dwelt well into your equipment...";
			close;
		}
	}
	end;
}
