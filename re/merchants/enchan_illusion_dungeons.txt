//===== rAthena Script =======================================
//= Episode 16.2 - Illusion Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Illusion series related merchants and enchanters
// Note:
// - Some dialog are missing in Illusion of Luanda exchange NPC.
//===== Changelog: ===========================================
//= 1.0 Initial release of Illusion of Moonlight [crazyarashi]
//= 1.1 Cleanup and improvements [Everade]
//= 1.2 Added Illusion of Vampire enchanter [Capuche]
//= 1.3 Added Illusion of Frozen enchanter [Capuche]
//= 1.4 Added Illusion of Turtle enchanter [Capuche]
//= 1.5 Added Illusion of Luanda enchanter [Capuche]
//= 1.6 Added Illusion of Underwater exchange [Capuche]
//= 1.7 Added Illusion of Twins enchanter [Capuche]
//= 1.8 Added Illusion Merchant [Haydrich]
//============================================================

//============================================================
//= Illusion of Moonlight
//============================================================
pay_d03_i,160,45,3	script	Gemcutter#ilp20	4_TOWER_17,3,3,{
	mes "[寶石切割者]";
	mes "你和我有生意嗎？";
	next;
	switch( select( "What are you doing here?", "Upgrade equipments" ) ) {
	case 1:
		mes "[寶石切割者]";
		mes "我來到了那個被毀壞的村莊";
		mes "尋找一些材料。感動了一些奇怪的光，現在我在這裡。";
		next;
		mes "[寶石切割者]";
		mes "我決定堅持一會兒。只要我靠近這位士兵，我就會安全。";
		mes "我有一個主張。我想要一些只能在這個地方找到的材料。";
		next;
		mes "[寶石切割者]";
		mes "為我找它們，我將改進您的設備以換取。只是您知道，我只能改善某些類型。";
		next;
		mes "[寶石切割者]";
		mes "如果您有興趣，我們可以討論討價還價的細節。";
		close;
	case 2:
		mes "[寶石切割者]";
		mes "在將其帶給我之前，請確保將 ^0000ffyour設備完善至至少 +9 ^000000。";
		mes "這是我升級服務對您的設備產生任何明顯影響的最低要求。";
		next;
		mes "[寶石切割者]";
		mes "確保您 ^0000ff in Ampection ^000000";
		mes "否則，我無法評估它的狀況。";
		next;
		mes "[寶石切割者]";
		mes "您可能已經猜到了，此後，您的設備將變成新事物。";
		mes "換句話說， ^0000ffit'將失去其當前的煉油水平，卡片和魅力。 ^000000";
		next;
		mes "[寶石切割者]";
		mes "而且我需要 ^0000灌溉石材和其他一些材料 ^000000來升級您的設備。";
		mes "選擇您想要的項目，我會告訴您我需要什麼。";
		close2;
		callshop( "barter_ill_moonlight" );
		end;
	}
	end;

OnTouch:
	if (illusion_moonlight > 7)
		npctalk "What kind of place is this?", "", bc_self;
	end;
}


//============================================================
//= Illusion of Vampire
//============================================================
gef_dun01,139,228,3	script	Great Merchant#illgef	4_M_HUMERCHANT,{
	mes "[偉大的商人]";
	mes "冒險家，您有 ^0000 climplusion石頭 ^000000嗎？如果您有一個 ^0000CDWEAPON，則在9級或更高級別的 ^000000處的裝甲 ^000000或 ^0000CDAN附件，";
	mes "然後，我可以以一些幻覺石和其他材料為代價將其換成更好的東西。";
	next;
	mes "[偉大的商人]";
	mes "那麼，您想要什麼？";
	next;
	switch( select( "Exchange illusion equipment", "What are Illusion Stones?", "Can I strengthen the exchanged item?", "Cancel" ) ) {
	case 1:
		specialeffect EF_HFLIMOON3;
		emotion ET_BEST;
		mes "[偉大的商人]";
		mes "很好地使用它，如果您發現更多幻覺石，請稍後再見！ *笑*";
		close2;
		callshop( "barter_ill_vampire" );
		end;
	case 2:
		mes "[偉大的商人]";
		mes "您問什麼是 ^0000CDillusion Stones ^000000？好吧...我認為沒有人確切知道他們是什麼。";
		next;
		mes "[偉大的商人]";
		mes "我只知道它們很少見，只能在某些特殊的地方找到，我在這里為客戶收集它們。";
		next;
		mes "[偉大的商人]";
		mes "我的客戶想弄清楚這些石頭是什麼。他們付了我很多錢，";
		next;
		mes "[偉大的商人]";
		mes "因此，我可以為您提供像您這樣的冒險家 ^0000CD昂貴的設備，以換取石頭 ^000000，但仍獲取利潤。";
		next;
		mes "[偉大的商人]";
		mes "帶上 ^0000CDA的精製設備，幻覺石 ^000000以及僅在此地方發現的各種材料。我將為您 ^0000CDUPGRADE設備，武器，裝甲或配件？ ^000000。";
		next;
		mes "[偉大的商人]";
		mes "這對我們倆有好處。讓我知道您是否對我的主張感興趣。";
		close;
	case 3:
		mes "[偉大的商人]";
		mes "因此，您想加強所獲得的設備。你很徹底。我喜歡！";
		next;
		mes "[偉大的商人]";
		mes "不久前，一位經過這個地方的冒險家告訴我，Prontera中的化學家改善了幻覺設備，以換取幻覺石頭。";
		next;
		mes "[偉大的商人]";
		mes "似乎幻覺石頭是到處都是流行的話題。每個人都想知道它們是什麼。";
		next;
		mes "[偉大的商人]";
		mes "如果您想完善設備，請轉到<NAVI> Inchanter <info> Prontera，90,115,000,0，</info> </navi>附近的城鎮辦公室。";
		close;
	case 4:
		mes "[偉大的商人]";
		mes "您找到了喜歡的東西嗎？如果您需要任何東西，請隨時告訴我。";
		close;
	}
	end;
}

//============================================================
//= Illusion of Frozen
//============================================================

// illusion exchange npc - trade items for illusion gears
ice_dun02,153,18,3	script	Illusion Stone Research	4_M_ALCHE_B,{
	disable_items;
	mes "[幻覺石研究人員]";
	mes "啊，幻覺石頭確實是神秘的...";
	next;
	switch( select( "What are you doing here?", "Upgrade equipments", "Cancel" ) ) {
	case 1:
		mes "[幻覺石研究人員]";
		mes "我在這裡找到一些我的研究所需的幻想石。";
		next;
		mes "[幻覺石研究人員]";
		mes "這個洞穴非常神秘，但我不足以獨自探索它。";
		mes "如果您進去，您能為我採購一些東西嗎？";
		next;
		mes "[幻覺石研究人員]";
		mes "作為交換，我將改進您的設備。";
		mes "你怎麼說？";
		close;
	case 2:
		mes "[幻覺石研究人員]";
		mes "在將其帶給我之前，請確保將 ^4d4dffyour設備完善至至少 +9 ^000000。";
		mes "確保您 ^4d4dfffffe是要改進 ^000000的項目。";
		next;
		mes "[幻覺石研究人員]";
		mes "您可能已經猜到了，此後，您的設備將變成新事物。";
		mes "換句話說， ^4d4dffit'LER會失去其當前的煉油和升級級別。 ^000000";
		next;
		mes "[幻覺石研究人員]";
		mes "我需要 ^4d4dfflusion石頭和其他一些材料 ^000000來升級您的設備。";
		mes "選擇您想要的項目。我會告訴你我需要什麼。";
		close2;
		callshop( "barter_ill_frozen" );
		end;
	case 3:
		mes "[幻覺石研究人員]";
		mes "很難得到..";
		close;
	}
	end;
}


// illusion enhancing - enhance the illusion gears
// note: unknown enhance rate
prontera,90,115,5	script	Illusion Enchanter#0	4_GEFFEN_09,3,3,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "我超重。我最好先照亮我的書包。";
		close;
	}
	disable_items;
	if (isbegin_quest(12415) == 0) {
		mes "[幻覺的魔法師]";
		mes "哦，你有幻覺設備嗎？";
		next;
		select("不，我連幻術裝備是什麼都不知道。");
		mes "[幻覺的魔法師]";
		mes "糟糕，對不起。我應該先介紹自己。";
		next;
		mes "[幻覺的魔法師]";
		mes "我是一名專門研究幻覺石頭的化學家，這已經在冒險家中流傳。";
		next;
		select("幻像石？");
		mes "[幻覺的魔法師]";
		mes "你不知道，不是嗎？這些天它們是最熱門的趨勢。";
		next;
		select("我不關心趨勢。");
		mes "[幻覺的魔法師]";
		mes "您一定已經聽說過最近到處亂逛的奇怪地方。幻覺石僅在那些地方發現";
		next;
		mes "[幻覺的魔法師]";
		mes "它們的原子結構與我見過的任何礦物質都不一樣。 *blah blah*...他們的化學鏈...*blah blah*";
		next;
		select("（呃哦，他走神了！）");
		mes "[幻覺的魔法師]";
		mes "無論如何，我可以用幻覺石交換或購買的設備。我稱他們為幻覺設備。";
		next;
		select("（他對名字沒有任何意義。）");
		mes "[幻覺的魔法師]";
		mes "該設備至少對幻覺結石做出了積極的反應，或者它有可能成功地迷住。";
		next;
		mes "[幻覺的魔法師]";
		mes "探索幻覺的世界，收集幻覺石頭並將其換成設備。我可以為您迷住設備。";
		next;
		mes "[幻覺的魔法師]";
		mes "這對我們倆有好處。我需要幻想石來學習，您需要有力的設備進行冒險。";
		next;
		mes "[幻覺的魔法師]";
		mes "就是說，我的服務不是免費的。它需要一些幻覺石頭和Zeny。";
		next;
		mes "[幻覺的魔法師]";
		mes "如果您有興趣，您想如何獲得我的服務會員資格？您可以以這種方式獲得折扣！";
		next;
		if (select( "Sure!", "Not interested." ) == 2) {
			mes "[幻覺的魔法師]";
			mes "好的，但是讓我知道您是否改變主意。";
			close;
		}
		mes "[幻覺的魔法師]";
		mes "好吧，然後我將您添加到我的會員列表中。我希望下次見到您時，您會擁有一些幻覺設備。";
		setquest 12415;
		completequest 12415;
		close;
	}
	mes "[幻覺的魔法師]";
	mes "我可以迷住幻覺設備。";
	next;
	switch( select( "How much do you charge?", "Add stats to Illusion equipment.", "Reset enchanted Illusion equipment." ) ) {
	case 1:
		mes "[幻覺的魔法師]";
		mes "首先，讓我告訴你，我只能吸引某些類型的幻覺設備。";
		next;
		mes "[幻覺的魔法師]";
		mes "最多可容納2個插座，每個插座迷人需要5個幻覺石。您可以隨時重置迷人的幻覺設備。";
		next;
		mes "[幻覺的魔法師]";
		mes "迷人永遠不會失敗，但是重置有時會失敗。即使是現代科學也無法改變這一點。";
		next;
		mes "[幻覺的魔法師]";
		mes "根據您選擇的成功機會，重置需要不同數量的Zeny。如果您想查看可用的付款選項，請選擇菜單。";
		close;
	case 2:
		.@reset = false;
		break;
	case 3:
		.@reset = true;
		break;
	}
	setarray .@location[0], EQI_HAND_R, EQI_ARMOR, EQI_SHOES, EQI_GARMENT, EQI_ACC_R, EQI_ACC_L, EQI_HEAD_TOP, EQI_HAND_L;
	.@s = select( "Right-handed Weapon", "Armor", "Shoes", "Garment", "Right Accessory", "Left accessory", "Helm (Upper)", "Left-handed Weapon/Shield" ) - 1;
	.@loc = .@location[.@s];
	.@eq_id = getequipid(.@loc);

	switch(.@eq_id) {
	// Illusion of Moonlight
		case 15195:	// Puente_Robe_IL
		case 16063:	// Long_Mace_IL
		case 19209:	// Nurse_Cap_IL
		case 20838:	// Muffler_IL
		case 19210:	// Apple_Of_Archer_IL
		case 22133:	// Shoes_IL
		case 26007:	// Spectral_Spear_IL
		case 26109:	// Staff_Of_Bordeaux_IL
		case 28725:	// Moonlight_Sword_IL
	// Illusion of Vampire
		case 28022:	// Infiltrator_IL
		case 28023:	// Ghoul_Leg_IL
		case 2039:	// Wizardy_Staff_IL
		case 18149:	// Balistar_IL
		case 28612:	// Book_Of_The_Apo_IL
		case 20840:	// Cape_Of_Ancient_Lord_IL
		case 28508:	// Skul_Ring_IL
		case 28509:	// Ring_IL
	// Illusion of Frozen
		case 1846:	// Combo_Battle_Glove_IL
		case 13337:	// Huuma_Flutter_Snow_IL
		case 19223:	// Cap_IL
		case 28922:	// Herald_Of_GOD_IL
		case 20847:	// Clack_Of_Servival_IL
	// Illusion of Teddy bear
		case 28745:	// Illusion_Counter_Dagger
		case 28244:	// Illusion_Gate_Keeper_DD
		case 2051:	// Illusion_Survivor's_Staff
		case 22190:	// Illusion_Boots
		case 19344:	// Illusion_Hot_blooded_Headband
	// Illusion of Turtle
		case 13469:	// Illusion_Immaterial_Sword
		case 1326:	// Illusion_War_Axe
		case 32005:	// Illusion_Pole_Axe
		case 13338:	// Illusion_Wing_Shuriken
		case 16065:	// Illusion_Iron_Driver
		case 19247:	// Illusion_Fancy_Flower
	// Illusion of Twins
		case 450182:	// Sprint_Mail_IL
		case 470066:	// Sprint_Shoes_IL
		case 490120:	// Sprint_Ring_IL
		case 490121:	// Sprint_Glove_IL
		case 550030:	// Thorn_Staff_IL
		case 550031:	// Dea_Staff_IL
		case 530015:	// Gelerdria_IL
		case 500030:	// Excalibur_IL
		case 620010:	// Doom_Slayer_IL
		case 510034:	// Ancient_Dagger_IL
		case 460017:	// Guard_IL
		case 460018:	// Siver_Guard_IL
	// Illusion of Luanda
		case 28626:	// Illusion_Tablet
		case 18174:	// Illusion_Hunter_Bow
		case 19366:	// Illusion_Goibne_Helm
		case 15348:	// Illusion_Goibne_Armor
		case 20923:	// Illusion_Goibne_Spaulders
		case 22192:	// Illusion_Goibne's_Greaves
	// Illusion of Labyrinth
		case 19428:	// Illusion_Morpheus's_Hood
		case 20948:	// Illusion_Morpheus's_Shawl
		case 32238:	// Illusion_Morpheus's_Ring
		case 32239:	// Illusion_Morpheus's_Bracelet
		case 28254:	// Illusion_Butcher
		case 21050:	// Illusion_Tae_Goo_Lyeon
		case 32301:	// Illusion_Gold_Lux
		case 28762:	// Illusion_Bazerald
	// Illusion of underwater
		case 570008:	// Electronic_Guitar_IL
		case 580008:	// Electric_Eel_IL
		case 630006:	// Brionac_IL
		case 610012:	// KatarOfCold_Icicle_IL
		case 600011:	// Death_Guidance_IL
		case 630007:	// Zephyrus_IL
		case 450144:	// Saint_Robe_IL
		case 450145:	// Water_Sprits_Armor_IL
		case 450146:	// Chain_Mail_IL
		case 400053:	// Morrigane's_Helm_IL
		case 480054:	// Morrigane's_Manteau_IL
		case 490069:	// Morrigane's_Belt_IL
		case 490070:	// Morrigane's_Pendant_IL
			break;
		case -1:
			mes "[幻覺的魔法師]";
			mes "您必須在所選位置中配備一個物品才能使其著迷。";
			close;
		default:
			mes "[幻覺的魔法師]";
			mes "該設備不是來自奇妙的幻想世界。我很生氣，你甚至向我展示了它。";
			close;
	}

	for ( .@i = 0; .@i < MAX_ITEM_RDM_OPT-1; ++.@i ) {
		if (getequiprandomoption(.@loc, .@i, ROA_ID) > 0) {
			mes "該設備檢查了 ^ff0000random選項。設備無法著迷。 ^000000";
			close;
		}
	}

	.@weapon_lvl = getequipweaponlv(.@loc);
	.@loc_enchant = .@loc;	// variable for enhancing function

	switch(.@weapon_lvl) {
	case 0:
		break;
	case 1:
	case 2:
	case 3:
		if (.@loc_enchant == EQI_HAND_L)
			.@loc_enchant = EQI_HAND_R;	// the equipment is a weapon so the enchantments must be for weapons
		break;
	default:	// illusion weapon level 4 can't be enhanced
		mes "[幻覺的魔法師]";
		mes "這是所有幻覺設備中最好的。當今時代，沒有可用的技術可以吸引它。即使是最熟練的魔法師也無法做到這一點。";
		next;
		mes "[幻覺的魔法師]";
		mes "好吧，除非他願意冒著身心健康的風險。";
		next;
		mes "[幻覺的魔法師]";
		mes "但是誰知道？也許有一天，您會遇到一個足夠有才華的人來處理這一寶貴設備的人。";
		close;
	}

	.@refine = getequiprefinerycnt(.@loc);
	.@card[0] = getequipcardid(.@loc,0);
	.@card[1] = getequipcardid(.@loc,1);
	.@card[2] = getequipcardid(.@loc,2);
	.@card[3] = getequipcardid(.@loc,3);

	switch(.@reset) {
	case false:
		.@item_req = 25271;	// IllusionStone
		.@item_req_count = 5;

		if (.@card[2] > 0 && .@card[3] > 0) {
			mes "[幻覺的魔法師]";
			mes "該設備已經掛在最大限制上。";
			close;
		}
		if (.@weapon_lvl > 0) {
			mes "[幻覺的魔法師]";
			mes "您想添加什麼樣的效果？";
			next;
			.@weapon_enchant_type = select( "Melee", "Ranged", "Magic" );
		}
		mes "[幻覺的魔法師]";
		if (.@card[3] > 0) {
			.@slot = 2;
			.@string$ = "second";
		}
		else {
			.@slot = 3;
			.@string$ = "first";
		}
		mes "我會嘗試著" + .@string$ + " ability for your item. ^ff0000Its existing Refining level and cards will not be damaged. It will not be destroyed, even if this attempt fails.^000000 Do you want to continue?";
		next;
		if (select( "I'll come back later.", "Sure!" ) == 1) {
			mes "[幻覺的魔法師]";
			mes "沒問題。如果您改變主意，請回來。";
			close;
		}
		if (countitem(.@item_req) < .@item_req_count) {
			mes "[幻覺的魔法師]";
			mes "此項目需要" + .@item_req_count + " " + getitemname(.@item_req) + ", and you don't have all that I need from you.";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@loc, .@eq_id) || callfunc("F_IsEquipCardHack", .@loc, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@loc, .@refine))
			close;

		.@card[.@slot] = callsub( S_Enchant, .@loc_enchant, .@slot, .@weapon_enchant_type );
		delitem .@item_req, .@item_req_count;
		delequip .@loc;
		getitem2 .@eq_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
		mes "[幻覺的魔法師]";
		mes "完成了。請檢查。";
		specialeffect2 EF_REPAIRWEAPON;
		close;

	case true:
		if (!F_IsCharm(.@card[2]) || !F_IsCharm(.@card[3])) {
			mes "[幻覺的魔法師]";
			mes "只能重置飽滿的物品。請兩次附魔，如果您仍然想重置它，那就把它帶給我。";
			close;
		}
		mes "[幻覺的魔法師]";
		mes "您的重置機會會有所不同，具體取決於您支付的Zeny數量。你想做什麼？";
		next;
		.@s = select( "I'll come back later.", "100,000 Zeny (50%)", "200,000 Zeny (60%)", "300,000 Zeny (70%)", "400,000 Zeny (80%)", "500,000 Zeny (90%)" ) - 1;
		if (.@s == 0) {
			mes "[幻覺的魔法師]";
			mes "沒問題。如果您改變主意，請回來。";
			close;
		}
		.@cost = 100000 * .@s;
		.@rate = 40 + 10 * .@s;

		if (Zeny < .@cost) {
			mes "[幻覺的魔法師]";
			mes "你沒有足夠的錢。";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@loc, .@eq_id) || callfunc("F_IsEquipCardHack", .@loc, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@loc, .@refine))
			close;

		Zeny -= .@cost;
		delequip .@loc;
		if (rand(100) < .@rate) {
			getitem2 .@eq_id,1,1,.@refine,0,.@card[0],.@card[1],0,0;
			mes "[幻覺的魔法師]";
			mes "完成了。請檢查。";
			specialeffect2 EF_REPAIRWEAPON;
			close;
		}
		mes "[幻覺的魔法師]";
		mes "啊，那是不幸的。好吧，你不能總是幸運。";
		specialeffect2 EF_LORD;
		close;
	}
	end;

S_Enchant:
	.@slot = getarg(1);
	switch( getarg(0)) {	// location
	case EQI_HAND_R:
		switch( getarg(2)) {// weapon enchant type
		case 1:// Melee
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate> (unknown)
				setarray .@item[0],
					4700, 1,	// Strength1
					4701, 1,	// Strength2
					4702, 1,	// Strength3
					4703, 1,	// Strength4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4808, 1,	// Fighting_Spirit4
					4809, 1,	// Fighting_Spirit3
					4810, 1,	// Fighting_Spirit2
					4811, 1,	// Fighting_Spirit1
					4820, 1,	// Fighting_Spirit5
					29081, 1,	// Expect1Lv
					29082, 1,	// Expect2Lv
					29083, 1,	// Expect3Lv
					29084, 1,	// Expect4Lv
					29085, 1,	// Expect5Lv
					29061, 1,	// Ambition1Lv
					29062, 1,	// Ambition2Lv
					29063, 1,	// Ambition3Lv
					29064, 1,	// Ambition4Lv
					29065, 1,	// Ambition5Lv
					4807, 1,	// Atk_Speed1
					4842, 1;	// Atk_Speed2
				break;
			default:
				return 0;
			}
			break;
		case 2:// Ranged
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate>
				setarray .@item[0],
					4730, 1,	// Agility1
					4731, 1,	// Agility2
					4732, 1,	// Agility3
					4733, 1,	// Agility4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4807, 1,	// Atk_Speed1
					4842, 1,	// Atk_Speed2
					4832, 1,	// Expert_Archer1
					4833, 1,	// Expert_Archer2
					4834, 1,	// Expert_Archer3
					4835, 1,	// Expert_Archer4
					4836, 1,	// Expert_Archer5
					29091, 1,	// ArchLine1Lv
					29092, 1,	// ArchLine2Lv
					29093, 1,	// ArchLine3Lv
					29094, 1,	// ArchLine4Lv
					29095, 1,	// ArchLine5Lv
					29071, 1,	// Tab1Lv
					29072, 1,	// Tab2Lv
					29073, 1,	// Tab3Lv
					29074, 1,	// Tab4Lv
					29075, 1;	// Tab5Lv
				break;
			default:
				return 0;
			}
			break;
		case 3:// Magic
			switch( .@slot ) {
			case 2:
			case 3:
				// <item ID>, <rate>
				setarray .@item[0],
					4710, 1,	// Inteligence1
					4711, 1,	// Inteligence2
					4712, 1,	// Inteligence3
					4713, 1,	// Inteligence4
					4720, 1,	// Dexterity1
					4721, 1,	// Dexterity2
					4722, 1,	// Dexterity3
					4723, 1,	// Dexterity4
					4750, 1,	// Luck1
					4751, 1,	// Luck2
					4752, 1,	// Luck3
					4753, 1,	// Luck4
					4812, 1,	// Spell4
					4813, 1,	// Spell3
					4814, 1,	// Spell2
					4815, 1,	// Spell1
					4826, 1,	// Spell5
					4805, 1,	// Heal_Amount2
					4850, 1;	// Heal_Amount3
				break;
			default:
				return 0;
			}
			break;
		default:
			return 0;
		}
		break;
	case EQI_ARMOR:
	case EQI_SHOES:
	case EQI_GARMENT:
	case EQI_HAND_L:
	case EQI_HEAD_TOP:
		switch( .@slot ) {
		case 2:
		case 3:
			// <item ID>, <rate>
			setarray .@item[0],
				4700, 700,	// Strength1
				4701, 600,	// Strength2
				4702, 500,	// Strength3
				4703, 200,	// Strength4
				4740, 700,	// Vitality1
				4741, 600,	// Vitality2
				4742, 500,	// Vitality3
				4743, 200,	// Vitality4
				4710, 700,	// Inteligence1
				4711, 600,	// Inteligence2
				4712, 500,	// Inteligence3
				4713, 200,	// Inteligence4
				4750, 700,	// Luck1
				4751, 600,	// Luck2
				4752, 500,	// Luck3
				4753, 200,	// Luck4
				4994, 10,	// Rune of Strength Lv 1
				4995, 10,	// Rune of Strength Lv 2
				4997, 10,	// Rune of Agility Lv 1
				4998, 10,	// Rune of Agility Lv 2
				29009, 10,	// Rune of Vitality Lv 1
				29010, 10,	// Rune of Vitality Lv 2
				29000, 10,	// Rune of Intellect Lv 1
				29001, 10,	// Rune of Intellect Lv 2
				29003, 10,	// Rune of Dexterity Lv 1
				29004, 10,	// Rune of Dexterity Lv 2
				29006, 10,	// Rune of Luck Lv 1
				29007, 10,	// Rune of Luck Lv 2
				4861, 700,	// MHP1
				4862, 600,	// MHP2
				4867, 440,	// MHP3
				4868, 100,	// MHP4
				4992, 10,	// HPAbsorb1_Supplement_Reactor
				4993, 10,	// SPAbsorb1_Supplement_Reactor
				29208, 10,	// SP_Absorption_2
				29210, 10;	// HP_Absorption_23
			break;
		default:
			return 0;
		}
		break;
	case EQI_ACC_L:
	case EQI_ACC_R:
		switch( .@slot ) {
		case 2:
		case 3:
			// <item ID>, <rate>
			setarray .@item[0],
				4700, 576,	// Strength1
				4701, 460,	// Strength2
				4702, 115,	// Strength3
				4703, 11,	// Strength4
				4730, 576,	// Agility1
				4731, 460,	// Agility2
				4732, 115,	// Agility3
				4733, 11,	// Agility4
				4740, 576,	// Vitality1
				4741, 460,	// Vitality2
				4742, 115,	// Vitality3
				4743, 11,	// Vitality4
				4710, 576,	// Inteligence1
				4711, 460,	// Inteligence2
				4712, 115,	// Inteligence3
				4713, 11,	// Inteligence4
				4720, 576,	// Dexterity1
				4721, 460,	// Dexterity2
				4722, 115,	// Dexterity3
				4723, 11,	// Dexterity4
				4750, 576,	// Luck1
				4751, 460,	// Luck2
				4752, 115,	// Luck3
				4753, 11,	// Luck4
				4928, 576,	// SP10
				4870, 460,	// SP25
				4800, 345,	// SP50
				4871, 115,	// SP75
				4801, 11,	// SP100
				4795, 576,	// HP100
				4796, 460,	// HP200
				4797, 345,	// HP300
				4798, 115,	// HP400
				4799, 11;	// HP500
			break;
		default:
			return 0;
		}
		break;
	default:
		return 0;
	}
	.@size = getarraysize(.@item);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		.@total_rate += .@item[.@i+1];
	.@r = rand(.@total_rate);
	for ( .@i = 0; .@i < .@size; .@i += 2 ) {
		.@rate_tmp += .@item[.@i+1];
		if (.@r < .@rate_tmp)
			break;
	}
	return .@item[.@i];

OnTouch:
	if (isbegin_quest(12415) == 0)
		npctalk "Illusion Enchanter: Come talk to me if you're interested in the Illusion equipment!", "", bc_self;
	end;
}


//============================================================
//= Illusion of Turtle
//============================================================

// Exchange npc
alberta,226,28,3	script	Equipment Researcher	4_TOWER_15,{
	.@illusion_stone_name$ = getitemname(25271);

	mes "[設備研究人員]";
	mes "我是研究的人" + .@illusion_stone_name$ + " to enhance armors and weapons.";
	mes "如果您有我需要研究的材料，您會不會將它們交給它們？";
	mes "如果您給我一些設備和一些材料，我將把它們換成加固的設備。";
	next;
	switch( select( "What are you doing here?", "Exchange equipment" ) ) {
	case 1:
		mes "[設備研究人員]";
		mes "我正在研究如何使用" + .@illusion_stone_name$ + " to strengthen equipment.";
		mes "我去去的地方" + .@illusion_stone_name$ + " was found, and I barely got back..";
		next;
		mes "[設備研究人員]";
		mes "我想我遇到了一個女孩...";
		mes "無論如何，在那之後，我找不到回去的方法。";
		mes "在這裡，我們正在從冒險家那裡獲得研究材料" + .@illusion_stone_name$ + ", and I'm offering enhanced equipment instead.";
		close;
	case 2:
		mes "[設備研究人員]";
		mes "我可以交換什麼設備？等一下。我會讓你知道。";
		next;
		mes "[設備研究人員]";
		mes "作為一種材料，您可以攜帶已精製到+9或更高的 ^0000CDEXISTISTING設備。 ^000000";
		mes "我需要一些 ^0000CD" + .@illusion_stone_name$ + " and other materials.^000000";
		next;
		mes "[設備研究人員]";
		mes "當然，我們為您提供了新的設備，所以不要忘記所有現有的 ^0000CDSMELTING和ENCHANTMENTS ^000000都會消失。";
		mes "那麼，您想要什麼樣的武器？";
		close2;
		callshop( "barter_ill_turtle" );
		end;
	}
}


//============================================================
//= Illusion of Luanda
//============================================================

// Exchange npc
com_d02_i,234,266,6	script	Village Soap#Lu	4_M_ORIENT01,{
	mes "[鄉村肥皂]";
	mes "我還為為村莊努力工作的冒險家滾了袖子。我可以使您的設備更堅固嗎？您想要的清單上有什麼嗎？";
	next;
	if (select( "Look at the illusion equipment", "Cancel" ) == 2) {
		mes "[鄉村肥皂]";
		mes "請稍後再來。只需帶上成分，我就會為您做。";
		close;
	}
	mes "[鄉村肥皂]";
	mes "誰做到了？隨身攜帶它可能對您有用。";
	close2;
	callshop( "barter_ill_luanda" );
	end;
}


//============================================================
//= Illusion of Underwater
//============================================================

iz_d04_i,134,228,4	script	Horen#Horen	4_M_BIBI,{
	if (checkweight(1201,3) == 0) {
		mes "談話無法進行，因為您有太多項目。";
		mes "請清理您的庫存，然後重試。";
		close;
	}
	mes "[歸屬]";
	mes "你好。如果您有這個地方的稀有材料，我可以增強您的設備。";
	next;
	mes "[歸屬]";
	mes "您想看可升級的設備嗎？";
	next;
	if (select( "Yes", "No" ) == 2) {
		mes "[歸屬]";
		mes "如果有興趣，請回來。";
		close;
	}
	mes "[歸屬]";
	mes "有很多類型，所以請花點時間。如果您想要一些東西，我可以立即增強它。";
	close2;
	callshop( "barter_ill_underwater" );
	end;
}

//============================================================
//= Illusion of Twins
//============================================================
// ant_d02_i,175,186,3	script	From#iltw	MECHANIC,{
ant_d02_i,175,186,3	script	From#iltw	HIDDEN_NPC,{
	if (checkweight(1201,3) == 0) {
		mes "- 您無法進行任務，因為您的項目太多。 -";
		close;
	}
	mes "[從]";
	mes "我覺得我要瘋了。";
	mes "這是因為我的心情。我是人類，這並不是說你誤會了。";
	mes "如果您在這裡得到很多，我會為您提供幫助。";
	mes "您想增強設備嗎？";
	close2;
	callshop( "barter_ill_twins" );
	end;

OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id, UNPC_CLASS, 4058;
	setunitdata .@npc_id, UNPC_SEX, SEX_MALE;
	setunitdata .@npc_id, UNPC_CLOTHCOLOR, 1;
	setunitdata .@npc_id, UNPC_HAIRSTYLE, 11;
	setunitdata .@npc_id, UNPC_HAIRCOLOR, 6;
	end;
}

//============================================================
//= Illusion Merchant
//============================================================
-	marketshop	market_resonance_stone	-1,100003:2000000:99999,100004:2000000:99999
prontera,88,113,5	script	Illusion Merchant#0829	HIDDEN_NPC,{
	mes "[幻覺商人]";
	mes "我正在出售我自己製作的兩個隨機可選武器贈款捲軸。您可以選擇自己喜歡的付款方式。";
	next;
	switch( select( "Purchase with Zeny.", "Purchase with Illusion Stones" )) {
		case 1:
			mes "[幻覺商人]";
			mes "我希望你喜歡它。";
			close2;
			callshop "market_resonance_stone";
			end;
		case 2:
			mes "[幻覺商人]";
			mes "我希望你喜歡它。";
			close2;
			callshop "resonance_stone_barter";
			end;
	}
OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id,UNPC_CLASS, JOB_MAGE_HIGH;
	setunitdata .@npc_id,UNPC_SEX,SEX_FEMALE;
	setunitdata .@npc_id,UNPC_HEADTOP,142;
	setunitdata .@npc_id,UNPC_HEADMIDDLE,92;
	setunitdata .@npc_id,UNPC_HAIRSTYLE,2;
	setunitdata .@npc_id,UNPC_HAIRCOLOR,2;

	// Restock
	npcshopupdate "market_resonance_stone",100003,2000000,99999;
	npcshopupdate "market_resonance_stone",100004,2000000,99999;
	end;
}
