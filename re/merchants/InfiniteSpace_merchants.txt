//===== rAthena Script =======================================
//= Infinite Space
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Infinite Space related merchants and enchanter
//===== Changelogs: ==========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Removed unecessary use of functions [Everade]
//= 1.2 Added warp scroll merchant [Everade]
//============================================================

// Food Merchant
-	shop	inf_ration	-1,512:-1,513:-1,515:-1,516:-1

cmd_fild07,63,268,1	script	Emergency Food Merchant#pa0829_01	4_M_BIBI,{
	mes "[緊急食品商人]";
	mes "當然，我這裡有很多東西，不是免費的。";
	close2;
	callshop "inf_ration",1;
	end;
}

// Warp Scroll Seller
cmd_fild07,375,167,1	script	Ruins Black Trader#pa0829_01	4_F_JOB_HUNTER,{
	mes "[廢墟黑人交易者]";
	mes "嘿，每次都很難走進這裡嗎？對於 ^0000FF20,000 ^000000 Zeny，您可以擁有一個滾動，可以將您直接帶到廢墟的入口處，您怎麼看？";
Purchase:
	next;
	switch( select("給我一個", "我不這麼認為" )) {
		case 1:
			if (!checkweight(22980,1) || (MaxWeight - Weight) < 1000) {
				mes "您無法進行對話，因為您有大量項目。";
				mes "請組織您的物品，然後重試。";
				close;
			}
			else if (Zeny < 20000) {
				mes "[廢墟黑人交易者]";
				mes "您似乎不再有錢了。滾動費用為20,000 Zeny。";
				close;
			}
			Zeny -= 20000;
			getitem 22980,1;
			mes "[廢墟黑人交易者]";
			mes "這是一筆不錯的交易。需要更多嗎？";
			goto Purchase;
		case 2:
			mes "[廢墟黑人交易者]";
			mes "隨時回來";
			close;
	}
}

// Equipment Shop
cmd_fild07,57,275,5	script	Artifact Appraiser#pa0829_01	1_F_02,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "您無法進行對話，因為您有大量項目。";
		mes "請組織您的物品，然後重試。";
		close;
	}
	.@stone_id = 6905;
	mes "[工件評估師]";
	mes "選擇要購買的設備類型。只要您擁有" + getitemname(.@stone_id) + ".";
	next;
	switch (select("取消:武器:盔甲")) {
		case 1:
			mes "[工件評估師]";
			mes "請隨時回來~";
			close;

		case 2:
			setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128;
			.@price = 50;
			break;

		case 3:
			setarray .@equip_id,15141,22075,20779,19033;
			.@price = 50;
			break;
	}
	.@menu$ = "Cancel:";
	for (.@i = 0; .@i < getarraysize(.@equip_id); .@i++)
		.@menu$ += getitemname(.@equip_id[.@i]) + ":";
	.@s = select(.@menu$) - 1;
	switch (.@s) {
		case 0:
			mes "[工件評估師]";
			mes "請隨時回來~";
			close;

		default:
			.@s--;
			mes "[工件評估師]";
			mes "您需要 ^0000ff" + .@price + "^000000 " + getitemname(.@stone_id) + " to purchase the " + getitemname(.@equip_id[.@s]) + "~";
			next;
			if (select("取消:購買") == 1) {
				mes "[工件評估師]";
				mes "請隨時回來~";
				close;
			}
			if (countitem(.@stone_id) < .@price) {
				mes "[工件評估師]";
				mes "你沒有足夠的" + getitemname(.@stone_id) + " to purchase this item.";
				close;
			}
			mes "[工件評估師]";
			mes "謝謝您的信任。請下次再回來。";
			delitem .@stone_id,.@price;
			getitem .@equip_id[.@s],1;
			close;
	}
}

// Equipment Enchanter
cmd_fild07,60,275,3	script	Artifact Enhancer#pa0829_01	4_F_JOB_BLACKSMITH,{
	if (!checkweight(1201,1) || (MaxWeight - Weight) < 1000) {
		mes "您無法進行對話，因為您有大量項目。";
		mes "請組織您的物品，然後重試。";
		close;
	}
	disable_items;
	.@stone_id = 6905;
	function equip_check;
	mes "[工件增強劑]";
	mes "如果您想加強在無限空間中獲得的設備，那麼您已經來到了正確的位置。";
	mes "你有什麼嗎" + getitemname(.@stone_id) + "?";
	next;
	switch (select("如何為我的裝備附魔？ :為裝備附魔。 :初始化裝備的附魔。")) {
		case 1:
			mes "[工件增強劑]";
			mes "您將能夠獲得" + getitemname(.@stone_id) + ", if you explore the space under the Paros Lighthouse.";
			next;
			mes "[工件增強劑]";
			mes "對於這種材料，我可以加強您從無限空間獲得的設備。";
			next;
			mes "[工件增強劑]";
			mes "我只能附魔第三和第四個插槽。";
			next;
			mes "[工件增強劑]";
			mes "在迷人的過程中，設備沒有可能會被摧毀，但是在初始化陳列機時，它很有可能會被破壞。";
			next;
			mes "[工件增強劑]";
			mes "然後，當您想迷住設備時來拜訪我。";
			break;

		case 2:
			.@stone_id = 6905;
			.@fee = 20;
			mes "[工件增強劑]";
			mes "請選擇要著迷的設備。";
			next;
			switch (select("取消:武器:盔甲:鞋子:服裝:頭盔")) {
				case 1:
					mes "[工件增強劑]";
					mes "然後，當您想迷住設備時來拜訪我。";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0;.@i < 4;.@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = getequipcardid(.@part,.@i);
			}
			if (.@card[2] > 0) {
				mes "[工件增強劑]";
				mes "我只能迷上第三個插槽。您的設備無法進一步迷戀。";
				close;
			}
			switch (.@part) {
				case EQI_HAND_R:
					setarray .@enchant$,
					"4700,4701:4710,4711:4720,4721",
					"4811,4810,4809,4808,4820,4821,4822,4823:4815,4814,4813,4812,4826,4827,4828,4829:4832,4833,4834,4835,4836,4837,4838,4839";
					break;

				case EQI_ARMOR:
				case EQI_SHOES:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4795,4796,4797:4870,4871,4800:4870,4871,4800";
					break;

				case EQI_GARMENT:
				case EQI_HEAD_TOP:
					setarray .@enchant$,
					"4700,4701,4702,4703:4710,4711,4712,4713:4720,4721,4722,4723",
					"4861,4862,4867,4868,4900:4861,4862,4867,4868,4900:4861,4862,4867,4868,4900";
					break;
			}
			.@slot = (.@card[3] > 0 ? 2 : 3);
			.@index = (.@slot == 3 ? 0 : 1);
			mes "[工件增強劑]";
			mes "您可以選擇3種類型的附魔，而附魔費是" + .@fee + " " + getitemname(.@stone_id) + ".";
			mes "我將確保沒有問題而迷人。";
			next;
			.@type = select("退出:物理:魔法:範圍") - 2;
			mes "[工件增強劑]";
			if (.@slot == 3)
				mes "好吧，讓我們開始第一個結界。";
			else
				mes "好吧，讓我們繼續第二次結界。";
			next;
			if (select("我稍後回來:請繼續。") == 1) {
				mes "[工件增強劑]";
				mes "如果您改變主意，只需回到我身邊。";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[工件增強劑]";
				mes "嗯。順便說一句，我認為你在聽，你還不夠" + getitemname(.@stone_id) + ".";
				close;
			}
			explode(.@T$,.@enchant$[.@index],":");
			explode(.@TT$,.@T$[.@type],",");
			.@enchant = atoi(.@TT$[rand(getarraysize(.@TT$))]);
			.@card[.@slot] = .@enchant;
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
			specialeffect2 EF_REPAIRWEAPON;
			mes "[工件增強劑]";
			mes "嗯。做得好。立即檢查您的設備。";
			break;
		
		case 3:
			.@stone_id = 6905;
			.@fee = 30;
			.@break_chance = 30;
			mes "[工件增強劑]";
			mes "請選擇要著迷的設備。";
			next;
			switch (select("取消:武器:盔甲:鞋子:服裝:頭盔")) {
				case 1:
					mes "[工件增強劑]";
					mes "然後，當您想迷住設備時來拜訪我。";
					close;

				case 2:
					.@part = EQI_HAND_R;
					break;

				case 3:
					.@part = EQI_ARMOR;
					break;

				case 4:
					.@part = EQI_SHOES;
					break;

				case 5:
					.@part = EQI_GARMENT;
					break;

				case 6:
					.@part = EQI_HEAD_TOP;
					break;
			}
			.@equip_id = getequipid(.@part);
			.@refine = getequiprefinerycnt(.@part);
			equip_check(.@equip_id);
			for (.@i = 0; .@i < 4; .@i++) {
				.@card[.@i] = getequipcardid(.@part,.@i);
				.@check[.@i] = .@card[.@i];
			}
			if (.@card[3] == 0) {
				mes "[工件增強劑]";
				mes "您的設備上沒有任何附魔。";
				close;
			}
			mes "[工件增強劑]";
			mes "在初始化過程中，您的無限太空設備有可能會破壞。您還願意繼續嗎？";
			next;
			if (select("退出:繼續") == 1) {
				mes "[工件增強劑]";
				mes "如果您改變主意，只需回到我身邊。";
				close;
			}
			if (countitem(.@stone_id) < .@fee) {
				mes "[工件增強劑]";
				mes "嗯。順便說一句，我認為你在聽，你還不夠" + getitemname(.@stone_id) + ".";
				close;
			}
			delitem .@stone_id,.@fee;
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@check[0], .@check[1], .@check[2], .@check[3]))
				close;
			delequip .@part;
			if (rand(1,100) > .@chance) {
				getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],0,0;
				specialeffect2 EF_REPAIRWEAPON;
				mes "[工件增強劑]";
				mes "嗯。做得好。立即檢查您的設備。";
			} else {
				specialeffect2 EF_REFINEFAIL;
				mes "[工件增強劑]";
				mes "好吧，我確實警告過你。你不幸運，是嗎？";
			}
			break;
	}
	close;

	function	equip_check	{
		setarray .@equip_id,1994,1938,13323,13126,28703,2024,16038,21014,28105,18128,15141,22075,20779,19033;
		if (!getarg(0)) {
			mes "[工件增強劑]";
			mes "你脫下設備了嗎？";
			close;
		}
		if (inarray(.@equip_id,getarg(0)) == -1) {
			mes "[工件增強劑]";
			mes "該設備不適合迷人。不要忘記，只有無限設備系列才能被迷住。";
			close;
		}
		return;
	}
}
