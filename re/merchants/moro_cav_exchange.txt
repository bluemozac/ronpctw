//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= [Official Conversion]
//= Hero Ring enchants NPCs
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//= 1.1 Slight change to match official script. [Capuche]
//============================================================

moro_cav,32,73,5	script	Hero Token Exchanger#1	4_F_SHADOWCHASER,{
	disable_items;
	mes "[英雄令牌交換器]";
	mes "如果你與摩洛哥戰鬥";
	mes "並收集";
	mes "英雄令牌，";
	mes "我可以為您換一個特殊的戒指。";
	mes "你想看看我的戒指嗎？";
	while(true) {
		next;
		if (checkweight(2163,2) == 0) {
			mes "[英雄令牌交換器]";
			mes "您的包裝有很多物品，";
			mes "安排後，請再來！";
			close;
		}
		switch( select("戒指說明", "與戒指交換。", "結束談話。" ) ) {
		case 1:
			mes "[英雄令牌交換器]";
			mes "這個英雄戒指";
			mes "不得";
			mes "看";
			mes "感人的，";
			next;
			mes "[英雄令牌交換器]";
			mes "但是把它帶到";
			mes "專業的魔法師伯恩斯（Byrnes），";
			mes "他可以著迷";
			mes "其中所有4個插座";
			mes "並帶出來";
			mes "它的潛在能力。";
			next;
			mes "[英雄令牌交換器]";
			mes "英雄戒指";
			mes "可以幫助您";
			mes "在征服中";
			mes "摩洛哥";
			mes "還有其他問題嗎？";
			continue;
		case 2:
			mes "- 英雄戒指 -";
			mes "^006400A為戰士製造的戒指^000000";
			mes "^006400ON擊敗Morocc的追求..^000000";
			mes "^006400時，迷人的^000000";
			mes "^006400借給其所有者^000000";
			mes "^006400面對摩洛哥的力量。 ^000000";
			mes "^006400CLAS：附件/DEF：0^000000";
			mes "^006400WAWEIGH：20/必需級別：160^000000";
			mes "^006400SLOT：0/設備到：所有工作^000000";
			next;
			mes "[英雄令牌交換器]";
			mes "英雄戒指需要";
			mes "30英雄令牌";
			mes "進行交流。";
			mes "您想交換嗎？";
			next;
			if (select("上一個菜單。", "兌換 30 個英雄代幣。" ) == 1) {
				mes "[英雄令牌交換器]";
				mes "思考";
				mes "再次。";
				continue;
			}
			if (countitem(6684) < 30) {
				mes "[英雄令牌交換器]";
				mes "^006400英雄令牌^000000";
				mes "被給予";
				mes "那些勇敢戰鬥的人。";
				mes "你沒有";
				mes "30 ^006400英雄令牌 ^000000，";
				mes "因此，我無法幫助您。";
				continue;
			}
			mes "[英雄令牌交換器]";
			mes "我會交換";
			mes "30 ^006400英雄令牌 ^000000";
			mes "帶有1 ^006400英雄環 ^000000。";
			mes "還要別的嗎？";
			delitem 6684,30;	// TokenOfHero
			getitem 2981,1;		// RingOfHero
			continue;
		case 3:
			mes "[英雄令牌交換器]";
			mes "一天";
			mes "你會發現自己";
			mes "需要英雄戒指。";
			close;
		}
	}
}

moro_cav,34,65,5	script	Expert Enchanter Byrnes	1_M_03,{
	disable_items;
	mes "[伯恩斯]";
	mes "你聽說過英雄戒指嗎？只有真正的英雄應該擁有那個宏偉的戒指，我一直在等待一個。";
	mes "我能為你做什麼？";
	next;
	switch( select("附魔英雄戒指。", "重置英雄戒指上的結界。", "取消。" ) ) {
	case 1:
		mes "[伯恩斯]";
		mes "我只能配備戒指。您想迷住哪個裝備的戒指？";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select("左邊", "正確的" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[伯恩斯]";
			mes "請裝備您想要著迷的英雄戒指。";
			close;
		case 2981:
			break;
		default:
			mes "[伯恩斯]";
			mes "請裝備您的英雄戒指。我什麼都不魔。";
			close;
		}
		// custom at this point
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[0] > 0) {
			mes "[伯恩斯]";
			mes "充滿著迷人的英雄戒指再也無法著迷了。";
			close;
		}
		if (.@card[3] == 0) {
			setarray .@enchant_list$[0], "DEF", "MDEF";
			.@slot = 4;
		}
		else if (.@card[2] == 0) {
			setarray .@enchant_list$[0], "Max HP", "Max SP";
			.@slot = 3;
		}
		else if (.@card[1] == 0) {
			setarray .@enchant_list$[0], "Atk", "MAtk";
			.@slot = 2;
		}
		else {
			setarray .@enchant_list$[0], "STR", "INT", "AGI", "VIT", "DEX", "LUK";
			.@slot = 1;
		}
		mes "[伯恩斯]";
		mes "您想要哪種能力作為老虎機上的附魔" + .@slot + "?";
		next;
		.@enchant_list_size = getarraysize(.@enchant_list$);
		for ( .@i = 0; .@i < .@enchant_list_size; ++.@i )
			.@menu$ += .@enchant_list$[.@i] + ":";
		.@type = select(.@menu$) - 1;

		switch(.@slot) {
		case 4:
			switch(.@type) {// enchant list index
			case 0:	// DEF
				setarray .@bonus[0], 4791, 4792, 4793, 4794, 4893;
				break;
			default:	// MDEF
				setarray .@bonus[0], 4890, 4786, 4891, 4787, 4892;
				break;
			}
			break;
		case 3:
			switch(.@type) {
			case 0:	// Max HP
				setarray .@bonus[0], 4861, 4862, 4867, 4868, 4900;
				break;
			default:	// Max SP
				setarray .@bonus[0], 4800, 4870, 4871, 4801, 4802;
				break;
			}
			break;
		case 2:
			switch(.@type) {
			case 0:	// Atk
				setarray .@bonus[0], 4882, 4766, 4767, 4894, 4895;
				break;
			default:	// Matk
				setarray .@bonus[0], 4883, 4896, 4897, 4898, 4899;
				break;
			}
			break;
		case 1:
			switch(.@type) {
			case 0:	// STR
				setarray .@bonus[0], 4700, 4701, 4702, 4703, 4704;
				break;
			case 1:	// INT
				setarray .@bonus[0], 4710, 4711, 4712, 4713, 4714; 
				break;
			case 2:	// AGI
				setarray .@bonus[0], 4730, 4731, 4732, 4733, 4734;
				break;
			case 3:	// VIT
				setarray .@bonus[0], 4740, 4741, 4742, 4743, 4744;
				break;
			case 4:	// DEX
				setarray .@bonus[0], 4720, 4721, 4722, 4723, 4724;
				break;
			default:	// LUK
				setarray .@bonus[0], 4750, 4751, 4752, 4753, 4754;
				break;
			}
			break;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		.@bonus_size = getarraysize(.@bonus);
		.@card[.@slot-1] = .@bonus[ rand(.@bonus_size) ];	// unknown rates
		specialeffect2 EF_ANGEL2;
		mes "[伯恩斯]";
		mes "結界完成了！";
		delequip .@eqi_slot[.@s];
		getitem2 2981,1,1,0,0,.@card[0],.@card[1],.@card[2],.@card[3];
		close;
	case 2:
		mes "[伯恩斯]";
		mes "為了重置您的英雄戒指，我需要3個英雄令牌。";
		next;
		if (countitem(6684) < 3) {
			mes "[伯恩斯]";
			mes "您沒有足夠的英雄令牌。我無法重置你的英雄戒指。";
			close;
		}
		mes "[伯恩斯]";
		mes "我只能重置配備的戒指。您想重置哪個裝備的戒指？";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select("左邊", "正確的" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[伯恩斯]";
			mes "您首先必須裝備物品以重置其結界狀態。";
			close;
		case 2981:
			break;
		default:
			mes "[伯恩斯]";
			mes "請裝備您的英雄戒指。我不能重置其他任何東西。";
			close;
		}
		// custom at this point
		if (countitem(6684) < 3) {
			mes "[伯恩斯]";
			mes "您沒有足夠的英雄令牌。我無法重置你的英雄戒指。";
			close;
		}
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[3] == 0) {
			mes "[伯恩斯]";
			mes "您的英雄戒指目前處於無魔力狀態。";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		specialeffect2 EF_ANGEL2;
		mes "[伯恩斯]";
		mes "初始化完成了。";
		delitem 6684,3;
		delequip .@eqi_slot[.@s];
		getitem 2981,1;
		close;
	case 3:
		close;
	}
}
