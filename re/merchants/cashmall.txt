//===== rAthena Script ======================================= 
//= Cash Mall
//===== Changelogs: ==========================================
//= 1.0 Initial version from replays. [Lemongrass]
//= 1.1 Cash Hair Stylist [Kisuka]
//= 1.2 Updated Hair Stylist [Lemongrass]
//============================================================

prontera,137,125,4	script	Cash Sales Guide#prontera::cashmall_warper	4_M_HUMAN_02,{
	mes "[指導]";
	mes "你好~";
	mes "本指南希望成為帶您去特殊地方的人。";
	mes "那是什麼樣的地方？";
	next;
	mes "[指導]";
	mes "這是一個充滿神秘物品的地方。";
	mes "要在那裡買東西，您需要特殊的錢。";
	next;
	mes "[指導]";
	mes "那麼，您準備好去那裡了嗎？";
	next;
	if (select( "Yes", "No" ) == 2) {
		mes "[指導]";
		mes "好吧~";
		mes "但是不要沮喪。";
		mes "總是有更多機會~";
		close;
	}
	mes "[指導]";
	mes "是的~你真的準備好了。";
	mes "好吧，得到一些好東西~";
	mes "糟糕，我忘了。當您出來時，一定要回到這裡。";
	mes "將這個~";
	close2;
	setarray .@maparray$[0], "prontera", "morocc", "moc_ruins", "geffen", "payon", "lighthalzen", "rachel";
	.@return = inarray(.@maparray$[0], strcharinfo(3));

	if (.@return > 0)
		cashmall_return = .@return;
	else // Default to Prontera if not found
		cashmall_return = 0;

	warp "itemmall",22,43;
	end;
}
morocc,164,39,4	duplicate(cashmall_warper)	Cash Sales Guide#morocc	4_M_HUMAN_02
moc_ruins,115,143,4	duplicate(cashmall_warper)	Cash Sales Guide#moc_ruins	4_M_HUMAN_02
geffen,115,168,4	duplicate(cashmall_warper)	Cash Sales Guide#geffen	4_M_HUMAN_02
payon,164,157,4	duplicate(cashmall_warper)	Cash Sales Guide#payon	4_M_HUMAN_02
lighthalzen,206,144,4	duplicate(cashmall_warper)	Cash Sales Guide#lighthalzen	4_M_HUMAN_02
rachel,128,144,4	duplicate(cashmall_warper)	Cash Sales Guide#rachel	4_M_HUMAN_02

itemmall,21,45,4	script	Exit Guide#cashmall_exit	4_M_HUMAN_02,{
	mes "[指導]";
	mes "你玩得開心嗎？";
	mes "非常感謝您使用我們的物品購物中心。";
	mes "我們將繼續為您帶來有價值的物品。";
	next;
	mes "[指導]";
	mes "您想從您的來源回來嗎？";
	next;
	if (select( "Yes", "No" ) == 2) {
		mes "[指導]";
		mes "還有更多的東西要看。";
		mes "玩的很開心。";
		close;
	}
	mes "[指導]";
	mes "謝謝你的到來。";
	mes "我會將您從您來自的地方寄回。";
	close2;
	switch(cashmall_return) {
		default:
		case 0:
			.@returnmap$ = "prontera";
			setarray .@xy[0],116,72;
			break;
		case 1:
			.@returnmap$ = "morocc";
			setarray .@xy[0],159,46;
			break;
		case 2:
			.@returnmap$ = "moc_ruins";
			setarray .@xy[0],109,137;
			break;
		case 3:
			.@returnmap$ = "geffen";
			setarray .@xy[0],120,38;
			break;
		case 4:
			.@returnmap$ = "payon";
			setarray .@xy[0],161,58;
			break;
		case 5:
			.@returnmap$ = "lighthalzen";
			setarray .@xy[0],159,95;
			break;
		case 6:
			.@returnmap$ = "rachel";
			setarray .@xy[0],115,125;
			break;
	}
	cashmall_return = 0;
	warp .@returnmap$,.@xy[0],.@xy[1];
	end;
}

itemmall,41,53,3	script	Cat Hand Salesman Macaroon#cashmall	4_M_MERCAT1,{
	mes "[杏仁餅]";
	mes "歡迎！";
	mes "今天並非每天都來！";
	mes "今天可以看到的東西~瞧！";
	mes "您只能與" + mesitemlink( 1000274 ) + "!";
	mes "隨時選擇！";
	next;
	switch( select( "3-1st Job Group Skill Shadow", "3-2nd Job Group Skill Shadow", "Extended Job Group Skill Shadow", "General Shadow by Occupation", "Shadow Cube", "Smelting, Modification, Useful Items", "Drop Box", "Spellbook" ) ){
		case 1:
			mes "[杏仁餅]";
			mes "^ff00663-1st Job Group技能Shadow^000000您說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon01";
			end;

		case 2:
			mes "[杏仁餅]";
			mes "^ff00663-2nd Job Group技能Shadow^000000您說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon02";
			end;

		case 3:
			mes "[杏仁餅]";
			mes "^ff0066延遲的工作組技能shadow^000000你說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon03";
			end;

		case 4:
			mes "[杏仁餅]";
			mes "^ff0066佔領的危險^000000你說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon04";
			end;

		case 5:
			mes "[杏仁餅]";
			mes "^ff0066shadow cube^000000你說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon05";
			end;

		case 6:
			mes "[杏仁餅]";
			mes "^ff0066smelting，修改，有用的項目^000000您說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon06";
			end;

		case 7:
			mes "[杏仁餅]";
			mes "^ff0066drop Box^000000您說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon07";
			end;

		case 8:
			mes "[杏仁餅]";
			mes "^ff0066spellbook^000000您說！";
			mes "他們在這裡！";
			close2;
			callshop "CachuaCoupon08";
			end;
	}
}

itemmall,24,77,4	script	Blacksmith Basta#pr	4_M_DWARF,{
	if( !getbattleflag( "feature.refineui" ) ){
		end;
	}

	mes "[鐵匠巴斯塔]";
	mes "我接管了Mighty Hammer，Brock和Shadow Blackmith的工作。從現在開始，我會很忙。";
	close2;
	refineui();
	end;
}

itemmall,27,77,4	script	Mighty Hammer#im	4_M_DWARF,{
	mes "[鐵匠強大的錘子]";
	mes "如果是關於煉油，請問巴斯塔。我退休了。";
	close;
}

itemmall,24,74,5	script	Blacksmith Brock#im	4_M_HUMAN_01,{
	mes "[鐵匠布羅克]";
	mes "我現在退休了。如果是關於完善的，請向那邊的Basta請求。";
	close;
}

itemmall,19,74,5	script	Stylist#cash	91,{
	if( getbattleflag( "feature.stylist" ) ){
		mes "[傑里米]";
		mes "你好可愛~";
		mes "我希望您稱我為主造型師傑里米（Jeremy）。";
		next;
		if( select("改變風格:結束對話") == 2 ){
			mes "[傑里米]";
			mes "我的商店總是開放的，所以請隨時回來。";
			close;
		}
		close2;
		openstylist();
		end;
	}

	mes "[設計師]";
	mes "嘿，我是卡尼基。";
	mes "我在這裡開始新的";
	mes "介紹我的趨勢";
	mes "特殊的新髮型！";
	mes "給我一個 ^ff0000new樣式優惠券 ^000000，";
	mes "我會換你的頭髮~";
	next;
	if (Sex == SEX_MALE) {
		mes "[機械的]";
		mes "我有兩個特別";
		mes "男人的髮型，";
		mes "緊急治愈珀斯";
		mes "和光環刀片切割。";
		mes "和光環刀片切割。";
		next;
		cutin "hair_m_24.bmp",4;
		mes "[機械的]";
		mes "這是緊急情況";
		mes "治愈燙髮。這是一個品牌";
		mes "我最近發明的新風格。";
		mes "你怎麼認為？酷，是嗎？";
		next;
		cutin "",255;
		cutin "hair_m_25.bmp",4;
		mes "[機械的]";
		mes "現在這是光環";
		mes "刀片切割。你沒有";
		mes "看到了類似的東西";
		mes "以前，不是嗎？";
		next;
		cutin "",255;
	}else{
		mes "[機械的]";
		mes "我有兩個新髮型";
		mes "婦女，假設";
		mes "和靈魂改變者削減了。";
		mes "你想看看如何";
		mes "這些樣式看起來？";
		next;
		cutin "hair_f_24.bmp",4;
		mes "[機械的]";
		mes "這是假設";
		mes "珀斯。真的很好";
		mes "在你身上，不是嗎？";
		next;
		cutin "",255;
		cutin "hair_f_25.bmp",4;
		mes "[機械的]";
		mes "接下來是靈魂";
		mes "剪裁。怎麼辦";
		mes "您認為？不是很好嗎？";
		next;
		cutin "",255;
	}
	mes "[機械的]";
	mes "你決定嗎？";
	mes "如果你想我";
	mes "改變髮型？";
	next;
	if (select( "Change Hairstyle", "Don't Change" ) == 2) {
		mes "[機械的]";
		mes "真的嗎？哦，太糟糕了。";
		mes "好吧，如果你改變了";
		mes "介意更新您的";
		mes "髮型，回來";
		mes "讓我知道，好嗎？";
		close;
	}
	if (countitem(7622) < 1) {
		mes "[機械的]";
		mes "對不起，但是我可以";
		mes "只提供我的髮型";
		mes "如果您帶來新的服務";
		mes "樣式優惠券。請來";
		mes "在你設法後回到我身邊";
		mes "得到這些優惠券之一，好嗎？";
		close;
	}
	mes "[機械的]";
	mes "太好了，你帶來了";
	mes "新樣式優惠券！";
	mes "好吧，哪個髮型";
	mes "你想擁有嗎？";
	next;
	if (Sex == SEX_MALE) {
		if(select("Emergency Heal Perm", "Aura Blade Cut") == 1) {
			delitem 7622,1;	// New_Style_Coupon
			setlook 1,24;
		}else{
			delitem 7622,1;	// New_Style_Coupon
			setlook 1,25;
		}
	}else{
		if(select("Assumptio Perm", "Soul Changer Cut") == 1) {
			delitem 7622,1;	// New_Style_Coupon
			setlook 1,24;
		}else{
			delitem 7622,1;	// New_Style_Coupon
			setlook 1,25;
		}
	}
	mes "[機械的]";
	mes "那裡 - ！完成了！";
	mes "你喜歡你的";
	mes "新頭髮？好吧，我希望";
	mes "再次見到你。小心！";
	close;
}
