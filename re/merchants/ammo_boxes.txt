//===== rAthena Script =======================================
//= Magazine Dealers
//===== Description: =========================================
//= Turns bullets into magazines/packs.
//===== Changelogs: ==========================================
//= 1.0 First version. [SinSloth]
//= 1.1 Optimized version - Reduced to only one function [SinSloth]
//= 1.2 Optimized^2, corrected npc's name [ultramage]
//= 1.2a Optimized. Please, ommit extra NPC names [Lupus]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.4 Updated to match AEGIS script again. [Masao]
//= 1.5 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 2.0 Clean-up. [Capuche]
//= 2.1 Fixes Issue #1482, where Rebellion jobs cannot use this npc. [Limestone]
//= 2.2 Updated to new bullets. [Aleos]
//============================================================

que_ng,187,149,3	script	Magazine Dealer Kenny::mdk	4_M_01,{
	mes "[肯尼]";
	mes "子彈袋將幫助您更輕鬆地攜帶子彈。";
	mes "快來看看~";
	next;
	switch(select("各種子彈盒:球體交換:舊球體交換:子彈交換:取消")) {
	case 1:
		switch(select("子彈盒:血腥子彈盒:銀子彈盒:彈藥AP子彈盒:彈藥熾熱子彈盒:彈藥冷凍子彈盒:彈藥閃電子彈盒:彈藥魔法石子彈盒:彈藥淨化彈盒:照明彈盒:閃電彈盒:冰彈盒:毒彈盒:盲彈盒:取消")) {
			case 1: callsub S_Casing,13200,12149;
			case 2: callsub S_Casing,13222,22737;
			case 3: callsub S_Casing,13221,22738;
			case 4: callsub S_Casing,13215,22744;
			case 5: callsub S_Casing,13216,22745;
			case 6: callsub S_Casing,13217,22746;
			case 7: callsub S_Casing,13218,22747;
			case 8: callsub S_Casing,13219,22748;
			case 9: callsub S_Casing,13220,22749;
			case 10: callsub S_Casing,13228,23123;
			case 11: callsub S_Casing,13229,23124;
			case 12: callsub S_Casing,13230,23125;
			case 13: callsub S_Casing,13231,23126;
			case 14: callsub S_Casing,13232,23127;
			case 15: callsub S_Close;
		}
	case 2:
		switch(select("火彈:風彈:冰凍彈:毒彈:致盲彈:取消")) {
		case 1: callsub S_Sphere_Exchange,13223,13228;
		case 2: callsub S_Sphere_Exchange,13224,13229;
		case 3: callsub S_Sphere_Exchange,13227,13230;
		case 4: callsub S_Sphere_Exchange,13225,13231;
		case 5: callsub S_Sphere_Exchange,13226,13232;
		case 6: callsub S_Close;
		}
	case 3:
		switch(select("火球體:風球體:冰凍球體:毒球體:致盲球體:取消")) {
		case 1: callsub S_Old_Sphere_Exchange,13203,13228;
		case 2: callsub S_Old_Sphere_Exchange,13204,13229;
		case 3: callsub S_Old_Sphere_Exchange,13207,13230;
		case 4: callsub S_Old_Sphere_Exchange,13205,13231;
		case 5: callsub S_Old_Sphere_Exchange,13206,13232;
		case 6: callsub S_Close;
		}
	case 4:
		switch(select("子彈彈藥 L:子彈彈藥 M:子彈彈藥 H:子彈彈藥 SH:子彈彈藥 XH:取消")) {
		case 1: callsub S_Slug_Exchange,13210,24;
		case 2: callsub S_Slug_Exchange,13211,12;
		case 3: callsub S_Slug_Exchange,13212,8;
		case 4: callsub S_Slug_Exchange,13213,6;
		case 5: callsub S_Slug_Exchange,13214,5;
		case 6: callsub S_Close;
		}
	case 5:
		callsub S_Close;
	}

S_Casing:
	.@bullet_id = getarg(0);
	.@pack_id = getarg(1);
	.@bullet_str$ = getitemname(.@bullet_id);
	.@pack_str$ = getitemname(.@pack_id);
	.@bullet_count = countitem(.@bullet_id);
	.@pack_count = .@bullet_count / 500;

	if (.@bullet_count < 500 || getequipid(EQI_AMMO) != -1 || BaseClass != Job_Gunslinger) {
		mes "[肯尼]";
		mes "嘿~您需要確切的商品來交換。去獲取更多。";
		mes "您必須有500個或更多的 ^ff0000" + .@bullet_str$ + "^000000, ok? Also, make sure not to ^FF0000wear^000000 them if you want to exchange.";
		close;
	}

	mes "[肯尼]";
	mes "您選擇了 ^ff0000" + .@pack_str$ + "^000000.";
	mes "您需要500 ^3131ff" + .@bullet_str$ + "^000000 to make 1 ^3131FF" + .@bullet_str$ + "^000000 item, and it will cost ^3131FF500^000000 zeny.";
	mes "您想交換幾個？";
	next;
	switch(select("全部交換:只交換1:取消")) {
		case 1:
			mes "[肯尼]";
			mes "子彈數： ^3131ff" + .@bullet_count;
			mes "子彈袋的組合： ^3131ff" + .@pack_count;
			mes "Zeny需要： ^3131ff" + .@pack_count * 500;
			next;
			mes "您想交換嗎？";
			next;
			switch(select("兌換:取消")) {
				case 1:
					if (Zeny < .@pack_count * 500) {
						mes "[肯尼]";
						mes "嘿~您需要確切的Zeny交換數量。去獲取更多。";
						mes "您必須有 ^ff0000" + .@pack_count * 500 + "^000000, ok?";
						close;
					}
					mes "[肯尼]";
					mes "一切都完成了。";
					mes "當庫存的重量超過70％時，袋子是沒有用的。請小心。";
					Zeny -= .@pack_count * 500;
					delitem .@bullet_id, .@pack_count * 500;
					getitem .@pack_id, .@pack_count;
					close;
				case 2:
					close;
			}
		case 2:
			if (Zeny < 500) {
				mes "[肯尼]";
				mes "嘿~您需要確切的Zeny交換數量。去獲取更多。";
				mes "您必須有 ^ff0000500 ^000000，好嗎？";
				close;
			}
			mes "[肯尼]";
			mes "一切都完成了。";
			mes "當庫存的重量超過70％時，袋子是沒有用的。請小心。";
			Zeny -= 500;
			delitem .@bullet_id, 500;
			getitem .@pack_id, 1;
			close;
		case 3:
			callsub S_Close;
	}

S_Sphere_Exchange:
	.@old_id = getarg(0);
	.@new_id = getarg(1);
	.@old_str$ = getitemname(.@old_id);
	.@new_str$ = getitemname(.@new_id);
	.@old_count = countitem(.@old_id);
	.@new_count = .@old_count / 10;

	if (.@old_count < 10 || getequipid(EQI_AMMO) != -1 || BaseClass != Job_Gunslinger) {
		mes "[肯尼]";
		mes "嘿~您需要確切的商品來交換。去獲取更多。";
		mes "您必須有10個或更多的 ^ff0000" + .@old_str$ + "^000000, ok? Also, make sure not to ^FF0000wear^000000 them if you want to exchange.";
		close;
	}

	mes "[肯尼]";
	mes "您選擇了 ^ff0000" + .@old_str$ + "^000000.";
	mes "您需要10 ^3131ff" + .@old_str$ + "^000000 to make 1 ^3131FF" + .@new_str$ + "^000000 item.";
	mes "您想交換幾個？";
	next;
	switch(select("全部交換:只交換1:取消")) {
		case 1:
			mes "[肯尼]";
			mes .@old_str$ + "計數： ^3131ff" + .@old_count;
			mes .@new_str$ + "計數： ^3131ff" + .@new_count;
			next;
			mes "您想交換嗎？";
			next;
			switch(select("兌換:取消")) {
				case 1:
					mes "[肯尼]";
					mes "一切都完成了。";
					delitem .@old_id, .@new_count * 10;
					getitem .@new_id, .@new_count;
					close;
				case 2:
					close;
			}
		case 2:
			mes "[肯尼]";
			mes "一切都完成了。";
			delitem .@old_id, 10;
			getitem .@new_id, 1;
			close;
		case 3:
			callsub S_Close;
	}

S_Old_Sphere_Exchange:
	.@old_id = getarg(0);
	.@new_id = getarg(1);
	.@old_str$ = getitemname(.@old_id);
	.@new_str$ = getitemname(.@new_id);
	.@old_count = countitem(.@old_id);

	if (.@old_count < 1 || getequipid(EQI_AMMO) != -1 || BaseClass != Job_Gunslinger) {
		mes "[肯尼]";
		mes "嘿~您需要確切的商品來交換。去獲取更多。";
		mes "您必須有1個或更多 ^ff0000" + .@old_str$ + "^000000, ok? Also, make sure not to ^FF0000wear^000000 them if you want to exchange.";
		close;
	}

	mes "[肯尼]";
	mes "您選擇了 ^ff0000" + .@old_str$ + "^000000.";
	mes "您需要1 ^3131ff" + .@old_str$ + "^000000 to make 1 ^3131FF" + .@new_str$ + "^000000 item.";
	mes "您想交換幾個？";
	next;
	switch(select("全部交換:只交換1:取消")) {
		case 1:
			mes "[肯尼]";
			mes .@old_str$ + "計數： ^3131ff" + .@old_count;
			mes .@new_str$ + "計數： ^3131ff" + .@old_count;
			next;
			mes "您想交換嗎？";
			next;
			switch(select("兌換:取消")) {
				case 1:
					mes "[肯尼]";
					mes "一切都完成了。";
					delitem .@old_id, .@old_count;
					getitem .@new_id, .@old_count;
					close;
				case 2:
					close;
			}
		case 2:
			mes "[肯尼]";
			mes "一切都完成了。";
			delitem .@old_id, 1;
			getitem .@new_id, 1;
			close;
		case 3:
			callsub S_Close;
	}

S_Slug_Exchange:
	.@slug_id = getarg(0);
	.@req_count = getarg(1);
	.@slug_str$ = getitemname(.@slug_id);
	.@new_slug_str$ = getitemname(25187);
	.@count = countitem(.@slug_id);
	.@new_count = .@count / .@req_count;

	if (.@count < .@req_count || getequipid(EQI_AMMO) != -1 || BaseClass != Job_Gunslinger) {
		mes "[肯尼]";
		mes "嘿~您需要確切的商品來交換。去獲取更多。";
		mes "你必須有" + .@req_count + " or more of ^FF0000" + .@slug_str$ + "^000000, ok? Also, make sure not to ^FF0000wear^000000 them if you want to exchange.";
		close;
	}

	mes "[肯尼]";
	mes "您選擇了 ^ff0000" + .@slug_str$ + "^000000.";
	mes "你需要" + .@req_count + " ^3131FF" + .@slug_str$ + "^000000 to make 1 ^3131FF" + .@new_slug_str$ + "^000000 item.";
	mes "您想交換幾個？";
	next;
	switch(select("全部交換:只交換1:取消")) {
		case 1:
			mes "[肯尼]";
			mes .@slug_str$ + "計數： ^3131ff" + .@count;
			mes .@new_slug_str$ + "計數： ^3131ff" + .@new_count;
			next;
			mes "您想交換嗎？";
			next;
			switch(select("兌換:取消")) {
				case 1:
					mes "[肯尼]";
					mes "一切都完成了。";
					delitem .@slug_id, .@new_count * .@req_count;
					getitem 25187, .@new_count;
					close;
				case 2:
					close;
			}
		case 2:
			mes "[肯尼]";
			mes "一切都完成了。";
			delitem .@slug_id, .@req_count;
			getitem 25187, 1;
			close;
		case 3:
			callsub S_Close;
	}

S_Close:
	mes "[肯尼]";
	mes "好的。";
	mes "如果您需要什麼";
	mes "請回來。";
	close;
}

alberta,118,157,3	duplicate(mdk)	Magazine Dealer Kenny#al	4_M_01
izlude_in,74,104,5	duplicate(mdk)	Magazine Dealer Kenny#iz	4_M_01
