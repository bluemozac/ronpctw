//===== rAthena Script =======================================
//= Episode 17.2 - Sage's Legacy Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Sage's Legacy related merchants and enchanters
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//= 1.2 Cleanup [Capuche]
//============================================================

ba_in01,87,370,3	script	Lisa#ep172_merchant	20698,{
	if (checkweight(1201,3) == 0) {
		mes "^ff0000it似乎您擁有的物品的類型或重量太多了。請組織您的庫存。 ^000000";
		close;
	}
	disable_items;
	mes "[麗莎]";
	mes "歡迎歡迎~";
	next;
	switch( select( "Explore Devices", "Automatic Engine Wing Enhancement", "Automatic Armor Enhancement", "Automatic Leg Enhancement", "Automatic Accessories(R) Enhancement", "Automatic Accessories(L) Enhancement" ) ) {
	case 1:
		mes "[麗莎]";
		mes "如果您攜帶自動設備和加固模塊，我們可以在設備上附魔模塊。";
		next;
		mes "[麗莎]";
		mes "這是根據規範模塊完成的，因此設備不會被破壞。但是，升級的數量可能會根據模塊而變化。";
		next;
		mes "[麗莎]";
		mes "然後，當您需要幫助時，請再次訪問~";
		close;
	case 2:
		.@part = EQI_GARMENT;
		setarray .@equip_required[0],480020,480021;
		break;
	case 3:
		.@part = EQI_ARMOR;
		setarray .@equip_required[0],450127,450128;
		break;
	case 4:
		.@part = EQI_SHOES;
		setarray .@equip_required[0],470022,470023;
		break;
	case 5:
		.@part = EQI_ACC_R;
		setarray .@equip_required[0],490024,490026;
		break;
	case 6:
		.@part = EQI_ACC_L;
		setarray .@equip_required[0],490025,490027;
		break;
	}
	.@equip_id = getequipid(.@part);
	.@equip_name$ = getequipname(.@part);
	.@refine = getequiprefinerycnt(.@part);
	for ( .@i = 0; .@i < 4; ++.@i )
		.@card[.@i] = getequipcardid(.@part,.@i);

	mes "[麗莎]";
	mes "如果您攜帶自動設備和加固模塊，我們可以在設備上附魔模塊。";
	next;
	mes "[麗莎]";
	mes strcharinfo(0) + "。";
	mes "請選擇要使用的模塊等級。";
	next;
	switch( select( "Cancel", "Normal Grade", "Rare Grade", "Unique Grade", "Legendary Grade", "Epic Grade 1", "Epic Grade 2", "Epic Grade 3" ) ) {
	case 1:
		mes "[麗莎]";
		mes "停止對話。";
		close;
	case 2:
		setarray .@module_etc[0],1000105,1000106,1000107,1000108,1000109,1000110,1000111,1000112;
		break;
	case 3:
		setarray .@module_etc[0],1000113,1000114,1000115,1000116,1000117,1000118,1000119,1000120,1000121,1000122,1000123,1000124,1000125,1000126,1000127;
		break;
	case 4:
		setarray .@module_etc[0],1000128,1000129,1000130,1000131,1000132,1000133,1000134,1000135,1000136,1000137,1000138,1000139,1000140,1000141,1000142,1000143,1000207,1000208;
		break;
	case 5:
		setarray .@module_etc[0],1000144,1000145,1000146,1000147,1000148,1000149;
		break;
	case 6:
		setarray .@module_etc[0],1000152,1000153,1000154,1000155,1000156,1000157,1000158,1000159,1000160,1000161,1000162,1000163,1000164,1000165,1000166,1000167,1000168;
		break;
	case 7:
		setarray .@module_etc[0],1000169,1000170,1000171,1000172,1000173,1000174,1000175,1000176,1000177,1000178,1000179,1000180,1000181,1000182,1000183,1000184,1000185;
		break;
	case 8:
		setarray .@module_etc[0],1000186,1000187,1000188,1000189,1000190,1000191,1000192,1000193,1000194,1000195,1000196,1000197,1000198,1000199,1000200,1000201,1000202;
		break;
	}
	.@size = getarraysize(.@module_etc);
	.@menu$ = "Cancel:";

	// Build the menu according to all the modules with the given grade in inventory
	for ( .@i = 0; .@i < .@size; ++.@i ) {
		if (countitem(.@module_etc[.@i]) < 1)
			.@menu$ += "^808080(Module Amount 0)^000000" + ":";
		else {
			.@item_name$ = getitemname(.@module_etc[.@i]);
			.@item_name$ = replacestr(.@item_name$, "Automatic Modification Module(", "");
			.@item_name$ = replacestr(.@item_name$, ")", "");
			.@menu$ += .@item_name$ + ":";
		}
	}
	.@s = select(.@menu$) - 2;
	if (.@s == -1) {
		mes "[麗莎]";
		mes "停止對話。";
		close;
	}
	.@module_id = .@module_etc[.@s];

	if (countitem(.@module_id) < 1) {
		mes "[麗莎]";
		mes "您必須在庫存中擁有一個模塊，該模塊與增強功能相匹配。";
		close;
	}
	if (.@equip_required[0] != .@equip_id && .@equip_required[1] != .@equip_id) {
		mes "[麗莎]";
		mes "請確保您已經配備了要增強的適當自動設備。";
		close;
	}
	if (.@card[1] > 0) {
		mes "[麗莎]";
		mes "該自動設備已經達到最大容量。";
		mes "請嘗試另一個自動設備。";
		close;
	}
	if (.@module_id < 1000105 || .@module_id == 1000150 || .@module_id == 1000151 || (.@module_id >= 1000203 && .@module_id != 1000207 && .@module_id != 1000208)) {
		mes "[麗莎]";
		mes "發生了未知錯誤。";
		close;
	}

	// Each part has a list of allowed module and a maximum allowed number of the same module
	switch( .@part ) {
	case EQI_GARMENT:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000125,2,1000126,2,1000127,2,1000135,1,1000142,1,1000208,1;
		break;
	case EQI_ARMOR:
		setarray .@module_etc_data[0],1000105,3,1000106,3,1000122,2,1000123,2,1000124,2,1000128,1,1000129,1,1000130,1,1000131,1,1000132,1,1000133,1,1000207,1,1000140,1;
		.@size = getarraysize(.@module_etc_data);

		// Epic enchants. Max 2 epic enchants of the same enchant allowed
		for (.@i = 1000152; .@i < 1000203; .@i++) {
			setarray .@module_etc_data[.@size], .@i, 2;
			.@size += 2;
		}
		break;
	case EQI_SHOES:
		setarray .@module_etc_data[0],
			1000105,3,1000106,3,1000119,2,1000120,2,1000121,2,1000134,1,1000141,1,
			1000144,1,1000145,1,1000146,1,1000147,1,1000148,1,1000149,1;	// Legendary enchants (must be unique on EQI_SHOES)
		break;
	case EQI_ACC_L:
	case EQI_ACC_R:
		setarray .@module_etc_data[0],1000107,3,1000108,3,1000109,3,1000110,3,1000111,3,1000112,3,1000113,2,1000114,2,1000115,1,1000116,1,1000117,1,1000118,1,1000136,1,1000137,1,1000138,1,1000139,1,1000143,1;
		break;
	}

	.@data_index = inarray(.@module_etc_data, .@module_id);

	if (.@data_index == -1) {
		mes "[麗莎]";
		mes "您無法將此模塊吸入此自動設備。";
		close;
	}
	// Max number of the selected module allowed
	.@max_module = .@module_etc_data[.@data_index+1];
	
	// Bind the module etc to the specific enchant
	for ( .@i = 1000105; .@i < 1000203; ++.@i ) {
		if (.@i == 1000150 || .@i == 1000151)
			continue;
		.@auto_module[.@i] = 310082 + .@size_auto_module;
		.@size_auto_module++;
	}
	.@auto_module[1000207] = 310178;
	.@auto_module[1000208] = 310179;

	// Check the count of the corresponding enchant in card slots
	.@enchant_id = .@auto_module[.@module_id];
	if (.@enchant_id == 0) {
		mes "[麗莎]";
		mes "發生了未知錯誤。";
		close;
	}
	if (countinarray(.@card[0], .@enchant_id) >= .@max_module) {
		mes "[麗莎]";
		mes "這種自動設備已經最大程度地提高了該模塊的增強功能。";
		mes "請嘗試另一個模塊。";
		close;
	}
	mes "[麗莎]";
	mes "您確定要掩蓋您的 ^ff0000" + .@equip_name$ + "^000000 with ^0000CD" + getitemname(.@module_id) + "^000000? This process cannot be reversed.";
	next;
	if (select( "Wait... Don't", "I'm sure. Do it." ) == 1) {
		mes "[麗莎]";
		mes "停止對話。";
		close;
	}
	delitem .@module_id,1;

	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipRefineHack", .@part, .@refine) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]))
		end;

	for ( .@i = 3; .@i > 0; .@i--) {
		if (.@card[.@i] == 0) {
			.@card[.@i] = .@enchant_id;
			break;
		}
	}
	delequip .@part;
	getitem2 .@equip_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	mes "[麗莎]";
	mes "woaaaah~我已經能感覺到您的自動設備的力量~！";
	close;
}

//= Merchants
-	marketshop	ep172littlemanager00	-1,1000227:172:999999

ba_in01,87,386,3	script	Child Manager #ep172_pet	EP17_2_CHILD_ADMIN1,{
	if (checkweight(1201,3) == 0) {
		mes "- 您的物品太多。";
		mes "請清除您的庫存，然後重試。 -";
		close;
	}
	mes "[兒童經理]";
	mes "我有很多小東西！";
	next;
	switch( select( "Little Manager", "Cloud Cotton", "Little Headress", "Cancel" ) ) {
	case 1:
		mes "[兒童經理]";
		mes "如果您想和Little Manager一起玩，這將花費您30張酒保門票。";
		next;
		if (select( "Exchange.", "Stop." ) == 2) {
			mes "[兒童經理]";
			mes "然後，是時候讓我玩了！";
			close;
		}
		if (countitem(1000103) < 30) {
			mes "[兒童經理]";
			mes "就像我說的那樣，如果您想和小經理一起玩，您將需要30張酒保門票！";
			close;
		}
		mes "[兒童經理]";
		mes "和你的新朋友一起玩！";
		delitem 1000103,30;
		getitem 9123,1;
		close;
	case 2:
		callshop "ep172littlemanager00";
		end;
	case 3:
		callshop "barter_Ep_17_2_C_Admin_Acc";
		end;
	case 4:
		mes "[兒童經理]";
		mes "然後，是時候讓我玩了！";
		close;
	}
	end;
}

//= Cube Shop
-	marketshop	ep172cubeshop00	-1,100251:10000000:-1,100252:30000000:-1

ba_in01,87,376,3	script	Cube Lane#ep172cube	4_EP17_GUARD_B,{
	mes "[立方體巷]";
	mes "我們提供的鋼筋可以完善您的 ^0000CD+4 ^000000幻覺和自動設備，以瞬間 ^ff0000+7 ^000000！";
	mes "毫無疑問，Varmundt是世界上最好的人！";
	close2;
	callshop "ep172cubeshop00";
	end;
}

//= Module Shop
ba_in01,87,373,3	script	Spiera#ep172_soapstone	MD_ASSISTANT,{
	callshop "barter_EP17_2_EP";
	end;
}

//= Equipment Shop
ba_in01,87,383,3	script	Yeoncheong#equipment_exchange	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega.bmp",2;
		mes "[延清]";
		mes "袋子裝滿了。在再次交談之前，請檢查您所擁有的物品的重量和數量。";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[延清]";
	mes "我們接受酒保門票將您的幻覺設備升級為自動申請。";
	mes "您想交換什麼？";
	next;
	switch( select( "Exchange Information", "Automatic Equipment Exchange", "Automatic Accessory Exchange", "Quit" ) ) {
	case 1:
		mes "[延清]";
		mes "您可以將 ^0000CD+9幻覺裝甲，發動機或腿部和900票票 ^000000交換為 ^ff00000000000000aatomatic裝甲，發動機或腿 ^000000。";
		next;
		mes "[延清]";
		mes "^ff0000automatic配件 ^000000可以換成 ^0000CDILLUSION配件和1500票票務 ^000000。";
		next;
		mes "[延清]";
		mes "您要交換的設備必須在您的庫存中並與我交談。";
		mes "^ff0000bring您只想交換並繼續進行一台設備。 ^000000";
		close3;
	case 2:
		.@shop$ = "barter_auto_equip_1";
		break;
	case 3:
		.@shop$ = "barter_auto_equip_2";
		break;
	case 4:
		mes "[延清]";
		mes "祝你有美好的一天，親愛的客人~";
		close3;
	}
	mes "[延清]";
	mes "^ff0000bring您只想交換並繼續進行一台設備。 ^000000";
	mes "如果您的庫存中有多個設備，則如果符合條件，它將隨機交換。";
	close2;
	cutin "",255;
	callshop .@shop$;
	end;
}

//= Device Module Exchange
ba_in01,87,380,3	script	Yecheon#ep172module_trader	4_EP17_CLEANER_W,{
	if (checkweight(1201,3) == 0) {
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "袋子裝滿了。在再次交談之前，請檢查您所擁有的物品的重量和數量。";
		close3;
	}
	disable_items;
	cutin "ep172_omega",2;
	mes "[醴泉]";
	mes "我可以為您提供自動模塊，物理自動改進設備和神奇的自動改進設備。它將幫助您改善自動設備的Abilites。";
	next;
	cutin "",255;
	switch( select( "More information.", "Get Automatic Module", "Get Physical Automatic Improvement Device", "Get Magical Automatic Improvement Device", "Cancel" ) ) {
	case 1:
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "您可以獲得 ^ff0000automatic模塊 ^000000，用於 ^0000CD90 Barmeal票 ^000000。我將為您提供一個不包括Epic等級模塊的隨機自動模塊。";
		next;
		mes "[醴泉]";
		mes "我還提供 ^ff0000 physical自動改進設備和神奇的自動改進設備 ^000000。您可以將其交換為 ^0000CD45 Barmeal門票 ^000000，或 ^0000CD1500000 Zeny ^000000。";
		next;
		mes "[醴泉]";
		mes "然後，戰鬥特有的客人可以安全地提高您的能力。";
		close3;
	case 2:
		.@price = 90;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我將交換1個隨機自動模塊，不包括史詩等級模塊" + .@price + " " + mesitemlink(1000103) + ".";
		next;
		cutin "",255;
		if (select( "Exchange Barmeal Ticket.", "Do not exchange" ) == 2) {
			mes "[醴泉]";
			mes "交易結束。";
			close3;
		}
		cutin "ep172_omega",2;
		if (countitem(1000103) < .@price) {
			mes "[醴泉]";
			mes "您沒有足夠的大酒保門票進行交換。";
			close3;
		}
		mes "[醴泉]";
		mes "我將其交換為一個隨機自動模塊。";
		delitem 1000103,.@price;
		getitem rand(1000105,1000149),1;
		close3;
	case 3:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我將交換 ^ff00001物理自動改進設備 ^000000 for" + .@price + " " + mesitemlink(1000103) + " or " + .@zeny + " Zeny.";
		next;
		cutin "",255;
		switch( select( "Exchange for Barmeal Ticket.", "Exchange for 1500000 Zeny.", "Do not exchange." ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[醴泉]";
				mes "您沒有足夠的大酒保門票進行交換。";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[醴泉]";
				mes "您沒有足夠的Zeny進行交流。";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[醴泉]";
			mes "交易結束。";
			close3;
		}
		mes "[醴泉]";
		mes "我將其交換為一個隨機的物理自動改進設備。";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100164,1;
		else if (.@r < 80)
			getitem 100165,1;
		else
			getitem 100166,1;
		close3;
	case 4:
		.@price = 45;
		.@zeny = 1500000;
		cutin "ep172_omega",2;
		mes "[醴泉]";
		mes "我將交換 ^ff00001神奇的自動改進設備 ^000000 for" + .@price + " " + mesitemlink(1000103) + " or " + .@zeny + " Zeny.";
		next;
		cutin "",255;
		switch( select( "Exchange for Barmeal Ticket.", "Exchange for 1500000 Zeny.", "Do not exchange." ) ) {
		case 1:
			cutin "ep172_omega",2;
			if (countitem(1000103) < .@price) {
				mes "[醴泉]";
				mes "您沒有足夠的大酒保門票進行交換。";
				close3;
			}
			delitem 1000103,.@price;
			break;
		case 2:
			cutin "ep172_omega",2;
			if (Zeny < .@zeny) {
				mes "[醴泉]";
				mes "您沒有足夠的Zeny進行交流。";
				close3;
			}
			Zeny -= .@zeny;
			break;
		case 3:
			mes "[醴泉]";
			mes "交易結束。";
			close3;
		}
		mes "[醴泉]";
		mes "我將其交換為一個隨機的魔法自動改進設備。";
		.@r = rand(100);
		if (.@r < 50)
			getitem 100167,1;
		else if (.@r < 80)
			getitem 100168,1;
		else
			getitem 100169,1;
		close3;
	case 5:
		mes "[醴泉]";
		mes "交易結束。";
		close3;
	}
	end;
}

ba_in01,87,389,2	script	Butterfly Merchant#ba_in01	4_EP17_MASTER_A,{
	if (ep17_2_main < 8) {
		mes "[蝴蝶商人]";
		mes "對不起。購買蝴蝶需要註冊。";
		close;
	}
	mes "[蝴蝶商人]";
	mes "歡迎。";
	mes "您需要翅膀搬到Barmund大廈嗎？";
	close2;
	callshop "barter_Teleport_Ep17_02";
	end;
}
