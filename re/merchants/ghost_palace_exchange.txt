//===== rAthena Script =======================================
//= Ghost Palace Exchange NPC.
//===== Description: =========================================
//= Walkthrough Conversion.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

dali02,51,130,4	script	Dimension Traveler	4_F_SHABBY,{
	if (checkweight(28100,1) == 0) {// Thanos Axe
		mes "- 您無法繼續進行，因為您有太多的物品。 -";
		close;
	}
	.@item_name_req$ = getitemname(6672);// Gray_Shard
	mes "[維度旅行者]";
	mes "冒險家朋友，" + mesitemlink( 6672 ) + " is what I need. How about exchanging it with something I have?";
	next;
	.@type = select("給我看看你的武器。", "讓我看看你的盔甲。", "有什麼特別的嗎？", "我不需要。" ) - 1;
	mes "[維度旅行者]";
	switch(.@type) {
	case 0:
		mes "哦！您對武器感興趣嗎？我們擁有從幽靈宮獲得的所有武器。你為什麼不選擇一個？";
		break;
	case 1:
		mes "盔甲？我們有可以從幽靈宮獲得的所有裝甲。你為什麼不選擇一個？";
		break;
	case 2:
		mes "嗯！似乎他們中的許多人已經聽說過我有最好的東西。隨時查看我的收藏。";
		break;
	case 3:
		mes "再見朋友~再來！";
		close;
	}
	next;
	mes "[維度旅行者]";
	mes "啊!旁邊寫的數字" + mesitemlink( 6672 ) + " will be needed.";
	mes "請記住這一點。";

	switch(.@type) {
	case 0:
		setarray .@items[0],
			13441, 200,		// Thanos Sword
			21009, 200, 	// Thanos Great Sword
			1438,  200,		// Thanos Spear
			1496,  200,		// Thanos Long Spear
			1669,  200,		// Thanos Staff
			2023,  200,		// Thanos Rod
			18119, 200,		// Thanos Bow
			13093, 200,		// Thanos Dagger
			28000, 200,		// Thanos Katar
			1836,  200,		// Thanos Knuckle
			16028, 200,		// Thanos Hammer
			28100, 200,		// Thanos Axe
			1933,  200,		// Thanos Violin
			1988,  200;		// Thanos Whip
		break;
	case 1:
		setarray .@items[0],
			2187,  100,		// Shield Of Gray
			15090, 100, 	// Armor Of Gray
			15091, 100,		// Gray Robe
			20721, 100,		// Cloak Of Gray
			22033, 100,		// Boots Of Gray
			18820, 100;		// Gray Helmet
		break;
	case 2:
		setarray .@items[0],
			27184, 10000,		// Knight Sakray Card
			23676, 50, 			// Gray Charcoal Melee (Melee)
			23677, 50,			// Gray Charcoal Magic (Magic)
			23678, 50;			// Gray Charcoal Range (Range)
		break;
	}
	.@size = getarraysize(.@items);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		.@menu$ += getitemname(.@items[.@i]) + " - " + .@items[.@i+1] + ":";

	while(true) {
		next;
		.@s = (select(.@menu$) - 1) * 2;
		mes "[維度旅行者]";
		if (.@type != 1)	// armor type doesn't display item info
			mes "你是說" + mesitemlink( .@items[.@s] ) + "?";
		mes "" + .@items[.@s+1] + " ^006400" + .@item_name_req$ + "^000000 are required in exchange.";
		next;
		switch( select("我們交換一下吧", "讓我看看其他的東西。", "我晚點回來。" ) ) {
		case 1:
			break;
		case 2:
			mes "[維度旅行者]";
			switch(.@type) {
			case 0:
				mes "我有足夠的武器。";
				break;
			case 1:
				mes "我們有很多適合您的裝甲。你為什麼不選擇一個？";
				break;
			case 2:
				mes "如果你帶我" + .@item_name_req$ + ", I will trade it with anything you want.";
				break;
			}
			continue;
		case 3:
			mes "[維度旅行者]";
			mes "照顧朋友~下次見！";
			close;
		}
		if (countitem(6672) < .@items[.@s+1]) {
			mes "[維度旅行者]";
			mes "朋友，你把我陷入困境，朋友。";
			mes "我需要" + .@item_name_req$ + ". I will exchange it for ^006400" + .@items[.@s+1] + " " + .@item_name_req$ + "^000000.";
			close;
		}
		mes "[維度旅行者]";
		mes "謝謝。如果您需要其他任何東西，請回來。";
		delitem 6672, .@items[.@s+1];
		getitem .@items[.@s], 1;
		close;
	}
}
