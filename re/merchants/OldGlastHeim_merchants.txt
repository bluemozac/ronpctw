//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Temporal Boots enchants NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version merged from idathena.
//=     Credit exneval. [Capuche]
//============================================================

// Merchant
//============================================================
glast_01,210,273,5	script	Hugin's Butler#pa0829	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。減輕體重並回來。";
		close;
	}
	mes "[Hugin的管家]";
	mes "帶上各種臨時靴子項目的時間水晶項目。選擇您想要的東西。";
	next;
	switch( select( "Cancel", "Buy Temporal Boots (Need 1 Temporal Crystal)", "Upgrade Temporal Boots (Need 5 Temporal Crystals)", "Upgrade Modified Boots (Need 5 Temporal Crystals)" ) ) {
	case 1:
		mes "[Hugin的管家]";
		mes "隨時隨地回來。";
		close;
	case 2:
		if (countitem(6607) > 0) {
			mes "[Hugin的管家]";
			mes "穿上這些臨時靴子。我們可以使用5個顳晶體來升級您的時間靴。";
			delitem 6607,1;// Temporal_Crystal
			getitem 2499,1;// Temporal_Boots
			close;
		}
		mes "[Hugin的管家]";
		mes "您需要更多的時間晶體。在您穿越舊的Glast Heim期間獲得更多的顳晶體時，請回來。";
		close;
	case 3:
		setarray .@equip_type[0],
			22000,	// Temporal_STR_Boots
			22001,	// Temporal_INT_Boots
			22002,	// Temporal_AGI_Boots
			22003,	// Temporal_VIT_Boots
			22004,	// Temporal_DEX_Boots
			22005;	// Temporal_LUK_Boots
		break;
	case 4:
		setarray .@equip_type[0],
			22107,	// Modified_STR_Boots
			22108,	// Modified_INT_Boots
			22109,	// Modified_AGI_Boots
			22110,	// Modified_VIT_Boots
			22111,	// Modified_DEX_Boots
			22112;	// Modified_LUK_Boots
		break;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin的管家]";
		mes "只有您穿的物品才能升級。";
		mes "穿 ^0000fftemporal靴子 ^000000您想升級，然後再次與我交談。";
		close;
	}
	if (getequipid(EQI_SHOES) != 2499) {
		mes "[Hugin的管家]";
		mes "您沒有穿顳靴。一旦您穿著它們，請與我交談。";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin的管家]";
		mes "時間晶體不足。您需要5個時間晶體來升級您的物品。";
		close;
	}
	mes "[Hugin的管家]";
	mes "您可以用以下1個物品來交易 ^0000FF1顳靴和5個顳晶體 ^000000。這些新的^000000不會保留^ff00000000000000。";
	next;
	.@s = select( "Cancel", "Str Boots", "Int Boots", "Agi Boots", "Vit Boots", "Dex Boots", "Luk Boots" ) - 2;
	if (.@s == -1) {
		mes "[Hugin的管家]";
		mes "隨時隨地回來。";
		close;
	}
	setarray .@type$[0], "Strength", "Intelligence", "Agility", "Vitality", "Dexterity", "Lucky";
	mes "[Hugin的管家]";
	mes "您確定要購買時間 ^ff0000" + .@type$[.@s] + "^000000 Boots? There's no refunds.";
	next;
	if (select( "Think again", "Yes, I am sure." ) == 1) {
		mes "[Hugin的管家]";
		mes "好的，當您確定時再回來。";
		close;
	}
	mes "[Hugin的管家]";
	mes "靴子按照您的意願進行了升級。";
	if (getequipid(EQI_SHOES) == 2499) {
		delequip EQI_SHOES;
		delitem 6607,5;// Temporal_Crystal
		getitem .@equip_type[.@s],1;
	}
	close;
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Hugin's Magician#pa0829	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。減輕體重並回來。";
		close;
	}
	mes "[Hugin的魔術師]";
	mes "因此，您來癡迷於臨時靴子。請注意，帶有插座的靴子不能迷住，並且這種升級僅適用於您穿的靴子。";
	next;
	if (select( "How does the enchanting work?", "Give effect to my Temporal Boots" ) == 1) {
		mes "[Hugin的魔術師]";
		mes "您可以通過旅行時間繼續獲取 ^0000 fffcoaged spell ^000000。";
		next;
		mes "[Hugin的魔術師]";
		mes "我可以迷住您的臨時靴子項目，因為該物品的數量。 ^0000FFNOT普通型靴子^000000。僅適用於這6個擴展的版本靴子。";
		next;
		mes "[Hugin的魔術師]";
		mes "但是，一旦將套接字放入項目中，就無法吸引它。確保記住這一點。";
		next;
		mes "[Hugin的魔術師]";
		mes "我不會隨機發出效果。您可以選擇所需的效果。";
		next;
		mes "[Hugin的魔術師]";
		mes "當然，它僅適用於第四個插槽。一開始您可能會產生較小的效果，然後效果變得更強大。";
		next;
		mes "[Hugin的魔術師]";
		mes "要升級最終選項，它將需要更凝結的咒語項目。不要忘記這一點。";
		next;
		mes "[Hugin的魔術師]";
		mes "第四插槽完成最佳選擇後，您就可以對第三插槽產生隨機獎勵效果。與..一定的付款~";
		next;
		mes "[Hugin的魔術師]";
		mes "另外，即使魔法失敗，您也不會丟失物品，我們也保證保留卡和精煉率。但是我們不能初始化迷人。";
		next;
		mes "[Hugin的魔術師]";
		mes "唷……";
		mes "無論如何，這都是基本的附魔信息。現在您應該自己看。";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin的魔術師]";
		mes "你穿這個物品了嗎？";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6
	setarray .@enchant_cost[0],1,4,15,30,10;

	switch(.@equip_id) {
	case 22000:	// Temporal_STR_Boots
	case 22001:	// Temporal_INT_Boots
	case 22002:	// Temporal_AGI_Boots
	case 22003:	// Temporal_VIT_Boots
	case 22004:	// Temporal_DEX_Boots
	case 22005:	// Temporal_LUK_Boots

	case 22107:	// Modified_STR_Boots
	case 22108:	// Modified_INT_Boots
	case 22109:	// Modified_AGI_Boots
	case 22110:	// Modified_VIT_Boots
	case 22111:	// Modified_DEX_Boots
	case 22112:	// Modified_LUK_Boots
		.@equip_name$ = getequipname(EQI_SHOES);
		setarray .@card[0],
			getequipcardid(EQI_SHOES,0),
			getequipcardid(EQI_SHOES,1),
			getequipcardid(EQI_SHOES,2),
			getequipcardid(EQI_SHOES,3);
		copyarray .@equip_card[0], .@card[0], 4;	// for final check
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);

		if (.@card[2] > 0) {
			mes "[Hugin的魔術師]";
			mes "這些靴子已經通過了迷人的極限。我們再也不能著迷他們了。";
			close;
		}
		if (.@card[3] == 0) {// 4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			mes "[Hugin的魔術師]";
			mes "想要著迷 ^0000ff" + .@equip_name$ + "^000000?";
			mes "對於第一個迷人，您需要 ^0000ff" + .@cost + "^000000 Coagulated Spell items.";
			next;
			.@s = select( "Quit", "Fighting Spirit", "Archery", "Spell", "Vitality", "Attack Speed", "Lucky" ) - 2;
			if (.@s == -1) {
				mes "[Hugin的魔術師]";
				mes "好的，準備好後回來。";
				close;
			}
			.@card[3] = .@enchant_1[.@s];
			.@string$ = "enchant number ^6300001^000000.";
		}
		else {
			for ( .@enchant_num = 1; .@enchant_num < 5; .@enchant_num++ ) {
				for ( .@enchant_type = 0; .@enchant_type < 6 && .@card[3] != getd( ".@enchant_" + .@enchant_num + "[" + .@enchant_type + "]" ); .@enchant_type++ )
					continue;
				if (.@enchant_type < 6)
					break;
			}
			if (.@enchant_num == 5) {
				mes "[Hugin的魔術師]";
				mes "發生了問題。";
				close;
			}
			.@cost = .@enchant_cost[.@enchant_num];

			mes "[Hugin的魔術師]";
			if (.@enchant_num == 4) {
				.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
				.@string$ = "^990000Bonus effect ^000000 upgrade.";
				mes "第四插槽已經升級到最終選項。您想要第三個插槽的隨機獎勵效果嗎？您需要 ^0000ff" + .@cost + "^000000 Coagulated Spell items.";
			}
			else {
				.@number = .@enchant_num + 1;
				.@card[3] = getd( ".@enchant_" + (.@enchant_num+1) + "[" + .@enchant_type + "]" );
				.@string$ = "enchant number ^630000" + .@number + "^000000.";
				mes "迷人 ^0000ff" + .@equip_name$ + "^000000's 4th slot as ^0000FF" + .@number + "^000000 level effect. Requires ^0000FF" + .@cost + "^000000 Coagulated Spell items.";
			}
			next;
			if (select("Quit","Effect Upgrade!") == 1) {
				mes "[Hugin的魔術師]";
				mes "好的，準備好後回來。";
				close;
			}
		}
		if (countitem(6608) < .@cost) {
			mes "[Hugin的魔術師]";
			mes "嗯，你缺少" + (.@cost - countitem(6608)) + " Coagulated Spell items. Go get more, and then we can talk about more enchants.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		delitem 6608,.@cost;// Coagulated_Spell

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
			close;

		delequip EQI_SHOES;
		mes "[Hugin的魔術師]";
		mes "嘗試" + .@string$;
		getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
		close;
	default:
		mes "[Hugin的魔術師]";
		mes "這不是這個附魔的正確項目。";
		mes "請記住，只有這6種類型的顳靴可用於迷人。";
		close;
	}
}

glast_01,210,270,0	script	Hugin's Craftsman#pa0829	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "你的包太重了。減輕體重並回來。";
		close;
	}
	mes "[Hugin的工匠]";
	mes "那位魔術師總是說迷人的顳靴是最好的。但是，我認為完全不同。";
	next;
	mes "[Hugin的工匠]";
	mes "我認為將一個插槽放入該項目將是最好的。即使有失敗的風險。";
	next;
	mes "[Hugin的工匠]";
	mes "帶我 ^0000FF5顳晶體 ^000000，我將嘗試將插槽放入您的高級顳靴中。但是，可以 ^ff0000 flail並打破項目 ^000000。可以嗎？";
	next;
	if (select( "Cancel", "Take the risk and try to add a slot" ) == 1) {
		mes "[Hugin的工匠]";
		mes "好的，隨時回來。";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin的工匠]";
		mes "您確定您穿了該物品嗎？";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);
	switch(.@equip_id) {
	case 22000:	callsub( S_Slot,22006 );	// Temporal_STR_Boots
	case 22001:	callsub( S_Slot,22009 );	// Temporal_INT_Boots
	case 22002:	callsub( S_Slot,22010 );	// Temporal_AGI_Boots
	case 22003:	callsub( S_Slot,22007 );	// Temporal_VIT_Boots
	case 22004:	callsub( S_Slot,22008 );	// Temporal_DEX_Boots
	case 22005:	callsub( S_Slot,22011 );	// Temporal_LUK_Boots

	case 22107:	callsub( S_Slot,22113 );	// Modified_STR_Boots
	case 22108:	callsub( S_Slot,22114 );	// Modified_INT_Boots
	case 22109:	callsub( S_Slot,22115 );	// Modified_AGI_Boots
	case 22110:	callsub( S_Slot,22116 );	// Modified_VIT_Boots
	case 22111:	callsub( S_Slot,22117 );	// Modified_DEX_Boots
	case 22112:	callsub( S_Slot,22118 );	// Modified_LUK_Boots
	default:
		mes "[Hugin的工匠]";
		mes "不是這樣！我們需要 ^0000 fffadvanced顳靴 ^000000項目！該項目 ^0000ffith具有統計效果，沒有插槽 ^000000！";
		close;
	}

S_Slot:
	.@equip_id = getequipid(EQI_SHOES);
	mes "[Hugin的工匠]";
	mes "如果您失敗了， ^ff0000all Enchant或精煉率將損失。 ^000000";
	mes "仍然想冒險嗎？";
	next;
	if (select( "Cancel", "I am ok with it!" ) == 1) {
		mes "[Hugin的工匠]";
		mes "好的，隨時回來。";
		close;
	}
	mes "[Hugin的工匠]";
	mes "再一次， ^ff0000that魔術師不會帶任何插槽的物品。 ^000000";
	mes "還想繼續嗎？";
	next;
	if (select( "Cancel", "I understand. Try to slot it." ) == 1) {
		mes "[Hugin的工匠]";
		mes "好的，隨時回來。";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin的工匠]";
		mes "您必須帶上 ^0000FF5顳晶體 ^000000。沒有什麼是免費的...";
		close;
	}
	delitem 6607,5;// Temporal_Crystal
	if (getequipid(EQI_SHOES) == .@equip_id) {
		delequip EQI_SHOES;
		if (rand(1,100) < 50) {
			mes "[Hugin的工匠]";
			mes "Arrggg，我們失敗了。下次運氣更好。";
			specialeffect2 EF_PHARMACY_FAIL;
			close;
		}
		mes "[Hugin的工匠]";
		mes "成功的！幹得好。";
		specialeffect2 EF_MAXPOWER;
		getitem getarg(0),1;
	}
	close;
}

// Temporal shoes slotted - npc enchant
glast_01,184,283,4	script	Dark magic master#pa082	1_F_01,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "你的包太重了。減輕體重並回來。";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "您似乎攜帶太多物品。將一些物品放在存儲中，然後再次回來。";
		close;
	}
	// requirements
	.@coagulated_id = 6608;// Coagulated_Spell
	.@polluted_id = 6755;// Polluted_Spell
	.@cost_zeny = 100000;

	mes "【黑魔法大師】";
	mes "我只處理插座的顳靴。";
	next;
	if (select( "How does the enchanting work?", "Give effect to my Temporal Boots" ) == 1) {
		mes "【黑魔法大師】";
		mes "您應該詢問Hugin的Magic掌握所有基本事物。我想更深入地談論刺激工作。";
		next;
		mes "【黑魔法大師】";
		mes "我需要4個項目。 ^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + ", " + getitemname(.@polluted_id) + ", " + getitemname(.@coagulated_id) + ", Temporal Boots(Socket) Series^000000. If you don't have any of these, I cannot help you.";
		next;
		mes "【黑魔法大師】";
		mes "這是我的小愛好..使用受污染和凝結的咒語吸引著插座的顳靴。";
		next;
		mes "【黑魔法大師】";
		mes "當然，只要您想一次又一次地著迷，您就必須應對更大的破壞風險。";
		next;
		mes "【黑魔法大師】";
		mes "附魔的命令與休金的魔術大師的魔法相同。請參考他的解釋。";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	switch(.@equip_id) {
	case 22006:	// Temporal_Str_Boots_
	case 22007:	// Temporal_Vit_Boots_
	case 22008:	// Temporal_Dex_Boots_
	case 22009:	// Temporal_Int_Boots_
	case 22010:	// Temporal_Agi_Boots_
	case 22011:	// Temporal_Luk_Boots_
	case 22113:	// Modified_Str_Boots_
	case 22114:	// Modified_Int_Boots_
	case 22115:	// Modified_Agi_Boots_
	case 22116:	// Modified_Vit_Boots_
	case 22117:	// Modified_Dex_Boots_
	case 22118:	// Modified_Luk_Boots_
		break;
	case -1:
		mes "【黑魔法大師】";
		mes "你穿這個物品了嗎？";
		close;
	default:
		mes "【黑魔法大師】";
		mes "這不是這個附魔的正確項目。請記住，只有6種帶有插座的時間靴可用於隱藏。";
		close;
	}
	// enchants ID
	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6

	// requirements amount
	setarray .@en_amount_coag[0],3,10,20,40,50;
	setarray .@en_amount_polluted[0],1,2,4,7,10;

	// (custom) chances of success (official value unknown)
	setarray .@chances[0],100,70,70,70,70;

	// data of item equipped
	.@equip_name$ = getequipname(EQI_SHOES);
	setarray .@card[0],
		getequipcardid(EQI_SHOES,0),
		getequipcardid(EQI_SHOES,1),
		getequipcardid(EQI_SHOES,2),
		getequipcardid(EQI_SHOES,3);
	copyarray .@equip_card[0], .@card[0], 4;
	.@equip_refine = getequiprefinerycnt(EQI_SHOES);

	if (.@card[2] > 0) {
		mes "【黑魔法大師】";
		mes "這些靴子已經通過了迷人的極限。我們再也不能著迷他們了。";
		close;
	}
	if (.@card[3] == 0) {// 4th slot 1st try enchanting
		.@amount_coag = .@en_amount_coag[0];
		.@amount_poll = .@en_amount_polluted[0];

		mes "【黑魔法大師】";
		mes "想要著迷 ^0000ff" + .@equip_name$ + " (Socket)^000000? For the 1st enchanting, you need ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", and " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000.";
		mes "它有失敗的風險。";
		next;
		.@s = select( "Quit", "Fighting Spirit", "Archery", "Spell", "Vitality", "Attack Speed", "Lucky" ) - 2;
		if (.@s == -1) {
			mes "【黑魔法大師】";
			mes "好的，準備好後回來。";
			close;
		}
		.@card[3] = .@enchant_1[.@s];
		.@string$ = "enchant number ^6300001^000000.";
		.@num = 0;
	}
	else {
		for ( .@num = 1; .@num < 5; .@num++ ) {
			for ( .@type = 0; .@type < 6 && .@card[3] != getd( ".@enchant_" + .@num + "[" + .@type + "]" ); .@type++ )
				continue;
			if (.@type < 6)
				break;
		}
		if (.@num == 5) {
			mes "【黑魔法大師】";
			mes "發生了問題。";
			close;
		}
		.@amount_coag = .@en_amount_coag[.@num];
		.@amount_poll = .@en_amount_polluted[.@num];

		mes "【黑魔法大師】";
		if (.@num == 4) {
			.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
			.@string$ = "^990000 Bonus effect ^000000 upgrade.";
			mes "您想要第三個插槽的隨機獎勵效果嗎？您需要 ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", and " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000. But it has high risk of failing..";
		}
		else {
			.@level = .@num + 1;
			.@card[3] = getd( ".@enchant_" + (.@num+1) + "[" + .@type + "]" );
			.@string$ = "enchant number ^990000" + .@level + "^000000.";
			mes "迷人 ^0000ff" + .@equip_name$ + " (Socket)^000000's 4th slot as ^0000ff" + .@level + "^000000 level effect. Requires ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", and " + callfunc("F_InsertComma",.@cost_zeny) + " z^000000. It has some risk of failing..";
		}
		next;
		if (select( "Quit", "Effect Upgrade!" ) == 1) {
			mes "【黑魔法大師】";
			mes "好的，準備好後回來。";
			close;
		}
	}
	if (countitem(.@coagulated_id) < .@amount_coag) {
		mes "【黑魔法大師】";
		mes "嗯，你缺少" + (.@amount_coag - countitem(.@coagulated_id)) + " " + getitemname(.@coagulated_id) + " items. Go get more, and then we can talk about more enchants.";
		close;
	}
	if (countitem(.@polluted_id) < .@amount_poll) {
		mes "【黑魔法大師】";
		mes "嗯，你缺少" + (.@amount_poll - countitem(.@polluted_id)) + " " + getitemname(.@polluted_id) + " items. Go get more, and then we can talk about more enchants.";
		close;
	}
	if (Zeny < .@cost_zeny) {
		mes "【黑魔法大師】";
		mes "您必須帶上 ^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000. Nothing is free you know...";
		close;
	}
	delitem .@coagulated_id, .@amount_coag;
	delitem .@polluted_id, .@amount_poll;
	Zeny -= .@cost_zeny;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
		close;

	delequip EQI_SHOES;
	if (.@chances[.@num] < rand(1,100)) {
		specialeffect2 EF_LORD;
		mes "【黑魔法大師】";
		mes "Arrggg，我們失敗了。下次運氣更好。";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	mes "[Hugin的魔術師]";
	mes "嘗試" + .@string$;
	getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
	close;
}

// Items exchange for card
glast_01,216,292,5	script	Portrait collector#0002	4_M_OILMAN,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "你的包太重了。減輕體重並回來。";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "您似乎攜帶太多物品。將一些物品放在存儲中，然後再次回來。";
		close;
	}
	.@coagulated_id = 6608;		// Coagulated_Spell
	.@polluted_id = 6755;		// Polluted_Spell
	.@white_card = 4608;		// White_Knightage_Card
	.@khalitzburg_card = 4609;	// Khaliz_Knightage_Card

	mes "[肖像收藏家]";
	mes "我已經收集了一年的肖像。現在是時候向人們展示我的收藏了。";
	next;
	mes "[肖像收藏家]";
	mes "嘿，你有很多嗎" + getitemname(.@polluted_id) + " or " + getitemname(.@coagulated_id) + "? If so, why don't you trade a nice portrait that I have?";
	next;
	switch( select( "Cancel", "Exchange for " + getitemname(.@white_card), "Exchange for " + getitemname(.@khalitzburg_card) ) ) {
	case 1:
		mes "[肖像收藏家]";
		mes "好，好。準備好後回來。";
		close;
	case 2:
		.@reward_id = .@white_card;
		.@amount_coag = 3000;
		.@amount_polluted = 70;
		break;
	case 3:
		.@reward_id = .@khalitzburg_card;
		.@amount_coag = 5000;
		.@amount_polluted = 100;
		break;
	}
	mes "[肖像收藏家]";
	mes "您想換什麼以換取" + getitemname(.@reward_id) + "?";
	next;
	if (countitem(.@coagulated_id) < .@amount_coag)
		.@color$[0] = "^666666";
	if (countitem(.@polluted_id) < .@amount_polluted)
		.@color$[1] = "^666666";
	switch( select( "Cancel", sprintf("%s%s %s^000000", .@color$[0], callfunc("F_InsertComma",.@amount_coag), getitemname(.@coagulated_id)), sprintf("%s%s %s^000000", .@color$[1], callfunc("F_InsertComma",.@amount_polluted), getitemname(.@polluted_id)) ) ) {
	case 1:
		mes "[肖像收藏家]";
		mes "好，好。準備好後回來。";
		close;
	case 2:
		.@id = .@coagulated_id;
		.@amount = .@amount_coag;
		break;
	case 3:
		.@id = .@polluted_id;
		.@amount = .@amount_polluted;
		break;
	}
	if (countitem(.@id) < .@amount) {
		mes "[肖像收藏家]";
		mes "嗯，您現在沒有足夠的咒語。去獲取更多。";
		close;
	}
	mes "[肖像收藏家]";
	mes "您確定不會後悔嗎？";
	next;
	if (select( "Cancel", "Sure" ) == 1) {
		mes "[肖像收藏家]";
		mes "好，好。準備好後回來。";
		close;
	}
	mes "[肖像收藏家]";
	mes "酷，讓我們達成協議。";
	delitem .@id, .@amount;
	getitem .@reward_id,1;
	close;
}
