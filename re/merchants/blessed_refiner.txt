//===== rAthena Script ======================================= 
//= Blessed Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use Blessed ores to refine equipment.
//= Upon failure, the equipment is not destroyed. The success
//= rate is identical to that for Enriched ores.
//= - "Blacksmith Dister" only refines from +6~12.
//= NOTE: This NPC is currently disabled on official servers.
//===== Changelog: ===========================================
//= 1.0 First version. [Euphy]
//= 1.1 Removed re-roll behavior. [Secret]
//============================================================

// Main NPC :: new_smelting612
//============================================================
-	script	::BlacksmithDister	-1,{
	disable_items;
	mes "[鐵匠拆除]";
	mes "在這個競爭激烈的社會中，我們必須有所不同才能生存！";
	mes "我只在+6~12級的設備中完善設備。";
	next;
	mes "[鐵匠拆除]";
	mes "我將需要 ^ff9999的武器 ^000000，而裝甲的 ^ff99999blesed Elunium ^000000。";
	mes "失敗的完善 ^ff0000將不會破裂或降低精煉水平 ^000000。";
	next;
	mes "[鐵匠拆除]";
	mes "怎麼樣？你想完善嗎？";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[Unequipped]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[鐵匠拆除]";
		switch(.@part) {
		case EQI_HEAD_TOP:
			mes "我是鐵匠，而不是髮型師。";
			break;
		case EQI_ARMOR:
			mes "用錘子，我將使您成為天空之星。";
			break;
		case EQI_HAND_L:
		case EQI_HAND_R:
			mes "製作人造手並不是我的專長。";
			break;
		case EQI_GARMENT:
			mes "帶出該物品，以便我可以完善它！";
			break;
		case EQI_SHOES:
			mes "這種怪異的氣味來自哪裡？";
			break;
		case EQI_ACC_L:
		case EQI_ACC_R:
			mes "附件在哪裡？";
			break;
		case EQI_HEAD_MID:
			mes "你要我完善什麼？";
			break;
		case EQI_HEAD_LOW:
			mes "嗯？你要我做什麼？";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[鐵匠拆除]";
		mes "該項目無法完善。";
		close;
	}
	.@equip_id = getequipid(.@part);
	.@itemtype = getiteminfo( .@equip_id, ITEMINFO_TYPE );
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	if (.@equip_refine < 6 || .@equip_refine > 12) {
		mes "[鐵匠拆除]";
		mes "該設備已完善"+.@equip_refine+". I only handle items with refine levels from +6 to +12!";
		close;
	}
	if( .@itemtype == IT_ARMOR ){
		.@equip_lv = getequiparmorlv( .@part );
		.@type$ = "Armor";

		switch(.@equip_lv) {
			case 1:
				set .@price,20000;
				set .@material,6439; //Unbreakable_Def
				break;
			default:
				// TODO:
				close;
		}
	}else if( .@itemtype == IT_WEAPON ){
		.@equip_lv = getequipweaponlv( .@part );
		.@type$ = "Weapon";

		switch( .@equip_lv ){
			case 1:
				set .@price,1000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 2:
				set .@price,2000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 3:
				set .@price,20000;
				set .@material,6438; //Unbreakable_Weap
				break;
			case 4:
				set .@price,40000;
				set .@material,6438; //Unbreakable_Weap
				break;
			default:
				// TODO:
				close;
		}
	}else{
		// TODO:
		close;
	}
	set .@ore$,"^ff9999Blessed "+.@type$+" Ore^000000";
	mes "[鐵匠拆除]";
	mes "這"+.@type$+" has been refined to "+.@equip_refine+". To refine it, I need "+.@ore$+" and a "+callfunc("F_InsertComma",.@price)+" zeny refining fee.";
	mes "你想繼續嗎？";
	next;
	if(select("是:否") == 2) {
		mes "[鐵匠拆除]";
		mes "我很忙，不要和我開玩笑！";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[鐵匠拆除]";
		mes "這"+.@type$+" has been refined many times. Although it will not disappear upon failure, the "+.@ore$+" will disappear!";
		next;
		mes "[鐵匠拆除]";
		mes "當然，精煉費將不會退還！你想繼續嗎？";
		next;
		if(select("是:否") == 2) {
			mes "[鐵匠拆除]";
			mes "好的。如果它也失敗了，我會很難過。";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[鐵匠拆除]";
		mes "材料不足。";
		mes "優化一個水平"+.@equip_lv+" "+.@type$+", I need "+.@ore$+" and a "+callfunc("F_InsertComma",.@price)+" zeny refining fee.";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	mes "[鐵匠拆除]";
	mes "塔克！塔克！塔克！";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		specialeffect EF_BLESSING;
		successrefitem .@part;
		next;
		mes "[鐵匠拆除]";
		mes "哈哈哈哈哈！";
		next;
		mes "[鐵匠拆除]";
		mes "精煉是巨大的成功~哈哈哈！";
		close;
	}
	specialeffect EF_CURSEATTACK;
	specialeffect2 EF_SUI_EXPLOSION;
	next;
	emotion (!rand(5))?ET_ANGER:ET_HUK;
	mes "[鐵匠拆除]";
	mes "哦，我的天啊！ ！ ！ ！";
	next;
	mes "[鐵匠拆除]";
	mes "哦！雖然它是幸運的"+.@ore$+" to retain the equipment...";
	next;
	mes "[鐵匠拆除]";
	mes "當我失敗時，我的心情會更糟！ ！";
	close;
}
prt_in,52,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#prt	826
payon,148,172,3	duplicate(BlacksmithDister)	Blacksmith Dister#pay	826
alberta_in,20,56,3	duplicate(BlacksmithDister)	Blacksmith Dister#alb	826
yuno_in01,175,18,3	duplicate(BlacksmithDister)	Blacksmith Dister#yuno	826
ein_in01,26,82,3	duplicate(BlacksmithDister)	Blacksmith Dister#ein	826
lhz_in02,280,15,3	duplicate(BlacksmithDister)	Blacksmith Dister#lhz	826
