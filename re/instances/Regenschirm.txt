//===== rAthena Script =======================================
//= Regenschirm
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 - Regenschirm
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@rgsr,1,1,0	script	#171_rgsr_control	HIDDEN_WARP_NPC,{
	end;

OnInstanceInit:
	'map$ = instance_mapname("1@rgsr");
	'rgsr = 'activate = 0;
	deletearray 'rescued;
	deletearray 'rsgr_c;

	disablenpc instance_npcname("Researcher#rgsr_ev_0");
	disablenpc instance_npcname("Access Controller#rgsr");
	disablenpc instance_npcname("#rgsr_touch_mob_0");
	disablenpc instance_npcname("#rgsr_touch_mob_1");
	disablenpc instance_npcname("#rgsr_warp_0");
	disablenpc instance_npcname("Est#rgsr");
	disablenpc instance_npcname("Rebellion Crew#rgsr_dum01");
	disablenpc instance_npcname("Aas#rgsr3");
	disablenpc instance_npcname("Goni#rgsr");
	disablenpc instance_npcname("Est#rgsr2");
	disablenpc instance_npcname("Central Room#rgsr");
	disablenpc instance_npcname("Restrained Specimen#rgsr");
	disablenpc instance_npcname("Control Device#rgsr0");
	disablenpc instance_npcname("Control Device#rgsr1");
	disablenpc instance_npcname("Control Device#rgsr2");
	disablenpc instance_npcname("Control Device#rgsr3");
	disablenpc instance_npcname("Aas#rgsr4");
	end;

OnEvent01:
	enablenpc instance_npcname("#rgsr_touch_mob_0");
	enablenpc instance_npcname("#rgsr_touch_mob_1");
	donpcevent instance_npcname("#rgsr_mob_1") + "::OnSummon";
	end;

OnEvent02:
	enablenpc instance_npcname("#rgsr_warp_0");
	donpcevent instance_npcname("#rgsr_mob_2") + "::OnSummon";
	end;

OnEvent03:
	enablenpc instance_npcname("Restrained Specimen#rgsr");
	enablenpc instance_npcname("Control Device#rgsr0");
	enablenpc instance_npcname("Control Device#rgsr1");
	enablenpc instance_npcname("Control Device#rgsr2");
	enablenpc instance_npcname("Control Device#rgsr3");
	end;

OnEvent04:
	if ('rgsr == 13) {
		'rgsr = 14;
		donpcevent instance_npcname("#rgsr_boss") + "::OnSummon";
	}
	end;
}

1@rgsr,123,42,5	script	Oscar#rgsr	4_M_LGTGUARD,{
	mes "[奧斯卡]";
	mes "船長在控制面板室偷偷摸摸。";
	mes "安靜地移動。";
	close;
}

1@rgsr,131,58,3	script	Aas#rgsr	4_EP17_AS,{
	cutin "ep171_as02",1;
	mes "[原子吸收光譜法]";
	mes "門通常用安全系統鎖定，因此我必須留在那裡並與控制器一起工作。";
	mes "如您所見，大多數入口已經被阻止。";
	mes "你好";
	next;
	mes "[原子吸收光譜法]";
	mes "一一瀏覽它們將需要一些時間。";
	mes "我將繼續通過對講機為您提供幫助。";
	next;
	mes "[原子吸收光譜法]";
	mes "您必須經過餐廳才能到達走廊。";
	mes "EST將單獨執行操作。";
	next;
	mes "[原子吸收光譜法]";
	mes "如果發生干擾，EST可能會引起它。因此，請在繼續之前與她一起檢查。";
	next;
	mes "[原子吸收光譜法]";
	mes "您現在應該去冒險家。我將繼續努力保護安全控制器。";
	mes "可能有研究人員被拘留在裡面。";
	next;
	mes "[原子吸收光譜法]";
	mes "請先考慮他們的安全。";
	mes "現在繼續。";
	close2;
	if ('rgsr == 0) {
		'rgsr = 1;
		donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent01";
	}
	warp 'map$,127,67;
	end;
}

1@rgsr,60,138,0	script	Access Controller#rgsr	4_ENERGY_WHITE,{
	enablenpc instance_npcname("Researcher#rgsr_ev_0");
	if ('rgsr == 2) {
		'rgsr = 3;
		npctalk "請求通訊。";
		sleep2 2750;
		npctalk "分析信號。";
		sleep2 2750;
		npctalk "啟動語音傳輸...";
		sleep2 2750;
		npctalk "你連接了嗎？是阿斯。";
		sleep2 2750;
		npctalk "我已經獲得了部分安全系統的控制權，但尚未獲得完全訪問權限。";
		sleep2 2750;
		npctalk "除非發生大的干擾，否則警報或安全裝置將不會工作。";
		sleep2 2750;
		npctalk "我認為你應該打開這扇門來打開走廊。";
		sleep2 2750;
		npctalk "請被拘留的研究人員將手放在設備上。";
		sleep2 2750;
		npctalk "...*扎普*....";
		sleep2 2750;
		npctalk "語音傳輸結束。";
		sleep2 2750;
		hideonnpc();
		enablenpc();
		npctalk "研究員：你介意我開門嗎？",instance_npcname("Researcher#rgsr_ev_0");
		'rgsr = 4;
	}
	end;
}

1@rgsr,59,132,7	script	Researcher#rgsr_ev_0	4_M_NFMAN,{
	if ('rgsr == 4 && 'activate == 0) {
		'activate = 1;
		mes "[研究員]";
		mes "生物識別掃描仍在運行中，我會留在這里以保持門的打開狀態。";
		mes "如果您很著急，請去...";
		dispbottom "Control : Identity confirmed.";
		donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent02";
		close2;
		sleep2 800;
		npctalk "研究員：……呃……這到底是什麼……";
		end;
	}
}

1@rgsr,36,211,3	script	Est#rgsr	4_F_ESTLOVELOY,{
	if ('rgsr < 7) {
		cutin "ep162_est03",2;
		mes "[東方]";
		mes "您是在外面去除毒氣的人嗎？";
		mes "你到了好時光。";
		mes "由於氣體，門被強行鎖定了！";
		next;
		cutin "ep162_est01",2;
		mes "[東方]";
		mes "AAS進入控制器並釋放氣體需要時間。";
		mes "我本人要自己做。";
		next;
		cutin "ep162_est02",2;
		mes "[東方]";
		mes "這裡的守衛穿著與以前看到的那些心臟獵人相同的衣服。";
		mes "我認為我不能獨自拯救所有研究人員。";
		next;
		mes "[東方]";
		mes "看來我們已經在這裡聚集了大多數。";
		mes "讓我們組織下一個策略。";
		next;
		cutin "ep162_est01",2;
		mes "[東方]";
		mes "另一側有實驗室，這很可能是一團糟。";
		mes "我想知道是否會有一場大戰。";
		next;
		mes "[東方]";
		mes "從現在開始，叛亂特種部隊將優先考慮被拘留的研究人員。";
		mes "如果您得到它，請快速移動！";
		next;
		mes "[東方]";
		mes "好的，冒險家，找到通信設備並與AAS聯繫。";
		next;
		mes "[東方]";
		mes "您必須打開私人實驗室的入口。";
		if ('rgsr == 4)
			'rgsr = 5;
		close3;
	}
	cutin "ep162_est02",2;
	mes "[東方]";
	mes "嗯。我們將重點關注首先拯救人質。";
	mes "尋找AAS談論的保安人員的房間。";
	close3;
}

1@rgsr,26,219,0	script	Access Controller#rgsr2	4_ENERGY_WHITE,{
	if ('rgsr < 5) {
		mes "代碼不活動。";
		mes "溝通已終止。";
		close;
	}
	if ('rgsr == 5) {
		mes "連接語音傳輸。";
		close2;
		if ('rgsr == 5)
			'rgsr = 6;
		npctalk "等待通訊回复...";
		sleep2 2750;
		npctalk "阿斯：艾斯特指揮官？我們關上了門廳鎖，但問題出在哪裡呢？";
		sleep2 2750;
		npctalk "阿斯：哦，你是冒險家。我會給你報告。我還沒有完全接管安全控制器系統。";
		sleep2 2750;
		npctalk "Aas：看來還需要一些時間，不過我已經控制了大部分監控模塊。";
		sleep2 2750;
		npctalk "Aas：大部分敵軍都在大廳和休息區，所以實驗室區域的戒備較少。";
		sleep2 2750;
		npctalk "Aas：但是，為了確保中央控制室的安全，你必須經過警衛室，那裡檢測到了許多測試對象。";
		sleep2 2750;
		npctalk "Aas：首要任務應該是保護實驗室，然後是警衛室……*點擊*";
		sleep2 2000;
		npctalk "Est : Eh, what was that?",instance_npcname("Est#rgsr");
		if ('rgsr == 6)
			'rgsr = 7;
		end;
	}
	if ('rgsr < 7)
		end;
	else {
		mes "代碼不活動。";
		mes "溝通已終止。";
		close;
	}
}

1@rgsr,235,44,5	script	Aas#rgsr2	4_EP17_AS,{
	if ('rgsr == 7) {
		for (.@i = 0; .@i < 3; .@i++) {
			if (!'rescued[.@i])
				.@miss++;
		}
		if (.@miss) {
			cutin "ep171_as02",2;
			mes "[原子吸收光譜法]";
			mes "嘿，您檢查了所有實驗室嗎？";
			mes "請檢查是否仍然有被拘留的研究人員。";
			close3;
		}
		cutin "ep171_as02",1;
		mes "[原子吸收光譜法]";
		mes "備用主密鑰仍然保留。";
		mes "沒有它，我將無法控制安全系統。你還好嗎？";
		next;
		mes "[原子吸收光譜法]";
		mes "現在，您只需要檢查中央房間即可。";
		mes "你說心獵人嗎？";
		mes "來自施萊因的叛徒已經逃脫了。";
		next;
		mes "[原子吸收光譜法]";
		mes "我們專注於那個房間。";
		mes "那是主要想法。";
		mes "EST在哪裡？";
		next;
		cutin "16go_01",0;
		mes "[追逐]";
		mes "…………";
		next;
		cutin "ep171_as02",1;
		mes "[原子吸收光譜法]";
		mes "您必須繼續尋找所有研究人員嗎？";
		mes "我明白這是必要的。";
		next;
		mes "[原子吸收光譜法]";
		mes "冒險家，我們前往中央房間。";
		mes "在那邊見我。";
		close2;
		cutin "",255;
		if ('rgsr == 7) {
			enablenpc instance_npcname("Central Room#rgsr");
			'rgsr = 8;
			disablenpc instance_npcname("#rgsr_mob_2");
		}
		end;
	}
	cutin "ep171_as02",1;
	mes "[原子吸收光譜法]";
	mes "冒險家，我們前往中央房間。";
	mes "在那邊見我。";
	close3;
}

1@rgsr,235,143,5	script	Detained Researcher#rgsr_q01	4_F_SCIENCE,{
	if (!'rescued[0]) {
		mes "[研究員]";
		mes "我的...我的搜索...材料...";
		mes "他們拿走了一切！";
		mes "他們拿走了一切！";
		next;
		cutin "16go_01",0;
		mes "[追逐]";
		mes "…………";
		next;
		cutin "",255;
		npctalk "我要活下去！謝謝你！";
		mes "哦，你在這裡保護我嗎？";
		mes "我和你一起去，謝謝！";
		next;
		mes "[研究員]";
		mes "哦，你在那裡保護我嗎？";
		mes "我和你一起去，謝謝！";
		'rescued[0] = 1;
		close2;
		disablenpc();
	}
	end;
}

1@rgsr,237,108,3	script	Detained Researcher#rgsr_q02	4_LGTSCIENCE,{
	if (!'rescued[1]) {
		mes "[研究員]";
		mes "我以為他們踢出了所有瘋狂的傢伙...";
		mes "我像這樣撞到了頭。";
		mes "TSK。";
		next;
		mes "[研究員]";
		mes "你在這裡拯救我們嗎？";
		mes "讓我離開這裡！";
		mes "我們無法逃跑，因為他們在門外放了有毒的氣體！";
		next;
		mes "[研究員]";
		mes "是的，我對獵人有點了解，我會告訴你一切！";
		next;
		cutin "ep162_est02",2;
		mes "[東方]";
		mes "啊？您還有更多信息要分享嗎？";
		mes "您想在外面和我平靜地談論它嗎？";
		next;
		cutin "ep162_est01",2;
		mes "[東方]";
		mes "我會把這個傢伙帶出這裡，你看其他房間。";
		'rescued[1] = 1;
		close2;
		cutin "",255;
		disablenpc();
	}
	end;
}

1@rgsr,237,71,3	script	Detained Researcher#rgsr_q03	4_F_FRUIT,{
	if (!'rescued[2]) {
		mes "[研究員]";
		mes "請幫我！";
		mes "我工作了很多，我沒有得到報酬！";
		next;
		mes "[研究員]";
		mes "施萊因，施萊因在哪裡？";
		mes "他是叛徒！";
		mes "我用自己的眼睛看到了它，他只是殺死了守衛...";
		next;
		select("冷靜下來。");
		mes "[研究員]";
		mes "現在安全嗎？";
		mes "當我被雷肯伯（Rekenber）僱用時，我很高興，但是這麼大的公司到底要做什麼...";
		next;
		mes "[研究員]";
		mes "說什麼？叛亂即將拯救我們？";
		mes "我不能只是和你逃跑嗎？";
		next;
		mes "[研究員]";
		mes "哦...你是偵察隊的一部分嗎？";
		mes "好吧。我將等待救援隊。";
		mes "許多人通過打破窗戶而逃脫了。";
		next;
		mes "[研究員]";
		mes "哦，你已經知道了。";
		mes "好的。我現在保持安靜，等待。";
		mes "你的製服真的很酷。";
		'rescued[2] = 1;
		close;
	}
	mes "[研究員]";
	mes "我知道。我知道。";
	mes "如果我在這裡等待，叛亂會來拯救我嗎？";
	mes "那我會沒事的。";
	next;
	mes "[研究員]";
	mes "但同樣，您的製服真的很酷。";
	close;
}

1@rgsr,241,51,0	script	Scattered Documents#rgsr	4_ENERGY_BLUE,{
	if (isbegin_quest(7863) == 0) {
		mes "這是企業安全團隊的列表。";
		mes "有些人周圍有圈子...有些人被弄斷了。";
		next;
		cutin "ep171_as01",2;
		mes "[原子吸收光譜法]";
		mes "那些盤旋的人可能是那些接受招募的人...";
		mes "我的團隊是旁邊有一個X的團隊。";
		next;
		mes "[原子吸收光譜法]";
		mes "這是一份僱傭合同...";
		mes "突襲當天有一個粗略的計劃。";
		mes "我簡直不敢相信這是...";
		next;
		mes "[原子吸收光譜法]";
		mes "這是為了挑釁我。";
		mes "這次行不通。我將跟到最後。";
		next;
		mes "[原子吸收光譜法]";
		mes "我最好把它帶回苔絲並進行分析。";
		setquest 7863;
		close3;
	}
	mes "我已經在這裡有數據。";
	mes "這裡沒有別的可見。";
	close;

OnInstanceInit:
	questinfo(QTYPE_CLICKME,QMARK_YELLOW,"isbegin_quest(7863) == 0");
	end;
}

1@rgsr,125,157,0	script	Central Room#rgsr	4_ENERGY_BLUE,{
	if ('rgsr == 8) {
		'rgsr = 9;
		enablenpc instance_npcname("Aas#rgsr3");
		enablenpc instance_npcname("Goni#rgsr");
		enablenpc instance_npcname("Est#rgsr2");
		npctalk "我遲到了嗎？",instance_npcname("Aas#rgsr3");
		sleep2 1800;
		npctalk "不，你來得正是時候。",instance_npcname("Est#rgsr2");
		end;
	}
	mes "該設備不起作用。";
	close;
}

1@rgsr,128,157,3	script	Aas#rgsr3	4_EP17_AS,{
	if ('rgsr > 13)
		end;
	if ('rgsr == 9) {
		'rgsr = 10;
		.@aas$ = instance_npcname("Aas#rgsr3");
		.@gonie$ = instance_npcname("Goni#rgsr");
		.@erst$ = instance_npcname("Est#rgsr2");
		npctalk "Aas：中央房間似乎是空的。我進去看看。",.@aas$;
		sleep2 3000;
		npctalk "Aas：我會帶研究人員離開這裡。這樣可以嗎？",.@erst$;
		sleep2 3000;
		npctalk "Aas : Yes. Nothing else is being detected on the system. With our progress so far, the enemies have all been restrained.",.@aas$;
		sleep2 3000;
		npctalk "Est：你還害怕什麼？我已經回去了！",.@erst$;
		sleep2 3000;
		npctalk "戈尼：……我也是。",.@gonie$;
		sleep2 3000;
		npctalk "Est : There were couple of researchers left in the labs, I'll go bring them along. I'll be waiting.",.@erst$;
		sleep2 3000;
		npctalk "Est : Alright, Goni. You go to the main lobby first and secure the road. I'll take the others at the conference room.",.@erst$;
		sleep2 3000;
		npctalk "戈尼：*點頭*",.@gonie$;
		sleep2 3000;
		disablenpc instance_npcname("Goni#rgsr");
		disablenpc instance_npcname("Est#rgsr2");
		npctalk "Aas : So adventurer, let's check out the Central Room.",.@aas$;
		if ('rgsr == 10)
			'rgsr = 11;
		end;
	}
	cutin "ep171_as02",1;
	mes "[原子吸收光譜法]";
	mes "那是前面的中央房間。";
	next;
	if (select("輸入:等待。") == 2) {
		mes "[原子吸收光譜法]";
		mes "準備好後，請跟我說話。";
		mes "我不知道為什麼，但是緊密關閉；維護訪問需要一些時間。";
		next;
		mes "[原子吸收光譜法]";
		mes "正如安全系統確認的那樣，內部沒有檢測到任何內容...";
		mes "無論如何，我仍然想在房間內確認。";
		close3;
	}
	mes "[原子吸收光譜法]";
	mes "首先，我需要禁用自動鎖定機制，然後立即關注。";
	if ('rgsr == 11) {
		'rgsr = 12;
		donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent03";
	}
	close2;
	cutin "",255;
	warp 'map$,126,164;
	end;
}

1@rgsr,124,152,5	script	Goni#rgsr	4_M_GONY,{
	cutin "16go_01",0;
	mes "[追逐]";
	mes "…………";
	close3;
}

1@rgsr,125,154,5	script	Est#rgsr2	4_F_ESTLOVELOY,{
	cutin "ep162_est01",2;
	mes "[東方]";
	mes "我不相信裡面有敵人，我會拉出。";
	mes "只需檢查中央房間，然後下車即可。";
	close3;
}

1@rgsr,125,188,3	script	Restrained Specimen#rgsr	20353,2,3,{
	if ('rgsr == 12) {
		specialeffect EF_RED_HIT;
		specialeffect EF_BEGINSPELL3;
		npctalk "不要……不要……靠近我……！！呃，頭……好痛……";
		mapannounce 'map$,"Do... do not... come close to me...!! Huh, my head... it hurts...",bc_map,0x00FF00;
		'rgsr = 13;
	}
	end;

OnFail:
	specialeffect EF_FREEZE;
	npctalk "驚人的-！ ！就是這樣……你做了什麼！ ！";
	mapannounce 'map$,"Awesome-!! That's it... What did you do!!",bc_map,0x00FF00;
	end;
}

1@rgsr,144,180,0	script	Control Device#rgsr0	4_ENERGY_BLUE,{
	if ('rgsr < 13) {
		mes "該設備的目的未知。";
		mes "房間裡有四個按鈕。";
		close;
	}
	.@index = atoi(replacestr(strnpcinfo(2),"rgsr",""));
	if (.@index > 0) {
		if (!'rsgr_c[ (.@index - 1) ]) {
			mes "該設備的目的未知。";
			mes "房間裡有四個按鈕。";
			next;
			select("按下按鈕。");
			donpcevent instance_npcname("Restrained Specimen#rgsr") + "::OnFail";
			close;
		}
	}
	if (!'rsgr_c[.@index]) {
		switch (.@index) {
			case 0: .@answer = 2; .@string$ = "three phases"; break;
			case 1: .@answer = 1; .@string$ = "two phases"; break;
			case 2: .@answer = 4; .@string$ = "one phase"; break;
			case 3: .@answer = 3;
		}
		mes "該設備的目的未知。";
		mes "房間裡有四個按鈕。";
		next;
		.@s = select("紅色按鈕.:藍色按鈕.:白色按鈕.:黃色按鈕.");
		if (.@s != .@answer)
			donpcevent instance_npcname("Restrained Specimen#rgsr") + "::OnFail";
		else {
			specialeffect EF_BEGINSPELL3,AREA,instance_npcname("Restrained Specimen#rgsr");
			if (.@index < 3) {
				mapannounce 'map$,"Gravity control adjustment... completed... There are " + .@string$ +" remaining.",bc_map,0x0D98BA;
				'rsgr_c[.@index] = 1;
			} else {
				'rsgr_c[.@index] = 1;
				mapannounce 'map$,"Gravity control adjustment... completed... Restraints have been disengaged.",bc_map,0x0D98BA;
				sleep2 2500;
				donpcevent instance_npcname("#171_rgsr_control") + "::OnEvent04";
			}
		}
		close;
	}
	mes "該設備已經激活。";
	close;
}

1@rgsr,108,168,0	duplicate(Control Device#rgsr0)	Control Device#rgsr1	4_ENERGY_RED
1@rgsr,114,191,0	duplicate(Control Device#rgsr0)	Control Device#rgsr2	4_ENERGY_WHITE
1@rgsr,143,168,0	duplicate(Control Device#rgsr0)	Control Device#rgsr3	4_ENERGY_YELLOW

1@rgsr,127,164,3	script	Aas#rgsr4	4_EP17_AS,{
	if ('rgsr != 15)
		end;
	cutin "ep171_as01",2;
	mes "[原子吸收光譜法]";
	mes "EST在內部處理了其他所有內容。";
	mes "抱歉，根本無法幫助您。";
	next;
	mes "[原子吸收光譜法]";
	mes "讓我們回到Einbroch。";
	mes "我會...我去我的辦公室加入你。";
	next;
	.@s = select("前往艾因布洛赫。 :前往萊特哈爾岑。 :停留一段時間。");
	if (.@s == 3) {
		cutin "ep171_as02",2;
		mes "[原子吸收光譜法]";
		mes "您還有其他調查嗎？";
		mes "我在這裡等。";
		close3;
	}
	mes "[原子吸收光譜法]";
	mes "我們將在子彈上等待您的貓。";
	mes "請前往那裡，在總部見我們。";
	completequest 7862;
	close2;
	if (.@s == 1)
		warp "einbroch",301,324;
	else
		warp "lighthalzen",54,272;
	end;
}

//= Monster
1@rgsr,1,1,0	script	#rgsr_mob_1	HIDDEN_WARP_NPC,{
OnSummon:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	setarray .@xy,
	153,80,155,81,154,82, //= Bottom Right
	138,85,136,87,138,89, //= Center Right
	152,94,152,98,153,98, //= Stairs Right
	145,114,151,114,156,114, //= Top Right
	97,114,102,114,107,114, //= Top Left
	102,98,101,96,101,95, //= Stairs Left
	116,88,118,88,121,87, //= Center Left
	117,101,117,101,117,102, //= Center Top
	93,66,93,67,94,68, //= Bottom Door
	58,65,56,57,44,59,31,53,15,55, //= Bottom Room
	59,94,48,98,53,103,36,110,33,126,46,132,55,131; //= Upper Room
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3627,1,.@event$;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$) && 'rgsr == 1) {
		'rgsr = 2;
		disablenpc();
		enablenpc instance_npcname("Access Controller#rgsr");
	}
	end;
}

1@rgsr,1,1,0	script	#rgsr_mob_2	HIDDEN_WARP_NPC,{
OnSummon:
	setarray .@xy,102,137,104,136,104,135,126,139,138,137,139,134,137,133,151,135,150,136,151,137,85,151,82,215,84,216,84,218,107,216;
	setarray .@left_xy,85,170,54,186,55,183,58,181,59,186,62,183,62,216,59,214,56,217,54,214,52,217;
	setarray .@right_xy,167,136,167,135,168,135,208,138,208,134,203,138,200,134,187,123,188,
	123,187,122,186,108,188,107,186,105,196,106,203,109,205,105,208,109,187,92,186,90,188,91,
	187,69,187,67,188,67,201,74,202,77,207,76,211,74,187,48,188,45,186,46,197,45,202,45,208,42,211,42;
	setarray .@top_xy,169,217,169,214,172,217,172,214,176,217,176,214,180,217,180,214,184,217,184,214,167,204,168,203,168,203,167,176,167,176,166,175;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3627,1;
	for (.@i = 0; .@i < getarraysize(.@left_xy); .@i += 2)
		monster 'map$,.@left_xy[.@i],.@left_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnLeftKill";
	for (.@i = 0; .@i < getarraysize(.@right_xy); .@i += 2)
		monster 'map$,.@right_xy[.@i],.@right_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnRightKill";
	for (.@i = 0; .@i < getarraysize(.@top_xy); .@i += 2)
		monster 'map$,.@top_xy[.@i],.@top_xy[.@i+1],"Poisonous Gas",20352,1,instance_npcname(strnpcinfo(0))+"::OnTopKill";
	end;

OnLeftKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnLeftKill";
	if (!mobcount('map$,.@event$)) {
		mapannounce 'map$,"Est : Who is it? Are you Aas? Are you the adventurer?",bc_map,0x0D98BA;
		enablenpc instance_npcname("Est#rgsr");
		enablenpc instance_npcname("Rebellion Crew#rgsr_dum01");
	}
	end;

OnRightKill:
OnTopKill:
	end;
}

1@rgsr,1,1,0	script	#rgsr_boss	HIDDEN_WARP_NPC,{
	end;
OnSummon:
	mapannounce 'map$,"Ahahaha.. ahahaha... I'll kill you all!!!!",bc_map,0x00FF00;
	sleep 2000;
	mapannounce 'map$,"Aas : What is going on? *click* The lock is...!!",bc_map;
	sleep 2000;
	mapannounce 'map$,"I'm not a defect! I'm not a monster!",bc_map,0x00FF00;
	specialeffect EF_LORD,AREA,instance_npcname("Restrained Specimen#rgsr");
	disablenpc instance_npcname("Restrained Specimen#rgsr");
	monster 'map$,125,188,"Failed Specimen",20353,1,instance_npcname(strnpcinfo(0))+"::OnBossKill";
	end;

OnBossKill:
	if ('rgsr == 14) {
		'rgsr = 15;
		hideonnpc instance_npcname("Aas#rgsr3");
		disablenpc instance_npcname("Aas#rgsr3");
		enablenpc instance_npcname("Aas#rgsr4");
		disablenpc();
	}
	end;
}

1@rgsr,94,68,5	script	#rgsr_touch_mob_0	3622,4,6,{
	end;

OnTouch:
	disablenpc();
	.@v = atoi(replacestr(strnpcinfo(2),"rgsr_touch_mob_",""));
	if (!.@v)
		setarray .@xy,98,66,94,68,95,70,92,68;
	else
		setarray .@xy,91,91,91,93,95,93,94,97,97,92;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 2)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"Special Guard",3622,1;
	end;
}

1@rgsr,95,93,5	duplicate(#rgsr_touch_mob_0)	#rgsr_touch_mob_1	3622,4,6

//= Warp/Other
1@rgsr,59,136,0	warp	#rgsr_warp_0	1,2,1@rgsr,66,136

1@rgsr,51,184,0	script	#rgsr_warp_1	WARPNPC,1,2,{
	end;

OnTouch:
	.@event$ = instance_npcname("#rgsr_mob_2")+"::OnLeftKill";
	if (mobcount('map$,.@event$)) {
		dispbottom "Voice Behind Door : Who's outside? exterminate that strange poison gas!!";
		end;
	}
	switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
		case 1: warp 'map$,46,184; break;
		case 2:	warp 'map$,46,216; break;
		case 3: warp 'map$,54,184; break;
		case 4: warp 'map$,54,216;
	}
	end;
}

1@rgsr,51,216,0	duplicate(#rgsr_warp_1)	#rgsr_warp_2	WARPNPC,1,2
1@rgsr,49,184,0	duplicate(#rgsr_warp_1)	#rgsr_warp_3	WARPNPC,1,2
1@rgsr,49,216,0	duplicate(#rgsr_warp_1)	#rgsr_warp_4	WARPNPC,1,2

1@rgsr,186,216,0	script	#rgsr_warp_5	WARPNPC,1,2,{
	.@event$ = instance_npcname("#rgsr_mob_2")+"::OnTopKill";
	if (mobcount('map$,.@event$)) {
		mes "已經檢測到一個未知的實體。拒絕訪問。";
		close;
	}
	switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
		case 5: warp 'map$,191,216; break;
		case 6: warp 'map$,183,216;
	}
	end;
}

1@rgsr,188,216,0	duplicate(#rgsr_warp_5)	#rgsr_warp_6	WARPNPC,1,2

1@rgsr,214,136,0	script	#rgsr_warp_7	WARPNPC,1,2,{
	.@event$ = instance_npcname("#rgsr_mob_2")+"::OnRightKill";
	if (mobcount('map$,.@event$) > 0 || 'rgsr < 7) {
		mes "已經檢測到一個未知的實體。拒絕訪問。";
		close;
	}
	switch (atoi(replacestr(strnpcinfo(2),"rgsr_warp_",""))) {
		case 7: warp 'map$,219,136; break;
		case 8: warp 'map$,211,136; break;
		case 9: warp 'map$,219,108; break;
		case 10: warp 'map$,211,108; break;
		case 11: warp 'map$,219,76; break;
		case 12: warp 'map$,211,76; break;
		case 13: warp 'map$,219,44; break;
		case 14:  warp 'map$,211,44;
	}
	end;
}

1@rgsr,216,136,0	duplicate(#rgsr_warp_7)	#rgsr_warp_8	WARPNPC,1,2
1@rgsr,214,108,0	duplicate(#rgsr_warp_7)	#rgsr_warp_9	WARPNPC,1,2
1@rgsr,216,108,0	duplicate(#rgsr_warp_7)	#rgsr_warp_10	WARPNPC,1,2
1@rgsr,214,76,0	duplicate(#rgsr_warp_7)	#rgsr_warp_11	WARPNPC,1,2
1@rgsr,216,76,0	duplicate(#rgsr_warp_7)	#rgsr_warp_12	WARPNPC,1,2
1@rgsr,214,44,0	duplicate(#rgsr_warp_7)	#rgsr_warp_13	WARPNPC,1,2
1@rgsr,216,44,0	duplicate(#rgsr_warp_7)	#rgsr_warp_14	WARPNPC,1,2

1@rgsr,37,126,0	script	Researcher#rgsr_dum00	4_F_SCIENCE,{
	mes "[被拘留的研究人員]";
	mes "為什麼我必須餵這個叛徒？";
	close;
}

1@rgsr,35,136,7	script	Researcher#rgsr_dum01	4_LGTSCIENCE,{
	mes "[被拘留的研究人員]";
	mes "我想回家...我只想回家...";
	close;
}

1@rgsr,38,139,3	script	Researcher#rgsr_dum02	4_M_NFMAN,{
	mes "[被拘留的研究人員]";
	mes "你在這裡拯救我們嗎？";
	mes "謝謝你！謝謝你！";
	close;
}

1@rgsr,31,186,3	script	Researcher#rgsr_dum03	4_LGTSCIENCE,{
	mes "[被拘留的研究人員]";
	mes "到底發生了什麼？";
	mes "aaaah ...我只想回家...";
	close;
}

1@rgsr,24,186,5	script	Researcher#rgsr_dum04	4_F_SCIENCE,{
	mes "[被拘留的研究人員]";
	mes "你為什麼來這裡救我？";
	mes "企業的警衛...不等待...";
	next;
	mes "[被拘留的研究人員]";
	mes "我以為你是把我們拘留的人...！";
	mes "他們為什麼要這樣做？";
	close;
}

1@rgsr,25,182,7	script	Researcher#rgsr_dum05	4_M_HUMAN_01,{
	mes "[被拘留的研究人員]";
	mes "這些卑鄙的人拿了我的研究報告，我在這裡逃脫了。";
	mes "這是阻塞吸血昆蟲的噴霧驅蟲劑！";
	next;
	mes "[被拘留的研究人員]";
	mes "那是我的私人研究，我不知道他們為什麼要接受！";
	mes "您不能只是拿走別人的研究報告。";
	close;
}

1@rgsr,40,186,3	script	Rebellion Crew#rgsr_dum01	4_M_ILYA,{
	mes "[叛亂船員]";
	mes "我已經做好了準備，但這比想像的要容易。";
	mes "現在我被困在這裡。呵呵！";
	close;
}

1@rgsr,33,185,3	script	Rebellion Crew#rgsr_dum02	4_M_REBELLION2,{
	mes "[叛亂船員]";
	mes "其餘的研究人員是為了安全的。";
	mes "叛亂將保護他們並幫助他們逃脫。";
	close;
}

1@rgsr,31,183,5	script	Rebellion Crew#rgsr_dum03	4_F_ANYA,{
	mes "[叛亂船員]";
	mes "我以為他們的內部力量較少，但我認為他們沒有安裝有毒的天然氣。";
	mes "大多數人已經逃走了，只有敵人留下來。";
	close;
}

1@rgsr,195,205,0	script	Research Report#rgsr_00	4_ENERGY_BLUE,{
	mes "它是有關新藥水的新效率和副作用的數據。";
	mes "但是，有有關核心成分的信息。";
	close;
}

1@rgsr,198,199,0	script	Research Report#rgsr_01	4_ENERGY_BLUE,{
	mes "其中一些圖紙被撕裂了。";
	mes "有人故意摧毀了相關部分。";
	close;
}

1@rgsr,207,210,0	script	Research Report#rgsr_02	4_ENERGY_BLUE,{
	mes "文件的一部分受到嚴重損壞。";
	mes "這似乎是在嘗試使用光的折射來創建設備。";
	close;
}

1@rgsr,226,226,0	script	Research Report#rgsr_03	4_ENERGY_BLUE,{
	mes "勞埃德的魅力和處理數據。";
	mes "只剩下標題，所有文本都被刪除了。";
	close;
}

1@rgsr,231,223,0	script	Research Report#rgsr_04	4_ENERGY_BLUE,{
	mes "小冊子缺少一些頁面。有人故意省略了材料。";
	close;
}

1@rgsr,243,223,0	script	Research Report#rgsr_05	4_ENERGY_BLUE,{
	mes "實驗數據已被刪除。有人銷毀信息的清晰痕跡。";
	close;
}
