//===== rAthena Script =======================================
//= Cor Operation
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 Securing Elyumina
//= Episode 17.1 EL1-A17T Suppression (Cor Memorial)
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@cor,1,1,0	script	#171_cor_control	-1,{
	end;

OnInstanceInit:
	'map$ = instance_mapname("1@cor");
	'step = 0;
	setcell 'map$,159,216,159,225,CELL_WALKABLE,0;
	setcell 'map$,159,216,159,225,CELL_SHOOTABLE,0;
	setcell 'map$,98,216,98,225,CELL_WALKABLE,0;
	setcell 'map$,98,216,98,225,CELL_SHOOTABLE,0;
	setcell 'map$,132,240,141,240,CELL_WALKABLE,0;
	setcell 'map$,132,240,141,240,CELL_SHOOTABLE,0;

	//= Story
	disablenpc instance_npcname("Reinforced Energy#171_box_0");
	disablenpc instance_npcname("Biological Battery#171_box_0");
	disablenpc instance_npcname("Chemical Poison#171_box_1");
	disablenpc instance_npcname("Rebellion#171_box_4");
	disablenpc instance_npcname("Elena Volkova#171_cmd_1");
	disablenpc instance_npcname("Elena Volkova#171_cmd_2");
	disablenpc instance_npcname("Elyumina#171_cor_end");
	disablenpc instance_npcname("Rebellion#171_cmd_6");
	disablenpc instance_npcname("Rebellion#171_cmd_7");
	disablenpc instance_npcname("#171_cor_warp_0");
	disablenpc instance_npcname("#171_cor_warp_1");
	for (.@i = 0; .@i < 6; .@i++)
		disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Rebellion#171_box_" + .@i);
		disablenpc instance_npcname("Box#171_box_" + .@i);
	}

	//= Daily
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Elyumina#171_box_d" + .@i);
		disablenpc instance_npcname("Box#171_box_d" + .@i);
	}
	disablenpc instance_npcname("Biological Battery#171_box_trap_0");
	disablenpc instance_npcname("Chemical Poison#171_box_trap_1");
	disablenpc instance_npcname("Biological Battery#171_box_trap_2");
	disablenpc instance_npcname("Chemical Poison#171_box_trap_3");
	disablenpc instance_npcname("Reinforced Energy#171_trap_0");
	disablenpc instance_npcname("Reinforced Energy#171_trap_2");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_1");

	//= Boss Stage
	for (.@i = 0; .@i < 11; .@i++) {
		disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
		disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
	}
	disablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;

OnStory01:
	disablenpc instance_npcname("Elena Volkova#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	for (.@i = 0; .@i < 6; .@i++)
		disablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
	for (.@i = 0; .@i < 4; .@i++) {
		enablenpc instance_npcname("Rebellion#171_box_" + .@i);
		enablenpc instance_npcname("Box#171_box_" + .@i);
	}
	enablenpc instance_npcname("Rebellion#171_box_4");
	end;

OnStory02:
	for (.@i = 0; .@i < 4; .@i++) {
		disablenpc instance_npcname("Rebellion#171_box_" + .@i);
		disablenpc instance_npcname("Box#171_box_" + .@i);
	}
	disablenpc instance_npcname("Rebellion#171_box_4");
	enablenpc instance_npcname("Elena Volkova#171_cmd_1");
	mapannounce 'map$,"Elena Volkova : Awesome, she came out! I've sent the signal, come to my location!",bc_map,0xFFFF99;
	viewpointmap 'map$,1,172,223,5,0xFFFFFF;
	viewpointmap 'map$,2,140,79,1,0xFFFFFF;
	viewpointmap 'map$,2,160,119,2,0xFFFFFF;
	viewpointmap 'map$,2,220,170,3,0xFFFFFF;
	viewpointmap 'map$,2,222,236,4,0xFFFFFF;
	end;

OnStory03:
	disablenpc instance_npcname("Elena Volkova#171_cmd_1");
	donpcevent instance_npcname("#171_cor_warp_0") + "::OnActive";
	end;

OnDaily01:
	disablenpc instance_npcname("Elena Volkova#171_cmd_0");
	disablenpc instance_npcname("Elyumina#171_cmd_0");
	for (.@i = 0; .@i < 4; .@i++) {
		enablenpc instance_npcname("Elyumina#171_box_d" + .@i);
		enablenpc instance_npcname("Box#171_box_d" + .@i);
	}
	end;

OnDaily02:
	viewpointmap 'map$,1,172,223,5,0xFFFFFF;
	mapannounce 'map$,"Elena Volkova : Once you've finish all of it, come to the barracks and Elyumina will guide you!",bc_map,0xFFFF99;
	enablenpc instance_npcname("Elyumina#171_cmd_1");
	end;

OnDaily03:
	disablenpc instance_npcname("Elyumina#171_cmd_1");
	donpcevent instance_npcname("#171_cor_warp_1") + "::OnActive";
	end;
}

1@cor,177,169,0	script	#171_cor_ev	HIDDEN_WARP_NPC,4,4,{
	end;

OnTouch:
	if (!is_party_leader())
		end;
	if (isbegin_quest(16360) == 2)
		'mode = 1;
	disablenpc();
	end;
}

1@cor,180,169,3	script	Elena Volkova#171_cmd_0	4_F_ELENA,5,5,{
	if (!'mode) {
		cutin "162elena_01",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "我將開始解釋操作。首先...";
		specialeffect EF_TETRA_GROUND;
		next;
		for (.@i = 0; .@i < 6; .@i++)
			enablenpc instance_npcname("Rebellion#171_cmd_" + .@i);
		next;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "我將開始解釋操作。首先...";
		mes "......現在你可以瘋狂。";
		next;
		cutin "",255;
		mes "[叛亂]";
		mes "那裡似乎已經爆炸了。火焰從前面升起！";
		next;
		mes "[叛亂]";
		mes "這些爆炸有4個確定的位置來自！我在您的minimap上有 ^ff0000shown。 ^000000";
		viewpointmap 'map$,1,140,79,1,0xFFFFFF;
		viewpointmap 'map$,1,160,119,2,0xFFFFFF;
		viewpointmap 'map$,1,220,170,3,0xFFFFFF;
		viewpointmap 'map$,1,222,236,4,0xFFFFFF;
		next;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "這很突然，但是我正在改變操作。";
		mes "不必擔心已經在前面的那些，他們會適應任何情況。";
		next;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "分為4個團隊，每個人在自己的位置，開始壓制線索。如果任何團隊找到了Elyumina，請立即告訴我們。";
		next;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "大家，到你的位置！開始操作！";
		npctalk "是的！",instance_npcname("Rebellion#171_cmd_0");
		npctalk "……",instance_npcname("Rebellion#171_cmd_1");
		npctalk "是的！",instance_npcname("Rebellion#171_cmd_2");
		npctalk "是的！",instance_npcname("Rebellion#171_cmd_3");
		npctalk "是的！",instance_npcname("Rebellion#171_cmd_4");
		npctalk "是的！",instance_npcname("Rebellion#171_cmd_5");
		next;
		cutin "162elena_02",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "我請你分開行動。請檢查您的 ^0000cdminimap ^000000，以盡快組織情況。如果他們需要支持，請加入團隊！";
		close2;
		cutin "",255;
		if ('step == 0) {
			'step = 1;
			donpcevent instance_npcname("#171_cor_control") + "::OnStory01";
		}
	} else {
		enablenpc instance_npcname("Elyumina#171_cmd_0");
		.@elyumina$ = instance_npcname("Elyumina#171_cmd_0");
		cutin "162elena_01",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "簡報...這裡的罪犯將是這樣做的。";
		mes "照顧罪犯很難。";
		npctalk "埃琳娜·沃爾科娃：簡報……這裡的罪犯將是做這件事的人。照顧罪犯是很困難的。";
		next;
		cutin "ep171_elyumina04",0;
		mes "[氧化鋁]";
		mes "哈哈！那不是你的工作嗎？正確執行。你為什麼要把簡報留給我？";
		npctalk "艾柳米娜：哈哈！這不是你的工作嗎？正確地做。你為什麼把簡報留給我？",.@elyumina$;
		next;
		cutin "162elena_01",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "太嘈雜了。在開始之前。您現在是USU的一部分嗎？";
		npctalk "埃琳娜·沃爾科娃：太吵了。在我們開始之前。您現在是 USU 的一員嗎？";
		next;
		cutin "ep171_elyumina03",0;
		mes "[氧化鋁]";
		mes "那就是他說的...你擔心我會很害怕！";
		mes "嘿，我會做解釋，因為我想照顧我的孩子！";
		npctalk "Elyumina : Heh, I'll do the explaining because I want to take care of my children!",.@elyumina$;
		next;
		cutin "ep171_elyumina01",0;
		mes "[氧化鋁]";
		mes "...好吧，幾乎是時候了。";
		mes "我的孩子將開始在<tipbox> [四個位置] <fofe> 8086 </info> </tipbox>中開始設置陷阱。";
		npctalk "Elyumina : ... Okay, it's almost time. My children will start setting up traps in four places.",.@elyumina$;
		next;
		mes "[氧化鋁]";
		mes "在這裡，拿出地圖。該信息也將交付給黨員。";
		mes "你不是一個人嗎？你知道我的孩子很強大嗎？";
		npctalk "Elyumina : Here, take the map. The information will also be delivered to the party members as well.",.@elyumina$;
		viewpointmap 'map$,1,140,79,1,0xFFFFFF;
		viewpointmap 'map$,1,160,119,2,0xFFFFFF;
		viewpointmap 'map$,1,220,170,3,0xFFFFFF;
		viewpointmap 'map$,1,222,236,4,0xFFFFFF;
		next;
		cutin "ep171_elyumina04",0;
		mes "[氧化鋁]";
		mes "繼續觸摸陷阱。陷阱將激活，我可愛的孩子會出現。";
		mes "您確實記得陷阱是如何工作的，對嗎？";
		npctalk "Elyumina：去觸碰陷阱吧。陷阱將會啟動，我可愛的孩子們就會出現。",.@elyumina$;
		next;
		cutin "ep171_elyumina03",0;
		mes "[氧化鋁]";
		mes "清除了四個陷阱後，EL1_A17T將會出來，這位女士將帶您到上一次。";
		npctalk "Elyumina : After you've cleared the four traps, EL1_A17T will come out, and this lady here will bring you to the same place as last time.",.@elyumina$;
		next;
		cutin "ep171_elyumina04",0;
		mes "[氧化鋁]";
		mes "就是這樣解釋。現在，上班了！肌肉白痴！啊！";
		npctalk "Elyumina : That's it for the explanation. Now, get to work! Muscle idiots! Ahahaha!",.@elyumina$;
		emotion ET_SMILE,getnpcid(0,.@elyumina$);
		next;
		cutin "162elena_01",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "...無論如何，我會與這個罪犯進行對話。冒險家，我指望你！";
		npctalk "Elena Volkova：……我稍後會和這個罪犯談談……無論如何。我指望你們了，冒險者們！";
		close2;
		cutin "",255;
		if ('step == 0) {
			'step = 1;
			donpcevent instance_npcname("#171_cor_control") + "::OnDaily01";
		}
	}
	end;

OnTouch:
	npctalk "埃琳娜·沃爾科娃：這邊走。等大家都到齊了之後我們再簡單介紹一下。";
	end;
}

1@cor,178,172,3	duplicate(dummy_npc)	Elyumina#171_cmd_0	4_EP17_ELYUMINA

1@cor,172,223,3	script	Elyumina#171_cmd_1	4_EP17_ELYUMINA,{
	cutin "ep171_elyumina03",0;
	mes "[氧化鋁]";
	mes "那很快...預計我的孩子不會贏。";
	mes "那裡還有很多麻煩~";
	npctalk "Elyumina：那很快……但預計我的孩子不會獲勝。上面還有很多麻煩事~";
	next;
	cutin "ep171_elyumina01",0;
	mes "[氧化鋁]";
	mes "...您要直接去找我的EL1-A17T嗎？";
	mapannounce 'map$,"Elena Volkova : Yeah, damn criminal! Why don't you open the portal right now?",bc_map,0xFFFF99;
	npctalk "Elyumina：...你直接去我的 EL1_A17T 嗎？";
	next;
	if (select("進去:先別進去。") == 2) {
		cutin "ep171_elyumina02",0;
		mes "[氧化鋁]";
		mes "來吧，我正要打開它。";
		close3;
	}
	cutin "ep171_elyumina04",0;
	mes "[氧化鋁]";
	mes "哈哈哈！是的。讓我們努力工作。為我收集更多戰斗數據！";
	npctalk "艾柳米娜：哈哈哈！是的。讓我們努力吧。為我收集更多的戰斗數據！";
	close2;
	cutin "",255;
	viewpointmap 'map$,2,172,223,5,0xFFFFFF;
	if ('step == 1) {
		'step = 2;
		donpcevent instance_npcname("#171_cor_control") + "::OnDaily03";
	}
	end;
}

1@cor,177,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_0	4_M_REBELLION3
1@cor,180,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_1	4_M_GONY
1@cor,183,165,1	duplicate(dummy_npc)	Rebellion#171_cmd_2	4_F_REBELLION3
1@cor,177,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_3	4_F_ANYA
1@cor,180,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_4	4_M_ILYA
1@cor,183,163,1	duplicate(dummy_npc)	Rebellion#171_cmd_5	4_F_REBELLION2

1@cor,224,238,3	script	Rebellion#171_box_0	4_M_REBELLION3,{
	if (isbegin_quest(16355) == 0) {
		mes "[叛亂]";
		mes "有一個可疑盒子！信號正在從內部傳輸，目前正在解密！";
		close;
	}
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
		mes "[叛亂]";
		mes "破壞生物電池並殺死怪物！";
		close;
	}
	if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
		mes "[叛亂]";
		mes "對不起，分析後期分析。這不是召喚怪物的設備。";
		next;
		mes "[叛亂]";
		mes "召喚怪物後，它召喚了一個生物爆炸來加強怪物和爆炸。";
		next;
		mes "[叛亂]";
		mes "如果您想處理它，只需銷毀它即可。很好，因為這是一個簡單的解決方案。";
		next;
		mes "[叛亂]";
		mes "好吧，看起來這已經結束了。";
		next;
		mes "[叛亂]";
		mes "看來您在這裡發現了一些有趣的東西。您應該檢查其他團隊！";
		close2;
		completequest 16355;
		disablenpc();
		disablenpc instance_npcname("Box#" + strnpcinfo(2));
		viewpointmap 'map$,2,224,236,4,0xFFFFFF;
		if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
			'step = 2;
			doevent instance_npcname("#171_cor_control") + "::OnStory02";
		}
	}
	end;
}

1@cor,222,236,3	script	Box#171_box_0	4_STEELBOX,{
	if (isbegin_quest(16355) == 0) {
		mes "[叛亂]";
		mes "爆炸的原因是未知的，但是我在網站上發現了這個可疑的盒子。這似乎是某種機制。";
		next;
		mes "[叛亂]";
		mes "有一個信號從內部傳輸...嗯，請小心。發生了什麼事！";
		close2;
		setquest 16355;
		specialeffect EF_BAKU;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
		end;
	}
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (isbegin_quest(16355) == 1 && mobcount('map$,.@event$)) {
		mes "[叛亂]";
		mes "破壞生物電池並殺死怪物！";
		close;
	}
	if (isbegin_quest(16355) == 1 && !mobcount('map$,.@event$))
		doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
	end;

OnSpawn:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	setarray .@xy,229,229,20341,217,235,20341,222,229,20341;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	initnpctimer;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		stopnpctimer;
		npctalk "Thank you for your hard work! Before you go, please listen to what I have to say for a while.",instance_npcname("Rebellion#" + strnpcinfo(2));
		killmonster 'map$,instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		stopnpctimer instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (mobcount('map$,.@event$)) {
		npctalk "The analysis result is, that battery is providing power to the summoned monsters. Step on it and avoid damage immediately!",instance_npcname("Rebellion#" + strnpcinfo(2));
		enablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
	} else {
		stopnpctimer;
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
	}
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@event2$ = instance_npcname("Biological Battery#" + strnpcinfo(2)) + "::OnBombKill";
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		disablenpc instance_npcname("Biological Battery#" + strnpcinfo(2));
		if (unitexists(getnpcid(0,instance_npcname("Biological Battery#" + strnpcinfo(2)))))
			initnpctimer;
	}
	end;
}

1@cor,222,235,3	script	Biological Battery#171_box_0	1738,3,3,{
	end;

OnTouch:
	disablenpc();
	stopnpctimer instance_npcname("Box#171_box_0");
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	if (!mobcount('map$,.@event$)) {
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		monster 'map$,.@x,.@y,"",20345,1,.@event$;
	}
	initnpctimer;
OnBombKill:
	end;

OnTimer7000:
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	if (mobcount('map$,.@event$))
		enablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
	else
		stopnpctimer;
	end;

OnTimer17000:
	.@event$ = instance_npcname("Box#" + strnpcinfo(2))+"::OnMobKill";
	.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#" + strnpcinfo(2))))) {
		disablenpc instance_npcname("Reinforced Energy#" + strnpcinfo(2));
		if (mobcount('map$,.@event2$))
			killmonster 'map$,.@event2$;
		stopnpctimer;
		if (mobcount('map$,.@event$))
			initnpctimer instance_npcname("Box#171_box_0");
	}
	end;
}

1@cor,225,232,3	script	Reinforced Energy#171_box_0	4_ENERGY_WHITE,{
	if (!getd("'rf_" + strnpcinfo(2))) {
		setd("'rf_" + strnpcinfo(2),1);
		specialeffect2 EF_ENHANCE;
		specialeffect2 EF_LIGHTSPHERE_STAR;
		sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
		npctalk "力量，壓倒性的力量！";
		unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
		sleep 1500;
		disablenpc();
		setd("'rf_" + strnpcinfo(2),0);
		stopnpctimer instance_npcname("Biological Battery#" + strnpcinfo(2));
		initnpctimer instance_npcname("Box#" + strnpcinfo(2));
	}
	end;
}

1@cor,218,172,5	script	Rebellion#171_box_1	4_F_REBELLION2,{
	if (isbegin_quest(16356) == 0) {
		mes "[叛亂]";
		mes "有一個可疑盒子！信號正在從內部傳輸，目前正在解密！";
		close;
	}
	.@event$ = instance_npcname("Box#" + strnpcinfo(2)) + "::OnMobKill";
	if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
		mes "[叛亂]";
		mes "避免化學毒物並殺死怪物！";
		close;
	}
	if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$)) {
OnTalk:
		mes "[叛亂]";
		mes "這是一個可怕但有趣的設備。這不是召喚怪物的設備。";
		next;
		mes "[叛亂]";
		mes "召喚怪物後，它不斷釋放化學毒物，使去除怪物更加困難。";
		next;
		mes "[叛亂]";
		mes "如果您觸摸化學毒物，它將像炸彈一樣散佈。您不應該更近。";
		next;
		mes "[叛亂]";
		mes "因此，看起來已經結束了，我將加入另一個團隊！你也應該。";
		close2;
		disablenpc();
		disablenpc instance_npcname("Box#" + strnpcinfo(2));
		viewpointmap 'map$,2,220,170,3,0xFFFFFF;
		completequest 16356;
		if (isbegin_quest(16355) == 2 && isbegin_quest(16356) == 2 && 'step == 1) {
			'step = 2;
			doevent instance_npcname("#171_cor_control") + "::OnStory02";
		}
	}
	end;
}

1@cor,220,170,3	script	Box#171_box_1	4_WOODBOX,{
	if (isbegin_quest(16356) == 0) {
		mes "[叛亂]";
		mes "爆炸的原因是未知的，但是我在網站上發現了這個可疑的盒子。這似乎是某種機制。";
		next;
		mes "[叛亂]";
		mes "有一個信號從內部傳輸...嗯，請小心。發生了什麼事！";
		close2;
		setquest 16356;
		specialeffect EF_BAKU;
		donpcevent instance_npcname(strnpcinfo(0)) + "::OnSpawn";
		end;
	}
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	if (isbegin_quest(16356) == 1 && mobcount('map$,.@event$)) {
		mes "[叛亂]";
		mes "避免化學毒物並殺死怪物！";
		close;
	}
	if (isbegin_quest(16356) == 1 && !mobcount('map$,.@event$))
		doevent instance_npcname("Rebellion#" + strnpcinfo(2)) + "::OnTalk";
	end;

OnSpawn:
	setd("'" + strnpcinfo(2),1);
	setarray .@xy,213,175,20342,216,174,20342,219,167,20342;
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	initnpctimer;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		stopnpctimer;
		killmonster 'map$,instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
		disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
		npctalk "Thank you for your hard work! Before you go, please listen to what I have to say for a while.",instance_npcname("Rebellion#" + strnpcinfo(2));
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (mobcount('map$,.@event$)) {
		npctalk "它噴出了有毒的化學物質！避免踩到它，繼續戰鬥吧！",instance_npcname("Rebellion#" + strnpcinfo(2));
		enablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
	} else
		stopnpctimer;
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@event2$ = instance_npcname("Chemical Poison#" + strnpcinfo(2)) + "::OnPoisonKill";
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		disablenpc instance_npcname("Chemical Poison#" + strnpcinfo(2));
		if (unitexists(getnpcid(0,instance_npcname("Chemical Poison#171_box_1"))))
			initnpctimer;
	}
	end;
}

1@cor,220,169,3	script	Chemical Poison#171_box_1	2531,3,3,{
	end;

OnTouch:
	disablenpc();
	stopnpctimer instance_npcname(strnpcinfo(0));
	npctalk "When you come in contact with the Chemical Poison, the toxic materials spread. You've already stepped on it, hurry up and avoid it!",instance_npcname("Rebellion#" + strnpcinfo(2));
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
	if (!mobcount('map$,.@event$)) {
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		monster 'map$,.@x,.@y,"",20344,1,.@event$;
	}
	initnpctimer;
	end;

OnPoisonKill:
	end;

OnTimer10000:
	stopnpctimer;
	initnpctimer instance_npcname("Box#" + strnpcinfo(2));
	end;
}

1@cor,162,117,1	script	Rebellion#171_box_2	4_M_GONY,{
	mes "[叛亂]";
	mes "…………";
	close;
}

1@cor,160,119,3	script	Box#171_box_2	4_STEELBOX,{
	mes "[叛亂]";
	mes "…………";
	specialeffect EF_M03;
	next;
	mes "[叛亂]";
	mes "…………";
	next;
	mes "- 煙從可疑盒子上升。 -";
	next;
	select("你已經這樣做了嗎？ :你還沉默嗎？");
	mes "[叛亂]";
	mes "…………";
	next;
	mes "[叛亂]";
	mes "*點頭，點頭*";
	next;
	mes "- 讓我們去支持另一個團隊。 -";
	viewpointmap 'map$,2,160,119,2,0xFFFFFF;
	close2;
	disablenpc();
	disablenpc instance_npcname("Rebellion#" + strnpcinfo(2));
	end;
}

1@cor,138,82,5	script	Rebellion#171_box_3	4_F_ANYA,{
	mes "[叛亂]";
	mes "我是否告訴過您我真的很高興與Secret Wing合作？多虧了他們，我被選為此操作。";
	next;
	mes "[叛亂]";
	mes "讓我們向他們展示我們的全部內容，並且我們是好人。";
	close;
}

1@cor,142,82,3	script	Rebellion#171_box_4	4_M_ILYA,{
	mes "[叛亂]";
	mes "我有一段時間沒見過你。";
	next;
	mes "[叛亂]";
	mes "我已經知道您的冒險家，但是請小心。";
	close;
}

1@cor,140,79,3	script	Box#171_box_3	4_WOODBOX,{
	mes "[叛亂]";
	mes "你好冒險家。";
	specialeffect EF_M03;
	next;
	mes "[叛亂]";
	mes "很高興成為這支球隊的一部分，我們已經照顧了這個盒子。";
	next;
	mes "[叛亂]";
	mes "這些機器使用化學毒物，請小心。";
	next;
	mes "[叛亂]";
	mes "現在，我們將進行搜索，直到信號出現為止。冒險家，小心！";
	viewpointmap 'map$,2,140,79,1,0xFFFFFF;
	close2;
	disablenpc();
	disablenpc instance_npcname("Rebellion#171_box_3");
	disablenpc instance_npcname("Rebellion#171_box_4");
	end;
}

1@cor,172,223,3	script	Elena Volkova#171_cmd_1	4_F_ELENA,{
	cutin "162elena_01",2;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "我一直在等你。我找到了瘋狂的研究人員。";
	mes "其他傢伙圍繞著這個地方。";
	next;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "為了以如此優秀的方式控制這種機器，Elyumina必須在周圍。";
	next;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "我有可能處理這台機器，但是我無法抓住Elyumina。";
	next;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "如果您要照顧機器，其他搜索方將會照顧Elyumina。";
	next;
	cutin "162elena_02",2;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "叛亂小隊的協調很好，這項工作最適合我們。因此，將該任務留給我們。";
	next;
	cutin "162elena_01",2;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "我們將進入的地方將在9點鐘。我確認沒有人躲在那裡，我們阻止了出口。";
	next;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "準備進來時給我一個信號。我告訴小隊讓你進去。";
	next;
	if (select("發出信號:還沒有。") == 2) {
		mes "賽琳娜·沃爾科瓦斯特";
		mes "是的，面對的是危險的對手。很抱歉必須讓您獨自戰鬥。讓我知道你準備好時。";
		close3;
	}
	cutin "162elena_02",2;
	mes "賽琳娜·沃爾科瓦斯特";
	mes "好吧，提防！ I opened the portal so that you can go in. It's a one way entry, so make sure you're prepared!";
	close2;
	cutin "",255;
	viewpointmap 'map$,2,172,223,5,0xFFFFFF;
	if ('step == 2) {
		'step = 3;
		donpcevent instance_npcname("#171_cor_control") + "::OnStory03";
	}
	end;
}

1@cor,138,221,3	script	Elena Volkova#171_cmd_2	4_F_ELENA,{
	if ('mode) {
		if (isbegin_quest(16363) == 0) {
			cutin "162elena_01",2;
			mes "賽琳娜·沃爾科瓦斯特";
			mes "哦是的。這次你再次努力工作。這很艱難！我們必須使用金屬碎片才能停止多長時間？";
			next;
			cutin "ep171_elyumina02",0;
			mes "[氧化鋁]";
			mes "我不是說不稱它為金屬廢料嗎？你白痴！";
			next;
			cutin "162elena_01",2;
			mes "賽琳娜·沃爾科瓦斯特";
			mes "是的，起初這不是廢金屬。但是現在我們已經抓住了它，它是廢金屬。無論如何，您已經努力工作。你現在回去休息。";
			next;
			cutin "",255;
			if (select("回去吧:稍等一下。") == 2) {
				mes "賽琳娜·沃爾科瓦斯特";
				mes "好吧，讓我知道您何時準備出去。";
				close3;
			}
			cutin "162elena_02",2;
			mes "賽琳娜·沃爾科瓦斯特";
			mes "好工作。有趣的是，我們明天必須再做一次。明天見。";
			setquest 16363;
			getitem 25723,1;
			getitem 25669,5;
			close2;
			warp "sp_cor",111,125;
		}
		end;
	}
	if (isbegin_quest(16354) == 1 && checkquest(16376,HUNTING) == 2) {
		cutin "162elena_02",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "做得好，" +strcharinfo(0)+ ". The operation was a success!";
		mes "我也逃跑了，這很尷尬，因為您擊倒了那巨大的金屬碎片。我還捕捉了令人著迷的東西。";
		next;
		cutin "ep171_elyumina02",0;
		mes "[氧化鋁]";
		mes "廢金屬！這是傑作！我的好孩子！";
		mes "你！你...傷害我的孩子！你摧毀了我的孩子！";
		next;
		cutin "162elena_02",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "你只是叫這些廢料孩子嗎？";
		mes "好吧，您的孩子可能很好。然後，讓我們去為孩子的錯誤付出代價。";
		next;
		cutin "162elena_01",2;
		mes "賽琳娜·沃爾科瓦斯特";
		mes "你現在要做什麼" + strcharinfo(0) + "?";
		next;
		if (select("我馬上回去:我會在這兒待一會兒") == 2) {
			mes "賽琳娜·沃爾科瓦斯特";
			mes "好吧，讓我知道您何時準備出去。";
			close3;
		}
		mes "賽琳娜·沃爾科瓦斯特";
		mes "做得好。我們將在這裡清理問題，您可以休息一下。我要詢問她。休息一下，然後回到我身邊。";
		completequest 16354;
		erasequest 16376;
		setquest 16357;
		setquest 16377;
		close2;
		warp "sp_cor",176,169;
	}
	end;
}

1@cor,140,221,3	script	Elyumina#171_cor_end	4_EP17_ELYUMINA,{
	npctalk "Elyumina：你們……你們殺了它？ ！你宰了它！劊子手！";
	end;
}
1@cor,141,222,3	script	Rebellion#171_cmd_6	4_M_ILYA,{
	npctalk "幻象研究員艾柳米娜被抓獲了！";
	end;
}
1@cor,141,220,3	script	Rebellion#171_cmd_7	4_F_ANYA,{
	npctalk "我們成功活捉了她，行動結束了！";
	end;
}

1@cor,1,1,0	script	#171_cor_boss	HIDDEN_WARP_NPC,{
	end;

OnSummonStory:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
	'boss_gid = $@mobid[0];
	getunitdata 'boss_gid,.@boss_data;
	getunitdata $@mobid[0],.@boss_data;
	.@DAMAGE = (.@boss_data[UMOB_MAXHP]/10) * 9;
	.@HP = (.@boss_data[UMOB_MAXHP] - .@DAMAGE)/2;
	setunitdata $@mobid[0],UMOB_HP,.@HP;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	initnpctimer;
	end;

OnSummonDaily:
	.@event$ = instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	monster 'map$,137,221,"EL1_A17T",20340,1,.@event$;
	'boss_gid = $@mobid[0];
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	initnpctimer;
	end;

OnBossKill:
	stopnpctimer;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnClear";
	enablenpc instance_npcname("Elena Volkova#171_cmd_2");
	enablenpc instance_npcname("Elyumina#171_cor_end");
	enablenpc instance_npcname("Rebellion#171_cmd_6");
	enablenpc instance_npcname("Rebellion#171_cmd_7");
	end;

OnClear:
	for (.@i = 0; .@i < 11; .@i++) {
		disablenpc instance_npcname("Biological Battery#171_cor_" + .@i);
		disablenpc instance_npcname("Chemical Poison#171_cor_" + .@i);
	}
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnPoisonKill";
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBombKill";
	killmonster 'map$,instance_npcname(strnpcinfo(0)) + "::OnBossKill";
	end;

OnTimer30000:
	stopnpctimer;
	donpcevent instance_npcname(strnpcinfo(0)) + "::OnSummonSkill";
	end;

OnSummonSkill:
	if (unitexists('boss_gid)) {
		mapannounce 'map$,"Elyumina : It's starting to spread something. Prepare yourself!",bc_map,0xFFFF99;
		if (rand(2)) {
			.@npc$ = "Chemical Poison#171_cor_";
			.@type$ = "trap_0_";
		} else {
			.@npc$ = "Biological Battery#171_cor_";
			.@type$ = "trap_1_";
		}
		while(true) {
			.@id = rand(11);
			if (getd("'" + .@type$ + .@id))
				continue;
			if (unitexists('boss_gid)) {
				enablenpc instance_npcname(.@npc$ + .@id);
				setd("'" + .@type$ + .@id,1);
			}
			break;
		}
		initnpctimer;
	}
	end;
}

1@cor,151,221,3	script	Biological Battery#171_cor_0	1738,3,3,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_cor_",""));
	disablenpc();
	setd("'trap_1_" + .@id,0);
	getmapxy('map$,.@x,.@y,BL_NPC);
	monster 'map$,.@x,.@y,"",20345,1,instance_npcname("#171_cor_boss") + "::OnBombKill";
	initnpctimer;
	end;

OnTimer7000:
	if (!'rf_171_cor_0)
		enablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;

OnTimer12000:
	stopnpctimer;
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
		disablenpc instance_npcname("Reinforced Energy#171_cor_0");
	end;
}

1@cor,151,221,3	script	Chemical Poison#171_cor_0	2531,3,3,{
	end;

OnTouch:
	disablenpc();
	getmapxy('map$,.@x,.@y,BL_NPC);
	.@event$ = instance_npcname("#171_cor_boss") + "::OnPoisonKill";
	if (!mobcount('map$,.@event$))
		monster 'map$,.@x,.@y,"",20344,1,.@event$;
	end;
}

1@cor,137,220,3	script	Reinforced Energy#171_cor_0	4_ENERGY_WHITE,{
	if (!getd("'rf_" + strnpcinfo(2))) {
		setd("'rf_" + strnpcinfo(2),1);
		specialeffect2 EF_ENHANCE;
		specialeffect2 EF_LIGHTSPHERE_STAR;
		sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
		npctalk "力量，壓倒性的力量！";
		unittalk getcharid(3),strcharinfo(0) + " : My whole body is full of power!",bc_self;
		sleep 1500;
		disablenpc();
		setd("'rf_" + strnpcinfo(2),0);
	}
	end;
}

1@cor,145,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_1	1738,3,3
1@cor,129,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_2	1738,3,3
1@cor,138,206,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_3	1738,3,3
1@cor,136,213,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_4	1738,3,3
1@cor,138,228,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_5	1738,3,3
1@cor,138,235,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_6	1738,3,3
1@cor,102,220,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_7	1738,3,3
1@cor,108,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_8	1738,3,3
1@cor,115,219,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_9	1738,3,3
1@cor,122,221,3	duplicate(Biological Battery#171_cor_0)	Biological Battery#171_cor_10	1738,3,3

1@cor,145,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_1	2531,3,3
1@cor,129,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_2	2531,3,3
1@cor,138,206,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_3	2531,3,3
1@cor,136,213,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_4	2531,3,3
1@cor,138,228,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_5	2531,3,3
1@cor,138,235,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_6	2531,3,3
1@cor,102,220,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_7	2531,3,3
1@cor,108,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_8	2531,3,3
1@cor,115,219,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_9	2531,3,3
1@cor,122,221,3	duplicate(Chemical Poison#171_cor_0)	Chemical Poison#171_cor_10	2531,3,3

1@cor,161,221,0	script	#171_cor_warp_0	WARPNPC,2,2,{
	end;

OnActive:
	enablenpc();
	specialeffect EF_READYPORTAL2;
	end;

OnTouch:
	if (!'boss_summon) {
		'boss_summon = 1;
		donpcevent instance_npcname("#171_cor_boss") + "::OnSummonStory";
	}
	warp 'map$,151,220;
	end;
}

1@cor,161,221,0	script	#171_cor_warp_1	WARPNPC,2,2,{
	end;

OnActive:
	enablenpc();
	specialeffect EF_READYPORTAL2;
	end;

OnTouch:
	if (!'boss_summon) {
		'boss_summon = 1;
		donpcevent instance_npcname("#171_cor_boss") + "::OnSummonDaily";
	}
	warp 'map$,151,220;
	end;
}

1@cor,224,238,3	script	Elyumina#171_box_d0	4_EP17_ELYUMINA,{
	if ('cor == 4) {
		npctalk "Elyumina：你知道軍營在哪裡嗎？我在你的小地圖上顯示了它。我很快就會前往那裡。";
		end;
	}
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	.@var = getd("'box_" + .@id);
	switch (.@var) {
		case 0: .@talk$ = "Here, here! Then please, touch the box and it will activate."; break;
		case 1: .@talk$ = "Chatting during a battle, quite relaxed aren't you? Hm, that's a bit annoying."; break;
		case 2: .@talk$ = "That's a very good combat data... Well, go ahead now!";
	}
	npctalk "氧化鋁：" + .@talk$;
	end;
}

1@cor,218,172,5	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d1	4_EP17_ELYUMINA
1@cor,162,117,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d2	4_EP17_ELYUMINA
1@cor,140,82,3	duplicate(Elyumina#171_box_d0)	Elyumina#171_box_d3	4_EP17_ELYUMINA

1@cor,222,236,3	script	Box#171_box_d0	4_STEELBOX,{
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	if (!getd("'box_" + .@id)) {
		setd("'box_" + .@id,1);
		switch (.@id) {
			case 0: setarray .@xy,228,232,20341,216,229,20341,223,242,20341,226,239,20341,227,242,20341,229,230,20341,214,241,20343,224,235,20343,219,244,20343,227,230,20355,215,228,20355,221,231,20355,230,227,20355; break;
			case 1: setarray .@xy,224,164,20342,215,175,20342,220,174,20342,215,170,20342,216,164,20342,217,175,20342,226,164,20343,224,165,20343,215,172,20343,219,170,20356,217,176,20356,213,165,20356; break;
			case 2: setarray .@xy,158,119,20341,165,125,20341,160,119,20341,162,119,20341,158,119,20341,165,113,20341,149,117,20343,162,125,20343,149,125,20343,166,113,20355,167,115,20355,166,119,20355; break;
			case 3: setarray .@xy,143,72,20342,141,77,20342,146,84,20342,139,82,20342,138,75,20342,144,82,20342,140,70,20343,140,73,20343,142,72,20343,133,85,20356,140,82,20356,142,76,20356;
		}
		specialeffect EF_BAKU;
		for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
			monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
		initnpctimer;
	}
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
		setd("'box_" + .@id,2);
		'cor += 1;
		if (.@id == 0 || .@id == 2) {
			.@npc$ = "Biological Battery#";
			disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
		} else
			.@npc$ = "Chemical Poison#";
		switch (.@id) {
		case 0:
			viewpointmap 'map$,2,222,236,4,0xFFFFFF;
			.@npc$ = "Biological Battery#";
			break;
		case 1:
			viewpointmap 'map$,2,220,170,3,0xFFFFFF;
			.@npc$ = "Chemical Poison#";
			break;
		case 2:
			viewpointmap 'map$,2,160,119,2,0xFFFFFF;
			.@npc$ = "Biological Battery#";
			break;
		case 3:
			viewpointmap 'map$,2,140,79,1,0xFFFFFF;
			.@npc$ = "Chemical Poison#";
		}
		killmonster 'map$,instance_npcname(.@npc$ + "171_box_trap_" + .@id) + "::OnMobKill";
		disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
		if ('cor == 4)
			donpcevent instance_npcname("#171_cor_control") + "::OnDaily02";
	}
	end;

OnTimer1000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	if (.@id == 0 || .@id == 2)
		.@npc$ = "Biological Battery#";
	else
		.@npc$ = "Chemical Poison#";
	if (mobcount('map$,.@event$))
		enablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
	else
		stopnpctimer;
	end;

OnTimer11000:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_d",""));
	stopnpctimer;
	if (mobcount('map$,.@event$)) {
		if (.@id == 0 || .@id == 2)
			.@npc$ = "Biological Battery#";
		else
			.@npc$ = "Chemical Poison#";
		disablenpc instance_npcname(.@npc$ + "171_box_trap_" + .@id);
		if (unitexists(getnpcid(0,instance_npcname(.@npc$ + "171_box_trap_" + .@id))))
			initnpctimer;
	}
	end;
}

1@cor,222,235,3	script	Biological Battery#171_box_trap_0	1738,3,3,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	stopnpctimer instance_npcname("Box#171_box_d" + .@id);
	.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
	disablenpc();
	if (mobcount('map$,.@event$)) {
		.@event2$ = instance_npcname(strnpcinfo(0)) + "::OnMobKill";
		getmapxy(.@m$,.@x,.@y,BL_NPC);
		if (!mobcount('map$,.@event2$))
			monster 'map$,.@x,.@y,"--ja--",.@mob,1,.@event2$;
		initnpctimer;
	}
	end;

OnMobKill:
	end;

OnTimer7000:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	if (.@mob == 20345) {
		if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_trap_" + .@id)))) {
			disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
			enablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
		}
	}
	end;

OnTimer10000:
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	if (.@id == 0 || .@id == 2)
		.@mob = 20345;
	else
		.@mob = 20344;
	if (.@mob == 20344) {
		stopnpctimer;
		initnpctimer instance_npcname("Box#171_box_d" + .@id);
	}
	end;

OnTimer17000:
	stopnpctimer;
	.@id = atoi(replacestr(strnpcinfo(2),"171_box_trap_",""));
	.@event$ = instance_npcname("Box#171_box_d" + .@id) + "::OnMobKill";
	if (unitexists(getnpcid(0,instance_npcname("Reinforced Energy#171_cor_0"))))
		disablenpc instance_npcname("Reinforced Energy#171_trap_" + .@id);
	if (mobcount('map$,.@event$))
		initnpctimer instance_npcname("Box#171_box_d" + .@id);
	end;
}

1@cor,220,169,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_1	2531,3,3
1@cor,160,118,3	duplicate(Biological Battery#171_box_trap_0)	Biological Battery#171_box_trap_2	1738,3,3
1@cor,140,78,3	duplicate(Biological Battery#171_box_trap_0)	Chemical Poison#171_box_trap_3	2531,3,3

1@cor,225,232,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_0	4_ENERGY_WHITE
1@cor,163,115,3	duplicate(Reinforced Energy#171_cor_0)	Reinforced Energy#171_trap_2	4_ENERGY_WHITE

1@cor,220,170,3	duplicate(Box#171_box_d0)	Box#171_box_d1	4_WOODBOX
1@cor,160,119,3	duplicate(Box#171_box_d0)	Box#171_box_d2	4_STEELBOX
1@cor,140,79,3	duplicate(Box#171_box_d0)	Box#171_box_d3	4_WOODBOX

1@cor,159,218,0	script	#cor_barricade_0	4_ROPEPILE,{
	end;

OnTouch:
	npctalk "入口被堵住了！";
	end;
}

1@cor,159,220,3	duplicate(#cor_barricade_0)	#cor_barricade_1	4_ROPEPILE
1@cor,159,222,3	duplicate(#cor_barricade_0)	#cor_barricade_2	4_ROPEPILE
1@cor,159,224,3	duplicate(#cor_barricade_0)	#cor_barricade_3	4_ROPEPILE,2,2
1@cor,98,218,3	duplicate(#cor_barricade_0)	#cor_barricade_4	4_ROPEPILE
1@cor,98,220,3	duplicate(#cor_barricade_0)	#cor_barricade_5	4_ROPEPILE
1@cor,98,222,3	duplicate(#cor_barricade_0)	#cor_barricade_6	4_ROPEPILE
1@cor,98,224,3	duplicate(#cor_barricade_0)	#cor_barricade_7	4_ROPEPILE,2,2
1@cor,134,240,3	duplicate(#cor_barricade_0)	#cor_barricade_8	4_ROPEPILE
1@cor,136,240,3	duplicate(#cor_barricade_0)	#cor_barricade_9	4_ROPEPILE
1@cor,138,240,3	duplicate(#cor_barricade_0)	#cor_barricade_10	4_ROPEPILE
1@cor,140,240,3	duplicate(#cor_barricade_0)	#cor_barricade_11	4_ROPEPILE,2,2
