//===== rAthena Script ======================================= 
//= Hazy Forest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Cross through the Hazy Forest to reach the Bifrost.
//= Contains the "Wandering Guardian" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Instance system rewrite. [Euphy]
//============================================================ 

// Instance Creation :: md_mistwoods_enter
//============================================================
bif_fild01,158,340,5	script	Laphine Soldier#mist	447,{

	set .@party_id,getcharid(1);
	set .@md_name$,"Mistwood Maze";

	mes "[拉芬士兵]";
	mes "你要去森林嗎？";
	next;
	if (!.@party_id) {
		mes "[拉芬士兵]";
		mes "好吧，好吧，你不是太重了嗎？";
		mes "你為什麼不至少組織聚會？";
		close;
	}
	set .@playtime, checkquest(7211,PLAYTIME);
	if (is_party_leader() == true) {
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[拉芬士兵]";
			mes "...您確定自己沒有做太多嗎？";
			mes "我認為您最好休息一下。";
			mes "您只會嘗試做太多而疲憊不堪";
			next;
			mes "[拉芬士兵]";
			mes "我認為當局也不會允許您允許。";
			mes "...即使是Sapha也會被阻止進入森林。";
			next;
			select("你我們的語言說得很好，不是嗎？");
			mes "[拉芬士兵]";
			mes "...是嗎？";
			mes "實際上，我不是說你的語言。";
			mes "我說的是甘表示語言。";
			next;
			mes "[拉芬士兵]";
			mes "但這是雙色的奧秘之一。";
			mes "即使我們說不同的語言，我們也可以彼此了解。";
			next;
			mes "[拉芬士兵]";
			mes "能夠與所有人交談...";
			mes "也許那是雙佛羅斯特的力量。";
			close;
		} else if (.@playtime == 2) {
			mes "[拉芬士兵]";
			mes "太好了，太好了。";
			mes "現在森林不會拒絕你。";
			mes "你現在要做什麼？";
			erasequest 7211;
		} else {
			mes "[拉芬士兵]";
			mes "您的人類冒險家令人難以置信。";
			mes "您總是走進森林，沒有其他人敢於去。";
			next;
			mes "[拉芬士兵]";
			mes "ph。但是我必須向當局報告";
			mes "每個進入森林的人，以防萬一。";
			next;
			mes "[拉芬士兵]";
			mes "您將獲得許可";
			mes "註冊後。";
			mes "您要冒險進入朦朧的森林嗎？";
		}
		next;
		switch(select("冒險進入朦朧森林。 :放棄。")) {
		case 1:
			mes "[拉芬士兵]";
			mes "派對名稱"+getpartyname(.@party_id)+"...";
			mes "政黨領袖"+strcharinfo(0)+"...";
			if (instance_create(.@md_name$) < 0) {
				mes "唔...";
				next;
				mes "[拉芬士兵]";
				if (.@playtime == -1)
					mes "這在森林裡很危險。";
				else
					mes "森林中的氣氛有些緊張。";
				mes "...你為什麼今天不回去？";
				close;
			}
			mes "我知道了。我在這裡的報告中寫下了它們。";
			next;
			mes "[拉芬士兵]";
			mes "您很快就會獲得許可。";
			mes "現在去登錄隧道，這是進入朦朧森林的唯一途徑。";
			mes "你明白嗎？";
			next;
			mes "[拉芬士兵]";
			mes "我們不能違背森林，";
			mes "但是也許你可以。";
			mes "祝你好運！";
			close;
		case 2:
			mes "[拉芬士兵]";
			mes "好吧，您做出了正確的決定。";
			if (.@playtime == -1)
				mes "那對你有好處。";
			close;
		}
	} else {
		mes "[拉芬士兵]";
		mes "您的聚會負責人在哪裡？";
		mes "那裡很危險。";
		next;
		if (.@playtime == 2) {
			mes "[拉芬士兵]";
			mes "我看到你以前去過森林。";
			mes "好吧，至少森林不會拒絕你。";
			mes "然而...";
			erasequest 7211;
			next;
		}
		mes "[拉芬士兵]";
		mes "把自己放在我的鞋子，入口後衛，讓我認識你的聚會的代表。";
		mes "我必須與他/她討論一些事情。";
		close;
	}
}

bif_fild01,161,355,0	script	Log Tunnel	844,{
	mes "我通過日誌隧道看到森林。";
	mes "看來我可以通過原木隧道進入森林。";
	next;
	if(select("進入隧道。 :放棄。") == 2)
		close;
	switch(instance_enter("Mistwood Maze")) {
	case IE_OTHER:
		mes "[拉芬士兵]";
		mes "感覺不正確。看起來危險的事情正在發生，所以今天您最好回頭。";
		close;
	case IE_NOINSTANCE:
		mes "您試圖爬進原木，但是一些神秘的力量會用一陣風將您推向回去。";
		mes "看來您不能強迫進入森林。";
		close;
	case IE_NOMEMBER:
		mes "[拉芬士兵]";
		mes "嘿，看！";
		mes "你一個人去嗎？";
		mes "那是不可能的。太皮疹了。";
		mes "與一些朋友合作，一起去！";
		close;
	case IE_OK:
		if (checkquest(7211,PLAYTIME) == 2) erasequest 7211;
		if (checkquest(7211,PLAYTIME) == -1) setquest 7211;
		mapannounce "bif_fild01",getpartyname(getcharid(1))+" party's "+strcharinfo(0)+" member is entering the Mistwood Maze.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		//warp "1@mist",89,29;
		close;
	default:
		mes "戶外隧道以外的世界看起來黑暗而令人窒息。";
		mes "您的本能尖叫著這森林很危險。";
		close;
	}
}

// Instance Scripts :: md_mistwoods_in
//============================================================
1@mist,89,29,0	script	#Whisper_mist	844,1,1,{
	end;
OnInstanceInit:
	enablenpc instance_npcname("#Whisper_mist");
	end;
OnTouch_:
	disablenpc instance_npcname("#Whisper_mist");
	initnpctimer;
	end;
OnTimer10000:
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"Whisper: Who's this? Who's this!? The forest is open! Tom, Tomba, Tired Rem, Remi! Wake up!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer60000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: What are the forest keepers doing? What are the gardeners doing??",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer120000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: Don't go out, wander around forever, and play with us!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer180000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: Useless, useless. Wandering around and around, you'll meet your end!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
}

// callfunc "F_Mora_Mist",<warp to enable>,<success message>,<fail message>{,<end flag>}
function	script	F_Mora_Mist	{
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") == 0) {
		mes "顯然沒有人照顧它。";
		mes "似乎您可以砍伐花園樹。";
		next;
		if(select("砍倒花園裡的樹。 :放棄。") == 2)
			close;
		mes "你砍掉正在阻塞迷宮路徑的樹，"+((getarg(3,0))?"clearing the way out of the forest.":"so now you can continue.");
		mapannounce .@map$,getarg(1),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
		enablenpc instance_npcname(getarg(0));
		disablenpc();
		if (getarg(4,0))
			initnpctimer;
		close;
	} else
		mapannounce .@map$,((getarg(3,0))?getarg(2):getarg(2)+"'s Cry: Huh? Who's doing bad things to my tree?!"),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
}

1@mist,251,121,3	script	Tom's Garden	858,{ end; }
1@mist,247,123,0	script	Tom's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a25_a18-1",
		"Roaring Cry: Tom's bolt is broken! Watch out, Tomba! Watch out, Tomba!",
		"Tom";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),249,120,"Tom",2136,1,instance_npcname("Tom's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,226,95,3	script	Tomba's Garden	858,{ end; }
1@mist,225,98,0	script	Tomba's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a27-2_af1-2",
		"Roaring Cry: Tomba's Garden Tree is gone with Tomba. Remi, what are you going to do now?",
		"Tomba";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),200,64,"Tomba",2136,1,instance_npcname("Tomba's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,157,183,3	script	Remi's Garden	858,{ end; }
1@mist,159,184,0	script	Remi's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a15-2_a4-1",
		"Roaring Cry: Remi, Remi's Garden Tree was chopped down- Rem, it's your turn next. Poor Rem!",
		"Remi";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),154,184,"Remi the Tired",2137,1,instance_npcname("Remi's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,58,39,3	script	Tired Rem's Garden	858,{ end; }
1@mist,61,39,0	script	Tired Rem's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"a4-2_a11",
		"Rem's Desperate Cry: Argh... Rem will sleep. Rem will sleep now, and won't wake up forever!",
		"Rem",
		0,1;
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),101,107,"Rem the Gardener",2136,1,instance_npcname("Tired Rem's Garden Tree")+"::OnMyMobDead";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@mist"),"Whisper of the Forest: So Tom's, Tomba's, Remi's, and Rem's garden trees were all chopped down?",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@mist"),"Whisper of the Forest: Trouble, trouble, we're in big trouble.",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"Loud Whisper: They've come to the second deepest forest. Gardeners of the deep forest, watch out!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,227,179,3	script	Ron's Garden	858,{ end; }
1@mist,230,179,0	script	Ron's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b4-2_b2",
		"Ron the Gardener's Cry: I'm getting off work! Don't look for Ron, the second gardener, any more!!",
		"Ron";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),227,178,"Ron the Gardener",2134,1,instance_npcname("Ron's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,288,226,3	script	Rover's Garden	858,{ end; }
1@mist,285,225,0	script	Rover's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b7_bf1-2",
		"Roaring Whisper: Ron's, and Rover's trees were chopped down as well. Who is forcing his way into the forest? Who is it?",
		"Rover";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),304,237,"Rover the Strutter",2134,1,instance_npcname("Rover's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,159,318,3	script	Mona's Garden	858,{ end; }
1@mist,161,316,0	script	Mona's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b31_b16",
		"Clamoring Whisper: Mona the Seedseeker's garden tree is gone! Mona is gone too! Namon will be okay, because he is brave, right? Namon, are you okay?",
		"Mona";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),239,253,"Mona the Seedseeker",2133,1,instance_npcname("Mona's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,205,300,3	script	Namon's Garden	858,{ end; }
1@mist,204,299,0	script	Namon's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b27-3_bN-1",
		"Clamoring Whisper: Brave Namon met his fate not so bravely... *giggle*",
		"Namon";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),89,173,"Brave Namon",2134,1,instance_npcname("Namon's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,219,232,3	script	Sad Neoron's Garden	858,{ end; }
1@mist,221,236,0	script	Sad Neoron's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b6-1_b33",
		"Lamenting Whisper: Sad Neoron has become cheerful! Spyder the Superior Spider, what are you going to do? What are you going to do now?",
		"Neoron";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),143,265,"Sad Neoron",2137,1,instance_npcname("Sad Neoron's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,206,202,3	script	Spyder's Garden	858,{ end; }
1@mist,206,200,0	script	Spyder's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"b5_b14",
		"Depressed Whisper: Now it's all over with the second deepest forest. Gardeners are dying out-",
		"Spyder",
		0,1;
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),209,200,"Spyder the Eight-Legged",2132,1,instance_npcname("Spyder's Garden Tree")+"::OnMyMobDead";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: The path is about to open. The tight bolts have been removed!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer9000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: A forest opened, another opened, and the other is going to open as well. Tito and Pumba, they are heading your way!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer13000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: Where you get after wandering around, around and around, everybody you've met while wandering around, around and around,",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer17000:
	mapannounce instance_mapname("1@mist"),"Roaring Whisper: all the gardeners will be waiting for you, with their clippers in their hands-!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,96,288,3	script	Tito's Garden	858,{ end; }
1@mist,95,287,0	script	Tito's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c1_c13-1",
		"Tito's Cry: *sob* *sob* Tito's gone now! Tito's done for! Boo hoo!!",
		"Tito";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),264,291,"Tito the Flipper",2133,1,instance_npcname("Tito's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,326,325,3	script	Pumba's Garden	858,{ end; }
1@mist,324,325,0	script	Pumba's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c13-3_c4",
		"Roaring Whisper: Tito the Hard Worker was defeated! Stop playing and go to work, Tete! Start working!",
		"Pumba";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),309,165,"Diligent Pumba",2134,1,instance_npcname("Pumba's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,278,345,3	script	Tete's Garden	858,{ end; }
1@mist,280,344,0	script	Tete's Garden Tree	527,{
	callfunc "F_Mora_Mist",
		"c19_c4",
		"Whisper: They are strong, strong indeed. What are we going to do now? Are there any gardeners left? Are there any?",
		"Tete";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),277,343,"Carefree Tete",2136,1,instance_npcname("Tete's Garden Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,345,186,0	script	The Gardeners' Tree	527,{
	callfunc "F_Mora_Mist",
		"#to_bif02",
		"Roaring Whisper: The forest will remember you! It will remember you, the one who broke the clippers of all the gardeners!",
		"Gardeners: We're not dead, yet!!!!",1;
	end;
OnInstanceInit:
	set .@map$, instance_mapname("1@mist");
	monster .@map$,318,137,"Baby Tom",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,321,137,"Tomba the Baby",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,324,137,"Exhausted Remi",2137,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,319,135,"Rem the Exhausted",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,323,135,"Ron the Ex-Gardener",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,320,133,"Rover the Strutter",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,323,133,"Mona the Seedpicker",2133,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,317,132,"Timid Namon",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,326,132,"Indifferent Neoron",2137,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,317,129,"Spyder the Seven-Legged",2132,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,320,129,"Tito the Flapper",2133,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,324,129,"Lazy Pumba",2134,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	monster .@map$,327,129,"Careless Tete",2136,1,instance_npcname("The Gardeners' Tree")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,73,290,0	script	Mysterious Flower#1	844,{
	specialeffect EF_LEVEL99_4;
	disablenpc();
	getitem 12561,1; //Mysterious_Seed
	end;
}
1@mist,69,295,0	duplicate(Mysterious Flower#1)	Mysterious Flower#2	844
1@mist,62,291,0	duplicate(Mysterious Flower#1)	Mysterious Flower#3	844
1@mist,54,293,0	duplicate(Mysterious Flower#1)	Mysterious Flower#4	844
1@mist,48,286,0	duplicate(Mysterious Flower#1)	Mysterious Flower#5	844
1@mist,54,277,0	duplicate(Mysterious Flower#1)	Mysterious Flower#6	844
1@mist,52,270,0	duplicate(Mysterious Flower#1)	Mysterious Flower#7	844
1@mist,62,268,0	duplicate(Mysterious Flower#1)	Mysterious Flower#8	844
1@mist,72,271,0	duplicate(Mysterious Flower#1)	Mysterious Flower#9	844
1@mist,72,276,0	duplicate(Mysterious Flower#1)	Mysterious Flower#10	844
1@mist,118,194,0	duplicate(Mysterious Flower#1)	Mysterious Flower#11	844
1@mist,122,204,0	duplicate(Mysterious Flower#1)	Mysterious Flower#12	844
1@mist,137,209,0	duplicate(Mysterious Flower#1)	Mysterious Flower#13	844
1@mist,144,203,0	duplicate(Mysterious Flower#1)	Mysterious Flower#14	844
1@mist,146,186,0	duplicate(Mysterious Flower#1)	Mysterious Flower#15	844
1@mist,142,182,0	duplicate(Mysterious Flower#1)	Mysterious Flower#16	844
1@mist,132,179,0	duplicate(Mysterious Flower#1)	Mysterious Flower#17	844
1@mist,123,184,0	duplicate(Mysterious Flower#1)	Mysterious Flower#18	844
1@mist,194,118,0	duplicate(Mysterious Flower#1)	Mysterious Flower#19	844
1@mist,188,109,0	duplicate(Mysterious Flower#1)	Mysterious Flower#20	844
1@mist,193,94,0	duplicate(Mysterious Flower#1)	Mysterious Flower#21	844
1@mist,205,92,0	duplicate(Mysterious Flower#1)	Mysterious Flower#22	844
1@mist,213,96,0	duplicate(Mysterious Flower#1)	Mysterious Flower#23	844
1@mist,216,103,0	duplicate(Mysterious Flower#1)	Mysterious Flower#24	844
1@mist,212,116,0	duplicate(Mysterious Flower#1)	Mysterious Flower#25	844
1@mist,207,120,0	duplicate(Mysterious Flower#1)	Mysterious Flower#26	844
1@mist,169,310,0	duplicate(Mysterious Flower#1)	Mysterious Flower#27	844
1@mist,172,296,0	duplicate(Mysterious Flower#1)	Mysterious Flower#28	844
1@mist,191,301,0	duplicate(Mysterious Flower#1)	Mysterious Flower#30	844
1@mist,195,301,0	duplicate(Mysterious Flower#1)	Mysterious Flower#31	844
1@mist,192,316,0	duplicate(Mysterious Flower#1)	Mysterious Flower#32	844
1@mist,187,320,0	duplicate(Mysterious Flower#1)	Mysterious Flower#33	844
1@mist,174,318,0	duplicate(Mysterious Flower#1)	Mysterious Flower#34	844
1@mist,308,136,0	duplicate(Mysterious Flower#1)	Mysterious Flower#35	844
1@mist,314,122,0	duplicate(Mysterious Flower#1)	Mysterious Flower#36	844
1@mist,321,118,0	duplicate(Mysterious Flower#1)	Mysterious Flower#37	844
1@mist,331,123,0	duplicate(Mysterious Flower#1)	Mysterious Flower#38	844
1@mist,335,126,0	duplicate(Mysterious Flower#1)	Mysterious Flower#39	844
1@mist,332,141,0	duplicate(Mysterious Flower#1)	Mysterious Flower#40	844
1@mist,328,144,0	duplicate(Mysterious Flower#1)	Mysterious Flower#41	844
1@mist,314,144,0	duplicate(Mysterious Flower#1)	Mysterious Flower#42	844
1@mist,238,224,0	duplicate(Mysterious Flower#1)	Mysterious Flower#43	844
1@mist,244,231,0	duplicate(Mysterious Flower#1)	Mysterious Flower#44	844
1@mist,257,232,0	duplicate(Mysterious Flower#1)	Mysterious Flower#45	844
1@mist,262,229,0	duplicate(Mysterious Flower#1)	Mysterious Flower#46	844
1@mist,265,216,0	duplicate(Mysterious Flower#1)	Mysterious Flower#47	844
1@mist,260,214,0	duplicate(Mysterious Flower#1)	Mysterious Flower#48	844
1@mist,252,206,0	duplicate(Mysterious Flower#1)	Mysterious Flower#49	844
1@mist,244,211,0	duplicate(Mysterious Flower#1)	Mysterious Flower#50	844

// Wandering Guardian Quest
//============================================================

// ----------------- Part of md_mistwoods_in -----------------
1@mist,2,2,0	script	#Dragon Herder	844,{
	end;
OnInstanceInit:
	switch(rand(1,6)) {
		case 1: setarray .@c[0],77,240; break;
		case 2: setarray .@c[0],147,261; break;
		case 3: setarray .@c[0],182,263; break;
		case 4: setarray .@c[0],194,350; break;
		case 5: setarray .@c[0],268,183; break;
		case 6: setarray .@c[0],65,278; break;
	}
	monster instance_mapname("1@mist"),.@c[0],.@c[1],"Wandering Purple Dragon",2131,1,instance_npcname("#Dragon Herder")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname("#Dragon Herder")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("Collapsed Girl#mist")+"::OnEnable";
		donpcevent instance_npcname("Mysterious Young Man#mis")+"::OnDisable";
		donpcevent instance_npcname("Loki#mist")+"::OnEnable";
		mapannounce .@map$,"The Dragon's Cry: Am I being punished... for abandoning the way... of the guardian...?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		disablenpc instance_npcname("#Dragon Herder");
		initnpctimer;
	}
	end;
OnTimer3000:
	stopnpctimer;
	mapannounce instance_mapname("1@mist"),"Voice in your head: ...Will you... save... me... please......?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	end;
}

1@mist,97,30,3	script	Mysterious Young Man#mis	512,{
	if (BaseLevel < 98) {
		mes "[神秘的年輕人]";
		mes "嗯？您打算在這里通過嗎？";
		mes ".....恐怕這超出了您的能力，但我不會干預您的事務。";
		mes "如果您生存，我們會再次見面。";
		close;
	}
	if (ep14_1_mistwoods == 0) {
		if (ep13_3_secret > 23) {
			mes "[神秘的年輕人]";
			mes "你...我記得在營地見你。";
			mes "您在AHAT上提供的信息很有用。";
			mes "謝謝！";
			next;
			mes "[神秘的年輕人]";
			mes "但是，您打算在這里通過嗎？";
			mes "跨雙弗羅斯特有什麼好處？";
			next;
		} else {
			mes "[神秘的年輕人]";
			mes "您打算在這里通過嗎？";
			mes "如果您只是好奇，我會建議您反對。";
			mes "它只會帶來您過早的死亡...";
			next;
		}
		mes "[神秘的年輕人]";
		mes "如果您決心通過這裡，";
		mes "我會尋求您的幫助。";
		next;
		select("你是誰？");
		mes "[洛基]";
		mes "Loki ...來自刺客行會。";
		mes "那是我的名字。";
		mes "你的呢？";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "...";
		mes "..."+strcharinfo(0)+" sir.";
		next;
		mes "[洛基]";
		mes "我懂了。"+strcharinfo(0)+".";
		mes "我再次問你。";
		mes "您打算穿過這個森林嗎？";
		next;
		switch(select("是的，我願意。 :不，我不這樣做。")) {
		case 1:
			mes "[洛基]";
			mes "那我會問你一個問題。";
			mes "您有沒有見過警衛...一個有紫色頭髮的女孩？";
			next;
			switch(select("不，我沒有:是的，我可能有……")) {
			case 1:
				mes "[洛基]";
				mes "所以你沒有。";
				mes "你在說實話。";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等待。";
				mes "（Loki更近。）";
				mes "... 不。";
				mes "你在說實話。";
				next;
				break;
			}
			mes "[洛基]";
			mes "現在我們必須在這裡說再見。";
			mes "當然，如果您在這森林中生存。";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		case 2:
			mes "[洛基]";
			mes "我明白。";
			mes "如果您還沒有準備好，請退後一步。";
			mes "這個森林不喜歡陌生人。";
			set ep14_1_mistwoods,1;
			close;
		}
	} else if (ep14_1_mistwoods == 1) {
		mes "[洛基]";
		mes "我告訴你 - 如果你不想死，退後一步。";
		mes "穿過森林並不容易。";
		mes "這可能會損失您的生命。";
		next;
		switch(select("好的:你想要什麼？")) {
		case 1:
			mes "- 退後一步，";
			mes "Loki輕微點頭，然後移開視線。 -";
			close;
		case 2:
			mes "[洛基]";
			mes "我在找人。";
			mes "您有沒有見過警衛...一個有紫色頭髮的女孩？";
			next;
			switch(select("不，我沒有:是的，我可能有……")) {
			case 1:
				mes "[洛基]";
				mes "所以你沒有。";
				mes "你在說實話。";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等待。";
				mes "（Loki更近。）";
				mes "... 不。";
				mes "你在說實話。";
				next;
				break;
			}
			mes "[洛基]";
			mes "現在我們必須在這裡說再見。";
			mes "希望我們會在莫拉再次見面。";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		}
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "希望我們會在莫拉再次見面。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "所以我們再次見面。";
		mes "你習慣了這個地方嗎？";
		mes "來到莫拉的旅館。";
		mes "我有東西要給你。";
		close;
	} else {
		mes "[洛基]";
		mes "這些天，我們彼此之間看到了很多。";
		mes "看來您對這個地方完全滿意。";
		mes "您最好不要為自己感到驕傲。";
		close;
	}
	end;
OnEnable:
	enablenpc instance_npcname("Mysterious Young Man#mis");
	end;
OnDisable:
	disablenpc instance_npcname("Mysterious Young Man#mis");
	end;
}

1@mist,183,304,3	script	Loki#mist	512,{
	if (ep14_1_mistwoods < 2) {
		mes "[洛基]";
		mes "森林裡的冒險家。";
		mes "…………";
		mes "您可以通過這森林去莫拉。";
		close;
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "是你照顧這個不守規矩的女孩嗎？";
		mes "我想我必須感謝你。";
		mes "你讓她平靜了下來。";
		next;
		select("這是怎麼回事？ :這個女孩……？");
		mes "[洛基]";
		mes "您也許可以改變外觀，但是您無法更改獨特的天生光線。";
		mes "一種足夠強烈的氣味，可以燃燒鼻子。";
		next;
		mes "[洛基]";
		mes "這是Yggdrasil的守護者Nydhogg，";
		mes "我正在尋找的人。";
		mes "現在她不會再瘋狂地徘徊了。";
		next;
		select("你現在要做什麼？");
		mes "[洛基]";
		mes "我會等她醒來，離開這個森林。";
		mes "我可以自己管理。";
		mes "我在莫拉見。";
		set ep14_1_mistwoods,3;
		erasequest 7212;
		setquest 7213;
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "我可以自己管理她。";
		mes "如果您有話要說，請在我們離開森林之後說出來。";
		close;
	} else {
		mes "[洛基]";
		mes "我不想和你一起去。";
		mes "如果您有話要說，請在我們離開森林之後說出來。";
		close;
	}
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Loki#mist");
	end;
OnEnable:
	enablenpc instance_npcname("Loki#mist");
	end;
}

1@mist,181,303,3	script	Collapsed Girl#mist	511,{
	mes "您會看到一個神秘頭髮的女孩在地面上塌陷。";
	mes "當您接近她時，您會聞到花的氣味，使您頭暈目眩。";
	close;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Collapsed Girl#mist");
	end;
OnEnable:
	enablenpc instance_npcname("Collapsed Girl#mist");
	end;
}

// --------------- Part of md_mistwoods_enter ----------------
mora,46,152,5	script	Flower Smelling Lady	510,{
	if (ep14_1_mistwoods < 3) {
		mes "[花氣]";
		mes "我需要休息。";
		mes "我太累了，無法和你說話。";
		mes "對不起。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[衛報Nydhogg]";
		mes "我一直在等你。";
		mes "我仍然感到困惑，但我知道我做錯了什麼。";
		next;
		mes "[衛報Nydhogg]";
		mes "如果不適合您，我仍然會在深淵的黑暗中徘徊。";
		mes "我從心底發表感謝。";
		next;
		mes "[衛報Nydhogg]";
		mes "我為自己感到羞恥，因為我的孤獨感在我身邊而來的甜蜜但空虛的話語。";
		mes "...";
		next;
		mes "[衛報Nydhogg]";
		mes "我不應該再成為監護人了。";
		mes "龍瘋狂的瘋狂不再是監護人。";
		next;
		mes "[洛基]";
		mes "Yggdrasil不這麼認為。";
		mes "尼迪，別忘了你的職責。";
		next;
		mes "[衛報Nydhogg]";
		mes "你是...？";
		mes "我懂了...";
		mes "我以為我被拋棄了...";
		next;
		mes "監護人慢慢地移開了思想的深刻視線。";
		set ep14_1_mistwoods,4;
		close2;
	} else if (ep14_1_mistwoods == 4) {
		mes "- 他似乎深深地思考。 -";
		close;
	} else {
		mes "[衛報Nydhogg]";
		mes "我還沒有得到所有的力量。";
		mes "對不起，我無法保護你。";
		next;
		mes "[衛報Nydhogg]";
		mes "洛基說...";
		mes "我不會再因為孤獨而哭泣，";
		mes "我將永遠有像你這樣的新朋友...";
		next;
		mes "[衛報Nydhogg]";
		mes "...我失去了一切，但我現在很高興。";
		close;
	}
}

mora,48,152,3	script	Sharp Eyed Man	512,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 您有太多的項目可以執行此任務。 -";
		close;
	}
	if (ep14_1_mistwoods < 3) {
		mes "他不會看著你。就像他一點都不感興趣。";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "所以你穿過森林。";
		mes "看來Niddy有話要說。";
		close;
	} else if (ep14_1_mistwoods == 4) {
		mes "[洛基]";
		mes "Nydhogg是世界樹的監護人之一。";
		mes "但是，當他屈服於摩洛哥的誘惑時，他放棄了擔任監護人的職責。";
		next;
		mes "[洛基]";
		mes "一些傻瓜進行了一種儀式，以打破摩洛哥的印章。";
		mes "但是儀式不起作用。摩洛哥的僕人未能打破密封。";
		next;
		mes "[洛基]";
		mes "但是密封件是如此薄弱，以至於可以隨時打開。";
		mes "世界樹的意志是伊格德拉西爾（Yggdrasil）阻止了印章的破裂。";
		next;
		mes "[衛報Nydhogg]";
		mes "這是正確的。世界樹的力量與整個世界有關。";
		mes "當監護人和Yggdrasil之間的紐帶破裂時，Yggdrasil變得不穩定。";
		next;
		mes "[衛報Nydhogg]";
		mes "這就是為什麼所有這些災難發生在Yggdrasil的原因。";
		mes "隨著世界樹變得不穩定，它對印章的影響變得越來越薄弱。";
		next;
		mes "[洛基]";
		mes "Morocc擺脫了印章。";
		mes "儘管他似乎使用了大部分力量只是為了打破印章...";
		next;
		mes "[衛報Nydhogg]";
		mes "這都是我的錯。";
		mes "我無法擺脫摩洛哥的誘惑。";
		mes "太久了...太久了";
		next;
		mes "[衛報Nydhogg]";
		mes "...我想擺脫數千年的孤獨。";
		mes "那時我不在我的頭腦中。";
		mes "與Yggdrasil打破了我的聯繫。";
		next;
		mes "[衛報Nydhogg]";
		mes "我失去了對自己的控制權，做了我不應該做的事情。";
		mes "我的另一面變成了陰影和魯ck的暴君。";
		next;
		mes "[衛報Nydhogg]";
		mes "面對世界，我意識到...";
		mes "我感到非常失落的感覺……這比我幾千年來忍受的孤獨感要痛苦得多。";
		next;
		mes "[衛報Nydhogg]";
		mes "這就是我一個人的方式。";
		mes "我感到非常失落的感覺……這比我幾千年來忍受的孤獨感要痛苦得多。";
		next;
		mes "[衛報Nydhogg]";
		mes "因此，我成為了一個罪人，被遺忘了世界。";
		next;
		mes "[洛基]";
		mes "還為時不晚。";
		mes "你仍然是監護人。";
		mes "您還有事情要做。";
		next;
		mes "[衛報Nydhogg]";
		mes "...哦是的。已經很晚了，但是我必須履行作為監護人的職責。";
		mes "現在我知道。我會用自己的手摧毀摩洛哥。";
		mes "我將履行作為監護人的職責。";
		next;
		switch(select("你並不孤單:這些都是非常重要的職責。")) {
		case 1:
			mes "[衛報Nydhogg]";
			mes "你說得對。";
			mes "這可能就是為什麼我沒有被遺棄 - 意識到我並不孤單。";
			mes "你是一個善良的靈魂。";
			next;
			break;
		case 2:
			mes "[衛報Nydhogg]";
			mes "但這是我的贖罪。";
			mes "我很高興。";
			mes "我不必再與孤獨作鬥爭了。";
			next;
			break;
		}
		mes "[衛報Nydhogg]";
		mes strcharinfo(0)+"...";
		mes "感謝您聽我的長話。";
		next;
		mes "[衛報Nydhogg]";
		mes "這就是我一直和我在一起的東西。";
		mes "我敢肯定，有一天它會保護您。";
		next;
		mes "[洛基]";
		mes "（將他的圍巾從脖子上移開，然後將圍巾交給您。）";
		next;
		mes "[衛報Nydhogg]";
		mes "願母親Yggdrasil的祝福與您同在...";
		set ep14_1_mistwoods,10;
		completequest 7213;
		getitem 2568,1; //Muffler_Of_Roki
		getitem 2858,1; //Pendant_Of_Guardian
		getexp 400000,400000;
		close;
	} else {
		mes "[洛基]";
		mes "我會在這裡待一會兒。";
		mes "我們可能會再次見面。";
		next;
		mes "[洛基]";
		mes "摩洛哥揭示他卑鄙的天性的那一天...";
		mes "我會等他。";
		close;
	}
}

// Monster Spawns :: md_mist_mob
//============================================================
1@mist,1,1,0	script	#Mist Mobs Respawn1	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Miming",2137,15,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Miming",2137,.@num_mobs,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
}

1@mist,1,2,0	script	#Mist Mobs Respawn2	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Pom Spider",2132,35,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Pom Spider",2132,.@num_mobs,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
}

1@mist,1,3,0	script	#Mist Mobs Respawn3	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Angra Mantis",2133,35,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Angra Mantis",2133,.@num_mobs,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
}

1@mist,1,4,0	script	#Mist Mobs Respawn4	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Parus",2134,35,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Parus",2134,.@num_mobs,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
}

1@mist,1,5,0	script	#Mist Mobs Respawn5	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"Little Fatum",2136,15,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"Little Fatum",2136,.@num_mobs,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
}

1@mist,1,6,0	script	#Fragments of Memory	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Fragments of Memory");
	monster instance_mapname("1@mist"),0,0,"Guardian's Fragments of Memory",2138,15;
	end;
}

// Warp Portals :: md_mist_in_link
//============================================================
bif_fild01,38,374,0	script	Mysterious Flower#ep14_1	844,{
	mes "這是一朵巨大的花，具有神秘的能量。";
	if (countitem(12561)) {
		mes "它對您與您一起的神秘花種子做出了反應。";
		next;
		switch(select("觀察反應:停止反應。")) {
		case 1:
			mes "花開始猛烈搖晃";
			mes "然後向前搖擺，好像試圖將種子盡可能地發送到遠處！ ！ ！";
			close2;
			delitem 12561,1; //Mysterious_Seed
			warp "bif_fild02",160,230;
			end;
		case 2:
			mes "當您緊緊抓住神秘的花種子時";
			mes "花猛烈搖動，然後再次變得平靜。";
			close;
		}
	}
	mes "看起來它有一些特殊的用途，但是您不確定是什麼。";
	close;
}

1@mist,104,23,0	warp	#to_bif01	1,1,bif_fild01,160,352

1@mist,109,70,0	script	a1_a2	45,1,1,{
	end;
OnTouch_:
	if (checkquest(7211,PLAYTIME) == -1)
		setquest 7211;
	warp instance_mapname("1@mist"),116,40;
	end;
}

1@mist,113,40,0	warp	a2_a7	1,1,1@mist,90,81
1@mist,93,81,0	warp	a7_a14	1,1,1@mist,108,152
1@mist,111,154,0	warp	a14_a24	1,1,1@mist,236,108
1@mist,236,110,0	warp	a24_a1	1,1,1@mist,106,69
1@mist,161,67,0	warp	a18-1_a22	1,1,1@mist,159,85
1@mist,180,116,0	warp	a23_a18-1	1,1,1@mist,164,66
1@mist,180,63,0	warp	a18-2_a9	1,1,1@mist,141,90
1@mist,116,84,0	warp	a8_a26	1,1,1@mist,222,133
1@mist,220,133,0	warp	a26_a27-1	1,1,1@mist,200,64
1@mist,197,64,0	warp	a27-1_a23	1,1,1@mist,177,116
1@mist,146,194,0	warp	af1-2_a27-2	1,1,1@mist,225,96
1@mist,133,195,0	warp	af1-1_af2-1	1,1,1@mist,202,110
1@mist,202,107,0	warp	af2-1_af1-1	1,1,1@mist,134,189
1@mist,217,110,0	warp	af2-2_a20-1	1,1,1@mist,240,56
1@mist,240,54,0	warp	a20-1_af2-2	1,1,1@mist,214,110
1@mist,234,67,0	warp	a20-2_a21	1,1,1@mist,239,80
1@mist,242,77,0	warp	a21_a15-1	1,1,1@mist,153,137
1@mist,156,139,0	warp	a15-1_a3	1,1,1@mist,123,55
1@mist,124,51,0	warp	a3_a20-1	1,1,1@mist,240,56
1@mist,48,41,0	warp	a4-1_a17	1,1,1@mist,174,165
1@mist,171,165,0	warp	a17_a16	1,1,1@mist,168,143
1@mist,169,141,0	warp	a16_a10	1,1,1@mist,101,107
1@mist,99,107,0	warp	a10_a4-2	1,1,1@mist,57,38
1@mist,65,113,0	warp	a11_a28	1,1,1@mist,218,151
1@mist,221,151,0	warp	a28_a13	1,1,1@mist,117,126
1@mist,114,126,0	warp	a13_a5	1,1,1@mist,36,62
1@mist,61,88,0	warp	a6_a19	1,1,1@mist,189,139
1@mist,186,140,0	warp	a19_a12	1,1,1@mist,98,126
1@mist,101,126,0	warp	a12_a29-1	1,1,1@mist,238,151
1@mist,235,152,0	warp	a29-1_af1-2	1,1,1@mist,144,194
1@mist,254,151,0	warp	a29-2E_b1	1,1,1@mist,271,164
1@mist,275,211,0	warp	b1_b13	1,1,1@mist,277,112
1@mist,301,141,0	warp	b14_b24	1,1,1@mist,99,232
1@mist,99,230,0	warp	b24_b25	1,1,1@mist,103,255
1@mist,102,253,0	warp	b25_b4-1	1,1,1@mist,215,177
1@mist,212,177,0	warp	b4-1_b1	1,1,1@mist,276,208
1@mist,289,167,0	warp	b2_b19	1,1,1@mist,152,219
1@mist,149,219,0	warp	b19_b20	1,1,1@mist,119,227
1@mist,115,227,0	warp	b20_b23	1,1,1@mist,230,353
1@mist,233,353,0	warp	b23_b7	1,1,1@mist,289,225
1@mist,76,273,0	warp	bf1-2_b7	1,1,1@mist,289,225
1@mist,61,282,0	warp	bf1-1_bf2-1	1,1,1@mist,256,220
1@mist,252,220,0	warp	bf2-1_bf1-1	1,1,1@mist,65,282
1@mist,267,213,0	warp	bf2-2_b28	1,1,1@mist,214,268
1@mist,212,265,0	warp	b28_b12	1,1,1@mist,288,266
1@mist,285,266,0	warp	b12_b21	1,1,1@mist,143,234
1@mist,143,231,0	warp	b21_b29	1,1,1@mist,222,281
1@mist,223,279,0	warp	b29_b31	1,1,1@mist,158,317
1@mist,134,287,0	warp	b30_b3	1,1,1@mist,293,195
1@mist,295,197,0	warp	b3_b10	1,1,1@mist,239,253
1@mist,236,253,0	warp	b10_b12	1,1,1@mist,288,266
1@mist,111,203,0	warp	b16_b34	1,1,1@mist,202,333
1@mist,199,333,0	warp	b34_b27-1	1,1,1@mist,203,251
1@mist,206,249,0	warp	b27-1_b22	1,1,1@mist,168,239
1@mist,165,239,0	warp	b22_b15	1,1,1@mist,89,173
1@mist,177,265,0	warp	b27-2_a14	1,1,1@mist,108,152
1@mist,166,314,0	warp	bN-1_b27-3	1,1,1@mist,206,297
1@mist,197,299,0	warp	bN-2_b17	1,1,1@mist,167,200
1@mist,165,200,0	warp	b17_bN-2	1,1,1@mist,193,299
1@mist,86,173,0	warp	b15_b32	1,1,1@mist,218,313
1@mist,215,313,0	warp	b32_b28	1,1,1@mist,214,268
1@mist,230,324,0	warp	b33_b6-1	1,1,1@mist,221,233
1@mist,230,230,0	warp	b6-2_b11	1,1,1@mist,263,261
1@mist,263,258,0	warp	b11_b9	1,1,1@mist,272,246
1@mist,269,246,0	warp	b9_b26	1,1,1@mist,143,265
1@mist,140,265,0	warp	b26_b5	1,1,1@mist,209,200
1@mist,195,206,0	warp	b18_b35	1,1,1@mist,169,341
1@mist,166,341,0	warp	b35_b36	1,1,1@mist,198,354
1@mist,201,354,0	warp	b36_b37-1	1,1,1@mist,83,224
1@mist,87,224,0	warp	b37-1_b35	1,1,1@mist,169,341
1@mist,84,273,0	warp	b37-2_c1	1,1,1@mist,98,287
1@mist,111,298,0	warp	c2_c3	1,1,1@mist,82,307
1@mist,79,307,0	warp	c3_c7	1,1,1@mist,284,89
1@mist,281,89,0	warp	c7_c12	1,1,1@mist,264,291
1@mist,261,291,0	warp	c12_c2	1,1,1@mist,115,298
1@mist,326,275,0	warp	c13-1_b13	1,1,1@mist,277,112
1@mist,294,293,0	warp	c13-2_c5	1,1,1@mist,78,330
1@mist,81,330,0	warp	c5_c17	1,1,1@mist,308,330
1@mist,308,327,0	warp	c17_c10	1,1,1@mist,309,165
1@mist,306,165,0	warp	c10_c13-2	1,1,1@mist,298,293
1@mist,46,316,0	warp	c4_c6	1,1,1@mist,113,328
1@mist,95,328,0	warp	c6_c9	1,1,1@mist,342,157
1@mist,339,157,0	warp	c9_c16	1,1,1@mist,343,328
1@mist,343,325,0	warp	c16_c8-2	1,1,1@mist,346,120
1@mist,344,123,0	warp	c8-2_c19	1,1,1@mist,277,343
1@mist,256,313,0	warp	c18_c15	1,1,1@mist,343,307
1@mist,342,305,0	warp	c15_cKEY	1,1,1@mist,332,121
1@mist,335,120,0	warp	cKEY_c18	1,1,1@mist,259,313
1@mist,317,89,0	warp	c8-1_c14	1,1,1@mist,339,284
1@mist,339,281,0	warp	c14_c11-1	1,1,1@mist,332,172
1@mist,329,171,0	warp	c11-1_c8-2	1,1,1@mist,346,120
1@mist,301,237,0	warp	b8_b2	1,1,1@mist,289,170

// Disable Garden Tree warps.
// Custom, but saves processing and lines.
1@mist,1,1,0	script	#mist_warp_init	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("a25_a18-1");
	disablenpc instance_npcname("a27-2_af1-2");
	disablenpc instance_npcname("a15-2_a4-1");
	disablenpc instance_npcname("a4-2_a11");
	disablenpc instance_npcname("b4-2_b2");
	disablenpc instance_npcname("b7_bf1-2");
	disablenpc instance_npcname("b31_b16");
	disablenpc instance_npcname("b27-3_bN-1");
	disablenpc instance_npcname("b6-1_b33");
	disablenpc instance_npcname("b5_b14");
	disablenpc instance_npcname("c1_c13-1");
	disablenpc instance_npcname("c13-3_c4");
	disablenpc instance_npcname("c19_c4");
	disablenpc instance_npcname("#to_bif02");
	disablenpc instance_npcname("#mist_warp_init");
	end;
}
1@mist,247,123,0	warp	a25_a18-1	1,1,1@mist,164,66	//Tom
1@mist,225,98,0	warp	a27-2_af1-2	1,1,1@mist,144,194	//Tomba
1@mist,159,184,0	warp	a15-2_a4-1	1,1,1@mist,49,37	//Remi
1@mist,61,39,0	warp	a4-2_a11	1,1,1@mist,68,113	//Rem
1@mist,230,179,0	warp	b4-2_b2	1,1,1@mist,289,170	//Ron
1@mist,285,225,0	warp	b7_bf1-2	1,1,1@mist,72,274	//Rover
1@mist,161,316,0	warp	b31_b16	1,1,1@mist,108,203	//Mona
1@mist,204,299,0	warp	b27-3_bN-1	1,1,1@mist,169,314	//Namon
1@mist,221,236,0	warp	b6-1_b33	1,1,1@mist,233,324	//Neoron
1@mist,206,200,0	warp	b5_b14	1,1,1@mist,298,142	//Spyder
1@mist,95,287,0	warp	c1_c13-1	1,1,1@mist,323,276	//Tito
1@mist,324,325,0	warp	c13-3_c4	1,1,1@mist,49,316	//Pumba
1@mist,280,344,0	warp	c19_c4	1,1,1@mist,49,316	//Tete
1@mist,345,186,0	warp	#to_bif02	1,1,bif_fild02,151,121	//The Gardeners' Tree
