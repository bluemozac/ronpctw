//===== rAthena Script =======================================
//= Sealed OS
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Episode 17.1 - Sealed OS
//===== Changelog: ===========================================
//= 1.0 Initial release [crazyarashi]
//= 1.1 Optimizations and cleanup [Everade]
//============================================================

1@os_b,1,1,0	script	#171_sealedos_control	-1,{
	end;

OnInstanceInit:
	'map$ = instance_mapname("1@os_b");
	'seal_os = 'lock = 0;
	hideonnpc instance_npcname("Dien#171_os_0");
	hideonnpc instance_npcname("Cotnes#171_os_0");
	hideonnpc instance_npcname("Dien#171_os_1");
	hideonnpc instance_npcname("Ridsh#171_os_1");
	hideonnpc instance_npcname("Cotnes#171_os_1");
	disablenpc instance_npcname("Suspicious Device#171_os");
	disablenpc instance_npcname("Thick Wire#171_os_0");
	disablenpc instance_npcname("Thick Wire#171_os_1");
	disablenpc instance_npcname("Thick Wire#171_os_2");
	disablenpc instance_npcname("Dien#171_os_2");
	disablenpc instance_npcname("Cotnes#171_os_2");
	disablenpc instance_npcname("Dien#171_os_3");
	hideonnpc instance_npcname("Est#171_os_0");
	hideonnpc instance_npcname("Cotnes#171_os_3");
	hideonnpc instance_npcname("Dien#171_os_4");
	disablenpc instance_npcname("Ridsh#171_os_2");
	disablenpc instance_npcname("#171_sos_dummy_6");
	disablenpc instance_npcname("#171_sos_dummy_7");
	disablenpc instance_npcname("Ridsh#171_os_3");
	disablenpc instance_npcname("Est#171_os_1");
	disablenpc instance_npcname("Cotnes#171_os_4");
	disablenpc instance_npcname("Dien#171_os_5");
	end;

OnEvent01:
	disablenpc instance_npcname("Ridsh#171_os_0");
	disablenpc instance_npcname("Dien#171_os_0");
	disablenpc instance_npcname("Cotnes#171_os_0");
	disablenpc instance_npcname("#171_os_warp_1");
	disablenpc instance_npcname("#171_os_warp_2");
	hideoffnpc instance_npcname("Dien#171_os_1");
	hideoffnpc instance_npcname("Ridsh#171_os_1");
	hideoffnpc instance_npcname("Cotnes#171_os_1");
	donpcevent instance_npcname("#171_sealedos_mob") + "::OnSummon00";
	end;

OnEvent02:
	for (.@i = 0; .@i < 6; .@i++)
		disablenpc instance_npcname("#171_sos_dummy_" +.@i);
	disablenpc instance_npcname("Suspicious Device#171_os");
	disablenpc instance_npcname("Dien#171_os_1");
	disablenpc instance_npcname("Ridsh#171_os_1");
	disablenpc instance_npcname("Cotnes#171_os_1");
	disablenpc instance_npcname("#171_os_warp_0");
	enablenpc instance_npcname("Thick Wire#171_os_0");
	enablenpc instance_npcname("Thick Wire#171_os_1");
	enablenpc instance_npcname("Thick Wire#171_os_2");
	enablenpc instance_npcname("Dien#171_os_2");
	end;

OnEvent03:
	disablenpc instance_npcname("Dien#171_os_2");
	enablenpc instance_npcname("Cotnes#171_os_2");
	enablenpc instance_npcname("Dien#171_os_3");
	enablenpc instance_npcname("Ridsh#171_os_2");
	end;

OnEvent04:
	'unlocked = 1;
	disablenpc instance_npcname("#171_os_warp_3");
	enablenpc instance_npcname("Ridsh#171_os_2");
	enablenpc instance_npcname("#171_sos_dummy_6");
	enablenpc instance_npcname("#171_sos_dummy_7");
	end;

OnEvent05:
	disablenpc instance_npcname("Ridsh#171_os_2");
	disablenpc instance_npcname("Est#171_os_0");
	disablenpc instance_npcname("Cotnes#171_os_3");
	disablenpc instance_npcname("Dien#171_os_4");
	disablenpc instance_npcname("#171_os_warp_5");
	enablenpc instance_npcname("Ridsh#171_os_3");
	enablenpc instance_npcname("Est#171_os_1");
	enablenpc instance_npcname("Cotnes#171_os_4");
	enablenpc instance_npcname("Dien#171_os_5");
	end;
}

1@os_b,188,45,5	script	Ridsh#171_os_0	4_F_LEEDSH,{
	if (!is_party_leader())
		end;
	.@dien$ =  instance_npcname("Dien#171_os_0");
	.@cot$ = instance_npcname("Cotnes#171_os_0");
	cutin "ep162_rds01",2;
	mes "[裡什]";
	mes "哦？";
	mes "你在這裡做什麼？";
	npctalk "裡什：哦？你在這裡做什麼。";
	next;
	cutin "ep162_rds05",2;
	mes "[裡什]";
	mes "我們陷入困境~~";
	mes "我們不能有人知道我們在這裡~~";
	npctalk "Ridsh：我們有麻煩了~~我們不能讓任何人知道我們在這裡~~";
	next;
	hideoffnpc .@dien$;
	hideoffnpc .@cot$;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "冒險家也在這裡！";
	mes "你為什麼害怕！";
	npctalk "Dien：冒險家也來了！你為什麼害怕！",.@dien$;
	next;
	mes "[迪恩]";
	mes "你好" + strcharinfo(0) + ".";
	sleep2 2000;
	cutin "ep162_dn01",2;
	mes "我們來這裡做點什麼...告訴年輕人太麻煩了。";
	npctalk "Dien：我們來這裡是為了做點什麼……告訴年輕人太麻煩了。",.@dien$;
	next;
	mes "[迪恩]";
	mes "如果您保留了一個秘密，就可以看到我們...";
	npctalk "Dien：如果你能保守秘密，知道你看到了我們……",.@dien$;
	sleep2 1000;
	specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#171_os_warp_1");
	sleep2 600;
	specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#171_os_warp_1");
	sleep2 600;
	specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#171_os_warp_2");
	next;
	cutin "ep162_rds04",2;
	mes "[裡什]";
	mes "姐姐~~";
	mes "您在說什麼，我看不到冒險家！";
	npctalk "Ridsh：姐姐~~你在說什麼啊，我沒看到冒險家啊！";
	next;
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "哦~";
	mes "是的？";
	mes "我也看不到冒險家~";
	npctalk "科特內斯：噢~是嗎？我也沒有看到冒險家~",.@cot$;
	next;
	mes "[科內斯]";
	mes "那現在呢？";
	mes "我們已經在裡面了。";
	npctalk "科特內斯：那現在怎麼辦？我們已經進去了。",.@cot$;
	next;
	cutin "ep162_rds05",2;
	mes "[裡什]";
	mes "它無法幫助~~";
	mes "我們必須和冒險家一起去。";
	npctalk "里德什：沒辦法啦~~我們必須和冒險家一起去。";
	next;
	cutin "ep162_dn01",2;
	mes "[迪恩]";
	mes "這很危險，所以請和我們一起去。";
	npctalk "Dien：這裡很危險，所以請跟我們走吧。",.@dien$;
	close2;
	cutin "",255;
	if ('seal_os == 0) {
		'seal_os = 1;
		donpcevent instance_npcname("#171_sealedos_control") + "::OnEvent01";
	}
	end;
}

1@os_b,212,41,0	script	Suspicious Device#171_os	4_ENERGY_BLUE,{
	mes "- 有一根管道穿過地板。 -";
	next;
	if (select("壓碎它:放下它。") == 2) {
		mes "- 什麼都沒發生。 -";
		close;
	}
	mes "- 當管道折斷時，機器停止了奇怪的噪音。 -";
	specialeffect EF_BLASTMINEBOMB;
	close2;
	mapannounce 'map$,"Ridsh : Alright! Let's move up along the railroad tracks!",bc_map,0xFF0000;
	if ('seal_os == 1) {
		'seal_os = 2;
		donpcevent instance_npcname("#171_sealedos_control") + "::OnEvent02";
	}
	end;
}

1@os_b,1,1,0	script	#171_sealedos_mob	HIDDEN_WARP_NPC,{
	end;

OnSummon00:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	npctalk "科特內斯：在這裡~~。",instance_npcname("Dien#171_os_1");
	sleep 1000;
	npctalk "Dien：天啊……真是太驚喜了！",instance_npcname("Dien#171_os_1");
	sleep 1000;
	npctalk "Dien：我們會照顧這一面的。請照顧那裡噁心的人！",instance_npcname("Dien#171_os_1");
	monster 'map$,205,46,"Dolor",20361,1,.@event$;
	monster 'map$,200,45,"Twin Caput",20365,1,.@event$;
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$)) {
		disablenpc();
		enablenpc instance_npcname("Suspicious Device#171_os");
		mapannounce 'map$,"Ridsh : Turn off the device at the bottom, while we deal with the enemies.",bc_map,0xFF0000;
	}
	end;
}

1@os_b,112,108,5	script	Dien#171_os_2	4_F_DIENE,{
	cutin "ep162_dn01",2;
	mes "[迪恩]";
	mes "哦，我！那件事在前面是什麼？";
	close3;
}

1@os_b,115,112,3	script	Dangerous-Looking Device#s_os_0	1_SHADOW_NPC,3,3,{
	end;

OnTouch:
	mes "[迪恩]";
	mes "哦，我！我認為這是精確的設備，將其放置在廢棄的礦山中。";
	next;
	mes "[迪恩]";
	mes "如果您不在另一側破壞機器，我們將無法通過它！";
	next;
	mes "[迪恩]";
	mes "現在穿過這是危險的，所以讓我們轉過身去設備所在的地方！";
	close2;
	warp 'map$,115,104;
	sleep2 2000;
	npctalk "Dien : If you don't break the machine on the other side, we will not be able pass it!",instance_npcname("Dien#171_os_2");
	end;
}

1@os_b,115,126,3	duplicate(Dangerous-Looking Device#s_os_0)	Dangerous-Looking Device#s_os_1	1_SHADOW_NPC,3,3
1@os_b,115,144,3	duplicate(Dangerous-Looking Device#s_os_0)	Dangerous-Looking Device#s_os_2	1_SHADOW_NPC,3,3

1@os_b,137,107,0	script	Thick Wire#171_os_0	4_ENERGY_BLUE,{
	.@id = atoi(replacestr(strnpcinfo(2),"171_os_",""));
	'lock += 1;
	disablenpc();
	disablenpc instance_npcname("Dangerous-Looking Device#s_os_" + .@id);
	if ('lock < 3)
		mapannounce 'map$,"Ridsh : The device hasn't turned off yet. Please take a look above for me.",bc_map,0xFF0000;
	else {
		mapannounce 'map$,"Ridsh : Great! The road is open. Let's move along the railroad tracks!",bc_map,0xFF0000;
		if ('seal_os == 2) {
			'seal_os = 3;
			donpcevent instance_npcname("#171_sealedos_control") + "::OnEvent03";
		}
	}
	end;
}

1@os_b,137,123,0	duplicate(Thick Wire#171_os_0)	Thick Wire#171_os_1	4_ENERGY_BLUE
1@os_b,137,139,0	duplicate(Thick Wire#171_os_0)	Thick Wire#171_os_2	4_ENERGY_BLUE

1@os_b,122,212,5	script	Dien#171_os_3	4_F_DIENE,{
OnTalk:
	cutin "ep162_dn01",2;
	mes "[迪恩]";
	mes "哦！";
	mes "如果您向右走，我認為您可以返回鐵路，而不必摔斷門。";
	npctalk "迪恩：哦！如果你向右走，我想你可以沿著鐵路回去，而不必打破門";
	if (!'unlocked)
		donpcevent instance_npcname("#171_sealedos_control") + "::OnEvent04";
	close3;
}

1@os_b,114,217,0	script	Cotnes#171_os_2	4_F_COATNEIS,{
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "哦，這大門太難了嗎？";
	mes "打破它需要花費一些時間~";
	npctalk "Cotnes：哦，這門是不是太難了？打破它需要一些時間。";
	next;
	doevent instance_npcname("Dien#171_os_3") + "::OnTalk";
	close;
}

1@os_b,146,221,7	script	Ridsh#171_os_2	4_F_LEEDSH,{
	if ('seal_os != 3)
		end;
	cutin "ep162_rds04",2;
	mes "[裡什]";
	mes "arg！";
	npctalk "裡什：啊啊！";
	next;
	.@est$ = instance_npcname("Est#171_os_0");
	hideoffnpc .@est$;
	sleep2 500;
	specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#171_sos_dummy_6");
	sleep2 500;
	disablenpc instance_npcname("#171_sos_dummy_6");
	specialeffect EF_BLASTMINEBOMB,AREA,instance_npcname("#171_sos_dummy_7");
	cutin "ep162_est01",2;
	mes "[東方]";
	mes "哦...";
	mes "我很著急，你在這裡做什麼？";
	npctalk "Est : I'm in a hurry, what are you doing here?",.@est$;
	disablenpc instance_npcname("#171_sos_dummy_7");
	next;
	cutin "ep162_est02",2;
	mes "[東方]";
	mes "請回去優雅地享用茶。";
	npctalk "Est：請回去優雅地享用茶吧。",.@est$;
	next;
	cutin "ep162_rds02",2;
	mes "[裡什]";
	mes "嗯！";
	mes "你是怎麼偷偷在這裡的？";
	npctalk "里德什：哈！你到底是怎麼偷偷溜進來的？";
	next;
	cutin "ep162_est02",2;
	mes "[東方]";
	mes "這只是監視常規。";
	mes "如果您閉上眼睛一秒鐘，您可能會遇到事故。";
	npctalk "吃：這只是一個監視程序。",.@est$;
	sleep2 2000;
	npctalk "Est : If you close your eyes for a second, you may run into an accident.",.@est$;
	next;
	cutin "ep162_rds02",2;
	mes "[裡什]";
	mes "是什麼讓您認為我們需要您的幫助？";
	npctalk "Ridsh：是什麼讓你認為我們需要你的幫助？";
	next;
	cutin "ep162_est02",2;
	mes "[東方]";
	mes "你現在不是那樣嗎？";
	mes "我認為我沒有聽說錯。我了嗎？";
	next;
	disablenpc instance_npcname("Cotnes#171_os_2");
	.@cot$ = instance_npcname("Cotnes#171_os_3");
	hideoffnpc .@cot$;
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "你們兩個真的~";
	mes "請停止戰鬥。";
	npctalk "Cotnes：你們兩個真是的~請別再打架了。",.@cot$;
	next;
	disablenpc instance_npcname("Dien#171_os_3");
	.@dien$ = instance_npcname("Dien#171_os_4");
	hideoffnpc .@dien$;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "你總是這樣打架？";
	npctalk "Dien：你怎麼總是這樣打架？",.@dien$;
	next;
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "我對此感到非常興奮~我變老了，我已經厭倦了戰鬥。";
	npctalk "Cotnes：我對此感到非常興奮~我已經老了，我厭倦了戰鬥。",.@cot$;
	next;
	cutin "ep162_dn03",1;
	mes "[迪恩]";
	mes "是這樣嗎？";
	npctalk "迪恩：是這樣嗎？",.@dien$;
	next;
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "是的~";
	mes "我厭倦了戰鬥，讓我們回去上班。";
	mes "這條路現在開放。";
	npctalk "Cotnes : Yeah~ I'm tired of fighting, let's go back to work. The road is open now.",.@cot$;
	close2;
	cutin "",255;
	if ('seal_os == 3) {
		'seal_os = 4;
		donpcevent instance_npcname("#171_sealedos_control") + "::OnEvent05";
	}
	end;
}

1@os_b,259,360,5	script	Dien#171_os_5	4_F_DIENE,{
	if ('seal_os != 4)
		end;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "最後..";
	mes "如果你越過這扇門...";
	next;
	cutin "ep162_est01",2;
	mes "[東方]";
	mes "讓我們現在回去，然後他們聚集更多的部隊。";
	next;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "打擾一下。";
	next;
	cutin "ep162_est01",2;
	mes "[東方]";
	mes "？";
	next;
	cutin "ep162_rds01",2;
	mes "[裡什]";
	mes "為什麼？";
	next;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "哦，我的，哦，我的！";
	mes "我們該怎麼辦！";
	next;
	cutin "ep162_rds04",2;
	mes "[裡什]";
	mes "為什麼，這是怎麼回事？";
	next;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "我想我已經離開了。";
	next;
	cutin "ep162_rds01",2;
	mes "[裡什]";
	mes "離開什麼？";
	next;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "我的保護魅力！";
	mes "如果我沒有它，我會感到緊張。";
	next;
	cutin "ep162_rds04",2;
	mes "[裡什]";
	mes "到目前為止，您一直沒有嗎？";
	next;
	cutin "ep162_dn03",2;
	mes "[迪恩]";
	mes "...";
	next;
	cutin "ep162_rds01",2;
	mes "[裡什]";
	mes "啊...對不起..";
	mes "好吧。";
	next;
	mes "[裡什]";
	mes "當我們打開最後一扇門時，您介意回到 ^ff0000clana nemieri ^000000嗎？";
	next;
	mes "[裡什]";
	mes "無論如何，我都必須聯繫幾個人，直到獲得保護魅力後，迪恩才會去任何地方。";
	next;
	mes "[裡什]";
	mes "姐姐，告訴我在哪裡。";
	next;
	cutin "ep162_dn02",2;
	mes "[迪恩]";
	mes "哦！";
	mes "非常感謝您";
	next;
	mes "[迪恩]";
	mes "我很難把它拿回來，因為它已經很遠了...";
	mes "請幫我一個忙。";
	next;
	cutin "ep162_dn01",2;
	mes "[迪恩]";
	mes "^ff0000go回到我的房間，在花瓶附近的窗戶左側尋找一個箱子。";
	next;
	mes "[迪恩]";
	mes "因為我們在這裡，應該很容易獲得。";
	next;
	cutin "ep162_rds05",2;
	mes "[裡什]";
	mes "哦！";
	mes "我們不應該告訴他們我們在哪裡嗎？";
	if (isbegin_quest(11600) == 1) {
		completequest 11600;
		setquest 11601;
		getitem 25669,25;
		getexp 600000,600000;
	}
	close2;
	warp "ein_fild03",258,267;
	end;

OnInstanceInit:
	questinfo(QTYPE_QUEST,QMARK_YELLOW,"isbegin_quest(11600) == 1");
	end;
}

1@os_b,260,363,5	script	Cotnes#171_os_4	4_F_COATNEIS,{
	cutin "ep162_ctn01",2;
	mes "[科內斯]";
	mes "有時候，像這樣快速散步是很好的~";
	close3;
}

1@os_b,262,362,4	script	Est#171_os_1	4_F_ESTLOVELOY,{
	cutin "ep162_est01",2;
	mes "[東方]";
	mes "我渴望探索這里以外的區域。";
	close3;
}

1@os_b,261,358,3	script	Ridsh#171_os_3	4_F_LEEDSH,{
	cutin "ep162_rds01",2;
	mes "[裡什]";
	mes "我累了！";
	mes "我想回家。";
	close3;
}

1@os_b,187,52,0	script	#171_os_warp_0	WARPNPC,4,4,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_os_warp_",""));
	switch (.@id) {
	case 0:
	case 1:
	case 2:
		warp 'map$,188,41;
		break;
	case 3:
	case 4:
		if (!'unlocked) {
			cutin "ep162_dn01",2;
			mes "[迪恩]";
			mes "最好走到門上。";
			npctalk "Dien：最好繞過門。",instance_npcname("Dien#171_os_4"),bc_map;
			close2;
			cutin "",255;
		}
		warp 'map$,114,204;
		break;
	case 5:
		warp 'map$,142,220;
	}
	end;
}

1@os_b,204,266,0	script	#171_os_trap_0	1_SHADOW_NPC,4,4,{
	end;

OnTouch:
	.@id = atoi(replacestr(strnpcinfo(2),"171_os_trap_",""));
	if (getd("'" + strnpcinfo(2)))
		end;
	switch (.@id) {
		case 0: setarray .@xy,209,264,20363,209,268,20355; break;
		case 1: setarray .@xy,234,269,20357,236,266,20357; break;
		case 2: setarray .@xy,250,268,20363,252,266,20355; break;
		case 3: setarray .@xy,263,285,20361,259,286,20365; break;
		case 4: setarray .@xy,262,303,20355,258,306,20355; break;
		case 5: setarray .@xy,262,318,20363,259,320,20361; break;
		case 6: setarray .@xy,258,336,20365,263,338,20355;
	}
	setd("'" + strnpcinfo(2),1);
	for (.@i = 0; .@i < getarraysize(.@xy); .@i += 3)
		monster 'map$,.@xy[.@i],.@xy[.@i+1],"--ja--",.@xy[.@i+2],1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	end;

OnMobKill:
	.@event$ = instance_npcname(strnpcinfo(0))+"::OnMobKill";
	if (!mobcount('map$,.@event$))
		disablenpc();
	end;
}

1@os_b,148,217,3	duplicate(dummy_npc)	Est#171_os_0	4_F_ESTLOVELOY
1@os_b,142,225,6	duplicate(dummy_npc)	Cotnes#171_os_3	4_F_COATNEIS
1@os_b,143,222,5	duplicate(dummy_npc)	Dien#171_os_4	4_F_DIENE

1@os_b,229,267,5	duplicate(#171_os_trap_0)	#171_os_trap_1	1_SHADOW_NPC,4,4
1@os_b,244,267,5	duplicate(#171_os_trap_0)	#171_os_trap_2	1_SHADOW_NPC,4,4
1@os_b,261,281,5	duplicate(#171_os_trap_0)	#171_os_trap_3	1_SHADOW_NPC,4,4
1@os_b,261,299,5	duplicate(#171_os_trap_0)	#171_os_trap_4	1_SHADOW_NPC,4,4
1@os_b,261,314,5	duplicate(#171_os_trap_0)	#171_os_trap_5	1_SHADOW_NPC,4,4
1@os_b,261,332,5	duplicate(#171_os_trap_0)	#171_os_trap_6	1_SHADOW_NPC,4,4

1@os_b,191,46,0	duplicate(#171_os_warp_0)	#171_os_warp_1	WARPNPC,3,3
1@os_b,192,49,0	duplicate(#171_os_warp_0)	#171_os_warp_2	WARPNPC,3,3
1@os_b,118,209,0	duplicate(#171_os_warp_0)	#171_os_warp_3	WARPNPC,3,3
1@os_b,119,215,0	duplicate(#171_os_warp_0)	#171_os_warp_4	WARPNPC,3,3
1@os_b,147,224,0	duplicate(#171_os_warp_0)	#171_os_warp_5	WARPNPC,3,3

1@os_b,190,44,3	duplicate(dummy_npc)	Dien#171_os_0	4_F_DIENE
1@os_b,190,47,6	duplicate(dummy_npc)	Cotnes#171_os_0	4_F_COATNEIS
1@os_b,199,51,7	duplicate(dummy_npc)	Dien#171_os_1	4_F_DIENE
1@os_b,201,49,3	duplicate(dummy_npc)	Cotnes#171_os_1	4_F_COATNEIS
1@os_b,203,51,0	duplicate(dummy_npc)	Ridsh#171_os_1	4_F_LEEDSH
1@os_b,199,56,5	duplicate(dummy_npc)	#171_sos_dummy_0	20355
1@os_b,202,56,3	duplicate(dummy_npc)	#171_sos_dummy_1	20357
1@os_b,198,53,3	duplicate(dummy_npc)	#171_sos_dummy_2	20355
1@os_b,200,54,4	duplicate(dummy_npc)	#171_sos_dummy_3	20361
1@os_b,203,54,5	duplicate(dummy_npc)	#171_sos_dummy_4	20357
1@os_b,201,52,3	duplicate(dummy_npc)	#171_sos_dummy_5	20363
1@os_b,144,221,5	duplicate(dummy_npc)	#171_sos_dummy_6	20363
1@os_b,148,221,3	duplicate(dummy_npc)	#171_sos_dummy_7	20365
1@os_b,200,276,5	duplicate(dummy_npc)	#171_sos_dummy_8	20361
1@os_b,207,275,3	duplicate(dummy_npc)	#171_sos_dummy_9	20363
1@os_b,205,258,3	duplicate(dummy_npc)	#171_sos_dummy_10	20365
1@os_b,213,257,2	duplicate(dummy_npc)	#171_sos_dummy_11	20357
1@os_b,231,274,5	duplicate(dummy_npc)	#171_sos_dummy_12	20361
1@os_b,234,276,5	duplicate(dummy_npc)	#171_sos_dummy_13	20365
1@os_b,244,275,3	duplicate(dummy_npc)	#171_sos_dummy_14	20361
1@os_b,242,276,3	duplicate(dummy_npc)	#171_sos_dummy_15	20365
1@os_b,253,286,5	duplicate(dummy_npc)	#171_sos_dummy_16	20363
1@os_b,265,284,3	duplicate(dummy_npc)	#171_sos_dummy_17	20355
1@os_b,252,302,5	duplicate(dummy_npc)	#171_sos_dummy_18	20363
1@os_b,251,300,5	duplicate(dummy_npc)	#171_sos_dummy_19	20361
1@os_b,61,263,3	duplicate(dummy_npc)	#171_sos_dummy_20	20365
1@os_b,264,267,3	duplicate(dummy_npc)	#171_sos_dummy_21	20361
1@os_b,263,262,3	duplicate(dummy_npc)	#171_sos_dummy_22	20363
1@os_b,192,276,5	duplicate(dummy_npc)	#171_sos_dummy_23	20365
1@os_b,194,250,5	duplicate(dummy_npc)	#171_sos_dummy_24	20355
1@os_b,196,251,3	duplicate(dummy_npc)	#171_sos_dummy_25	20363
1@os_b,197,250,3	duplicate(dummy_npc)	#171_sos_dummy_26	20357
1@os_b,124,230,5	duplicate(dummy_npc)	#171_sos_dummy_27	20365
1@os_b,126,232,5	duplicate(dummy_npc)	#171_sos_dummy_28	20365
1@os_b,128,231,5	duplicate(dummy_npc)	#171_sos_dummy_29	20361
1@os_b,134,230,3	duplicate(dummy_npc)	#171_sos_dummy_30	20363
