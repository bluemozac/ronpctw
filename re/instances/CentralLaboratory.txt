//===== rAthena Script =======================================
//= Central Laboratory
//===== Description: =========================================
//= [Official Conversion]
//= Central Laboratory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Updated to match official script. [Capuche]
//============================================================

verus01,153,155,5	script	Temporary Dimension-mov#pa0829_01	PORTAL,{
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[臨時尺寸移動]";
		mes "您沒有伴侶加入您嗎？即使獨自旅行，組織您的聚會也是如此。";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		switch( instance_enter("Central Laboratory") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
			mes "[臨時尺寸移動]";
			mes "維度移動的段落尚未激活。";
			close;
		case IE_OTHER:
			mes "[臨時尺寸-MOV]";
			mes "未知錯誤。";
			close;
		case IE_OK:
			mapannounce "verus01", "" + strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering Central Laboratory.",bc_map,"0x00FF99";
			setquest 12347;// Trace of Laboratory Access
			end;
		}
		end;
	case 0:
	case 1:
		mes "[臨時尺寸移動]";
		mes "最後一個事件的剩餘痕跡。拒絕訪問。";
		close;
	case 2:
		mes "^0000 fffuse dimension-Mover似乎是可能的。 ^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

verus01,149,155,5	script	Civilization Explorer#pa0829_01	4_LGTSCIENCE,{
	if (BaseLevel < 135) {
		mes "[文明探險家]";
		mes "好吧...我不知道您是否可以派上用場，似乎仍然存在差距！";
		next;
		mes "[文明探險家]";
		mes "當您變得更強壯時，請回到我身邊。您應該幾乎是135級！";
		close;
	}
	mes "^ff0000taming怪物會阻礙這個地牢中的進步。請注意。 ^000000";
	next;
	mes "[文明探險家]";
	switch( isbegin_quest(12346) ) {
	case 0:
		mes "關於該地區的古代文明有很多有趣的事情。";
		next;
		mes "[文明探險家]";
		mes "觀察土壤地層估計為500至600年！";
		mes "但是這裡的許多挖掘物體已經超過了我們當前的技術。";
		next;
		mes "[文明探險家]";
		mes "在恢復的記錄中也發現了許多未定義的術語。";
		next;
		select("什麼樣的未知術語？");
		mes "[文明探險家]";
		mes "^0000ffmother計算機^000000或，^0000ff lsi芯片^000000或其他。 ^0000FFCHICKEN^000000？";
		next;
		mes "[文明探險家]";
		mes "MM是對的。肯定有雞肉。我不知道哪種技術可能是...";
		next;
		mes "[文明探險家]";
		mes "它在似乎在許多技術痕跡中“紮根”的人們的記錄中被引用。";
		next;
		if (select("這不是我感興趣的話題。", "有趣的。有結果了嗎？" ) == 1) {
			mes "[文明探險家]";
			mes "不幸。";
			close;
		}
		mes "[文明探險家]";
		mes "我的目的是研究與相關時間相關的技術的使用。即使我到目前為止發現的東西並不重要。";
		next;
		mes "[文明探險家]";
		mes "如果我有自己的方式，我會用尺寸搬運工親自看，但是工作太多了。此外，我不是冒險家...";
		next;
		mes "[文明探險家]";
		mes "有機會，您有興趣去過去嗎？";
		next;
		mes "[文明探險家]";
		mes "如果您找到一個有趣的對象，請向我展示，甚至更好。";
		next;
		if (select("不感興趣。", "好的。" ) == 1) {
			mes "[文明探險家]";
			mes "不幸。";
			close;
		}
		mes "[文明探險家]";
		mes "你是冒險家。告訴我你決定做什麼。";
		if (isbegin_quest(12346) == 0)
			setquest 12346;// Strange Ancient Science
		break;
	case 1:
		mes "我該如何幫助您？";
		break;
	case 2:
		mes "這次你想做什麼？";
		break;
	}
	next;
	if (isbegin_quest(12346) == 1)
		completequest 12346;
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[文明探險家]";
		mes "在組成1個或以上或參加聚會之後，請與我交談。";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		if (is_party_leader() == false) {
			mes "[文明探險家]";
			mes "啊。你不是黨的負責人。帶我去你的領導者！也許把你的領導帶給我。";
			close;
		}
		if (select("產生時間裂縫", "取消" ) == 2)
			close;
		if (instance_create("Central Laboratory") < 1) {
			mes "派對：" + getpartyname( getcharid(1) ) + "";
			mes "領導者：" + getpartyleader( getcharid(1) ) + "";
			mes "^0000 ffcentral實驗室 ^000000-未知錯誤";
			close;
		}
		mes "[文明探險家]";
		mes "當時間的裂縫打開時，呼吸再次說話。";
		close;
	case 0:
	case 1:
		mes "[文明探險家]";
		mes "啊，哦，不。";
		mes "尺寸旅行的剩餘後效應。我們不能在這種情況下再次旅行。";
		close;
	case 2:
		mes "^0000 FFLALL進入中央實驗室的痕跡已被刪除。似乎有可能與文明探險家進行交談。 ^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

// Entrance
1@lab,104,34,4	script	Probationary researcher#pa0829	4_LGTSCIENCE,{
	if ('switch_enabled == 1) {
		mes "[試用研究人員]";
		mes "入口門已經打開。";
		close;
	}
	if (is_party_leader() == false) {
		mes "[試用研究人員]";
		mes "請握住。我正在與指南談論您是否已獲得授權。";
		close;
	}
	mes "[試用研究人員]";
	mes "您是作為研究項目32001實驗的助手參加嗎？";
	next;
	if (select("是的。", "不。" ) == 2) {
		mes "[試用研究人員]";
		mes "看來您正在錯誤的實驗室中。請退出並尋求幫助。";
		close;
	}
	mes "[試用研究人員]";
	mes "我們需要大約5秒的時間。確認訪問代碼。";
	close2;
	sleep 3000;
	npctalk "試用研究員：訪問代碼匹配。我會打開門。";
	sleep 6000;
	npctalk "試用研究員：現在可以進入了。請注意，您無法從另一側打開門。";
	if ('switch_enabled == 0) {
		'switch_enabled = 1;
		enablenpc instance_npcname("lab_warp#1");
		enablenpc instance_npcname("lab_warp#2");
		enablenpc instance_npcname("lab_warp#3");
		enablenpc instance_npcname("Main Switch#");
		enablenpc instance_npcname("Senior researcher#pa082");
		enablenpc instance_npcname("#eff_nc");
		for ( .@i = 1; .@i <= 8; ++.@i )
			enablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;

OnInstanceInit:
	'map_name$ = instance_mapname("1@lab");
	'code_found = 'bool_event = 'switch_enabled = 0;
	for ( .@i = 1; .@i <= 8; .@i++ )
		'switch_position[.@i] = 0;

	// Warps
	disablenpc instance_npcname("lab_warp#1");
	disablenpc instance_npcname("lab_warp#2");
	disablenpc instance_npcname("lab_warp#3");
	disablenpc instance_npcname("lab_warp#4");
	disablenpc instance_npcname("lab_warp#5");

	// Switchs
	disablenpc instance_npcname("Main Switch#");
	disablenpc instance_npcname("Senior researcher#pa082");
	disablenpc instance_npcname("#eff_nc");
	for ( .@i = 1; .@i <= 8; ++.@i )
		disablenpc instance_npcname( "Switch" + .@i + "#" );

	// Experiments
	disablenpc instance_npcname("DR Silvia#pa0829");
	disablenpc instance_npcname("DR Federico#pa0829");
	disablenpc instance_npcname("DR Artemia#pa0829");
	disablenpc instance_npcname("DR Vincent#pa0829");
	disablenpc instance_npcname("#wp01");
	disablenpc instance_npcname("#npc_con01");
	disablenpc instance_npcname("#npc_con03");
	disablenpc instance_npcname("#npc_con05");
	disablenpc instance_npcname("#npc_con06");
	disablenpc instance_npcname("#npc_con07");
	disablenpc instance_npcname("#npc_con08");
	disablenpc instance_npcname("#npc_con10");
	disablenpc instance_npcname("#npc_con11");
	for ( .@i = 1; .@i <= 14; ++.@i )
		hideonnpc instance_npcname("#gen_" + (.@i < 10 ? "0" : "") + .@i);
	disablenpc instance_npcname("lab_warp#end");
	end;
}
// 1@lab,104,34,4	script	Probationary researcher#pa0829_2	4_LGTSCIENCE

// Switchs
1@lab,37,37,3	script	Switch1#	CLEAR_NPC,{
	.@num = charat( strnpcinfo(1),6 );
	setarray .@position$[0], "OFF", "ON";
	mes "[轉變" + .@num + "]";
	mes "該開關目前在" + .@position$[ 'switch_position[.@num] ] + " position. What will you do?";
	next;
	'switch_position[.@num] = !( select("在","離開" ) - 1 );
	close;
}
1@lab,39,37,3	duplicate(Switch1#)	Switch2#	CLEAR_NPC
1@lab,41,37,3	duplicate(Switch1#)	Switch3#	CLEAR_NPC
1@lab,43,37,3	duplicate(Switch1#)	Switch4#	CLEAR_NPC
1@lab,46,37,3	duplicate(Switch1#)	Switch5#	CLEAR_NPC
1@lab,48,37,3	duplicate(Switch1#)	Switch6#	CLEAR_NPC
1@lab,50,37,3	duplicate(Switch1#)	Switch7#	CLEAR_NPC
1@lab,52,37,3	duplicate(Switch1#)	Switch8#	CLEAR_NPC

1@lab,34,37,3	script	Main Switch#	CLEAR_NPC,{
	.@string$ = 'switch_position[1] + "" + 'switch_position[2] + "" + 'switch_position[3] + "" + 'switch_position[4] + "" +
		'switch_position[5] + "" + 'switch_position[6] + "" + 'switch_position[7] + "" + 'switch_position[8];
	.@current_code = strtol( .@string$,2 );
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	npctalk "今天的訪問代碼" + .@code + ", currently entered code " + .@current_code + ".";
	sleep 6000;
	if ('code_found > 0)
		npctalk "門已經打開了。";
	else if (.@current_code != .@code)
		npctalk "代碼不正確。請再試一次。";
	else {
		'code_found = 1;
		npctalk "代碼是正確的。開門。";
		enablenpc instance_npcname("lab_warp#4");
		enablenpc instance_npcname("lab_warp#5");
		enablenpc instance_npcname("DR Silvia#pa0829");
		enablenpc instance_npcname("DR Federico#pa0829");
		enablenpc instance_npcname("DR Artemia#pa0829");
		enablenpc instance_npcname("DR Vincent#pa0829");
		enablenpc instance_npcname("#wp01");
		donpcevent instance_npcname("#eff_nc") + "::OnStop";
		for ( .@i = 1; .@i <= 8; ++.@i )
			disablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;
}

1@lab,45,32,3	script	Senior researcher#pa082	4_LGTSCIENCE,{
	if ('code_found > 0) {
		mes "[高級研究員]";
		mes "門是打開的，所以您現在可以進入。請注意，您不能從另一側打開門。";
		close;
	}
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	mes "[高級研究員]";
	mes "看來您在這裡參加粒子加速實驗。";
	next;
	mes "[高級研究員]";
	mes "今天的訪問代碼為 ^0000ff" + .@code + "^000000.";
	mes "該代碼需要作為二進制編號輸入。使用我身後的開關輸入。";
	next;
	mes "[高級研究員]";
	mes "輸入代碼後，按入口附近的主按鈕。";
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect EF_STEAL, AREA, instance_npcname("Switch" + .@i + "#");
	close2;
	donpcevent instance_npcname("#eff_nc") + "::OnStart";
	end;
}

1@lab,1,1,0	script	#eff_nc	-1,{
	end;
OnStart:
OnTimer2000:
	specialeffect 203,AREA, instance_npcname("Main Switch#");
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect (203+.@i),AREA, instance_npcname("Switch" + .@i + "#");
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}


// Experiments
1@lab,81,84,7	script	DR Silvia#pa0829	4_F_SCIENCE,{
	mes "[Silvia博士]";
	mes "實驗後，我想回家享用冰鎮啤酒和一些炸雞。";
	next;
	mes "[Silvia博士]";
	mes "我已經有近2年沒有回家了。";
	next;
	mes "^0000FFSILVIA正在查看外觀非常疲憊的粒子加速器設備。 ^000000";
	close;
}

1@lab,80,87,7	script	DR Federico#pa0829	4_LGTSCIENCE,{
	mes "[Federico博士]";
	mes "我的觀看次數越多，此設備就會更加驚人。從來沒有這樣的粒子加速器以這種量表製成。";
	next;
	mes "[Federico博士]";
	mes "有了這個尺寸，我想知道是否可以創建一個小型黑洞...";
	next;
	mes "[Federico博士]";
	mes "啊，我只是在說話。這樣的事件比實驗哈哈更接近災難。";
	close;
}

1@lab,80,90,6	script	DR Artemia#pa0829	4_F_SCIENCE,{
	mes "[Artemia博士]";
	mes "看，當您抓住一根繩子並給拖船時，現實不喜歡它。";
	next;
	mes "[Artemia博士]";
	mes "通過如此強大的粒子加速度，可能會發生尺寸失真。";
	next;
	mes "[Artemia博士]";
	mes "我對此有一種不好的感覺，因為...自然找到了一種方法。";
	close;
}

1@lab,81,93,6	script	DR Vincent#pa0829	4_LGTSCIENCE,{
	mes "[文森特博士]";
	mes "機器很完美。什麼都不會錯！";
	next;
	mes "[文森特博士]";
	mes "Artemia太擔心了。無論如何，實驗後我要去旅行。";
	close;
}

1@lab,80,89,0	script	#wp01	HIDDEN_WARP_NPC,9,9,{
	end;
OnTouch:
	donpcevent instance_npcname("#npc_con01") + "::OnStart";
	disablenpc instance_npcname("#wp01");
	end;
}

// Round 1
1@lab,4,1,3	script	#npc_con01	CLEAR_NPC,{
	end;
OnStart:
	'vincent$ = instance_npcname("DR Vincent#pa0829");
	'federico$ = instance_npcname("DR Federico#pa0829");
	'silvia$ = instance_npcname("DR Silvia#pa0829");
	'artemia$ = instance_npcname("DR Artemia#pa0829");
	'gen_01$ = instance_npcname("#gen_01");
	'gen_02$ = instance_npcname("#gen_02");
	'gen_03$ = instance_npcname("#gen_03");
	'gen_04$ = instance_npcname("#gen_04");
	'gen_05$ = instance_npcname("#gen_05");
	'gen_06$ = instance_npcname("#gen_06");
	'gen_07$ = instance_npcname("#gen_07");
	'gen_08$ = instance_npcname("#gen_08");
	'gen_09$ = instance_npcname("#gen_09");
	'gen_10$ = instance_npcname("#gen_10");
	'gen_11$ = instance_npcname("#gen_11");
	'gen_12$ = instance_npcname("#gen_12");
	'gen_13$ = instance_npcname("#gen_13");
	'gen_14$ = instance_npcname("#gen_14");

	npctalk "費德里科：讓我們開始吧。嗯...弗雷薩博士，你準備好了嗎？", 'federico$;
	sleep 5000;
	npctalk "Artemia：可能會進行最後的確認檢查。這種規模的實驗需要如此謹慎。", 'artemia$;
	sleep 6000;
	npctalk "Silvia：第一次實驗的成品需要小心操作。", 'silvia$;
	sleep 4000;
	npctalk "Vincent : Ah, I hope we can finish quickly so I can go eat chicken.", 'vincent$;
	sleep 3000;
	npctalk "西爾維婭：啊！我也喜歡這樣！你走的時候給我打電話。", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "?????: Ah, ah, one two one two, mic test.", bc_map,0x00ff77;
	sleep 2000;
	npctalk "費德里科：她在這裡。", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "?????: Chief director Lona Fresa of project particle accelerator.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Current time " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day 25 minutes after 10.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Testing the effects of particle acceleration on its surroundings in an open space.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "Artemia：啊……我真的不喜歡這個實驗。", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : Starting the experiment. Chief researcher Dr. Federico, Artemia, Silvia, Vincent and assistant researchers.", bc_map,0x00ff77;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "鹵蟲：我們開始吧~", 'artemia$;
	sleep 3000;
	npctalk "文森特：發電機開始旋轉。", 'vincent$;
	sleep 3000;
	npctalk "Federico：發電機轉速固定為 10000 RPM。", 'federico$;
	sleep 2500;
	npctalk "西爾維婭：電壓仍在容許範圍內。", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : This experiment is for the creation of new material that hasn't existed before.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : Recorders make sure to make detailed records of the catalyst bed's reaction when it is installed to the accelerator.", bc_map,0x00ff77;
	sleep 6000;
	npctalk "鹵蟲：接觸催化劑床數 1。", 'artemia$;
	sleep 9000;
	npctalk "西爾維婭：發電機電壓不穩定。仍在公差範圍內。", 'silvia$;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	npctalk "Silvia：檢測到接近催化劑床和加速器之間的公差帶的熱量！", 'silvia$;
	sleep 5000;
	npctalk "文森特：加速器正在加熱。", 'vincent$;
	sleep 5000;
	npctalk "文森特：320c。", 'vincent$;
	sleep 5000;
	npctalk "Vincent：670c 升溫太快了！", 'vincent$;
	sleep 3000;
	npctalk "Vincent：突破1000c！ ！！弗雷薩博士！我們必須暫時停止設備！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : It is fine.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "西爾維婭：這不是一個簡單的問題。氣溫已經遠遠超出了預計的數字！", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : This problem was predicted. Maintain current state.", bc_map,0x00ff77;
	sleep 3000;
	npctalk "Artemia：但至少……我稍後會吃雞肉。", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Receiving changed circumstance through main server. Detecting dimensional distortion between particle accelerator and catalyst.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "鹵蟲：弗雷薩博士！這是主機計算機的危險情況。關閉主電源！", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "文森特：發生什麼事了。有人關掉那台電腦嗎！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con05") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con05	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con05");
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1087;	// ORK_HERO
	else if (.@boss_call <= 14)		.@mob_id = 1147;	// MAYA
	else if (.@boss_call <= 21)		.@mob_id = 1190;	// ORC_LORD
	else if (.@boss_call <= 28)		.@mob_id = 1115;	// EDDGA
	else if (.@boss_call <= 35)		.@mob_id = 1086;	// GOLDEN_BUG
	else if (.@boss_call <= 42)		.@mob_id = 1038;	// OSIRIS
	else if (.@boss_call <= 49)		.@mob_id = 1159;	// PHREEONI
	else if (.@boss_call <= 56)		.@mob_id = 1389;	// DRACULA
	else if (.@boss_call <= 63)		.@mob_id = 1046;	// DOPPELGANGER
	else if (.@boss_call <= 70)		.@mob_id = 1059;	// MISTRESS
	else if (.@boss_call <= 77)		.@mob_id = 1150;	// MOONLIGHT
	else if (.@boss_call <= 84)		.@mob_id = 1688;	// LADY_TANEE
	else if (.@boss_call <= 91)		.@mob_id = 1039;	// BAPHOMET
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con05") + "::OnMyMobDead";
	sleep 3000;
	npctalk "Artemia：那……那是什麼？ ！", 'artemia$;
	sleep 3000;
	npctalk "Silvia：它沒有對我們做出反應。也許它看不到我們？", 'silvia$;
	sleep 5000;
	npctalk "Vincent：但是它對項目助理充滿敵意！那是什么生物！ ？", 'vincent$;
	sleep 5000;
	npctalk "文森特：我的身體動不了！我認為空間被凍結了！", 'vincent$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Restarting device.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con06") + "::OnStart";
	disablenpc instance_npcname("#npc_con05");
	stopnpctimer;
	end;
}

// Round 2
1@lab,4,1,3	script	#npc_con06	CLEAR_NPC,{
	end;
OnStart:
	sleep 9000;
	npctalk "鹵蟲：MA-4885！ ！你按照誰的命令重啟！停止！ ！", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: Unrecognized command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 6000;
	npctalk "Artemia：弗雷薩博士！ ！我們必須停下來！這太危險了！", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 3000;
	npctalk "文森特：弗雷薩！我們沒有時間！關掉電腦！ ！", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "西爾維婭：弗雷薩！做出決定吧！", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa : Current time 20 minutes past 11. Momentarily postpone experiment.", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	mapannounce 'map_name$, "Lona Fresa : Mother Comp MA-4885. I am your Chief director Dr. Fresa. Cease current progress.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Access denied. Cannot understand command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "費德里科：可惡！還有別的東西來了！ ！", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: Alert. New unidentified creature approaching from demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "文森特：噢……該死。這就是為什麼您不應該安裝最新的 CPU。", 'vincent$;
	sleep 3000;
	npctalk "Federico：啊……我的身體……動不了。該死", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con07") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con07	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con07");
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	.@boss_call = rand(1,98);
	if (.@boss_call <= 7)		.@mob_id = 1980;	// KUBLIN
	else if (.@boss_call <= 14)	.@mob_id = 1157;	// PHARAOH
	else if (.@boss_call <= 21)	.@mob_id = 1112;	// DRAKE
	else if (.@boss_call <= 28)	.@mob_id = 1251;	// KNIGHT_OF_WINDSTORM
	else if (.@boss_call <= 35)	.@mob_id = 2068;	// BOITATA
	else if (.@boss_call <= 42)	.@mob_id = 1373;	// LORD_OF_DEATH
	else if (.@boss_call <= 49)	.@mob_id = 2156;	// LEAK
	else if (.@boss_call <= 56)	.@mob_id = 1272;	// DARK_LORD
	else if (.@boss_call <= 63)	.@mob_id = 1630;	// BACSOJIN_
	else if (.@boss_call <= 70)	.@mob_id = 1252;	// GARM
	else if (.@boss_call <= 77)	.@mob_id = 1779;	// KTULLANUX
	else if (.@boss_call <= 84)	.@mob_id = 1708;	// THANATOS
	else if (.@boss_call <= 86)	.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 88)	.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 90)	.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 92)	.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 94)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 96)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 98)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con07") + "::OnMyMobDead";
	sleep 6000;
	npctalk "西爾維婭：和上次一樣。它似乎對我們沒有興趣。", 'silvia$;
	sleep 4000;
	npctalk "文森特：讓我們在主電源暫時關閉的情況下，徹底關閉母機吧！", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Answering question. This space is currently stuck in a distorted phase.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Direct interaction between the demi-plane creature and this world's organisms will be possible after the phase has been balanced.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "Artemia：那團瘋狂的金屬在說什麼？難道說我們和那些怪物之類的東西不是同一個階段？", 'artemia$;
	sleep 4000;
	npctalk "Federico: Aah, I guess we should hope that Mother computer doesn't make the right calculations.", 'federico$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Phase adjustment failed. Restarting relevant procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con08") + "::OnStart";
	disablenpc instance_npcname("#npc_con07");
	stopnpctimer;
	end;
}

// Round 3
1@lab,4,1,3	script	#npc_con08	CLEAR_NPC,{
	end;
OnStart:
	sleep 13000;
	mapannounce 'map_name$, "Lona Fresa: Mother Computer MA-4885. Looks like you are trying to synchronize the other dimension and this world.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: That is correct.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 6000;
	npctalk "Artemia：弗雷薩博士！ ！這個荒唐的事件與我們原來的實驗完全無關！", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: I have fully realized that.", bc_map, 0x00ff77;
	sleep 6000;
	npctalk "西爾維婭：我們不能完全關閉主電源嗎？", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Restarting particle acceleration procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	mapannounce 'map_name$, "Lona Fresa: " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day. Current time 38 minutes past 11 temporarily terminating particle accelerator project.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Ending record with reasons of Mother Computer malfunction and overheating of accelerator.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "西爾維婭：好的！現在弗雷薩博士只需關閉電源即可。", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Cutting off power to Mother Computer MA-4885.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: Suspending operation of all equipment as per Administrator request.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: .....command... ----!!!  Zizip... ---!! -- following command ----- !! Zizip.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = false;
	npctalk "Artemia：呼……結束了嗎？", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: Completed shut down of Mother computer MA-4885. Confirm equipment suspension.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "西爾維婭：看來一切都結束了。", 'silvia$;
	sleep 4000;
	npctalk "Federico：工廠初始化是最好的。", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "Lona Fresa: Recording all experiment progress and terminating at current point.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: The final experiment failure is unfortunate. But I will promise a more perfect experiment next time.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: Good work everybody.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Artemia：看來已經結束了。", 'artemia$;
	sleep 5000;
	npctalk "Federico：我以為這是 Fresa 的教條式實驗。但這是母機的問題。", 'federico$;
	sleep 5500;
	npctalk "西爾維婭：我們冷靜一下吧。離開這個可怕的實驗室。", 'silvia$;
	sleep 5000;
	npctalk "文森特：順便說一下...我還是動不了，這是怎麼回事？", 'vincent$;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 2000;
	mapannounce 'map_name$, "MA-4885: CODE NAME - DESCENT", bc_map, 0xff0000,FW_NORMAL,16,0;
	sleep 5000;
	npctalk "費德里科：什麼？這不是初始化階段嗎？", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: Starting final phase transformation.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	npctalk "西爾維婭：一切都結束了。", 'silvia$;
	sleep 5500;
	mapannounce 'map_name$, "MA-4885: A new organism will be soon transmitted from the demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 second remaining", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	donpcevent instance_npcname("#npc_con10") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Temporarily shutting down main power for syntonization with transformed phase.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con10	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con10");
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1623;	// RSX_0806
	else if (.@boss_call <= 14)		.@mob_id = 1418;	// DARK_SNAKE_LORD
	else if (.@boss_call <= 21)		.@mob_id = 1312;	// TURTLE_GENERAL
	else if (.@boss_call <= 28)		.@mob_id = 1785;	// ATROCE
	else if (.@boss_call <= 35)		.@mob_id = 1734;	// KIEL_
	else if (.@boss_call <= 42)		.@mob_id = 1719;	// DETALE
	else if (.@boss_call <= 49)		.@mob_id = 1768;	// GLOOMUNDERNIGHT
	else if (.@boss_call <= 56)		.@mob_id = 2087;	// QUEEN_SCARABA
	else if (.@boss_call <= 63)		.@mob_id = 1751;	// RANDGRIS
	else if (.@boss_call <= 70)		.@mob_id = 2253;	// DAEHYON
	else if (.@boss_call <= 77)		.@mob_id = 2255;	// KADES
	else if (.@boss_call <= 84)		.@mob_id = 1832;	// IFRIT
	else if (.@boss_call <= 91)		.@mob_id = 1874;	// BEELZEBUB_
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con10") + "::OnMyMobDead";
	mapannounce 'map_name$, "MA-4885: Final phase transformation process.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: This world will be summoned to the demi-plane after the phase interference phenomenon with creature fades.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	end;
OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Phase synchronization completed before creature is eliminated. Starting dimensional travel.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con11") + "::OnStart";
	disablenpc instance_npcname("#npc_con10");
	stopnpctimer;
	end;
}

1@lab,4,1,3	script	#npc_con11	CLEAR_NPC,{
	end;
OnStart:
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	sleep 12000;
	mapannounce 'map_name$, "MA-4885: Conducting calculations to restore balance to distorted phase. Please wait a moment.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	npctalk "Federico：Mother comp 正在進行一項超出我們想像的巨大實驗。", 'federico$;
	sleep 2500;
	npctalk "文森特：看起來比我們寬宏大量。弗雷薩！！ ！你在聽嗎？拉響警報！", 'vincent$;
	sleep 5000;
	npctalk "文森特：我們至少應該疏散其他實驗室的人。", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: But ... you.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Silvia : Gee, I don't know what will happen but I am curious about shifting dimensions. Don't mind me!", 'silvia$;
	sleep 5000;
	npctalk "Artemia：在嘗試新材料生產時進行維度移動。陷入了不同級別的實驗中。", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : Alert! Alert! Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : I repeat. Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	npctalk "Federico：這就是移動維度的感覺嗎？多麼奇怪啊...", 'federico$;
	sleep 4000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'federico$;
	disablenpc 'federico$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 20% progress. There could be dimensional interference with certain creatures. Please be aware.", bc_map, 0xffff00,FW_NORMAL,13,0;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	npctalk "文森特：不知道其他人撤離了嗎？", 'vincent$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'vincent$;
	disablenpc 'vincent$;
	npctalk "西爾維婭：啊……我應該一邊吃雞肉和啤酒一邊看這個……", 'silvia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'silvia$;
	disablenpc 'silvia$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 78% progress. Most of the dimension encroached by demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
	npctalk "Artemia：哈...現在誰去吃雞？", 'artemia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'artemia$;
	disablenpc 'artemia$;
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 10000;
	'bool_event = false;
	mapannounce 'map_name$, "MA-4885: Dimension move 100% completed. Welcome to the demi-plane dimension.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: A gateway possible for contact with the demi-plane open at 3 o'clock of the laboratory.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Cannot guarantee what kind of dimension will be met due to the instability of the gateway...", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: If there are any surviving organisms I wish you luck. I hope to see you again.", bc_map, 0xffff00,FW_NORMAL,13,0;
	enablenpc instance_npcname("lab_warp#end");
	end;
}


// Display effects
1@lab,4,1,5	script	#npc_con03	CLEAR_NPC,{
	end;

function effect_gen {
	.@effect = getarg(0);
	.@count = getargcount();
	for ( .@i = 1; .@i < .@count; .@i++ )
		specialeffect .@effect, AREA, getd( "'gen_" + ( getarg(.@i) < 10 ? "0" : "" ) + getarg(.@i) + "$" );
	return;
}

function effect_1_10_gen { effect_gen( getarg(0), 1,2,3,4,5,6,7,8,9,10 ); }

OnStart:
	effect_gen( EF_MAGICALATTHIT, 1 );
	sleep 2000;
	effect_gen( EF_MAGICALATTHIT, 1,3 );
	sleep 1900;
	effect_gen( EF_MAGICALATTHIT, 1,3,8 );
	sleep 1700;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4 );
	sleep 1600;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6 );
	sleep 1500;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2 );
	sleep 1400;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7 );
	sleep 1300;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9 );
	sleep 1200;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9,5 );
	sleep 1100;
	while( 'bool_event ) {
		effect_1_10_gen( EF_MAGICALATTHIT );
		sleep 1000;
	}
	end;
OnStart2:
	effect_gen( EF_BLASTMINEBOMB, 1 );
	sleep 2000;
	effect_gen( EF_BLASTMINEBOMB, 1,3 );
	sleep 1900;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8 );
	sleep 1800;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4 );
	sleep 1700;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6 );
	sleep 1600;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2 );
	sleep 1500;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7 );
	sleep 1400;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9 );
	sleep 1300;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9,5 );
	sleep 1200;
	while( 'bool_event ) {
		effect_1_10_gen( EF_BLASTMINEBOMB );
		sleep 1100;
	}
	end;

OnStart3:
	while( 'bool_event ) {
		specialeffect EF_LORD, AREA, 'gen_11$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_12$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_13$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_14$;
		sleep 2000;
	}
	end;
}

1@lab,91,80,5	script	#gen_01	CLEAR_NPC,{ end; }
1@lab,96,81,5	duplicate(#gen_01)	#gen_02	CLEAR_NPC
1@lab,99,85,5	duplicate(#gen_01)	#gen_03	CLEAR_NPC
1@lab,99,91,5	duplicate(#gen_01)	#gen_04	CLEAR_NPC
1@lab,96,95,5	duplicate(#gen_01)	#gen_05	CLEAR_NPC
1@lab,91,97,5	duplicate(#gen_01)	#gen_06	CLEAR_NPC
1@lab,85,96,5	duplicate(#gen_01)	#gen_07	CLEAR_NPC
1@lab,82,91,5	duplicate(#gen_01)	#gen_08	CLEAR_NPC
1@lab,82,86,5	duplicate(#gen_01)	#gen_09	CLEAR_NPC
1@lab,85,81,5	duplicate(#gen_01)	#gen_10	CLEAR_NPC
1@lab,90,97,5	duplicate(#gen_01)	#gen_11	CLEAR_NPC
1@lab,82,89,5	duplicate(#gen_01)	#gen_12	CLEAR_NPC
1@lab,91,80,5	duplicate(#gen_01)	#gen_13	CLEAR_NPC
1@lab,99,88,5	duplicate(#gen_01)	#gen_14	CLEAR_NPC


// Warps
1@lab,102,28,0	warp2	lab_warp#1	2,2,1@lab,97,28
1@lab,80,28,0	warp2	lab_warp#2	2,2,1@lab,75,28
1@lab,58,28,0	warp2	lab_warp#3	2,2,1@lab,53,28
1@lab,29,38,0	warp2	lab_warp#4	2,2,1@lab,29,43
1@lab,48,82,0	warp2	lab_warp#5	2,2,1@lab,58,82
1@lab,123,88,0	warp2	lab_warp#end	2,2,verus01,152,150


//-- GM NPCs
1@lab,1,1,3	script	#adsw_	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1)
		end;
	mes "[時間管理員]";
	mes "您想去幾點？";
	next;
	switch( select("取消", "1 開始", "2 開始", "3開始", "4開始" ) ) {
	case 1:
		close;
	case 2:
		donpcevent instance_npcname("#npc_con01") + "::OnStart";
		break;
	case 3:
		donpcevent instance_npcname("#npc_con06") + "::OnStart";
		break;
	case 4:
		donpcevent instance_npcname("#npc_con08") + "::OnStart";
		break;
	case 5:
		end;
	}
	warp instance_mapname("1@lab"), 84,90;
	end;
}

verus01,1,5,5	script	Laboratory Administrator#pa0829_01	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1) {
		warp "prontera",100,100;
		end;
	}
	mes "[實驗室管理員]";
	mes "一切都很好！";
	close2;
	if (isbegin_quest(12347) > 0)
		erasequest 12347;
	end;
}
