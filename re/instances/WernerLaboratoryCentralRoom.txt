//===== rAthena Script =======================================
//= Instances Werner Laboratory central room.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Require Terra Gloria main quest.
//- Note: For now there are 2 instances (officially one) to
//  avoid exploit, the warp entry being the same. The first
//  (main quest) is a solo instance and the 2nd (daily) a
//  a party instance. The instance is a basic version,
//  KRO added Seyren boss fight in the next updates.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

slabw01,232,91,5	duplicate(dummy_cloaked_npc)	Skia#ep162_06	4_EP16_SKIA
slabw01,233,92,3	duplicate(dummy_cloaked_npc)	Scared researcher#ep162	4_M_ALCHE_A

// Werner Laboratory central room (solo instance)
slabw01,236,91,3	script	Rookie#ep162_04	4_M_ROOKIE,{
	if (terra_gloria_main == 20) {
		mes "[新秀]";
		mes "看看這個。";
		mes "根據設計圖，該部分連接到中央房間...但是我只看到一個可疑的大門。";
		next;
		mes "[新秀]";
		mes "難怪他們看起來有一些支持他們的東西。";
		mes "我們應該使用爆炸性並炸毀它嗎？";
		next;
		cloakoffnpc( "Skia#ep162_06", getcharid(0) );
		cloakoffnpc( "Scared researcher#ep162", getcharid(0) );
		npctalk "等待！", "Skia#ep162_06", bc_self;
		mes "[滑雪]";
		mes "不是這樣粗略的！";
		mes "好吧，當我來到這裡時，我做出了一個很好的決定。";
		mes "告訴他打開這個。";
		next;
		npctalk "哇！這女人還真是聰明啊！", "", bc_self;
		npctalk "為什麼……你為什麼要這樣對我？ ！", "Scared researcher#ep162", bc_self;
		mes "[滑雪]";
		mes "我問他是否知道這裡的道路，他的回答是肯定的。所以我把他帶到這裡。";
		mes "他將激活通往中央房間的大門。";
		next;
		npctalk "這……就是這樣做的方法。", "Scared researcher#ep162", bc_self;
		mes "[新秀]";
		mes "嗯，我明白了。那就是操作它的方法。";
		mes "太好了，現在我明白了。";
		mes "而且我們不再需要他了。";
		next;
		npctalk "什...什麼？！請不要殺我！", "Scared researcher#ep162", bc_self;
		mes "[滑雪]";
		mes "真的嗎？然後，我將把這位研究人員帶到外面的中隊。";
		mes "關於中央房間，" + strcharinfo(0) + " I am counting on you!";
		next;
		cloakonnpc( "Skia#ep162_06", getcharid(0) );
		cloakonnpc( "Scared researcher#ep162", getcharid(0) );
		mes "[新秀]";
		mes "哈哈，他們讓我們打開中央房間的大門！";
		mes "一旦您準備好進入這裡，請再次與我交談。";
		next;
		mes "[新秀]";
		mes "激活大門後，我們必須從外部進行防護。";
		mes "所以我不能和你一起去那裡。";
		terra_gloria_main = 21;
		close;
	}
	if (terra_gloria_main == 21) {
		if (is_party_leader() == false) {
			mes "^4d4dffthis事件進行到紀念館。";
			mes "組織聚會後，請再試一次。 ^000000";
			close;
		}
		mes "[新秀]";
		mes "你準備好了嗎？";
		mes "如果您現在進入，我將激活大門。";
		next;
		if (select("現在進入。", "不許進入。" ) == 2) {
			mes "[新秀]";
			mes "我們倆都知道我們沒有時間來做到這一點，不是嗎？";
			close;
		}
		mes "[新秀]";
		mes "哇！就是這樣！我總是很快學習東西！";
		mes "好吧，請現在進去！";
		cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
		instance_create("Werner Laboratory central room#1");	// officially there is only one 'Werner Laboratory central room' instance
		close;
	}
	if (terra_gloria_main == 22) {
		mes "[新秀]";
		mes "你收回了東西嗎？";
		close;
	}
	switch( checkquest(7742,HUNTING) ) {
	case -1:
		mes "[新秀]";
		mes "我們在中央房間發現了一個奇怪的現象。我們沒有調查它。";
		mes "也許裡面還有一個強大的實驗學科。";
		next;
		mes "[新秀]";
		mes "這是什麼樣的設施？";
		mes "SSSS。";
		mes "一旦得到適當識別，我可能會要求您達成和解...";
		mes "我將不得不再次保護中央房間的大門。";
		close;
	case 0:
	case 1:
		if (getcharid(1) < 1) {
			mes "^4d4dffthis事件進行到紀念館。";
			mes "組織聚會後，請再試一次。 ^000000";
			close;
		}
		if (is_party_leader() == false) {
			mes "[新秀]";
			mes "如果有一個已經激活的門，您可以看到它。";
			next;
			mes "[新秀]";
			mes "否則，請向您的黨長要求入場。";
			close;
		}
		mes "[新秀]";
		mes "你準備好了嗎？";
		mes "如果您現在進入，我將激活大門。";
		next;
		if (select("現在進入。", "不許進入。" ) == 2) {
			mes "[新秀]";
			mes "我們倆都知道我們沒有時間來做到這一點，不是嗎？";
			close;
		}
		mes "[新秀]";
		mes "哇！就是這樣！我總是很快學習東西！";
		mes "好吧，請現在進去！";
		cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
		instance_create("Werner Laboratory central room#2");
		close;
	case 2:
		mes "[新秀]";
		mes "我們不確定那個艾森·沃納（Eisen Werner）的傢伙製造和拋棄了多少個實驗性主題。";
		next;
		mes "[新秀]";
		mes "但是，讓我們稱之為一天。";
		close;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, " checkquest(7742,HUNTING) == 0 || checkquest(7742,HUNTING) == 1 " );
	end;
}

slabw01,246,88,3	script	Central_room#evt_gate01	PORTAL,{
	if (terra_gloria_main != 21 && terra_gloria_main < 23)
		end;
	if (getcharid(1) < 1 || (terra_gloria_main == 21 && is_party_leader() == false)) {
		mes "^4d4dffmd只有政黨負責人現在可以進入[Central Room]。 ^000000";
		close;
	}
	mes "您可以搬到中央房間。";
	next;
	if (select("進去吧。", "不要進去。" ) == 2) {
		mes "[新秀]";
		mes "哦...我每次都必須打開它。這真是不便！";
		close;
	}
	if (terra_gloria_main == 21)
		.@md_name$ = "Werner Laboratory central room#1";
	else
		.@md_name$ = "Werner Laboratory central room#2";	// officially player without the daily hunting quest can enter in the instance, allowing to spam the instance without delay
	switch( instance_enter(.@md_name$) ) {
	case IE_NOMEMBER:
		end;
	case IE_NOINSTANCE:
		mes "[新秀]";
		mes "大門現在正在故障。這樣很危險...";
		mes "請等待一會兒。";
		close;
	case IE_OTHER:
		mes "發生了未知錯誤。";
		close;
	case IE_OK:
		mapannounce "slabw01", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
		// warp "1@slw",187,24;
		end;
	}
	end;

OnInit:
	cloakonnpc( strnpcinfo(0) );
	end;
}

// warps
1@slw,187,170,0	warp	#slwwarp01	1,1,1@slw,71,32
1@slw,71,77,0	warp	#slwwarp02	1,1,1@slw,54,134
1@slw,55,150,0	warp	Emergency exit#exit_war	1,1,que_swat,155,58

// Entrance - event type
1@slw,187,25,0	script	#sl_evt01	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	if (is_party_leader() == false)
		end;
	disablenpc instance_npcname("#sl_evt01");
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
		enablenpc instance_npcname("Eisen Werner#werner01");
	else {
		enablenpc instance_npcname("Guard#gard01");
		enablenpc instance_npcname("Guard#gard02");
		enablenpc instance_npcname("Guard#gard03");
		enablenpc instance_npcname("Guard#gard04");
	}
	end;

OnInstanceInit:
	'device_state[0] = 'device_state[1] = 0;
	'event_final = 0;

	'map_slw$ = instance_mapname("1@slw");

	disablenpc instance_npcname("#slwwarp01");
	disablenpc instance_npcname("#slwwarp02");
	disablenpc instance_npcname("Central Entrance#door");
	disablenpc instance_npcname("Emergency exit#exit_war");

	disablenpc instance_npcname("Eisen Werner#werner01");
	disablenpc instance_npcname("Eisen Werner#werner02");
	disablenpc instance_npcname("Eisen Werner#werner03");
	disablenpc instance_npcname("Eisen Werner#werner04");
	disablenpc instance_npcname("Eisen Werner#werner05");

	disablenpc instance_npcname("Security device#switch01");
	disablenpc instance_npcname("Security device#switch02");
	disablenpc instance_npcname("Security device#switch11");
	disablenpc instance_npcname("Security device#switch12");

	disablenpc instance_npcname("Guard#gard01");
	disablenpc instance_npcname("Guard#gard02");
	disablenpc instance_npcname("Guard#gard03");
	disablenpc instance_npcname("Guard#gard04");
	disablenpc instance_npcname("Guard#gard05");
	disablenpc instance_npcname("Guard#gard06");
	disablenpc instance_npcname("Guard#gard07");

	disablenpc instance_npcname("Seyren Windsor#seiren");
	disablenpc instance_npcname("Pet child#boss_master");
	disablenpc instance_npcname("#boss_master_dummy");
	end;
}

// Room 1
// * Samples and devices
1@slw,174,145,3	script	Displayed sample#01	CLEAR_NPC,{
	setarray .@sample$[0],
		"^ff0000Purity^000000", "MT-Wxx0s57b",
		"^4d4dffRose^000000", "MT-Wxx00b21",
		"^4d4dffContradiction^000000", "MT-Wxx023-f1",
		"^4d4dffJoy^000000", "OT-Wxx01-c",
		"^4d4dffSea^000000", "MT-Wxc1c1b",
		"^ff0000Eternity^000000", "MT-Wx267b",
		"^4d4dffWay back home^000000", "OTM-Wxff01",
		"^4d4dffLoneliness^000000", "MT-Wxx00c46",
		"^4d4dffGlow^000000", "OT-Wxx4d4dff",
		"^ff0000Dawn^000000", "MT-Wff_01v",
		"^4d4dffTwilight^000000", "OT-Wxx00c3b",
		"^4d4dffGirl^000000's ^ff0000dream^000000", "MT-Wxx0ax2-1";
	.@num = (atoi(strnpcinfo(2)) - 1) * 2;
	mes "[" + .@sample$[.@num] + "]";
	mes "perveriment_number:" + .@sample$[.@num+1] + "";
	close;
}
1@slw,201,145,3	duplicate(Displayed sample#01)	Displayed sample#02	CLEAR_NPC
1@slw,174,132,3	duplicate(Displayed sample#01)	Displayed sample#03	CLEAR_NPC
1@slw,201,132,3	duplicate(Displayed sample#01)	Displayed sample#04	CLEAR_NPC
1@slw,174,118,3	duplicate(Displayed sample#01)	Displayed sample#05	CLEAR_NPC
1@slw,201,118,3	duplicate(Displayed sample#01)	Displayed sample#06	CLEAR_NPC
1@slw,174,104,3	duplicate(Displayed sample#01)	Displayed sample#07	CLEAR_NPC
1@slw,201,104,3	duplicate(Displayed sample#01)	Displayed sample#08	CLEAR_NPC
1@slw,174,90,3	duplicate(Displayed sample#01)	Displayed sample#09	CLEAR_NPC
1@slw,201,90,3	duplicate(Displayed sample#01)	Displayed sample#10	CLEAR_NPC
1@slw,174,76,3	duplicate(Displayed sample#01)	Displayed sample#11	CLEAR_NPC
1@slw,201,76,3	duplicate(Displayed sample#01)	Displayed sample#12	CLEAR_NPC

// * Events - First entrance
1@slw,188,42,3	script	Eisen Werner#werner01	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "這是主角。";
	mes "老實說，我給我留下了深刻的印象。我沒想到你會一直來這裡。";
	next;
	mes "[鋼鐵維爾納]";
	mes "好吧，無論如何。";
	mes "歡迎。";
	mes "歡迎來到Eisen Werner的世界。";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "我從來沒有想過這樣的事情會突然這樣做。";
	mes "任何。誰在乎？";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "無論如何，我放棄了保存這個實驗室。";
	mes "您想看看嗎？";
	next;
	mes "[鋼鐵維爾納]";
	mes "有很多事情，包括尺寸的裂縫等。";
	mes "在某些世界中，時間流是扭曲的。";
	next;
	mes "[鋼鐵維爾納]";
	mes "這是一個平行的世界嗎？";
	mes "還是有人去過過去的某個點？";
	next;
	mes "[鋼鐵維爾納]";
	mes "時間是什麼意思？你有沒有考慮過？";
	mes "它的流量是相對的。";
	next;
	mes "[鋼鐵維爾納]";
	mes "那個傢伙現在停了下來。";
	mes "如果時間生物學上，這意味著有人變老了。";
	next;
	mes "[鋼鐵維爾納]";
	mes "那被武力停止了。";
	mes "在這種情況下，我們使用這樣的表達式。";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "時間已經停止了。";
	next;
	mes "[鋼鐵維爾納]";
	mes "...";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "您想看一下嗎？";
	mes "花點時間瀏覽走廊。";
	next;
	mes "[鋼鐵維爾納]";
	mes "我會指導你。";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner01");
		enablenpc instance_npcname("Eisen Werner#werner02");
	}
	end;
}

1@slw,189,164,3	script	Eisen Werner#werner02	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "你喜歡我的工作嗎？";
	mes "我把愚蠢的傢伙重新開始。";
	next;
	mes "[鋼鐵維爾納]";
	mes "好吧，這一點都不容易。";
	mes "......";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "如果那個孩子回去了，他就不會那樣結束...";
	next;
	mes "[鋼鐵維爾納]";
	mes "我的意思是，如果他停下來...";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "主席...我的意思是，那個人沉迷於過去。";
	mes "多虧了主席，我可以根據需要進行研究。";
	next;
	mes "[鋼鐵維爾納]";
	mes "你不好奇嗎？";
	mes "為什麼他如此痴迷於過去？";
	next;
	mes "[鋼鐵維爾納]";
	mes "順便說一句，如果您想超越這一點，就必須在左右操作儀器。 ...您能做到嗎？";
	next;
	mes "[鋼鐵維爾納]";
	mes "由於這是一個簡單的設備，我想您可以。";
	mes "更改 ^ff0000red ^000000為 ^4d4d4dffblue ^000000。";
	mes "然後將 ^4d4dffblue ^000000更改為 ^ff0000red ^000000。就是這樣。";
	next;
	mes "[鋼鐵維爾納]";
	mes "如果您正確地看著我的工作，您會理解我在說什麼。";
	mes "然後，我將在下一個區域見。";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner02");
		enablenpc instance_npcname("Security device#switch01");
		enablenpc instance_npcname("Security device#switch02");
	}
	end;
}

1@slw,155,191,3	script	Security device#switch01	CLEAR_NPC,{
	if (is_party_leader() == false)
		end;
	.@number = atoi( replacestr( strnpcinfo(2), "switch", "" ) );
	.@type = (.@number < 10) ? 0 : 1;
	.@npc_num = (.@number < 10) ? .@number : (.@number-10);
	if ('device_state[.@type] & .@npc_num) {
		mes "它已經被激活了。";
		close;
	}
	callsub S_Code, .@string$, .@answer;
	mes "有安全代碼寫下來。";
	mes "[" + .@string$ + "]";
	mes "您將輸入什麼？";
	next;
	if (select("^ff0000" + .@string$ + "^000000", "^4d4dff" + .@string$ + "^000000" ) != .@answer) {
		'device_state[.@type] = 0;
		mes "該設備已初始化。";
		close;
	}
	if (is_party_leader() == false)
		end;
	'device_state[.@type] = 'device_state[.@type] | .@npc_num;
	if ('device_state[.@type] != 3) {
		mes "操作安全設備。";
		mes "似乎還有一個安全設備。";
		close;
	}
	mes "操作安全設備。";
	mes "現在激活了大門。";
	switch( .@type ) {
	case 0:
		enablenpc instance_npcname("#slwwarp01");
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
			enablenpc instance_npcname("Eisen Werner#werner03");
		else {
			enablenpc instance_npcname("Guard#gard05");
			enablenpc instance_npcname("Guard#gard06");
			enablenpc instance_npcname("Guard#gard07");
		}
		break;
	case 1:
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")
			enablenpc instance_npcname("Eisen Werner#werner04");
		else
			enablenpc instance_npcname("Central Entrance#door");
		enablenpc instance_npcname("#slwwarp02");
		break;
	}
	close;

S_Code:
	setarray .@sample$[0],
		"Purity", 2,
		"Eternity", 2,
		"Dawn", 2,
		"Rose", 1,
		"Contradiction", 1,
		"Joy", 1,
		"Sea", 1,
		"Way back home", 1,
		"Loneliness", 1,
		"Glow", 1,
		"Twilight", 1;
	.@size = getarraysize(.@sample$) / 2;
	.@r = rand(.@size) * 2;
	set getarg(0), .@sample$[.@r];
	set getarg(1), .@sample$[.@r+1];
	return;
}
1@slw,220,191,3	duplicate(Security device#switch01)	Security device#switch02	CLEAR_NPC
1@slw,22,61,3	duplicate(Security device#switch01)	Security device#switch11	CLEAR_NPC
1@slw,122,61,3	duplicate(Security device#switch01)	Security device#switch12	CLEAR_NPC


// * Events - Daily
1@slw,188,58,3	script	Guard#gard01	3622,4,4,{
	end;
OnTouch:
	.@num = atoi( replacestr( strnpcinfo(2), "gard0", "" ) );
	getmapxy .@map$,.@x,.@y, BL_NPC;
	switch( .@num ) {
	case 1:	// 2 spawn area
		npctalk ".. 哎呀，你怎麼進來的？";
		areamonster .@map$, 186,92,190,94, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
		.@mob_count = 3;
		break;
	case 2:	// 3 spawn area
		npctalk ".. 哎呀，你怎麼進來的？";
		areamonster .@map$, 186,142,190,143, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
		areamonster .@map$, 186,160,190,162, "Special Guard", 3622,3;
		.@mob_count = 3;
		break;
	case 5:
		npctalk ".. 哎呀，你怎麼進來的？";
		.@mob_count = 3;
		break;
	case 3:
		npctalk ".. 哎呀，你怎麼進來的？";
		.@mob_count = 1;
		.@label$ = instance_npcname("#security_device_lab") + "::OnLeft1";
		break;
	case 4:
		npctalk ".. 哎呀，你怎麼進來的？";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + "::OnRight1";
		break;
	case 6:
		npctalk "誰在那裡？";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + "::OnLeft2";
		break;
	case 7:
		npctalk "讓我來結束你吧！";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + "::OnRight2";
		break;
	}
	disablenpc();
	areamonster .@map$, (.@x-2), (.@y-2), (.@x+2), (.@y+2), "Special Guard", 3622, .@mob_count, .@label$;	// EP16_2_MM_S_GUARDS
	end;
}
1@slw,189,117,3	duplicate(Guard#gard01)	Guard#gard02	3622,4,4
1@slw,171,167,3	duplicate(Guard#gard01)	Guard#gard03	3622,4,4
1@slw,206,167,3	duplicate(Guard#gard01)	Guard#gard04	3622,4,4
1@slw,70,38,5	duplicate(Guard#gard01)	Guard#gard05	3622,4,4
1@slw,33,52,3	duplicate(Guard#gard01)	Guard#gard06	3622,4,4
1@slw,111,52,3	duplicate(Guard#gard01)	Guard#gard07	3622,4,4

1@slw,1,1,3	script	#security_device_lab	HIDDEN_WARP_NPC,{
	end;
OnLeft1:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnLeft1" ) < 1) {
		mapannounce 'map_slw$, "Security device L-01 has been activated.", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch01");
	}
	end;
OnRight1:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnRight1" ) < 1) {
		mapannounce 'map_slw$, "Security device R-01 has been activated.", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch02");
	}
	end;
OnLeft2:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnLeft2" ) < 1) {
		mapannounce 'map_slw$, "Security device L-02 has been activated.", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch11");
	}
	end;
OnRight2:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + "::OnRight2" ) < 1) {
		mapannounce 'map_slw$, "Security device R-02 has been activated.", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch12");
	}
	end;
}


// Room 2
// * Events - First entrance
1@slw,72,70,3	script	Eisen Werner#werner03	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "比您想像的要容易嗎？";
	mes "下一個安全設備相似，因此我不必再解釋了。";
	next;
	mes "[鋼鐵維爾納]";
	mes "環顧四周之後，您怎麼看？";
	mes "這不是很棒嗎？";
	mes "您去過 ^4d4dffverus ^000000嗎？";
	next;
	mes "[鋼鐵維爾納]";
	mes "那是我提到的實驗室。";
	mes "這是我的類型。";
	mes "......";
	next;
	mes "[鋼鐵維爾納]";
	mes "這項技術與我們到那時完全不同。";
	mes "我們已經使用了所有可用的東西。";
	next;
	mes "[鋼鐵維爾納]";
	mes "你怎麼說？";
	mes "看起來不錯嗎？";
	next;
	select("你想知道我為什麼說這些話嗎？");
	mes "[鋼鐵維爾納]";
	mes "嗯，那是因為無論如何您都會在這裡死。";
	mes "因此，我想促進自己的成就。";
	next;
	select("死在這裡？我？");
	mes "[鋼鐵維爾納]";
	mes "當然。你會在這裡死。";
	mes "您認為我很愚蠢，可以獨自一人留在這裡嗎？";
	next;
	mes "[鋼鐵維爾納]";
	mes "我知道您來這裡是符文米加特王國的婚外情。";
	mes "所以我已經準備了一些東西。";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "帶著巨大的感覺。";
	mes "你將死。就在這裡。";
	next;
	mes "[鋼鐵維爾納]";
	mes "因此，只是成為我的客人。";
	mes "讓我們移到下一個區域。";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner03");
		enablenpc instance_npcname("Security device#switch11");
		enablenpc instance_npcname("Security device#switch12");
	}
	end;
}


// Room 3
// * Events - First entrance
1@slw,57,141,3	script	Eisen Werner#werner04	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "在這堵牆之外，您將獲得一些東西。";
	next;
	mes "[鋼鐵維爾納]";
	mes "正確的。您將那件東西雕刻成一顆寶石，並用作皇冠上的裝飾。";
	mes "你甚至知道它是什麼嗎？";
	next;
	mes "[鋼鐵維爾納]";
	mes "儘管這是一個廢料，但它是伊米爾的心臟碎片。";
	mes "它不能與模仿。";
	next;
	mes "[鋼鐵維爾納]";
	mes "...王國中的傻瓜甚至無法利用他們已經擁有的力量。";
	mes "真可惜。";
	mes "所以我決定使用它...";
	next;
	mes "[鋼鐵維爾納]";
	mes "您想收回嗎？這一切都取決於您的能力。";
	mes "我沒有用空手等待。";
	next;
	mes "[鋼鐵維爾納]";
	mes "讓我再次告訴你。不要驚訝。";
	mes "不，如果您感到驚訝，我會很高興。";
	mes "呼呼呼……";
	next;
	cutin "ep16_eisen03.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "現在，看看我準備的禮物。";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner04");
		enablenpc instance_npcname("Central Entrance#door");
	}
	end;
}

1@slw,54,146,0	script	Central Entrance#door	WARPNPC,1,1,{
	end;
OnTouch:
	if (is_party_leader() == false) {
		warp 'map_slw$,55,153;
		end;
	}
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
		if ('event_final == 0) {
			'event_final = 1;
			enablenpc instance_npcname("Eisen Werner#werner05");
		}
		warp 'map_slw$,55,153;
		end;
	}
	switch( 'event_final ) {
	case 1:
		mes "重播故事。";
		close2;
		warp 'map_slw$,55,153;
		end;
	case 2:
		warp 'map_slw$,55,153;
		end;
	}
	mes "您在下一個區域中有兩個選擇。";
	mes "如果您想重播Seyren Windsor的故事，請選擇 ^4d4dffproceed with the Story ^000000。如果您想看到艾森·沃納（Eisen Werner）的傑作，請選擇 ^4d4dffproceed the Battle ^000000。";
	next;
	if (select("繼續講故事。", "繼續戰鬥。" ) == 1) {
		mes "您已經選擇了這個故事。";
		close2;
		if ('event_final == 0) {
			'event_final = 1;
			enablenpc instance_npcname("Eisen Werner#werner05");
		}
		warp 'map_slw$,55,153;
		end;
	}
	mes "您已經選擇了戰鬥。";
	close2;
	if ('event_final == 0) {
		'event_final = 2;
		enablenpc instance_npcname("Pet child#boss_master");
		enablenpc instance_npcname("#boss_master_dummy");
	}
	warp 'map_slw$,55,153;
	end;
}


// Room Boss
// * Story
1@slw,56,171,3	duplicate(dummy_npc)	Seyren Windsor#seiren	4_M_SEIREN_UC

1@slw,53,171,5	script	Eisen Werner#werner05	4_M_EISEN,{
	if (is_party_leader() == false) {
		mes "^4d4dffonly黨的負責人可以繼續進行此活動。 ^000000";
		close;
	}
	if (checkweight(501,1) == 0) {
		mes "- 無法繼續探索，因為您擁有的項目太多了。 -";
		close;
	}
	.@seyren$ = instance_npcname("Seyren Windsor#seiren");
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "這個地方是實驗室的核心，其中包含我的所有研究。";
	npctalk "這裡是實驗室的核心，包含了我研究的一切。";
	next;
	mes "[鋼鐵維爾納]";
	mes "伊米爾的心臟碎片具有構成世界的基本力量。它還擁有生命的起源。";
	npctalk "尤彌爾的心臟碎片，掌握著構成世界的根本力量。它也蘊藏著生命的起源。";
	next;
	mes "[鋼鐵維爾納]";
	mes "仔細看一下。";
	mes "您要收回的是做一些奇妙的事情！";
	npctalk "仔細看看這個。你來奪回的目的是做一些了不起的事情！";
	next;
	mes "[鋼鐵維爾納]";
	mes "從此流入地面上的魔術圈的能量...哈哈哈。當然，這個魔術圈已經神奇地重新詮釋了。";
	npctalk "由此提取的能量流入地面的魔法陣中……哈哈哈。當然，這個魔法陣已經被神奇地重新詮釋了。";
	next;
	mes "[鋼鐵維爾納]";
	mes "無論如何，能量進入了實驗主題...使用該力量，我已經調整了他們的時間。";
	next;
	cutin "ep16_eisen03.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "...當然，其中一些人由於切開的細胞而被殺死。哈哈！";
	next;
	select("這就是你想說的全部嗎？");
	unittalk getcharid(3), "" + strcharinfo(0) + " : Is that all you want to say?";
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "不！絕不！";
	mes "您是否已經忘記了我之前告訴您的話？";
	mes "我告訴過你我準備了禮物。";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "某物...";
	mes "那會讓你感到驚訝。";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	sleep2 3000;
	enablenpc .@seyren$;
	npctalk "......", .@seyren$;
	sleep2 1000;
	setpcblock PCBLOCK_NPC, false;
	unittalk getcharid(3), "" + strcharinfo(0) + " : ... Seyren?!";
	mes "[鋼鐵維爾納]";
	mes "您如何喜歡我準備的禮物？";
	mes "你滿意嗎？";
	cutin "ep16_eisen01.bmp",1;
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "這個傢伙是那個愚蠢的王室代表團之一。";
	mes "多虧了一個勇敢的傢伙的實驗，我得到了這些很棒的東西。";
	next;
	select("您是在談論體內實驗嗎？");
	unittalk getcharid(3), "" + strcharinfo(0) + " : Are you talking about the In Vivo Experiment?";
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "是的，到底是。";
	mes "好吧，除了那個傢伙之外，還有很多人在體內進行體內實驗，但他是第一個勇敢地增強生物的人。";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "使用模仿的YMIR的心臟碎片";
	mes "對於體內實驗來說，這是一個很好的轉折點。";
	next;
	mes "[鋼鐵維爾納]";
	mes "我試圖將其應用於多種東西。";
	next;
	mes "[鋼鐵維爾納]";
	mes "順便一提....";
	mes "你應該打個招呼。正確的？";
	next;
	mes "[鋼鐵維爾納]";
	mes "啊~。這是正確的。";
	mes "您不需要打招呼，因為你們兩個都會死！";
	cutin "ep16_eisen03.bmp",1;
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[鋼鐵維爾納]";
	mes "塞倫，護送這些客人去地獄。";
	mes "你了解我在說什麼嗎？";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[塞倫]";
	mes "…………";
	npctalk "......", .@seyren$;
	next;
	cutin "ep16_evt_ws.bmp",4;
	mes "[鋼鐵維爾納]";
	mes "唔？";
	mes "這是什麼，塞倫？現在是負階段嗎？";
	next;
	mes "[塞倫]";
	mes "我覺得我已經從長時間的睡眠中醒了。";
	mes "也許是這一天。";
	npctalk "我感覺自己好像從漫長的睡眠中醒來。也許就是為了這一天。", .@seyren$;
	next;
	mes "[鋼鐵維爾納]";
	mes "塞倫，不要忘記你的職責。";
	mes "你是武器。您只不過是一個帶有名為Seyren Windsor的騎士的木偶。";
	next;
	mes "[鋼鐵維爾納]";
	mes "您需要我逐步解釋嗎？";
	mes "在移植了伊米爾心臟的假人之後，身體無法忍受力量並最終崩潰。";
	next;
	mes "[鋼鐵維爾納]";
	mes "然後，它的精神重複了主體，並不斷地反複分裂。";
	mes "在此過程中，它的記憶，意識，靈魂，思想和自我都像沙子一樣散落。他們變得毫無意義。";
	next;
	mes "[鋼鐵維爾納]";
	mes "當我們將YMIR的心臟的假人移植到一個身心強壯的人中時，我們希望該人將成為一種更強大的生物武器。";
	next;
	mes "[鋼鐵維爾納]";
	mes "我們從來沒有想到它會復制並無休止地分裂。結果，該實驗被視為失敗。";
	next;
	mes "[鋼鐵維爾納]";
	mes "更糟糕的是，這是一個無法控制的生物，只充滿了憤怒。";
	next;
	mes "[塞倫]";
	mes "我在這裡。";
	npctalk "我在這兒。", .@seyren$;
	next;
	mes "[鋼鐵維爾納]";
	mes "你知道嗎？他們現在仍在分裂。";
	mes "只是模仿主體，它們甚至在其中生長。";
	next;
	mes "[鋼鐵維爾納]";
	mes "由於現在他們是不完美的生物，因此他們希望變得完美。";
	mes "他們現在是不完美的生物，因此他們希望變得完美，而不僅僅是模仿主體並將其作為虛擬圖像生活。";
	next;
	mes "[鋼鐵維爾納]";
	mes "他們希望作為一個單一實體存在。";
	next;
	mes "[鋼鐵維爾納]";
	mes "唯一成功的實驗是您。我們現在叫你塞倫。";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[塞倫]";
	mes "......這是您遺囑的終結嗎？";
	npctalk "……你的意志就這樣結束了嗎？", .@seyren$;
	next;
	cutin "ep16_eisen03.bmp",0;
	mes "[鋼鐵維爾納]";
	mes "嘿，寶貝？你不能對我這樣做。";
	mes "你必須服從我！";
	next;
	cutin "ep16_eisen02.bmp",0;
	mes "[鋼鐵維爾納]";
	mes "您不記得我努力將您帶出那個地獄有多努力嗎？";
	mes "不要被已經死亡的人的意識所震撼。";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[塞倫]";
	mes "...艾森·沃納（Eisen Werner）。";
	mes "讓我再次問你。這是您意志的終結嗎？";
	npctalk "...艾森維爾納。讓我再問你一次。這是你意志的終結嗎？", .@seyren$;
	next;
	cutin "ep16_eisen01.bmp",0;
	mes "[鋼鐵維爾納]";
	mes "該死！";
	mes "我從沒想到過！";
	mes "這個世界不是一個有趣的地方嗎？哈哈哈哈哈";
	next;
	mes "[鋼鐵維爾納]";
	mes "永遠不要以為我在這裡完成了。";
	mes "我也不需要失敗者。";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	specialeffect EF_BEGINSPELL3;
	sleep2 1000;
	specialeffect EF_SCREEN_QUAKE;
	sleep2 1000;
	specialeffect EF_LORD;
	disablenpc instance_npcname("Eisen Werner#werner05");
	sleep2 2000;
	setpcblock PCBLOCK_NPC, false;
	npctalk "哦....", .@seyren$;
	mes "[塞倫]";
	mes "...艾森。您已經放棄了這樣的一切。";
	npctalk "……艾森。你就這樣拋棄了一切。", .@seyren$;
	specialeffect2 EF_DEVIL3;
	cutin "ep16_seiren01.bmp",2;
	next;
	mes "[塞倫]";
	mes "..啊...";
	npctalk ".. 啊...", .@seyren$;
	cutin "ep16_seiren02.bmp",2;
	next;
	mes "[塞倫]";
	mes "堅固再次削弱。";
	mes "現在，我對您而不是艾森有一個更緊急的信息。";
	npctalk "The solidity is weakening again. Now, I have a much more urgent message for you rather than for Eisen.", .@seyren$;
	next;
	mes "[塞倫]";
	mes "我已經等你很長一段時間了。";
	mes "正如艾森（Eisen）所說，無論我是否塞倫（Seyren），這都不再重要。";
	npctalk "I've been waiting for you for a long time. As Eisen said, it's no longer important whether I'm Seyren or not.", .@seyren$;
	next;
	mes "[塞倫]";
	mes "由於我有思考的能力，所以我不知道自己是誰，也不知道自己的存在。";
	next;
	specialeffect2 EF_DEVIL4;
	mes "[塞倫]";
	mes "連續的劃分創造了我自己的無數副本。";
	next;
	mes "[塞倫]";
	mes "與伊米爾心臟的假人相關的力量，對我的意願和同伴的意願做出了回應。他們想團結起來，但不斷地劃分自己。";
	next;
	specialeffect2 EF_DEVIL5;
	cutin "ep16_seiren01.bmp",2;
	mes "[塞倫]";
	mes "到目前為止，我已經創建了多少份？";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[塞倫]";
	mes "在連續分裂之後，我失去了自我，但被恢復為先前的形狀。我認為這不是巧合。";
	next;
	mes "[塞倫]";
	mes "我認為一定有原因。";
	next;
	mes "[塞倫]";
	mes "我在查看 ^4d4dffterra gloria ^000000後意識到了這一點。";
	mes "上帝給了我這個機會的原因。";
	next;
	specialeffect2 EF_DEVIL6;
	cutin "ep16_seiren01.bmp",2;
	mes "[塞倫]";
	mes "你來回收這個嗎？";
	mes "...土地消失了，只有恆星保留。";
	next;
	specialeffect2 EF_DEVIL7;
	cutin "ep16_seiren02.bmp",2;
	mes "[塞倫]";
	mes "艾森是對的。 ^4d4dfffeyren Windsor不再在這個世界上。 ^000000";
	mes "我現在所擁有的只是不斷轉移一些東西的強大意願。";
	next;
	specialeffect2 EF_SCREEN_QUAKE;
	specialeffect2 EF_DEVIL8;
	cutin "ep16_seiren01.bmp",2;
	mes "[塞倫]";
	mes "也許那種意志使我存在。";
	mes "但是現在結束了。";
	cutin "ep16_seiren02.bmp",2;
	next;
	specialeffect2 EF_DEVIL9;
	cutin "ep16_seiren01.bmp",2;
	mes "[塞倫]";
	mes "…………";
	next;
	mes "[塞倫]";
	mes "我真的沒有更多的時間。";
	mes "此刻之後， ^4d4dffi將被分開並分散。 ^000000。";
	next;
	mes "[塞倫]";
	mes "雖然我現在仍然有遺囑，但我有一條消息可以給你。";
	next;
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
		mes "[塞倫]";
		mes "請，繼續。";
		mes "塞恩·溫莎（Seyren Windsor），prontera騎士在任務中失敗了。";
		next;
		mes "[塞倫]";
		mes "^4d4dffme和我所有的同伴很久以前就去世了。";
		mes "請不要浪費您對已經死亡的屍體的感情。 ^000000";
		next;
		mes "[塞倫]";
		mes "它們就像被果皮覆蓋的鬼魂一樣。";
		mes "我很幸運能在你面前展示我的最後一位遺囑。";
		next;
		mes "[塞倫]";
		mes "它們是我和我的同伴的 ^4d4dffrelics ^000000。";
		mes "請把他們交給我們的家人。";
		if (is_party_leader() == true) {
			getitem 25179,1;	// Blessing_Star
			getitem 23087,1;	// Small_Leather_Bag
			erasequest 7739;// Find Star of Blessing!
			setquest 7740;// Star of Blessing obtained
			terra_gloria_main = 22;
		}
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		specialeffect EF_ICECRASH, AREA, .@seyren$;
		disablenpc .@seyren$;
		// monster 'map_slw$,56,171, "Seyren", SEYREN, 1;
		sleep2 1000;
		specialeffect2 EF_SCREEN_QUAKE;
		sleep2 1000;
		setpcblock PCBLOCK_NPC, false;
		specialeffect2 EF_SCREEN_QUAKE;
		specialeffect2 EF_RAIN_PARTICLE2;
		warp "que_swat",155,50;
		end;
	}
	// daily
	mes "[塞倫]";
	mes "最終見到你真是太好了。";
	mes "謝謝...再見。";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	specialeffect EF_ICECRASH, AREA, .@seyren$;
	cloakonnpc .@seyren$;
	// monster 'map_slw$,56,171, "Seyren", SEYREN, 1;
	sleep2 1000;
	specialeffect2 EF_SCREEN_QUAKE;
	sleep2 1000;
	specialeffect2 EF_SCREEN_QUAKE;
	specialeffect2 EF_RAIN_PARTICLE2;
	setpcblock PCBLOCK_NPC, false;
	if (is_party_leader() == true)
		'event_final = 0;
	mapannounce 'map_slw$, "The setting of the central room has been initialized.", bc_map,0xFF99;
	warpparty 'map_slw$,54,134,getcharid(1),'map_slw$,2,2;
	disablenpc .@seyren$;
	end;
}

// * Battle
1@slw,55,170,3	duplicate(dummy_npc)	Pet child#boss_master	3621

1@slw,55,170,3	script	#boss_master_dummy	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	if (is_party_leader() == false)	// officially player can spawn multiple boss changing party leader
		end;
	setpcblock PCBLOCK_NPC, true;
	.@npc_name$ = instance_npcname("Pet child#boss_master");
	npctalk "你怎麼敢進來！你真勇敢！", .@npc_name$;
	sleep2 3000;
	npctalk "能與我戰鬥，你一定感到很榮幸！", .@npc_name$;
	sleep2 3000;
	npctalk "…………", .@npc_name$;
	sleep2 3000;
	npctalk ".. 該死的維爾納。為什麼把我和狼混在一起？", .@npc_name$;
	sleep2 3000;
	npctalk "A... anyway, you are dead meat.", .@npc_name$;
	sleep2 3000;
	setpcblock PCBLOCK_NPC, false;
	disablenpc instance_npcname("#boss_master_dummy");
	npctalk "蜜蜂準備好了！ ！ ！", .@npc_name$;
	specialeffect EF_DANCE_BLADE_ATK, AREA, instance_npcname("Pet child#boss_master");
	specialeffect EF_ALL_FULL_THROTTLE, AREA, instance_npcname("Pet child#boss_master");
	donpcevent instance_npcname("#boss_master_lab") + "::OnStart";
	mapannounce 'map_slw$, "The Magic field has been activated.", bc_map,0xFF99;
	cloakonnpc instance_npcname("Pet child#boss_master");
	end;
}

1@slw,1,1,3	script	#boss_master_lab	HIDDEN_WARP_NPC,5,5,{
	end;
OnStart:
	enablenpc instance_npcname("#boss_master_lab");
	if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + "::OnMobDead" ) < 1)
		monster 'map_slw$,55,170, "Pet child", 3621, 1, instance_npcname("#boss_master_lab") + "::OnMobDead";	// EP16_2_MM_CUTIE
	end;
OnMobDead:
	if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + "::OnMobDead" ) < 1) {
		mapannounce 'map_slw$, "The Magic field has been eliminated. An entrance has been created.", bc_map,0xFF99;
		enablenpc instance_npcname("Emergency exit#exit_war");
		disablenpc instance_npcname("#boss_master_lab");
	}
	end;
}
