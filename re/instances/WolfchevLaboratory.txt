//===== rAthena Script =======================================
//= Wolfchev's Laboratory
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Defeat Wolfchev's human experimentations.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//= 1.1 Moved the Enchant (Sorcerer#Bio4Reward) and Reward
//=		(Weird old man#Bio4Reward) NPC in merchant folder. [Capuche]
//============================================================

// Giacomo Girolam
//============================================================
lighthalzen,303,303,6	script	Giacomo Girolamo#kiup2	4_M_KHMAN,3,3,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	if (lhz_curse > 30) {
		if (!lght_duk01) {
			if (Sex == SEX_MALE) {
				emotion ET_HNG;
				mes "[詹姆斯·杰羅姆]";
				mes "....一個男人....？";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "啊!嘿！男人！";
				next;
				select("這是怎麼回事？");
				emotion ET_QUESTION, playerattached();
				mes "[詹姆斯·杰羅姆]";
				mes "您是否聽說過Rekenber Corporation犯下的可怕人類實驗，其總部位於Lighthalzen？";
				next;
				select("哇啊？ ！人體實驗？");
				emotion ET_HUK, playerattached();
				mes "[詹姆斯·杰羅姆]";
				mes "是的！人類實驗！ ！";
				mes "有很多謠言說，實際上是進行了可怕的人類實驗，但是這些人在貧民窟中……不想相信由於雷肯伯的福利運動而不想相信這一點。";
				next;
				emotion ET_PROFUSELY_SWEAT;
				mes "[詹姆斯·杰羅姆]";
				mes "出色地！那些有錢的人似乎知道一些東西，但他們從未提及...";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "啊！這是一個人知道的秘密。我從Rekenber Lab。";
				next;
				emotion ET_SCRATCH;
				mes "[詹姆斯·杰羅姆]";
				mes "反正！ ！一個名叫沃爾夫的科學家。突然被解雇了。我認為他必須與這個謠言有聯繫...";
				next;
				select("你是說……沃爾夫切夫？");
				emotion ET_SURPRISE;
				mes "[詹姆斯·杰羅姆]";
				mes "哦？ ！是的，沃爾夫切夫！沒錯，名字叫沃爾夫切夫..瘋狂的科學家。";
				next;
				emotion ET_OK;
				mes "[詹姆斯·杰羅姆]";
				mes "我想您可以更快地理解這個故事...我確定他參與了謠言。我發誓。";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "順便說一句..你呢？您對他有什麼了解嗎？";
				next;
				if (select("Pretend you know nothing.", "Tell him what you know.") == 1) {
					mes "["+ strcharinfo(0) +"]";
					mes "並不真地。我聽說過他的名字，但我對他一無所知。";
					next;
					mes "[詹姆斯·杰羅姆]";
					mes "嗯....你聽起來可疑，老兄！嗯？";
					next;
				} else {
					mes "["+ strcharinfo(0) +"]";
					mes "他碰巧研究了人類的“ Homunculus”，而沒有告訴Rekenber Corp.，並且有一個向公司報告的舉報人。所以他被解雇了。";
					next;
					emotion ET_BEST;
					mes "[詹姆斯·杰羅姆]";
					mes "哦！男人..！您似乎知道很多！有機會，您是否關心Rekenber？";
					next;
					emotion ET_THINK;
					mes "["+ strcharinfo(0) +"]";
					mes "......................................";
					next;
				}
				emotion ET_SCRATCH;
				mes "[詹姆斯·杰羅姆]";
				mes "不用擔心。我沒什麼意思。";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "出色地！我只是好奇，只是因為貧民窟和一些冒險家失了，";
				mes "我聽說Rekenber正在與失人員進行人類實驗。希望你不要誤會我的意思。";
				next;
				lght_duk01 = 1;
				setquest 5109;
				mes "實際上，這不是一個有趣的謠言。";
				mes "應該調查實驗進行實驗的實驗室。";
				close;
			} else {
				emotion ET_THROB;
				mes "[詹姆斯·杰羅姆]";
				mes "哦！多麼美麗的女士！";
				next;
				emotion ET_QUESTION;
				mes "[詹姆斯·杰羅姆]";
				mes "在一個有危險的謠言的地方，這裡的美麗怎麼樣？";
				next;
				switch(select("Walking...", "To hang out...", "Shopping...")) {
				case 1:
					mes "[詹姆斯·杰羅姆]";
					mes "啊..女士，即使您只是走路，您也是如此優雅.....";
					next;
					break;
				case 2:
					mes "[詹姆斯·杰羅姆]";
					mes "女士。即使您在這個可憐的小鎮閒逛，您也是一位莊重的女士...";
					next;
					break;
				case 3:
					mes "[詹姆斯·杰羅姆]";
					mes "啊..女士..在貧民窟購物！";
					next;
					break;
				}
			}
			select("………………");
			mes "[詹姆斯·杰羅姆]";
			mes "好吧，我敢問你，對女士的所作所為感興趣...";
			next;
			emotion ET_OK;
			mes "[詹姆斯·杰羅姆]";
			mes "我只是希望那位女士以危險的謠言離開這個小鎮。";
			next;
			select("危險的謠言？");
			emotion ET_QUESTION, playerattached();
			mes "[詹姆斯·杰羅姆]";
			mes "啊...女士可能沒有聽說過謠言。";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "最近，有些來自貧民窟的城鎮的人……有些冒險家失了...";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "然後謠言出現了，因為失案件經常發生，雷肯伯（Rekenber）在其秘密實驗室進行了可怕的人類實驗。";
			next;
			select("打擾一下？ ！人體實驗？");
			emotion ET_HUK, playerattached();
			mes "[詹姆斯·杰羅姆]";
			mes "是的！人類實驗！ ！";
			mes "有很多謠言說，實際上是進行了可怕的人類實驗，但是這些人在貧民窟中……不想相信由於雷肯伯的福利運動而不想相信這一點。";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "出色地！有錢人要么想相信..我想..啊！幾週前。嗯。他叫什麼名字？沃爾夫切夫??";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "反正！ ！這項研究從實驗室被解僱。我認為他必須參與這個謠言。";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "所以，拜託，女士。請不要在這樣的地方保持安全的方式。";
			next;
			lght_duk01 = 1;
			setquest 5109;
			mes "................................................................................................................";
			mes "應該調查實驗進行實驗的實驗室。";
			close;
		} else {
			mes "[詹姆斯·杰羅姆]";
			if (Sex == SEX_MALE) {
				emotion ET_WRAP;
				mes "照顧好自己，不要消失！";
			} else {
				emotion ET_BIGTHROB;
				mes "小心，女士。";
			}
			close;
		}
	} else {
		if (Sex == SEX_MALE) {
			emotion ET_DELIGHT;
			mes "[詹姆斯·杰羅姆]";
			mes "只有那些美麗的女士是我一生的原因~！";
			mes "我，giacomo girolamo casanova de seingalt我從來都不對像你這樣的人感興趣！";
			next;
			emotion ET_FRET;
			mes "[詹姆斯·杰羅姆]";
			mes "但是我可以聽你的...你想對我說什麼嗎？";
			next;
			switch(select("No..?", "About ladies..", "You Womanizer!!", "Envy you...", "What's the rumor lately?")) {
			case 1:
				emotion ET_SCRATCH;
				mes "[詹姆斯·杰羅姆]";
				mes "嘿，你，那就離開我的路。";
				close;
			case 2:
				emotion ET_FRET;
				mes "[詹姆斯·杰羅姆]";
				mes "我不想談論我的寶貴女士取笑他們。你和你的朋友一起做。";
				close;
			case 3:
				mes "[詹姆斯·杰羅姆]";
				mes "嘿..對不起！ ！ ！但是我不是一個“女人”，我只是一個崇拜女士的人。";
				next;
				emotion ET_OK;
				mes "[詹姆斯·杰羅姆]";
				mes "當然.....是的...如果我是女性，地球上的所有男人也是！我只是卡薩諾瓦。 UMHAHAHA。";
				close;
			case 4:
				emotion ET_QUESTION;
				mes "[詹姆斯·杰羅姆]";
				mes "哈..你....嗎？ ？沒有任何女人，但是你周圍的姐姐和母親？";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "......................................";
				next;
				emotion ET_PROFUSELY_SWEAT;
				mes "[詹姆斯·杰羅姆]";
				mes "哦......對不起..我明白了。";
				close;
			case 5:
				mes "[詹姆斯·杰羅姆]";
				mes "你..不像你看起來嗎？有趣的..";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "是的，實際上最近有很多貧民窟的人和一些流浪的冒險家消失了。我不知道為什麼。";
				next;
				mes "[詹姆斯·杰羅姆]";
				mes "你也要小心..";
				close;
			}
		} else {
			emotion ET_QUESTION;
			mes "[詹姆斯·杰羅姆]";
			mes "嘿，女士？";
			next;
			mes "[詹姆斯·杰羅姆]";
			switch(rand(1,10)) {
			case 1:
				mes "當你從天上掉下來時，這不是很痛苦嗎？";
				next;
				break;
			case 2:
				mes "你一定很累。因為你總是在徘徊我的想法。";
				next;
				break;
			case 3:
				mes "您昨晚不厭倦在我的夢中走來走去嗎？";
				next;
				break;
			case 4:
				mes "打擾一下。但是我想我失去了心。你能藉我嗎？";
				next;
				break;
			case 5:
				mes "你相信一見鍾情嗎？還是我們可以重新開始..？";
				next;
				break;
			case 6:
				mes "我很抱歉，但是，你能和一個剛認識的人親吻嗎？努？好的，讓我自我介紹。";
				next;
				break;
			case 7:
				mes "我在這裡迷路了，因為這是第一次。你能告訴我哪種通往你的心？";
				next;
				break;
			case 8:
				mes "請給我一張地圖..要找到一種方法。 ...我陷入了你的眼睛。";
				next;
				break;
			case 9:
				mes "你的眼睛....我想永遠在那裡。";
				next;
				break;
			case 10:
				mes "在任何困難情況下，我保證我將成為您的一切。";
				next;
				break;
			}
			emotion ET_BIGTHROB;
			mes "[詹姆斯·杰羅姆]";
			mes "今天是我們見面的第一天，但我愛你，女士<3";
			next;
			mes "[詹姆斯·杰羅姆]";
			mes "我告訴你這是因為我只是擔心你。您需要離開這個充滿失案件的城鎮。";
			close;
		}
	}

OnTouch:
	if (!lght_duk01)
		emotion (Sex == SEX_MALE? ET_HNG : ET_BIGTHROB), "Giacomo Girolamo#kiup2";
	end;
}

// Warp portal to the 4th Fl.
//============================================================
lhz_dun03,239,78,1	script	lhz_dun03_lhz_dun04	WARPNPC,1,1,{
	end;

OnTouch:
	if (lght_duk01 > 0 && lght_duk01 < 6) {
		mes "為了調查人類的實驗，由於聲音可怕，我不想下降。";
		close2;
		if (lght_duk01 < 3) {
			warp "que_lhz", 245, 56;
		} else if (lght_duk01 == 3) {
			warp "que_lhz", 96, 136;
		} else {
			warp "que_lhz", 148, 215;
		}
	} else {
		if (lhz_curse < 31) {
			mes "- 吹口哨的聲音 -";
			mes "從下面，有一個可怕的聲音與風融合在一起。";
			close2;
		}
		warp "lhz_dun04", 245, 56;
	}
	end;
}

// Warp Portal to the 3rd Fl.
//============================================================
lhz_dun04,244,61,1	warp	lhz_dun04_lhz_dun03	1,1,lhz_dun03,240,75

// Inside the Lab
//============================================================
que_lhz,242,50,0	script	#01Startpoint	-1,3,3,{
	end;

OnInit:
	initnpctimer;
	end;

OnTimer60000:
	mapannounce "que_lhz", "Man's whisper: Pl.. please.. save me.. I don't wanna die.. wouaaaa..", bc_map, "0x7DCBF0";
	end;

OnTimer120000:
	mapannounce "que_lhz", "Woman's whisper: .. Save.. aaaah.. us...", bc_map, "0x7DCBF0";
	end;

OnTimer180000:
	mapannounce "que_lhz", "Kid's whisper: Bawwww~ Where am I? Mama......aaaa....", bc_map, "0x7DCBF0";
	end;

OnTimer240000:
	mapannounce "que_lhz", "Oldman's whisper: Kirk...hel...help......", bc_map, "0x7DCBF0";
	end;

OnTimer300000:
	stopnpctimer;
	initnpctimer;
	end;

OnTouch:
	if (lght_duk01 == 1) {
		mes "我到達了一個研究機構，似乎已經使用了，直到這些天來一直使用。";
		next;
		mes "有一些破壞的部分，但基本設施看起來不錯。";
		next;
		lght_duk01 = 2;
		mes "再走一點...";
		close;
	}
	end;
}

que_lhz,206,74,4	script	Researcher#02	4_LGTSCIENCE,3,3,{
	end;

OnTouch:
	if (lght_duk01 == 2) {
		emotion ET_HUK, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "你！ ！ ！ ！是沃爾夫切夫嗎？";
		next;
		emotion ET_KIK;
		mes "[沃爾夫切夫]";
		mes "人！ Kaahaha。人們傾向於討厭這個地方，你為什麼在這裡？";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "你！ ！ ！我聽說您正在進行人類的實驗。沃爾夫切夫！";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "而且我看到您仍在犯下！ ！ ！";
		next;
		emotion ET_KIK;
		mes "[沃爾夫切夫]";
		mes "基希希。我可以看到你對我有所了解。那是你指責我的人嗎？";
		mes "這樣做後，你怎麼敢回到我身邊。基希希";
		next;
		mes "[沃爾夫切夫]";
		mes "我很高興見到人";
		mes "很長一段時間以來，我得走了。基希希。";
		next;
		mes "- 沃爾夫切夫（Wolfchev）逃跑了，發出尷尬的笑聲。 -";
		next;
		erasequest 5109;
		setquest 5110;
		lght_duk01 = 3;
		emotion ET_HUK, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "哇！沃爾夫切夫，停下來！";
		close2;
		warp "que_lhz", 96, 136;
	}
	end;
}

que_lhz,94,119,4	script	Researcher#03	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	if (lght_duk01 == 3) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[沃爾夫切夫]";
		mes "（喘著粗氣）PSHAW ...您仍在關注我。嘿，你為什麼繼續關注我？";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "沃爾夫切夫..你以前說過。您正在為每個人進行研究。";
		next;
		mes "[沃爾夫切夫]";
		mes "我可能已經說了....那呢？";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "但是，您的實驗似乎真的很危險。這是每個人都想要的？";
		next;
		emotion ET_KIK;
		mes "[沃爾夫切夫]";
		mes "每個人都渴望變得更強壯，並希望統治他人，因此其他人服從並欣賞您。你沒有那種慾望嗎？";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "......................................";
		next;
		mes "[沃爾夫切夫]";
		mes "如果您想節省自己，應該停止關注我。 Bugger離開。";
		next;
		mes "沃爾夫切夫再次以一種奇怪的聲音逃跑。";
		next;
		lght_duk01 = 4;
		mes "["+ strcharinfo(0) +"]";
		mes "awwww！你！ ！再次逃跑.....停止！";
		close2;
		warp "que_lhz", 148, 215;
	}
	end;
}

que_lhz,147,224,4	script	Researcher#04	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	if (lght_duk01 == 4) {
		mes "[沃爾夫切夫]";
		mes "你很持久，不是嗎？還是我認為........你喜歡我，是嗎？基希伊。";
		next;
		emotion ET_THINK;
		mes "["+ strcharinfo(0) +"]";
		mes "......................................";
		next;
		specialeffect EF_SIGHTRASHER;
		mes "沃爾夫切夫以沉默嘲笑你，並按下了一個按鈕。然後幾內亞豬在他周圍醒來。";
		next;
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "Human Guinea pig#0"+.@i+"::OnEnable";
		mes "[沃爾夫切夫]";
		mes "再次..是時候說再見了。";
		next;
		emotion ET_SURPRISE;
		mes "[沃爾夫切夫]";
		mes "出色地！或許....";
		next;
		mes "[沃爾夫切夫]";
		mes "如果您來找我進行實驗，我可以更好地對待您。";
		next;
		donpcevent "#Eventctrl::OnEnable";
		progressbar "FF00FF", 5;
		emotion ET_HUK;
		mes "[沃爾夫切夫]";
		mes "哦，我的...發生了什麼事？";
		next;
		donpcevent "#Eventctrl::OnEnable2";
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "Human Guinea pig#0"+.@i+"::OnDisable";
		mes "[沃爾夫切夫]";
		mes "不！不要來找我！不！ ！";
		next;
		lght_duk01 = 5;
		mes "首先，我必須處理那些豚鼠！";
		close;
	} else if (lght_duk01 == 5) {
		.@i = rand(1,3);
		if (.@i == 2) {
			mes "沃爾夫切夫（Wolfchev）感到困惑，現在不知道這些怪物也在攻擊他。";
			next;
			specialeffect EF_STUNATTACK;
			specialeffect EF_STUNATTACK;
			mes "- 打他叫醒他 -";
			next;
			specialeffect EF_DARKBREATH;
			emotion ET_STARE_ABOUT;
			mes "["+ strcharinfo(0) +"]";
			mes "嘿，伙計！無論如何，讓我們先離開這裡！沒有任何地方無法到達嗎？";
			next;
			mes "[沃爾夫切夫]";
			mes "哦，然後我的....我的實驗室....到我的實驗室...";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "你的實驗室??好吧，那在哪裡？";
			next;
			mes "[沃爾夫切夫]";
			mes "在北部.......。離這裡不遠。";
			next;
			erasequest 5110;
			setquest 5111;
			lght_duk01 = 6;
			mes "轉到'^0000ffwolfchev的實驗室^000000'在12點的方向...";
			close;
		} else {
			emotion ET_CONFUSE;
			mes "沃爾夫切夫（Wolfchev）感到困惑，現在不知道這些怪物也在攻擊他。";
			close;
		}
	} else if (lght_duk01 == 6) {
		mes "轉到'^0000ffwolfchev的實驗室^000000'在12點的方向...";
		close;
	}
	end;
}

// Warp to lhz_dun04 from Wolfchev's Lab
//============================================================
que_lhz,148,251,0	script	#Move2lab	-1,12,0,{
	end;

OnTouch:
	if (lght_duk01 == 6) {
		mes "我到達了'^0000ffwolfchev的實驗室^000000'。";
		close2;
		warp "lhz_dun04", 148, 269;
		end;
	} else {
		mes "我仍然與沃爾夫切夫有關。";
		close;
	}
}

// Event Controller
//============================================================
que_lhz,1,1,0	script	#Eventctrl	-1,{
	end;

OnEnable:
	initnpctimer;
	end;

OnEnable2:
	monster "que_lhz", 139, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 224, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 224, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	end;

OnTimer1000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#01";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#01";
	end;

OnTimer2000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#02";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#02";
	end;

OnTimer3000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#03";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#03";
	end;

OnTimer4000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#04";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#04";
	end;

OnTimer5000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#05";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#05";
	end;

OnTimer6000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#06";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#06";
	end;

OnTimer7000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#07";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#07";
	end;

OnTimer8000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#08";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#08";
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount("que_lhz", "#Eventctrl::OnMyMobDead") < 4)
		killmonster "que_lhz", "#Eventctrl::OnMyMobDead";
}
que_lhz,139,232,0	script	#01	-1,{ end; }
que_lhz,147,232,0	script	#02	-1,{ end; }
que_lhz,156,232,0	script	#03	-1,{ end; }
que_lhz,156,224,0	script	#04	-1,{ end; }
que_lhz,156,215,0	script	#05	-1,{ end; }
que_lhz,147,215,0	script	#06	-1,{ end; }
que_lhz,139,215,0	script	#07	-1,{ end; }
que_lhz,139,224,0	script	#08	-1,{ end; }

// Human Guinea pigs
//============================================================
que_lhz,140,231,1	script	Human Guinea pig#01	2224,{
	end;

OnInit:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	end;

OnDisable:
	specialeffect EF_FLASHER;
	disablenpc();
	end;
}
que_lhz,147,231,0	duplicate(Human Guinea pig#01)	Human Guinea pig#02	2224
que_lhz,155,231,7	duplicate(Human Guinea pig#01)	Human Guinea pig#03	2224
que_lhz,155,224,6	duplicate(Human Guinea pig#01)	Human Guinea pig#04	2224
que_lhz,155,216,5	duplicate(Human Guinea pig#01)	Human Guinea pig#05	2224
que_lhz,147,216,4	duplicate(Human Guinea pig#01)	Human Guinea pig#06	2224
que_lhz,140,216,3	duplicate(Human Guinea pig#01)	Human Guinea pig#07	2224
que_lhz,140,224,2	duplicate(Human Guinea pig#01)	Human Guinea pig#08	2224

// Instance Creation
//============================================================
lhz_dun04,151,276,3	script	Researcher#memo	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000you由於擁有太多物品而無法繼續該過程。 ^000000";
		close;
	}
	.@quest_time = checkquest(5112, PLAYTIME);
	.@killed_bosses = true;
	for(.@quest_id = 5113; .@quest_id <= 5125; .@quest_id++) {
		if (checkquest(.@quest_id, HUNTING) <= 1)
			.@killed_bosses = false;
	}
	if (lght_duk01 < 6) {
		mes "[研究員]";
		mes "什麼！你在這裡有生意嗎？";
		next;
		if (select("Yes I do!!", "Umm.. No..") == 1) {
			mes "[研究員]";
			mes "為什麼不停止盯著別人的實驗室並前進？";
			close;
		}
		mes "[研究員]";
		mes "別想在這裡想知道，並在路上。你會？ khh khh。";
		close;
	} else if (lght_duk01 == 6) {
		mes "就像沃爾夫切夫所說的那樣，似乎實驗生物不能靠近實驗室。";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "嘿，有什麼方法可以阻止這些生物嗎？你應該知道一些東西，你做了這些東西！";
		next;
		mes "[沃爾夫切夫]";
		mes "urrgg ..我沒想到他們會像這樣失去控制...";
		next;
		mes "[沃爾夫切夫]";
		mes "這整個事情怎麼了？";
		next;
		mes "[沃爾夫切夫]";
		mes "URR ..我認為他們會破壞我的整個實驗室！";
		next;
		emotion ET_BLABLA, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "嘿！沃爾夫切夫！";
		next;
		mes "[沃爾夫切夫]";
		mes "你！ ？嘿，請阻止他們！";
		mes "生物是 ^9f6077在相同的工作領域 ^000000的機構連接。";
		next;
		mes "[沃爾夫切夫]";
		mes "有一個特殊的生物是他們的 ^46b951boss位置 ^000000 ...";
		mes "如果您可以停用該生物，那麼所有 ^a1bb44slave生物 ^000000在這裡走來走去將停止移動。";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "老闆生物？";
		mes "嘿！沃爾夫切夫！ ^46b951boss生物在哪裡 ^000000？";
		next;
		mes "[沃爾夫切夫]";
		mes "內部..實驗室..現在應該喚醒。破壞我的工作生活真是可悲，但我不能再忍受了。";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "我如何進入沃爾夫切夫的實驗室？";
		next;
		mes "[沃爾夫切夫]";
		mes "哦..等等！";
		mes "我會幫助您進入那裡。";
		next;
		mes "[沃爾夫切夫]";
		mes "好消息是，即使是奴隸，也不會設置老闆生物一次醒來。";
		next;
		erasequest 5111;
		lght_duk01 = 7;
		for(.@i = 5113; .@i <= 5125; .@i++)
			setquest .@i;
		mes "[沃爾夫切夫]";
		mes "但是，這確實是危險的，因為我們不知道他們何時會從那裡逃脫。";
		mes "因此，請為您的黨員做好準備，並照顧那些老闆生物。我不確定這個地方可以持續多長時間。";
		close;
	} else if (lght_duk01 > 6) {
		if (getcharid(1) > 0) {
			mes "[沃爾夫切夫]";
			mes "你們都在那兒嗎？如果您是該黨的領導者，我將準備入口。";
			next;
			switch(select("Any warnings?", "Going into the laboratory", "Do not enter")) {
			case 1:
				mes "[沃爾夫切夫]";
				mes "我不知道按下緊急警報會導致此類問題。管理系統似乎已更改為緊急模式。";
				next;
				mes "[沃爾夫切夫]";
				mes "實驗室內部設置了太多系統，所以夏娃我現在無法確定係統如何扭曲。";
				next;
				mes "[沃爾夫切夫]";
				mes "啊!如果緊急系統正在激活，則只有黨的領導人才能控制一切。我了解這是很大的壓力。";
				next;
				mes "[沃爾夫切夫]";
				mes "就這樣，您就知道，請不要出於好奇而觸摸隨機的東西。我不想你受傷。";
				close;
			case 2:
				if (.@quest_time == 0) {
					mes "[沃爾夫切夫]";
					mes "對不起，但看來您仍然無法進入實驗室。你以後再回來嗎？該系統有點棘手。";
					close;
				} else {
					//if (.@quest_time == 2)
						//recall_completequest 5112;
					if (isbegin_quest(5112))
						erasequest 5112;
					if (!.@killed_bosses) {
						if (lght_duk01 == 9) {
							mes "[沃爾夫切夫]";
							mes "你回來了，ka ha ha。";
							next;
							mes "[沃爾夫切夫]";
							mes "離開後，我正在修復一些系統零件。然後突然發生了奇怪的事情。";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "那是什麼？";
							next;
							mes "[沃爾夫切夫]";
							mes "您關心的所有老闆生物都開始重生。";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "什麼..怎麼可能...？ ！";
							next;
							mes "[沃爾夫切夫]";
							mes "我在這裡也完全迷失了..但是，這不應該發生。";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "......................................";
							next;
							mes "[沃爾夫切夫]";
							mes "如您所見，您是我唯一可以信任的人。您和您的聚會會再次幫助我停用這些生物嗎？";
							next;
							if (select("I am not sure", "Why not") == 1) {
								mes "[沃爾夫切夫]";
								mes "我懂了。我了解您為什麼會猶豫，不想再按下您。但是，我仍然不確定你為什麼要去那裡，";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[沃爾夫切夫]";
								mes "非常感謝。"+strcharinfo(0)+"";
								mes "我將立即幫助您進入實驗室。";
								next;
							}
						} else {
							mes "[沃爾夫切夫]";
							mes "嗯..那些老闆生物再次再生。";
							next;
							mes "[沃爾夫切夫]";
							mes "你能再幫我一次嗎？";
							next;
							if (select("I am not sure", "Why not") == 1) {
								mes "[沃爾夫切夫]";
								mes "我懂了。我了解您為什麼會猶豫，不想再按下您。但是，我仍然不確定你為什麼要去那裡，";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[沃爾夫切夫]";
								mes "謝謝。"+strcharinfo(0)+"";
								mes "您想快速進入那裡，對嗎？";
								next;
							}
						}
					}
				}
				if (is_party_leader() == true) {
					if (instance_create("Wolfchev's Laboratory") < 0) {
						mes "[沃爾夫切夫]";
						mes "嗯..實驗室輸入系統有點奇怪。會等我檢查系統嗎？";
						close;
					}
					mes "[沃爾夫切夫]";
					mes "請堅持...";
					next;
					mes "沃爾夫切夫開始控制某些齒輪。";
					next;
					progressbar "FF00FF", 2;
					mes "[沃爾夫切夫]";
					mes "好吧！我已將LAN輸入系統代碼為";
					mes "^0000ff"+ strcharinfo(1) +"^000000 party leader's name ^0000ff"+ strcharinfo(0) +"^000000!!.";
					next;
					mes "[沃爾夫切夫]";
					mes "現在，您的聚會可以自由地通過入口。";
					close;
				} else {
					mes "[沃爾夫切夫]";
					mes "您不是派對 ^000000的 ^0000fleader，是嗎？請去找領導者。";
					close;
				}
			case 3:
				mes "[沃爾夫切夫]";
				mes "需要更多準備嗎？拜託，我們沒有一整天。";
				close;
			}
		} else {
			mes "[沃爾夫切夫]";
			mes "您要去那裡嗎？那是自殺！我們的實驗室進入系統並不像您想的那樣容易。";
			close;
		}
	} else {
		mes "[沃爾夫切夫]";
		mes "哇！發生了一些奇怪的事情。如果這種情況一直在發生，也許我們應該嘗試其他方式。";
		close;
	}
}

lhz_dun04,147,279,0	script	Laboratory Entrance#memo	CLEAR_NPC,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000you由於擁有太多物品而無法繼續該過程。 ^000000";
		close;
	}
	.@lhz_time = checkquest(5112, PLAYTIME);
	if (lght_duk01 < 7) {
		mes "它說，";
		mes "'^0000ff ****的實驗室成員^000000'";
		close;
	}
	if (.@lhz_time == -1) {
		mes "這是通往沃爾夫切夫實驗室的入口。入口處有些how叫。";
		next;
		if (select("Go inside", "Think one more time") == 2) {
			mes "您已經停止進入沃爾夫切夫的實驗室。";
			close;
		}
		if (instance_enter("Wolfchev's Laboratory") != IE_OK) {  // probably missing failure cases
			mes "^ff0000000000";
			mes ""+ strcharinfo(0) +". . .";
			mes "^ff0000unregistered人員^000000";
			next;
			pushpc 0, 10;
			percentheal -5,0;
			specialeffect EF_AUTOCOUNTER;
			specialeffect2 EF_BASH;
			mes "您已受到實驗室入口系統攻擊的傷害。";
			close;
		}
		mapannounce "lhz_dun04","Laboratory entrance system ["+ strcharinfo(1) +"] member ["+ strcharinfo(0) +"] access granted..",bc_map,"0x0DF297";
		setquest 5112;
		//warp "1@lhz", 45, 148;
		close;
	} else if (.@lhz_time == 0) {
		mes "滴滴滴-";
		next;
		mes ""+ strcharinfo(0) +". . .";
		mes "^ff0000 forbidden人員^000000";
		next;
		mes "該系統拒絕您的入口。";
		close;
	} else if (.@lhz_time >= 1) {
		mes "滴滴滴-";
		next;
		//if (.@lhz_time == 2)
			//recall_completequest 5112;
		erasequest 5112;
		mes ""+ strcharinfo(0) +". . .";
		mes "^ff0000Access否認已被停用。 ^000000";
		close;
	} else {
		mes "^ff000000ror！錯誤！ ^000000";
		mes "^ff0000please重試。 ^000000";
		close;
	}
}

// The Lab No.1 : System 1_1
//============================================================
1@lhz,35,108,4	script	Lab1#1_1	CLEAR_NPC,{
	end;

OnInstanceInit:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnType_02:
	donpcevent instance_npcname("Manual Sheet#1_1")+"::OnEnable";
	donpcevent instance_npcname("Valve#1_2")+"::OnEnable";
	donpcevent instance_npcname("Valve#1_3")+"::OnEnable";
	mapannounce instance_mapname("1@lhz"),"[Security System] the persons concerned, access with the emergency access method.",bc_map,"0xff9977";
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice! Intruder Detected.",bc_map,"0xff9977";
	end;

OnTimer2000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Lab No.1's security system will be activated.",bc_map,"0xff9977";
	end;

OnTimer7000:
	donpcevent instance_npcname("MobCtrl1#mob01")+"::OnEnable";
	end;
}

// The Lab No.1 Monster Control
//============================================================
1@lhz,37,108,4	script	MobCtrl1#mob01	CLEAR_NPC,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	mapannounce .@map$,"[Security System] Security system Activated !! Experimental animals are released. The persons concerned have to evacuate.",bc_map,"0xff9977";
	areamonster .@map$,35,159,50,169,"Starving Lab animal",2242,rand(10,15),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;

OnTimer180000:
	stopnpctimer;
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("Lab1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Lab No.1's security system has been stopped.",bc_map,"0xff9977";
	} else {
		initnpctimer;
	}
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("Lab1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Lab No.1's security system has been stopped.",bc_map,"0xff9977";
		stopnpctimer;
	}
	end;
}

// Emergency Access Valve Manual
//============================================================
1@lhz,39,168,0	script	Manual Sheet#1_1	CLEAR_NPC,{
	mes "地板上有閥門手動紙。";
	next;
	if (is_party_leader() == true) {
		mes "您是黨的負責人，開始閱讀手冊。";
		next;
		mes "[緊急訪問閥手冊]";
		switch(lght_duk02) {
		case 0:
			switch(rand(1,6)) {
			case 1:
				lght_duk02 = 1;
				mes "兩次順時針旋轉左閥，一次逆時針，順時針旋轉一次";
				break;
			case 2:
				lght_duk02 = 2;
				mes "順時針旋轉一次左閥，逆時針兩次，順時針旋轉一次";
				break;
			case 3:
				lght_duk02 = 3;
				mes "順時針旋轉一次左閥，逆時針一次，順時針兩次";
				break;
			case 4:
				lght_duk02 = 4;
				mes "逆時針逆時針兩次，順時針旋轉一次，一次逆時針旋轉一次";
				break;
			case 5:
				lght_duk02 = 5;
				mes "逆時針逆時針旋轉右閥，兩次順時針，逆時針旋轉一次";
				break;
			case 6:
				lght_duk02 = 6;
				mes "逆時針逆時針旋轉一次，順時針旋轉一次，逆時針兩次";
				break;
			default:
				mes "這是一個未知錯誤。如果您再嘗試一次後會遇到同樣的問題，請聯繫GM。";
				break;
			}
			break;
		case 1:
			mes "兩次順時針旋轉左閥，一次逆時針，順時針旋轉一次";
			break;
		case 2:
			mes "順時針旋轉一次左閥，逆時針兩次，順時針旋轉一次";
			break;
		case 3:
			mes "順時針旋轉一次左閥，逆時針一次，順時針兩次";
			break;
		case 4:
			mes "逆時針逆時針兩次，順時針旋轉一次，一次逆時針旋轉一次";
			break;
		case 5:
			mes "逆時針逆時針旋轉右閥，兩次順時針，逆時針旋轉一次";
			break;
		case 6:
			mes "逆時針逆時針旋轉一次，順時針旋轉一次，逆時針兩次";
			break;
		default:
			mes "這是一個未知的錯誤。如果您再嘗試一次後會遇到同樣的問題，請聯繫GM。";
			break;
		}
	} else {
		mes "讓您的黨負責人處理閥門。";
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Manual Sheet#1_1");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Manual Sheet#1_1");
	end; 
}

// Left Valve
//============================================================
1@lhz,41,172,0	script	Valve#1_2	CLEAR_NPC,{
	.@open_portal = 0;
	mes "這是用於緊急訪問的左閥。";
	if (is_party_leader() == true) {
		next;
		mes "您想向哪個方向轉向？";
		next;
		if (lght_duk02 < 1 || lght_duk02 > 3) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("Clockwise", "Counterclockwise");
				mes "- 金屬聲音 -";
				if (.@i != 4)
					mes "而且，向哪個方向呢？";
				next;
			}
		} else {
			// lght_duk02 == 1 : Clockwise, Clockwise, Counterclockwise, Clockwise
			// lght_duk02 == 2 : Clockwise, Counterclockwise, Counterclockwise, Clockwise
			// lght_duk02 == 3 : Clockwise, Counterclockwise, Clockwise, Clockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("Clockwise", "Counterclockwise")) {
				case 1:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 1) || (.@i == 3 && lght_duk02 == 3)) {
						.@open_portal += 1;
					}
				case 2:
					if ((.@i == 2 && lght_duk02 != 1) || (.@i == 3 && lght_duk02 != 3)) {
						.@open_portal += 1;
					}
				}
				mes "- 金屬聲音 -";
				if (.@i != 4)
					mes "而且，向哪個方向呢？";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "- 開放 -";
			next;
			mes "連接到實驗室2的門現已打開。";
		} else {
			lght_duk02 = 0;
			mes "什麼都沒有發生。";
			next;
			mes "您需要查看手冊。";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Valve#1_2");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Valve#1_2");
	end;
}

// Right Valve
//============================================================
1@lhz,52,172,0	script	Valve#1_3	CLEAR_NPC,{
	.@open_portal = 0;
	mes "這是用於緊急訪問的左閥。";
	if (is_party_leader() == true) {
		next;
		mes "您想向哪個方向轉向？";
		next;
		if (lght_duk02 < 4 || lght_duk02 > 6) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("Clockwise", "Counterclockwise");
				mes "- 金屬聲音 -";
				if (.@i != 4)
					mes "而且，向哪個方向呢？";
				next;
			}
		} else {
			// lght_duk02 == 4 : Counterclockwise, Counterclockwise, Clockwise, Counterclockwise
			// lght_duk02 == 5 : Counterclockwise, Clockwise, Clockwise, Counterclockwise
			// lght_duk02 == 6 : Counterclockwise, Clockwise, Counterclockwise, Counterclockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("Clockwise", "Counterclockwise")) {
				case 1:
					if ((.@i == 2 && lght_duk02 != 4) || (.@i == 3 && lght_duk02 != 6))
						.@open_portal += 1;
					break;
				case 2:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 4) || (.@i == 3 && lght_duk02 == 6))
						.@open_portal += 1;
					break;
				}
				mes "- 金屬聲音 -";
				if (.@i != 4)
					mes "而且，向哪個方向呢？";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "- 開放 -";
			next;
			mes "連接到實驗室2的門現已打開。";
		} else {
			lght_duk02 = 0;
			mes "什麼都沒有發生。";
			next;
			mes "您需要查看手冊。";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Valve#1_3");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Valve#1_3");
	end;
}


// The Lab No.1 >> The Lab No.2 Warp portal
//============================================================
1@lhz,45,173,0	script	#potal_01	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_01");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.2 is now open.",bc_map,"0xff9977";
	enablenpc instance_npcname("#potal_01");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 151, 29;
	end;
}

// The Lab No.2
//============================================================
1@lhz,151,29,0	script	#2_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#2_0");
	end;

OnDisable:
	disablenpc instance_npcname("#2_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab2#2_1")+"::OnEnable";
	donpcevent instance_npcname("#2_0")+"::OnDisable";
	end;
}

// The Lab No.2 2_1 System - Control of Monster Wave and Pipe right before exploding
//============================================================
1@lhz,35,106,4	script	Lab2#2_1	CLEAR_NPC,{
	end;

OnInstanceInit:
OnDisable:
	stopnpctimer;
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice! Intruder Detected in The Lab No.2!!",bc_map,"0xff9977";
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Lab No.2 Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure in the Lab No.2 is too high.",bc_map,"0xff99ff";
	end;

OnTimer8000:
	mapannounce instance_mapname("1@lhz"),"[Security System] First security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On01";
	end;

OnTimer10000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer190000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer303000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Second Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer308000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Second security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On02";
	end;

OnTimer370000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer550000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer603000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Third Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer608000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Third security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On03";
	end;

OnTimer730000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer903000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Fourth Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer908000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fourth security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On04";
	end;

OnTimer910000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer1090000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer1203000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Fifth Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer1208000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fifth security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On05";
	end;

OnTimer1270000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	stopnpctimer;
	end;
}

// The Lab No.2 2_2 System - Pipe Operation Control
// When 3 pipes explode, system will shut down.
//============================================================
1@lhz,37,106,4	script	Lab2#2_2	CLEAR_NPC,{
	end;

OnInstanceInit:
	'broken_pipes = 0;
	stopnpctimer;
	end;

OnPipePressure:
	switch(rand(1,6)) {
	case 1:
		donpcevent instance_npcname("Valve#2_1")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_01";
	case 2:
		donpcevent instance_npcname("Valve#2_2")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_02";
	case 3:
		donpcevent instance_npcname("Valve#2_3")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_03";
	case 4:
		donpcevent instance_npcname("Valve#2_4")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_04";
	case 5:
		donpcevent instance_npcname("Valve#2_5")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_05";
	case 6:
		donpcevent instance_npcname("Valve#2_6")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_06";
	}
	initnpctimer;
	end;

OnDisableValve1:
	disablenpc instance_npcname("Valve#2_1");
	stopnpctimer;
	end;

OnDisableValve2:
	disablenpc instance_npcname("Valve#2_2");
	stopnpctimer;
	end;

OnDisableValve3:
	disablenpc instance_npcname("Valve#2_3");
	stopnpctimer;
	end;

OnDisableValve4:
	disablenpc instance_npcname("Valve#2_4");
	stopnpctimer;
	end;

OnDisableValve5:
	disablenpc instance_npcname("Valve#2_5");
	stopnpctimer;
	end;

OnDisableValve6:
	disablenpc instance_npcname("Valve#2_6");
	stopnpctimer;
	end;

OnTimer63000:
	for(.@i = 1; .@i < 7; .@i++) {
		specialeffect EF_SUI_EXPLOSION, AREA, instance_npcname("Valve#2_"+.@i+"");
		disablenpc instance_npcname("Valve#2_"+.@i+"");
	}
	'broken_pipes += 1;
	if ('broken_pipes == 3) {
		donpcevent instance_npcname("#2_3")+"::OnEnable";
		stopnpctimer;
	} else {
		mapannounce instance_mapname("1@lhz"),"[Management System] Pipe explosion detected! When "+ (3 - 'broken_pipes) +" more break, the system will stop.",bc_map,"0xff99ff";
	}
	end;
}

// The Lab No.2 2_3 System
//============================================================
1@lhz,151,48,0	script	#2_3	-1,20,20,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2_3");
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Management System] System Freezes! System Freezes! Evacuate from the Lab in 10 seconds.",bc_map,"0xff99ff";
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Evacuate!!",bc_map,"0xff99ff";
	enablenpc instance_npcname("#2_3");
	end;

OnTouch:
	warp "lhz_dun04", 147, 273;
	end;
}

// The Lab No.2 Monster Control
//============================================================
1@lhz,39,106,4	script	MobCtrl2#mob02	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,10,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,20,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,16,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,19,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,20,.@label$;
	end;

On2_01:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),155,43,161,49,"Starving Lab animal",2243,5,.@label$;
	end;

On2_02:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,40,143,46,"Starving Lab animal",2243,5,.@label$;
	end;

On2_03:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,51,143,57,"Starving Lab animal",2243,5,.@label$;
	end;

On2_04:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),143,56,149,62,"Starving Lab animal",2243,5,.@label$;
	end;

On2_05:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),160,48,166,54,"Starving Lab animal",2243,5,.@label$;
	end;

On2_06:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),135,44,141,50,"Starving Lab animal",2243,5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_02")+"::OnEnable";
		donpcevent instance_npcname("Lab2#2_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Security system of The Lab No.2 has been stopped.",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.2 Valves
//============================================================
-	script	bio4FLab2Valve	-1,{
	if (is_party_leader() == true) {
		progressbar "0xffff00", 20;
		stopnpctimer;
		donpcevent instance_npcname("Lab2#2_2")+"::OnDisableValve1";
		disablenpc();
		end;
	} else {
		mes "讓您的黨負責人處理閥門。";
		close;
	}

OnInstanceInit:
	disablenpc();
	end;

OnEnable:
	enablenpc();
	initnpctimer;
	end;

OnTimer3000:
	specialeffect EF_DRAGONSMOKE,AREA,instance_npcname(strnpcinfo(0));
	stopnpctimer;
	initnpctimer;
	end;
}
1@lhz,158,46,0	duplicate(bio4FLab2Valve)	Valve#2_1	CLEAR_NPC
1@lhz,138,43,0	duplicate(bio4FLab2Valve)	Valve#2_2	CLEAR_NPC
1@lhz,139,54,0	duplicate(bio4FLab2Valve)	Valve#2_3	CLEAR_NPC
1@lhz,145,60,0	duplicate(bio4FLab2Valve)	Valve#2_4	CLEAR_NPC
1@lhz,165,51,0	duplicate(bio4FLab2Valve)	Valve#2_5	CLEAR_NPC
1@lhz,137,47,0	duplicate(bio4FLab2Valve)	Valve#2_6	CLEAR_NPC

// The Lab No.2 >> The Lab No.3 Warp portal(General)
//============================================================
1@lhz,151,64,0	script	#potal_02	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_02");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.3 is now open.",bc_map,"0xff99ff";
	enablenpc instance_npcname("#potal_02");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 84, 28;
	end;
}

// The Lab No.3
//============================================================
1@lhz,84,28,0	script	#3_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#3_0");
	end;

OnDisable:
	disablenpc instance_npcname("#3_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab3#3_1")+"::OnEnable";
	donpcevent instance_npcname("#3_0")+"::OnDisable";
	end;
}

// The Lab No.3 3_1 System - Monster wave control
//============================================================
1@lhz,35,104,4	script	Lab3#3_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice!",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::OnDisable";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Intruder Detected in The Lab No.3!!",bc_map,"0xff9977";
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Security system of The Lab No.3 will start working.",bc_map,"0xff9977";
	end;

OnTimer12000:
	mapannounce instance_mapname("1@lhz"),"[Security System] First security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On01";
	end;

OnTimer192000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Second security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On02";
	end;

OnTimer372000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Third security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On03";
	end;

OnTimer552000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fourth security system of The Lab No.3activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On04";
	end;

OnTimer732000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fifth security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On05";
	stopnpctimer;
	end;
}

// The Lab No.3 Monster Control
//============================================================
1@lhz,37,104,4	script	MobCtrl3#mob03	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2243,1,.@label$;		
	monster .@map$,86,60,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2243,1,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount2,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount1,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount2,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@amount5 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2243,.@amount4,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount5,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2243,.@amount5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_03")+"::OnEnable";
		donpcevent instance_npcname("Lab3#3_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Security system of The Lab No.3 has been stopped.",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.3 >> Boss's Room Warp portal
//============================================================
1@lhz,83,62,0	script	#potal_03	WARPNPC,2,2,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#potal_03");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.4 is now open.",bc_map,"0x7799ff";
	enablenpc instance_npcname("#potal_03");
	end;

OnDisable:
	disablenpc instance_npcname("#potal_03");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 137, 100;
	end;
}

// The Lab No.4
//============================================================
1@lhz,137,100,0	script	#4_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#4_0");
	end;

OnDisable:
	disablenpc instance_npcname("#4_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab4#4_1")+"::OnEnable";
	donpcevent instance_npcname("#4_0")+"::OnDisable";
	end;
}

// The Lab No.4 4_1 System - Summon of Boss Monster
//============================================================
1@lhz,35,102,4	script	Lab4#4_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Who are you....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Seyren Windsor#boss01");
	end;

OnTimer2000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Cecile Damon#boss02");
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Do not approach more...",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Gertie Wie#boss03");
	end;

OnTimer4000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Eremes Guile#boss04");
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"Whisper: This is a very dangerous place..",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Trentini#boss05");
	end;

OnTimer6000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Chen Liu#boss06");
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"Whisper: You won't go out alive....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Alphochio Basil#boss07");
	end;

OnTimer8000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Flamel Emul#boss08");
	end;

OnTimer9000:
	mapannounce instance_mapname("1@lhz"),"Whisper: If you don't want to be a part of us....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Randel Lawrence#boss09");
	end;

OnTimer10000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Celia Alde#boss10");
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Leave now....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Kathryne Keyron#boss11");
	end;

OnTimer12000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Margaretha Sorin#boss12");
	end;

OnTimer13000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Before this place is thoroughly filled with sorrow and pain....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Howard Alt-Eisen#boss13");
	end;

OnTimer14000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Gren#boss14");
	end;

OnTimer15000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Roke#boss15");
	end;

OnTimer16000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Dree#boss16");
	end;

OnTimer17000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Ah....it....it's too late......",bc_map,"0x7DCBF0";
	donpcevent instance_npcname("MobCtrl4#mob04")+"::OnEnable";
	stopnpctimer;
	end;
}

// Area No. 4 - Boss Summon Control
//============================================================
1@lhz,37,102,4	script	MobCtrl4#mob04	CLEAR_NPC,{
	end;

OnEnable:
	switch(rand(1,13)) {
	case 1:
		donpcevent instance_npcname("Seyren Windsor#boss01")+"::OnEnable";
		end;
	case 2:
		donpcevent instance_npcname("Cecile Damon#boss02")+"::OnEnable";
		end;
	case 3:
		donpcevent instance_npcname("Gertie Wie#boss03")+"::OnEnable";
		end;
	case 4:
		donpcevent instance_npcname("Eremes Guile#boss04")+"::OnEnable";
		end;
	case 5:
		donpcevent instance_npcname("Trentini#boss05")+"::OnEnable";
		end;
	case 6:
		donpcevent instance_npcname("Chen Liu#boss06")+"::OnEnable";
		end;
	case 7:
		donpcevent instance_npcname("Alphochio Basil#boss07")+"::OnEnable";
		end;
	case 8:
		donpcevent instance_npcname("Flamel Emul#boss08")+"::OnEnable";
		end;
	case 9:
		donpcevent instance_npcname("Randel Lawrence#boss09")+"::OnEnable";
		end;
	case 10:
		donpcevent instance_npcname("Celia Alde#boss10")+"::OnEnable";
		end;
	case 11:
		donpcevent instance_npcname("Kathryne Keyron#boss11")+"::OnEnable";
		end;
	case 12:
		donpcevent instance_npcname("Margaretha Sorin#boss12")+"::OnEnable";
		end;
	case 13:
		donpcevent instance_npcname("Howard Alt-Eisen#boss13")+"::OnEnable";
		end; 
	}

OnDisable:
	//stopnpctimer;
	end;
}

// Random Experiments in Tubes
//============================================================
1@lhz,156,148,3	script	Type_**Gren#boss14	4_M_03,{ end; }
1@lhz,152,152,3	script	Type_**Roke#boss15	4_M_HUMAN_01,{ end; }
1@lhz,148,156,3	script	Type_**Dree#boss16	4_M_DWARF,{ end; }

// Bosses
//============================================================
-	script	bio4FBoss	-1,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	specialeffect EF_SUI_EXPLOSION;

	if (strnpcinfo(0) == "Seyren Windsor#boss01")
		monster .@map$,129,154,"Load Knight Seyren",1646,1,.@label$;
	else if (strnpcinfo(0) == "Cecile Damon#boss02")
		monster .@map$,125,150,"Sniper Cecile",1650,1,.@label$;
	else if (strnpcinfo(0) == "Gertie Wie#boss03")
		monster .@map$,121,146,"Stalker Gertie",2239,1,.@label$;
	else if (strnpcinfo(0) == "Eremes Guile#boss04")
		monster .@map$,117,142,"Assassin Cross Eremes",1647,1,.@label$;
	else if (strnpcinfo(0) == "Trentini#boss05")
		monster .@map$,117,125,"Gypsy Trentini",2241,1,.@label$;
	else if (strnpcinfo(0) == "Chen Liu#boss06")
		monster .@map$,121,121,"Champion Chen",2238,1,.@label$;
	else if (strnpcinfo(0) == "Alphochio Basil#boss07")
		monster .@map$,125,117,"Crown Alphochio",2240,1,.@label$;
	else if (strnpcinfo(0) == "Flamel Emul#boss08")
		monster .@map$,129,113,"Creator Flamel",2236,1,.@label$;
	else if (strnpcinfo(0) == "Randel Lawrence#boss09")
		monster .@map$,146,113,"Paladin Randel",2235,1,.@label$;
	else if (strnpcinfo(0) == "Celia Alde#boss10")
		monster .@map$,150,117,"Professor Celia",2237,1,.@label$;
	else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
		monster .@map$,154,121,"High Wizard Kathryne",1651,1,.@label$;
	else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
		monster .@map$,158,125,"High Priest Margaretha",1649,1,.@label$;
	else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
		monster .@map$,158,142,"White Smith Howard",1648,1,.@label$;

	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	disablenpc();
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		.@map$ = instance_mapname("1@lhz");

		if (strnpcinfo(0) == "Seyren Windsor#boss01")
			mapannounce .@map$,"Awww... Where are all my fellows....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Cecile Damon#boss02")
			mapannounce .@map$,"Any..one, please help.. please.....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Gertie Wie#boss03")
			mapannounce .@map$,".. did I act too.. rashly...?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Eremes Guile#boss04")
			mapannounce .@map$,"Ah..... I am absolutely defeated....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Trentini#boss05")
			mapannounce .@map$,"ah.. If I.. ever can ... dance and.... sing...happily....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Chen Liu#boss06")
			mapannounce .@map$,"I.... am so .. sorry....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Alphochio Basil#boss07")
			mapannounce .@map$,"...the new song......was ....for.....her....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Flamel Emul#boss08")
			mapannounce .@map$,"Ha...I wanted to show.... the new potion....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Randel Lawrence#boss09")
			mapannounce .@map$,"Wooooo.. everyone got defeated.....??",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Celia Alde#boss10")
			mapannounce .@map$,"No..... this experiment is.....dang...",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
			mapannounce .@map$,"awww.. is everyone Ok....?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
			mapannounce .@map$,"My good boy.. Don't cry... ah.... our Great Odin.. please save us...",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
			mapannounce .@map$,"oh...no.... can't breath.... and my sister...?? sister!!",bc_map,"0xFD3B02";

		donpcevent instance_npcname("Wolfchev#last")+"::OnEnable";
		donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
	}
	end;
}
1@lhz,127,156,5	duplicate(bio4FBoss)	Seyren Windsor#boss01	1646
1@lhz,123,152,5	duplicate(bio4FBoss)	Cecile Damon#boss02	1650
1@lhz,119,148,5	duplicate(bio4FBoss)	Gertie Wie#boss03	2239
1@lhz,115,144,5	duplicate(bio4FBoss)	Eremes Guile#boss04	1647
1@lhz,115,123,7	duplicate(bio4FBoss)	Trentini#boss05	2241
1@lhz,119,119,7	duplicate(bio4FBoss)	Chen Liu#boss06	2238
1@lhz,123,115,7	duplicate(bio4FBoss)	Alphochio Basil#boss07	2240
1@lhz,127,111,7	duplicate(bio4FBoss)	Flamel Emul#boss08	2236
1@lhz,148,111,1	duplicate(bio4FBoss)	Randel Lawrence#boss09	2235
1@lhz,152,115,1	duplicate(bio4FBoss)	Celia Alde#boss10	2237
1@lhz,156,119,1	duplicate(bio4FBoss)	Kathryne Keyron#boss11	1651
1@lhz,160,123,1	duplicate(bio4FBoss)	Margaretha Sorin#boss12	1649
1@lhz,160,144,3	duplicate(bio4FBoss)	Howard Alt-Eisen#boss13	1648

// Wolfchev - Final encounter
//============================================================
1@lhz,137,156,7	script	Wolfchev#last	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^ff0000檢查您的庫存。您有太多的項目無法執行任務。 ^000000";
		close;
	}
	for(.@i = 5113; .@i <= 5125; .@i++) {
		switch( checkquest(.@i,HUNTING) ) {
		case -1:
			break;
		case 0:
		case 1:
			.@all_quests_complete += 1;
			break;
		case 2:
			.@all_quests_complete += 2;
			break;
		}
	}
	if (.@all_quests_complete == 26) {
		// Quests Complete. Get Rewards.
		if (lght_duk01 == 8) {
			// First time beating the instance.
			mes "[沃爾夫切夫]";
			mes "您終於擊敗了我的老闆生物。";
			next;
			mes "[沃爾夫切夫]";
			mes "最後。這是結果嗎？我為誰實驗？他們欺騙了我嗎？";
			next;
			mes "[沃爾夫切夫]";
			mes "該按鈕應該按下緊急情況...但是..";
			next;
			mes "[沃爾夫切夫]";
			mes "嘿...你和我遇到了一個不良的聯繫...。";
			next;
			mes "[沃爾夫切夫]";
			mes "但是....，你幫助我...你怎麼能做到？";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "當我第一次見到你時，即使您可以進行那些殘酷而恐怖的實驗，您也似乎很生氣，但很難過。";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "但是實際上，您之所以這樣做是因為您很孤獨。";
			next;
			mes "[沃爾夫切夫]";
			mes "哈哈哈哈。孤獨...你說？";
			next;
			mes "[沃爾夫切夫]";
			mes "可能會這樣看。但是，不是。孤獨只是失敗者的好藉口。";
			next;
			mes "[沃爾夫切夫]";
			mes "我很高興做實驗追求完美，我從不感到孤獨。";
			next;
			mes "[沃爾夫切夫]";
			mes "無論如何，由於我意識到Rekenber出賣了我，所以我不能讓他們做他們想做的事。";
			next;
			mes "[沃爾夫切夫]";
			mes "目前，我會讓他們一個人呆著，但是我會讓他們哭泣，並為試圖刪除沃爾夫切夫而感到遺憾。";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "………………………………";
			next;
			lght_duk01 = 9;
			callsub L_Reward;
			mes "[沃爾夫切夫]";
			mes "啊啊啊，"+ strcharinfo(0) +" Thank you so much to remove my boys who had gone mad. Not a big gift but I wish you don't deny, I don't need this anymore.";
			next;
			mes "[沃爾夫切夫]";
			mes "而且，如果您可以...希望您經常回來，尤其是與朋友在一起。我希望這個地方再次與人心臟在一起，以便這裡的悲傷消失了。";
			next;
			mes "離開他的實驗室之前，我再次看著沃爾夫切夫。我能感覺到他不再著迷了。";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		} else {
			mes "[沃爾夫切夫]";
			mes "您確實擊敗了所有老闆生物。再次感謝。";
			next;
			callsub L_Reward;
			mes "[沃爾夫切夫]";
			mes "我再說一遍"+ strcharinfo(0) +" Thank you so much to remove my boys who had gone mad. Not a big gift but I wish you don't deny, I don't need this anymore.";
			next;
			mes "[沃爾夫切夫]";
			mes "哈哈哈，再次見到你。您想現在離開這裡嗎？";
			next;
			if (select("Not yet", "Yes") == 1) {
				mes "[沃爾夫切夫]";
				mes "然後告訴我你什麼時候。";
				close;
			}
			mes "[沃爾夫切夫]";
			mes "那我會送你出去。";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		}
	} else {
		if (.@all_quests_complete == 0) {
			mes "[沃爾夫切夫]";
			mes "我看著系統。 ...不知道是誰做的.....但是，得到的是如此復雜..沒有想法...";
			next;
		} else {
			mes "[沃爾夫切夫]";
			mes "kakakaah ..謝謝。您很好地處理了那些老闆生物...";
			next;
		}
		if (lght_duk01 == 7) {
			lght_duk01 = 8;
			mes "[沃爾夫切夫]";
			mes "當你掙扎的時候...";
			next;
			mes "[沃爾夫切夫]";
			mes "我弄清楚了一些東西...還有一個系統錯誤，這不會讓我們自由地走，所以您可以在72小時內幫助我...在回來之前，請去某個地方休息。";
			next;
			mes "[沃爾夫切夫]";
			mes "啊...我可以把你送出我的實驗室。你想要？";
			next;
		} else {
			mes "[沃爾夫切夫]";
			mes "您想現在離開這裡嗎？";
			next;
		}
		if (select("Not yet", "Yes") == 1) {
			mes "[沃爾夫切夫]";
			mes "然後告訴我你什麼時候。";
			close;
		}
		mes "[沃爾夫切夫]";
		mes "那我會送你出去。";
		close2;
		warp "lhz_dun04", 147, 273;
		end;
	}

L_Reward:
	for(.@i = 5113; .@i <= 5125; .@i++) {
		erasequest .@i;
	}
	.@r = rand(1, 1000);
	if      (.@r < 84)  getitem 2582,  1; //Salvage_Cape
	else if (.@r < 167) getitem 18570, 1; //Ancient_Gold_Deco
	else if (.@r < 250) getitem 1490,  1; //Giant_Lance
	else if (.@r < 333) getitem 16017, 1; //Bloody_Cross
	else if (.@r < 416) getitem 1291,  1; //Guillotine_Katar
	else if (.@r < 499) getitem 1584,  1; //Chilly_Spell_Book
	else if (.@r < 582) getitem 6471, 10; //Goast_Chill
	else if (.@r < 665) getitem 6470, 10; //Blood_Thirst
	else if (.@r < 748) getitem 6469, 10; //Will_Of_Warrior
	else if (.@r < 831) getitem 6471, 20; //Goast_Chill
	else if (.@r < 914) getitem 6470, 20; //Blood_Thirst
	else                getitem 6469, 20; //Will_Of_Warrior
	return;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Wolfchev#last");
	end;

OnEnable:
	enablenpc instance_npcname("Wolfchev#last");
	end;
}
