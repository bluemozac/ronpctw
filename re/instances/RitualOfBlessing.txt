//===== rAthena Script =======================================
//= Instance Ritual of Blessing.
//===== Description: =========================================
//- [Walkthrough conversion].
//- Required in Banquet main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

2@mir,100,17,5	script	Skia#skia01	4_EP16_SKIA,{
	mes "[滑雪]";
	mes "歡迎！";
	mes "我一直在想你什麼時候到達。";
	next;
	mes "[新山]";
	mes "你在這裡！";
	mes "來自其他家庭的人們都坐下了。";
	next;
	mes "[新山]";
	mes "順便一提.........";
	mes "我離開宴會廳後";
	mes "....不，沒關係。";
	npctalk "...？", instance_npcname("Skia#skia01");
	next;
	mes "[新山]";
	mes "如果我們讓他們等待再等待，什麼也不會聽到。讓我們開始。";
	next;
	mes "[滑雪]";
	mes "^4d4dffthere是理查德勳爵旁邊留下的座位。它必須是為觀察者保留的座位。 ^000000";
	next;
	mes "[滑雪]";
	mes "好吧，走吧。";
	close2;
	enablenpc instance_npcname("Skia Nerius#skia02");
	enablenpc instance_npcname("Nyhill M. Heine#nihil02");
	disablenpc instance_npcname("Skia#skia01");
	disablenpc instance_npcname("Nyhill#nihil01");
	end;
}
2@mir,103,17,3	duplicate(Skia#skia01)	Nyhill#nihil01	4_EP16_NIHIL

2@mir,107,77,0	script	#royal_evt02	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch_:
	disablenpc instance_npcname("#royal_evt02");
	donpcevent instance_npcname("#royal_evt02") + "::OnEvent";// player can move
	end;
OnEvent:
	disablenpc instance_npcname("Skia#skia01");	// extra
	disablenpc instance_npcname("Nyhill#nihil01");
	enablenpc instance_npcname("Skia Nerius#skia02");
	enablenpc instance_npcname("Nyhill M. Heine#nihil02");

	mapannounce 'map_2mir$, "Kronecker: Although there has been a slight delay, let me proceed.", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker: According to the Royal Assembly which has passed down in the palace,", bc_map,0xFF99;
	sleep 3000;
	npctalk "海涅", instance_npcname("Friedrich S. Heine#stol_1");
	npctalk "海涅", instance_npcname("Nyhill M. Heine#nihil02");
	sleep 3000;
	npctalk "海神屬", instance_npcname("Skia Nerius#skia02");
	npctalk "海神屬", instance_npcname("Spica Nerius#spica01");
	sleep 3000;
	npctalk "瓦爾特", instance_npcname("Isenhonor Walther#eisen");
	npctalk "瓦爾特", instance_npcname("Ian Walther#ian01");
	sleep 3000;
	npctalk "維格納", instance_npcname("Levuiere Wigner#louvier1");
	npctalk "維格納", instance_npcname("Jurgen Wigner#jurgen01");
	npctalk "維格納", instance_npcname("Isaac Wigner#isaac01");
	npctalk "維格納", instance_npcname("Katrin Wigner#kath01");
	sleep 3000;
	npctalk "喬博格", instance_npcname("Catherine D. Geoborg#ca1");
	npctalk "喬博格", instance_npcname("Ernst Geoborg#ernst01");
	sleep 3000;
	npctalk "理查德", instance_npcname("Po Richard#po01");
	sleep 3000;
	npctalk "羅根堡", instance_npcname("Maximilian Roegenburg#m1");
	npctalk "羅根堡", instance_npcname("Wolf Roegenburg#wolf01");
	npctalk "羅根堡", instance_npcname("Helmut Roegenburg#helmu");
	npctalk "羅根堡", instance_npcname("Agnes Roegenburg#agnes0_1");
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker: On the behalf of the seven royal families supporting Rune-Midgarts,", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker: Here is the new representative appointed", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker: The blessing star light may reach the glory of the earth...", bc_map,0xFF99;
	sleep 3000;
	npctalk "Please allow the power from the source granted for all beings,", instance_npcname("Friedrich S. Heine#stol_1");
	npctalk "Please allow the power from the source granted for all beings,", instance_npcname("Nyhill M. Heine#nihil02");
	sleep 3000;
	npctalk "The courage to take a step forward without turning back, and stab the enemy with a dagger,", instance_npcname("Catherine D. Geoborg#ca1");
	npctalk "The courage to take a step forward without turning back, and stab the enemy with a dagger,", instance_npcname("Ernst Geoborg#ernst01");
	sleep 3000;
	npctalk "The heart to embrace everything in this world,", instance_npcname("Skia Nerius#skia02");
	npctalk "The heart to embrace everything in this world,", instance_npcname("Spica Nerius#spica01");
	sleep 3000;
	npctalk "The strong will to protect everything in the arms,", instance_npcname("Po Richard#po01");
	sleep 3000;
	npctalk "The intact pride which never bends in the justice and faith no matter what,", instance_npcname("Isenhonor Walther#eisen");
	npctalk "The intact pride which never bends in the justice and faith no matter what,", instance_npcname("Ian Walther#ian01");
	sleep 3000;
	npctalk "The virtue of looking up to others with a humble mind,", instance_npcname("Maximilian Roegenburg#m1");
	npctalk "The virtue of looking up to others with a humble mind,", instance_npcname("Wolf Roegenburg#wolf01");
	npctalk "The virtue of looking up to others with a humble mind,", instance_npcname("Helmut Roegenburg#helmu");
	npctalk "The virtue of looking up to others with a humble mind,", instance_npcname("Agnes Roegenburg#agnes0_1");
	sleep 3000;
	npctalk "以及看待真相的智慧", instance_npcname("Levuiere Wigner#louvier1");
	npctalk "以及看待真相的智慧", instance_npcname("Jurgen Wigner#jurgen01");
	npctalk "以及看待真相的智慧", instance_npcname("Isaac Wigner#isaac01");
	npctalk "以及看待真相的智慧", instance_npcname("Katrin Wigner#kath01");
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker: To our new king...", bc_map,0xFF99;
	sleep 1500;
	mapannounce 'map_2mir$, "I am sorry to interrupt the fun!", bc_map,0xDDDDDD;
	sleep 500;
	specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Peter Heine#peter01");
	disablenpc instance_npcname("Peter Heine#peter01");
	enablenpc instance_npcname("Fallen Peter#peter02");
	sleep 500;
	mapannounce 'map_2mir$, "Kronecker: Peter!!!", bc_map,0xFF99;
	sleep 500;
	disablenpc instance_npcname("Kronecker G. Heine#gran1");
	.@kronecker_2$ = instance_npcname("Kronecker G. Heine#gran2");
	enablenpc .@kronecker_2$;
	sleep 500;
	.@intruder$ = instance_npcname("Intruder#hunter01");
	enablenpc .@intruder$;
	npctalk "Oh, hello! You must be the famous Archduke Grantz the Sage!", .@intruder$;
	sleep 3000;
	npctalk "你在幹什麼！ ！ ！", .@kronecker_2$;
	sleep 3000;
	npctalk "No, nothing much. Nothing worth doing here anyways.", .@intruder$;
	sleep 3000;
	npctalk "聽說這裡藏著一些很不錯的東西……", .@intruder$;
	sleep 3000;
	npctalk "但這是什麼？這是假的！", .@intruder$;
	specialeffect EF_BEGINSPELL,AREA,.@kronecker_2$;
	sleep 500;
	specialeffect EF_BEGINSPELL6,AREA, .@intruder$;
	specialeffect EF_LEVEL99,AREA, .@intruder$;
	sleep 2000;
	npctalk "You old man, and the noble people behind him...", .@intruder$;
	sleep 3000;
	npctalk "把它放在那裡。這一點也不好玩。我可能想毀掉一些東西。", .@intruder$;
	sleep 3000;
	npctalk "例如...", .@intruder$;
	sleep 3000;
	npctalk "這個贗品很像尤彌爾之心！", .@intruder$;
	sleep 3000;
	npctalk "……你想要什麼？", .@kronecker_2$;
	sleep 3000;
	npctalk "The piece of Ymir's Heart. I have heard that it is here, but I don't see any, do I?", .@intruder$;
	sleep 3000;
	npctalk "...你在說什麼？", .@kronecker_2$;
	sleep 3000;
	npctalk "Oh, you didn't know?", .@intruder$;
	sleep 3000;
	npctalk "Well, I see. If you knew that you wouldn't have kept it in the ground, wasting your time on this ritual and all.", .@intruder$;
	sleep 3000;
	npctalk "But I can't help it, old man. This is fake.", .@intruder$;
	sleep 2000;
	npctalk "……你怎麼敢……", .@kronecker_2$;
	sleep 2000;
	npctalk "Don't give me that look. I have wasted my time here as well, you know.", .@intruder$;
	sleep 3000;
	npctalk "Well, I'll get going without casing a scene this time.", .@intruder$;
	sleep 3000;
	npctalk "哈!當你不知道如何使用時，再多的力量又有什麼用呢？我想你不再需要這個了。", .@intruder$;
	sleep 3000;
	npctalk "是時候讓這顆假尤彌爾之心消失了！", .@intruder$;
	sleep 500;
	specialeffect EF_BEGINSPELL3,AREA, .@intruder$;
	sleep 500;
	specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Peter Heine#peter01");
	specialeffect EF_LORD,AREA, .@kronecker_2$;
	specialeffect EF_LORD,AREA, instance_npcname("Levuiere Wigner#louvier1");
	specialeffect EF_LORD,AREA, instance_npcname("Catherine D. Geoborg#ca1");

	disablenpc instance_npcname("Ian Walther#ian01");
	disablenpc instance_npcname("Levuiere Wigner#louvier1");
	disablenpc instance_npcname("Jurgen Wigner#jurgen01");
	disablenpc instance_npcname("Catherine D. Geoborg#ca1");
	disablenpc instance_npcname("Katrin Wigner#kath01");
	disablenpc instance_npcname("Isaac Wigner#isaac01");

	enablenpc instance_npcname("Ian Walther#ian02");
	enablenpc instance_npcname("Levuiere Wigner#louvier2");
	enablenpc instance_npcname("Jurgen Wigner#jurgen02");
	enablenpc instance_npcname("Catherine D. Geoborg#ca2");
	enablenpc instance_npcname("Katrin Wigner#kath02");
	enablenpc instance_npcname("Isaac Wigner#isaac02");
	sleep 500;
	specialeffect EF_LORD,AREA, instance_npcname("Maximilian Roegenburg#m1");
	disablenpc instance_npcname("Ernst Geoborg#ernst01");
	disablenpc instance_npcname("Maximilian Roegenburg#m1");
	disablenpc instance_npcname("Wolf Roegenburg#wolf01");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_1");

	enablenpc instance_npcname("Maximilian Roegenburg#m2");
	enablenpc instance_npcname("Ernst Geoborg#ernst02");
	enablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	enablenpc instance_npcname("Wolf Roegenburg#wolf02");

	specialeffect EF_LORD,AREA, instance_npcname("Agnes Roegenburg#agnes0_2");

	mapannounce 'map_2mir$, "Intruder: Then ladies and gentlemen, have a good time!", bc_map,0xDDDDDD;
	disablenpc .@intruder$;
	enablenpc instance_npcname("Intruder#hunter02");
	enablenpc instance_npcname("Intruder#hunter03");
	enablenpc instance_npcname("Intruder#hunter04");
	enablenpc instance_npcname("Intruder#hunter05");
	enablenpc instance_npcname("Intruder#hunter06");

	sleep 500;
	donpcevent instance_npcname("Intruder_main") + "::OnEvent";

	disablenpc instance_npcname("Po Richard#po01");
	disablenpc instance_npcname("Spica Nerius#spica01");
	disablenpc instance_npcname("Isenhonor Walther#eisen");
	disablenpc instance_npcname("Friedrich S. Heine#stol_1");
	disablenpc instance_npcname("Helmut Roegenburg#helmu");
	disablenpc instance_npcname("Skia Nerius#skia02");
	disablenpc instance_npcname("Nyhill M. Heine#nihil02");

	enablenpc instance_npcname("Skia Nerius#skia03");
	enablenpc instance_npcname("Nyhill M. Heine#nihil03");
	enablenpc instance_npcname("Spica Nerius#spica02");
	enablenpc instance_npcname("Po Richard#po02");
	enablenpc instance_npcname("Isenhonor Walther#eisen2");
	enablenpc instance_npcname("Friedrich S. Heine#stol_2");
	enablenpc instance_npcname("Helmut Roegenburg#helmu2");
	end;
}

2@mir,1,1,0	script	Intruder_main	-1,{
	end;
OnEvent:
	enablenpc instance_npcname("Intruder_main");
	.@label$ = instance_npcname("Intruder_main") + "::OnMobDead";
	monster 'map_2mir$,107,73,"Suspicious Intruder",3454,1, .@label$;	// HEART_HUNTER_N
	setunitdata $@mobid[0], UMOB_HP, 10000;
	monster 'map_2mir$, 98,73,"Suspicious Intruder",3454,1, .@label$;
	setunitdata $@mobid[0], UMOB_HP, 10000;
	monster 'map_2mir$,101,83,"Suspicious Intruder",3454,1, .@label$;
	setunitdata $@mobid[0], UMOB_HP, 10000;
	'intruder_count = 0;

	donpcevent instance_npcname("Intruder#hunter02") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter03") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter04") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter05") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter06") + "::OnStart";
	end;
OnMobDead:
	'intruder_count++;
	switch( 'intruder_count ) {
	case 1:
		donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
		break;
	case 2:
		donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";
		break;
	case 3:
		donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
		break;
	}
	sleep 100;	// it seems there is a slight delay

	switch( 'intruder_count ) {
	case 3:
		mapannounce 'map_2mir$, "Fellows! Let's leave this place!", bc_map,0xDDDDDD;
		// display the effect a second time
		donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";

		disablenpc instance_npcname("Intruder#hunter02");
		disablenpc instance_npcname("Intruder#hunter03");
		disablenpc instance_npcname("Intruder#hunter04");
		disablenpc instance_npcname("Intruder#hunter05");
		disablenpc instance_npcname("Intruder#hunter06");
		break;
	default:
		break;
	}
	end;
}

2@mir,87,79,5	script	Intruder#hunter02	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter02");	// officially disabled, changed to hideonnpc to display the effect a second time
	end;
OnTimer1000:
	specialeffect EF_PIERCE;
	end;
OnTimer2000:
	specialeffect EF_CONCENTRATION, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer4000:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer5000:
	npctalk "沒有用！";
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer7000:
	specialeffect EF_SHIELDCHARGE;
	end;
OnTimer8000:
	specialeffect EF_CHAINCOMBO, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer10000:
	specialeffect EF_PIERCE;
	end;
OnTimer11000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
	npctalk "我們沒有時間聊天！", instance_npcname("Isenhonor Walther#eisen2");
	initnpctimer;
	end;
}

2@mir,91,84,5	script	Intruder#hunter03	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter03");
	end;
OnTimer1500:
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2500:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer4500:
	specialeffect EF_HIT5;
	end;
OnTimer5500:
	specialeffect EF_HIT6, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer7500:
	specialeffect EF_HIT2;
	end;
OnTimer8500:
	npctalk "沒有用！";
	specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer11100:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer12000:
	specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
	initnpctimer;
	end;
}

2@mir,94,86,5	script	Intruder#hunter04	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter04");
	end;
OnTimer1200:	// weird timer but ~correct
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2200:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer3800:
	specialeffect EF_HIT5;
	end;
OnTimer4800:
	npctalk "呵呵！";
	specialeffect EF_HIT6, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer8200:
	specialeffect EF_HIT2;
	end;
OnTimer9200:
	specialeffect EF_HIT1, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer11500:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer12500:
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	npctalk "……這些混蛋是哪裡來的？", instance_npcname("Friedrich S. Heine#stol_2");
	initnpctimer;
	end;
}

2@mir,107,86,5	script	Intruder#hunter05	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter05");
	end;
OnTimer1000:
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer4000:
	npctalk "死！！！";
	specialeffect EF_HIT5;
	end;
OnTimer4500:
	npctalk "看看誰在說話！ ！ ！", instance_npcname("Helmut Roegenburg#helmu2");
	specialeffect EF_HIT6, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer7000:
	specialeffect EF_HIT2;
	end;
OnTimer8000:
	specialeffect EF_HIT1, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer10000:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer11000:
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	npctalk "啊啊！ ！ ！", instance_npcname("Helmut Roegenburg#helmu2");
	initnpctimer;
	end;
}

2@mir,110,85,5	script	Intruder#hunter06	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter06");
	end;
OnTimer1000:
	specialeffect EF_HITDARK;
	end;
OnTimer2000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer4000:
	npctalk "死！！！";
	specialeffect EF_LIGHTBOLT;
	end;
OnTimer5000:
	specialeffect EF_HIT6, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer7000:
	specialeffect EF_COMBOATTACK1;
	end;
OnTimer8000:
	specialeffect EF_HIT1, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer10000:
	specialeffect EF_NAPALMBEAT;
	end;
OnTimer12000:
	npctalk "Oh, this doesn't look good.", instance_npcname("Po Richard#po02");
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Po Richard#po02");
	initnpctimer;
	end;
}

2@mir,97,80,7	script	Friedrich S. Heine#stol_1	4_EP16_STOLZ,{
	npctalk "... 安靜的。";
	end;
}

2@mir,88,78,2	script	Isenhonor Walther#eisen2	4_M_1STPRIN1,{
	if ('intruder_count < 3) {
		npctalk "我們沒有時間聊天！";
		end;
	}
	mes "[艾森奧諾爾]";
	mes "到底是怎麼回事...";
	mes "我們將首先離開這個地方。";
	next;
	mes "[艾森奧諾爾]";
	mes "每個人都很好...";
	close2;
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Ian Walther#ian02");
	end;
}

2@mir,106,85,7	script	Helmut Roegenburg#helmu2	4_EP16_HELMUT,{
	if ('intruder_count < 3) {
		npctalk "嗯！";
		end;
	}
	mes "[赫爾穆特]";
	mes "一切都很好。";
	mes "...我要去。兄弟倆看起來不太好。";
	close2;
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Wolf Roegenburg#wolf02");
	disablenpc instance_npcname("Maximilian Roegenburg#m2");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	end;
}

2@mir,92,83,2	script	Skia Nerius#skia03	4_EP16_SKIA,{
	if ('intruder_count < 3) {
		npctalk "……呃……他們是從哪裡來的？";
		end;
	}
	mes "[滑雪]";
	mes "PHEW，讓我們離開這裡。";
	mes "姐姐，讓我們出去吧！";
	mes "尼希爾，你應該和我們一起來！";
	next;
	mes "[滑雪]";
	mes "其他人會自己下車！";
	close2;
	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Spica Nerius#spica02");
	end;
}

2@mir,109,84,7	script	Po Richard#po02	4_M_6THPRIN1,{
	if ('intruder_count < 3) {
		npctalk "……哈哈，沒想到會在這裡戰鬥！";
		end;
	}
	npctalk "由於大廳似乎已經被清理乾淨，我現在就離開。這下可麻煩了！";
	sleep 2000;
	disablenpc instance_npcname("Po Richard#po02");
	end;
}

2@mir,95,85,1	script	Friedrich S. Heine#stol_2	4_EP16_STOLZ,{
	if ('intruder_count < 3) {
		npctalk "請照顧其他人。";
		end;
	}
	mes "[弗里德里希]";
	mes "...似乎沒有人死了。";
	npctalk "你還在等什麼！帶著傷員離開這裡！";
	close2;
OnDisable:
	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Spica Nerius#spica02");
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Ian Walther#ian02");
	disablenpc instance_npcname("Levuiere Wigner#louvier2");
	disablenpc instance_npcname("Jurgen Wigner#jurgen02");
	disablenpc instance_npcname("Katrin Wigner#kath02");
	disablenpc instance_npcname("Ernst Geoborg#ernst02");
	disablenpc instance_npcname("Catherine D. Geoborg#ca2");
	disablenpc instance_npcname("Po Richard#po02");
	disablenpc instance_npcname("Maximilian Roegenburg#m2");
	disablenpc instance_npcname("Wolf Roegenburg#wolf02");
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	disablenpc instance_npcname("Isaac Wigner#isaac02");
	end;
}

2@mir,101,93,7	script	Kronecker G. Heine#gran2	4_EP16_GRANZ,{
	if ('intruder_count < 3) {
		mes "[克羅內克]";
		mes "彼得處於危急狀態。";
		mes "...您介意與我們的入侵者作鬥爭嗎？";
		close;
	}
	if (isbegin_quest(7692) == 1) {
		mes "[克羅內克]";
		mes "...";
		mes ".... ^4d4d4dffterra gloria ^000000缺少...";
		next;
		select("土地榮耀？");
		mes "[克羅內克]";
		mes "這是在符文中宮的王冠。";
		mes "這意味著不僅僅是普通冠。";
		next;
		mes "[克羅內克]";
		mes "他們被他們採取了嗎？";
		mes "到底是什麼...！";
		next;
		mes "[克羅內克]";
		mes "任何人都不知道的東西...除了七個王室...！";
		next;
		mes "[克羅內克]";
		mes "這意味著...";
		mes "...嗯，首先，有一些事情需要找出答案。";
		next;
		mes "[克羅內克]";
		mes "讓我們現在離開這裡。";
		mes "還有很多可用於找出答案。";
		erasequest 7692;
		setquest 7693;// Surprise Attack
		ep16_royal = 9;
		next;
		mes "[克羅內克]";
		mes "誰離開了這裡，與您附近的受傷者清除！";
		mes "意識室將關閉一段時間！";
		donpcevent instance_npcname("Friedrich S. Heine#stol_2") + "::OnDisable";
		disablenpc instance_npcname("Nyhill M. Heine#nihil03");
		close;
	}
	mes "[克羅內克]";
	mes "彼得受傷了。";
	mes "我們需要快速移動他。";
	next;
	mes "[克羅內克]";
	mes "我在那裡見。";
	mes "您可以通過進入的門離開。";
	mes "讓我們在過去的王室回憶中見面。";
	close;
}

2@mir,93,80,2	script	Nyhill M. Heine#nihil03	4_EP16_NIHIL,4,4,{
	if ('intruder_count < 3) {
		npctalk "……哦……我的……";
		end;
	}
	mes "[新山]";
	mes "...這就是...我的錯...";
	close;
OnTouch_:
	npctalk "……我能幫忙嗎？";
	npcskill "AL_HEAL",11,99,60;
	npcskill "PR_KYRIE",10,99,60;
	end;
}

2@mir,97,65,5	script	Jurgen Wigner#jurgen02	4_M_4THPRIN1,{
	npctalk "我指望你了！";
	npcskill "AL_INCAGI",10,99,60;
	end;
}

2@mir,111,66,4	script	Catherine D. Geoborg#ca2	4_EP16_EGEO,{
	npctalk "...希望對您有幫助。";
	npcskill "AL_HEAL",10,130,115;
	end;
}

2@mir,107,61,6	script	Isaac Wigner#isaac02	4_EP16_ISAAC,{
	npctalk "開心點！";
	npcskill "AL_BLESSING",10,99,60;
	end;
}

2@mir,112,71,1	script	Agnes Roegenburg#agnes0_2	4_EP16_AGNES,{
	npctalk "啊！當心！";
	end;
}

2@mir,96,83,2	script	Spica Nerius#spica02	4_EP16_SPICA,{
	if ('intruder_count < 3)
		npctalk "...S...到...一個...";
	else
		npctalk "Skia，為什麼這傢伙救了尼希爾勳爵，而不是我？";
	end;
}


2@mir,102,97,3	script	Intruder#hunter01	3454,{
	end;
OnInstanceInit:
	'map_2mir$ = instance_mapname("2@mir");
	'intruder_count = 0;

	disablenpc instance_npcname("Spica Nerius#spica02");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	disablenpc instance_npcname("Isaac Wigner#isaac02");
	disablenpc instance_npcname("Catherine D. Geoborg#ca2");
	disablenpc instance_npcname("Jurgen Wigner#jurgen02");
	disablenpc instance_npcname("Kronecker G. Heine#gran2");
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Po Richard#po02");
	disablenpc instance_npcname("Friedrich S. Heine#stol_2");

	disablenpc instance_npcname("Intruder#hunter01");
	disablenpc instance_npcname("Intruder#hunter02");
	disablenpc instance_npcname("Intruder#hunter03");
	disablenpc instance_npcname("Intruder#hunter04");
	disablenpc instance_npcname("Intruder#hunter05");
	disablenpc instance_npcname("Intruder#hunter06");

	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Nyhill M. Heine#nihil03");
	end;
}

2@mir,96,83,0	script	dummy_npc_rituel_disabled	-1,{
	end;
OnInstanceInit:
	disablenpc();
	end;
}
2@mir,88,74,3	duplicate(dummy_npc_rituel_disabled)	Ian Walther#ian02	1_M_JOBTESTER
2@mir,86,70,4	duplicate(dummy_npc_rituel_disabled)	Levuiere Wigner#louvier2	4_EP16_LOUVIERE
2@mir,94,72,3	duplicate(dummy_npc_rituel_disabled)	Katrin Wigner#kath02	4_EP16_POE
2@mir,112,66,3	duplicate(dummy_npc_rituel_disabled)	Ernst Geoborg#ernst02	4_M_2NDPRIN1
2@mir,91,69,7	duplicate(dummy_npc_rituel_disabled)	Maximilian Roegenburg#m2	4_EP16_MAX
2@mir,94,66,3	duplicate(dummy_npc_rituel_disabled)	Wolf Roegenburg#wolf02	4_EP16_WOLF
2@mir,100,80,7	duplicate(dummy_npc_rituel_disabled)	Nyhill M. Heine#nihil02	4_EP16_NIHIL
2@mir,101,95,3	duplicate(dummy_npc_rituel_disabled)	Fallen Peter#peter02	4_EP16_PETER
2@mir,97,77,7	duplicate(dummy_npc_rituel_disabled)	Skia Nerius#skia02	4_EP16_SKIA


2@mir,101,88,4	duplicate(dummy_npc)	Kronecker G. Heine#gran1	4_EP16_GRANZ
2@mir,97,71,7	duplicate(dummy_npc)	Levuiere Wigner#louvier1	4_EP16_LOUVIERE
2@mir,100,71,7	duplicate(dummy_npc)	Jurgen Wigner#jurgen01	4_M_4THPRIN1
2@mir,97,68,7	duplicate(dummy_npc)	Isaac Wigner#isaac01	4_EP16_ISAAC
2@mir,100,68,7	duplicate(dummy_npc)	Katrin Wigner#kath01	4_EP16_POE
2@mir,103,80,2	duplicate(dummy_npc)	Catherine D. Geoborg#ca1	4_EP16_EGEO
2@mir,106,80,2	duplicate(dummy_npc)	Ernst Geoborg#ernst01	4_M_2NDPRIN1
2@mir,103,77,2	duplicate(dummy_npc)	Po Richard#po01	4_M_6THPRIN1
2@mir,103,74,2	duplicate(dummy_npc)	Maximilian Roegenburg#m1	4_EP16_MAX
2@mir,106,74,2	duplicate(dummy_npc)	Wolf Roegenburg#wolf01	4_EP16_WOLF
2@mir,101,94,0	duplicate(dummy_npc)	Peter Heine#peter01	4_M_3RDPRIN1
2@mir,100,77,7	duplicate(dummy_npc)	Spica Nerius#spica01	4_EP16_SPICA
2@mir,106,71,2	duplicate(dummy_npc)	Agnes Roegenburg#agnes0_1	4_EP16_AGNES
2@mir,100,74,7	duplicate(dummy_npc)	Ian Walther#ian01	1_M_JOBTESTER
2@mir,97,74,7	duplicate(dummy_npc)	Isenhonor Walther#eisen	4_M_1STPRIN1
2@mir,103,71,2	duplicate(dummy_npc)	Helmut Roegenburg#helmu	4_EP16_HELMUT

2@mir,101,10,0	script	to_prt_lib	WARPNPC,1,1,{
	if (isbegin_quest(7693) == 1) {
		warp "prt_lib",88,80;
		end;
	}
	mes "[" + strcharinfo(0) + "]";
	mes "我們應該觀看祝福的儀式。";
	mes "不要現在出去。";
	close;
}
