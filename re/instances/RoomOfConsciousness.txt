//===== rAthena Script =======================================
//= Instance Room of Consciousness.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Require Banquet main quest.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Added a setting to prevent an exploit
//      (searchs "'exploit_disabled" variable - the variable is
//      false by default like on official server). [Capuche]
//============================================================

1@mir,103,40,3	script	Fenrir#1mir	4_F_FENRIR,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "密封在地下";
	mes "Prontera城堡...";
	mes "我沒想到會這麼大...";
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "裡面到底是什麼？";
	next;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "我不知道。讓我們首先調查內部。";
	close2;
	cutin "",255;
	disablenpc instance_npcname("Iris#1mir");
	disablenpc instance_npcname("Fenrir#1mir");
	end;

OnInstanceInit:
	'map_name$ = instance_mapname("1@mir");
	'step = 0;

	// On official server the instance can be repeated if the player log out after the death of Bijou (main quest)
	// true: prevent the exploit
	// false: like on official - exploitable
	'exploit_disabled = false;

	disablenpc instance_npcname("Bijou#2mir");

	disablenpc instance_npcname("Fenrir#3mir");
	disablenpc instance_npcname("Iris#3mir");
	disablenpc instance_npcname("Bijou#3mir");
	disablenpc instance_npcname("Renovated Amdarais#3mir");

	disablenpc instance_npcname("Fenrir#4mir");
	disablenpc instance_npcname("Iris#4mir");
	disablenpc instance_npcname("Bijou#4mir");

	disablenpc instance_npcname("Fenrir#5mir");
	disablenpc instance_npcname("Iris#5mir");
	disablenpc instance_npcname("Bijou#5mir");
	disablenpc instance_npcname("Sarah#5mir");

	disablenpc instance_npcname("Fenrir#6mir");
	disablenpc instance_npcname("Iris#6mir");
	disablenpc instance_npcname("Bijou#6mir");
	disablenpc instance_npcname("Sarah#6mir");

	disablenpc instance_npcname("Fenrir#boss1a");
	disablenpc instance_npcname("Fenrir#boss1b");
	disablenpc instance_npcname("Fenrir#boss1c");
	disablenpc instance_npcname("Fenrir#boss1d");
	disablenpc instance_npcname("Iris#boss1a");
	disablenpc instance_npcname("Iris#boss1b");
	disablenpc instance_npcname("Iris#boss1c");
	disablenpc instance_npcname("Iris#boss1d");

	disablenpc instance_npcname("Fenrir#boss2a");
	disablenpc instance_npcname("Fenrir#boss2b");
	disablenpc instance_npcname("Fenrir#boss2c");
	disablenpc instance_npcname("Fenrir#boss2d");
	disablenpc instance_npcname("Iris#boss2a");
	disablenpc instance_npcname("Iris#boss2b");
	disablenpc instance_npcname("Iris#boss2c");
	disablenpc instance_npcname("Iris#boss2d");

	disablenpc instance_npcname("eq#mir2");
	disablenpc instance_npcname("eq#mir3");
	disablenpc instance_npcname("eq#mir4");
	disablenpc instance_npcname("eq#mir5");
	disablenpc instance_npcname("eq#mir6");
	end;
}

1@mir,100,94,7	script	Iris#2mir	4_F_IRIS,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	if ('step != 0)
		end;
	mes "[虹膜]";
	mes "這個...這一定是...";
	mes "伊米爾的心！";
	next;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "這...";
	mes "這是伊米爾的心...";
	next;
	cutin "hero_iris_01",255;
	mes "~隆隆~";
	next;
	specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Iris#2mir");
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "為什麼這個地方顫抖？";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "~咕unt~他們也一定也開始攻擊這個地方。";
	next;
	enablenpc instance_npcname("Bijou#2mir");
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "~咧嘴~很高興見到大家。";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "!!";
	mes "你必須在跟我開玩笑！";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "從這一刻起，我們是Valkyrie Himelmez勳爵的不朽軍團";
	mes "將接管這裡。";
	next;
	specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Iris#2mir");
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "什麼？你怎麼敢！";
	mes "我是遊戲。來吧！";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "~笑~我看起來好像有時間去處理像你一樣的小事嗎？";
	mes "但是，出於純粹的憐憫，我會讓您品嚐不朽軍團的可怕力量。";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "~咕grounts~所有退伍軍人何時到達這裡？";
	close2;
	cutin "",255;
	if ('step == 0) {
		'step = 1;
		donpcevent instance_npcname("eq#mir1") + "::OnEvent";
		disablenpc instance_npcname("Iris#2mir");
	}
	end;
}

1@mir,101,104,0	script	eq#mir1	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	initnpctimer;
	end;
OnTimer200:
	disablenpc instance_npcname("Iris#1mir");
	disablenpc instance_npcname("Fenrir#1mir");
	disablenpc instance_npcname("Fenrir#2mir");
	disablenpc instance_npcname("Bijou#2mir");
	mapannounce 'map_name$, "Fenrir: The enemies will show up soon! Brace yourself!", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer2200:
	mapannounce 'map_name$, "Fenrir: The enemies are swarming in. We must get rid of them all!", bc_map,0xFFFF00,FW_NORMAL,12;
	// coords inaccurate
	monster 'map_name$,83,67,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_SKELETON
	monster 'map_name$,91,51,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_SKELETON
	monster 'map_name$,111,67,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_SKELETON
	monster 'map_name$,92,69,"Enchanted Soldier Skeleton",3447,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_SOLDIER_SKELETON
	monster 'map_name$,109,50,"Enchanted Soldier Skeleton",3447,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_SOLDIER_SKELETON
	monster 'map_name$,109,47,"Enchanted Archer Skeleton",3445,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_ARCHER_SKELETON
	monster 'map_name$,90,48,"Enchanted Archer Skeleton",3445,1, instance_npcname("eq#mir1") + "::OnMobDead";// P_ARCHER_SKELETON
	'skeleton_wave[0] = 7;
	stopnpctimer;
	end;

OnMobDead:
	'skeleton_wave[0]--;
	if ('skeleton_wave[0] == 0) {
		mapannounce 'map_name$, "Iris: ~Exhales~ Did we knock out all the enemies now?", bc_map,0xFFFF00,FW_NORMAL,12;
		startnpctimer;
	}
	end;
OnTimer4200:
	mapannounce 'map_name$, "Fenrir: I don't think so. Some of the remnants are still here. Watch out!", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer6200:
	// coords inaccurate
	monster 'map_name$,83,67,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_SKELETON
	monster 'map_name$,91,51,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_SKELETON
	monster 'map_name$,111,67,"Enchanted Skeleton",3446,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_SKELETON
	monster 'map_name$,92,69,"Enchanted Soldier Skeleton",3447,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_SOLDIER_SKELETON
	monster 'map_name$,109,50,"Enchanted Soldier Skeleton",3447,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_SOLDIER_SKELETON
	monster 'map_name$,109,47,"Enchanted Archer Skeleton",3445,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_ARCHER_SKELETON
	monster 'map_name$,90,48,"Enchanted Archer Skeleton",3445,1, instance_npcname("eq#mir1") + "::OnMobDead2";// P_ARCHER_SKELETON
	'skeleton_wave[1] = 7;
	stopnpctimer;
	end;

OnMobDead2:
	'skeleton_wave[1]--;
	if ('skeleton_wave[1] == 0 && 'step == 1) {
		'step = 2;
		mapannounce 'map_name$, "Fenrir: Phew! I think we've done here.", bc_map,0xFFFF00,FW_NORMAL,12;
		enablenpc instance_npcname("Fenrir#3mir");
		enablenpc instance_npcname("Iris#3mir");
		enablenpc instance_npcname("Bijou#2mir");
		disablenpc instance_npcname("eq#mir1");
	}
	end;
}

1@mir,103,85,1	script	Iris#3mir	4_F_IRIS,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	if ('step != 2)
		end;
	mes "[珠寶]";
	mes "~輕笑~我認為你對人類還不錯。";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "但是，您認為您可以再次擺脫這種情況嗎？醒來，我的底層！";
	specialeffect EF_WARP,AREA, instance_npcname("Renovated Amdarais#3mir");
	sleep2 3000;
	specialeffect EF_ENTRY,AREA, instance_npcname("Renovated Amdarais#3mir");
	enablenpc instance_npcname("Renovated Amdarais#3mir");
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "什麼...這個怪物怎麼了？";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "啊...";
	mes "我去之前讓你進去。";
	mes "我是比約。";
	mes "我是維爾基里勳爵·梅爾梅斯（Lord Valkyrie Himelmez）的副官。";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "~gro吟~";
	mes "女武神的下屬從未讓我失望，你也沒有。";
	mes "Ymir的心臟是巨大的能源。您還是要使用什麼？";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "......";
	mes "您不需要知道。無論如何，您都會很快被消滅。";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "我認為您不會出於任何充分的理由使用它。";
	mes "我將阻止Bijou抓住伊米爾的心。您繼續照顧那個巨大的怪物！";
	close2;
	cutin "",255;
	if ('step == 2) {
		'step = 3;
		donpcevent instance_npcname("eq#mir2") + "::OnEvent";
	}
	end;
}

1@mir,101,104,0	script	eq#mir2	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("eq#mir2");
	enablenpc instance_npcname("eq#mir3");
	disablenpc instance_npcname("Bijou#2mir");
	disablenpc instance_npcname("Fenrir#3mir");
	disablenpc instance_npcname("Iris#3mir");
	disablenpc instance_npcname("Renovated Amdarais#3mir");

	monster 'map_name$,101,95,"Renovated Amdarais",3448,1, instance_npcname("eq#mir2") + "::OnMobDead";// P_AMDARAIS
	'boss_id = $@mobid[0];
	'target_event = 1;	// Amdarais
	initnpctimer;
	end;
OnTimer1000:
	getunitdata 'boss_id, .@data;
	if (.@data[UMOB_HP] == .@data[UMOB_MAXHP]) {
		initnpctimer;
		end;
	}
	mapannounce 'map_name$, "Iris: Oh, this monster...I don't think its HP doesn't seem to drop no matter how many times I hit.", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer3000:
	mapannounce 'map_name$, "Bijou: ~Chuckles~ Amdarais won't get knocked down that easily.", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer5000:
	mapannounce 'map_name$, "Fenrir: This undead seems to be different from any other undeads.", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer7000:
	mapannounce 'map_name$, "Fenrir: Yes, it's the nucleus! Attack its nucleus to inflict huge damage!", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer9000:
	donpcevent instance_npcname("eq#mir3") + "::OnEvent";
	stopnpctimer;
	end;
OnMobDead:
	if ('step != 3)
		end;
	'step = 4;
	stopnpctimer;
	donpcevent instance_npcname("eq#mir3") + "::OnStop";
	if ('random_letter$ != "")
		donpcevent instance_npcname( "Fenrir#boss1" + 'random_letter$ ) + "::OnStop";
	disablenpc instance_npcname("eq#mir2");

	enablenpc instance_npcname("Fenrir#4mir");
	enablenpc instance_npcname("Iris#4mir");
	end;
}

1@mir,101,104,0	script	eq#mir3	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	callsub S_Announce, true;
OnEvent2:
	callsub S_Announce, false;
S_Announce:
	setarray .@list$[0],"a","b","c","d";
	'random_letter$ = .@list$[ rand(4) ];
	donpcevent instance_npcname( "Fenrir#boss" + 'target_event + "" + 'random_letter$ ) + "::OnEvent";	// 1: P_AMDARAIS / 2: BIJOU
	if (getarg(0) == true)
		mapannounce 'map_name$, "Fenrir: Lure it to where I am!", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnStart:
	initnpctimer;
	end;
OnTimer25000:
	donpcevent instance_npcname("eq#mir3") + "::OnEvent";
	stopnpctimer;
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("eq#mir3");
	end;
}

1@mir,103,80,3	script	Fenrir#boss1a	4_F_FENRIR,2,2,{
	end;
OnEvent:
	'fenrir_name$ = instance_npcname( strnpcinfo(0) );
	'iris_name$ = instance_npcname( "Iris#" + strnpcinfo(2) );
	enablenpc 'fenrir_name$;
	enablenpc 'iris_name$;
	end;

OnTouchNPC:
	if ('touch_mob == 0) {
		npctalk "Fenrir: Good work! Now it's on you, Iris!", 'fenrir_name$;
		'touch_mob = 1;
		initnpctimer;
	}
	end;
OnTimer2000:
	npctalk "Iris: Alright, let me do it! What I, Iris, want is...!", 'iris_name$;
	specialeffect EF_BEGINSPELL,AREA, 'iris_name$;
	end;
OnTimer4000:
	npctalk "Iris: Mabi Amulet! Go!!", 'iris_name$;
	if ('target_event == 1)
		unittalk 'boss_id, "Aaarrgghhh---!!";
	else
		unittalk 'boss_id, "Bijou: What the...!!";
	end;
OnTimer5000:
	npctalk "Iris: It worked!", 'iris_name$;
	end;
OnTimer7000:
	npctalk "Fenrir: So this is my turn, then. ~Spirited yell~", 'fenrir_name$;
	specialeffect EF_BEGINSPELL,AREA, 'fenrir_name$;
	specialeffect EF_TETRACASTING,AREA, 'fenrir_name$;
	progressbar_npc "000000",3;
	end;
OnTimer10000:
	npctalk "Fenrir: Take this!!", 'fenrir_name$;
	specialeffect EF_SUI_EXPLOSION,AREA, 'fenrir_name$;
	specialeffect EF_LORD,AREA, 'fenrir_name$;
	specialeffect EF_FLAMELAUNCHER,AREA, 'fenrir_name$;
	if ('target_event == 1)
		unittalk 'boss_id, "~Screams~";
	else
		unittalk 'boss_id, "Bijou: ~Groans~ More hurting than I thought...!";
	getunitdata 'boss_id, .@data;
	if (.@data[UMOB_HP] > 0) {
		.@damage = rand(700,1300) * 1000;
		if (.@damage >= .@data[UMOB_HP])
			.@mob_hp = 0;
		else
			.@mob_hp = .@data[UMOB_HP] - .@damage;
		setunitdata 'boss_id, UMOB_HP, .@mob_hp;
	}
	end;
OnTimer13000:
	npctalk "Fenrir: It's not perfect but I think we've caused a pretty significant amount of damage!", 'fenrir_name$;
	end;
OnTimer14500:
	npctalk "Fenrir: I've consumed magic too much. I'll take a short break. I'm counting on you, then!", 'fenrir_name$;
	end;
OnTimer16500:
	donpcevent instance_npcname("eq#mir3") + "::OnStart";
OnStop:
	stopnpctimer;
	disablenpc();
	disablenpc instance_npcname( "Iris#" + strnpcinfo(2) );
	'touch_mob = 0;
	'random_letter$ = "";
	end;
}

1@mir,100,90,7	script	Iris#4mir	4_F_IRIS,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	if ('step != 4)
		end;
	mes "[虹膜]";
	mes "是...嗎？現在走了嗎？";
	mes "所以現在剩下的就是...";
	next;
	enablenpc instance_npcname("Bijou#4mir");
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "我...我是唯一的一個...";
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "什麼？ ！";
	next;
	mes "～戰俘！ ～";
	specialeffect EF_SUI_EXPLOSION,AREA, instance_npcname("Iris#4mir");
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "啊啊啊——！";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "我必須說這是相當意外的。 ~輕笑~";
	mes "我沒想到有人會擊敗我花了三年的時間來完成的Amdarais。";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "你會付款";
	mes "為了破壞我的三年努力！ ！";
	mes "慢慢...";
	mes "而且非常痛苦...！";
	close2;
	cutin "",255;
	if ('step == 4) {
		'step = 5;
		donpcevent instance_npcname("eq#mir4") + "::OnEvent";
	}
	end;
}

1@mir,101,104,0	script	eq#mir4	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("eq#mir4");
	disablenpc instance_npcname("Fenrir#4mir");
	disablenpc instance_npcname("Iris#4mir");
	disablenpc instance_npcname("Bijou#4mir");
	monster 'map_name$,102,95,"Bijou",3450,1, instance_npcname("eq#mir4") + "::OnMobDead";// BIJOU
	'boss_id = $@mobid[0];
	initnpctimer;
	end;
OnTimer1000:
	getunitdata 'boss_id, .@data;
	if (.@data[UMOB_HP] == .@data[UMOB_MAXHP]) {
		initnpctimer;
		end;
	}
	donpcevent instance_npcname("eq#mir4") + "::OnMobDead";
	end;
OnMobDead:
	if ('step != 5)
		end;
	'step = 6;
	killmonster 'map_name$, instance_npcname("eq#mir4") + "::OnMobDead";
	stopnpctimer;
	enablenpc instance_npcname("Fenrir#5mir");
	enablenpc instance_npcname("Iris#5mir");
	enablenpc instance_npcname("Bijou#5mir");
	disablenpc instance_npcname("eq#mir4");
	end;
}

1@mir,103,90,1	script	Fenrir#5mir	4_F_FENRIR,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	if ('step != 6)
		end;
	mes "[芬里爾]";
	mes "~gro吟~我不敢相信什麼都沒有用！";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "~輕笑~";
	mes "我會讓你嚐到絕望的感覺。";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "弗羅斯特潛水員！";
	specialeffect EF_LOCKON,AREA, instance_npcname("Fenrir#5mir");
	sleep2 3000;
	specialeffect EF_FREEZE,AREA, instance_npcname("Fenrir#5mir");
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "~吟~";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "感覺如何？痛苦，嗯？";
	mes "您現在必須癱瘓。";
	mes "...但是不要指望我很容易殺了你...";
	next;
	mes "[珠寶]";
	mes "我應該先照顧這個徹頭徹尾的煩人的牧師加爾。";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "我...虹膜！";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "......";
	enablenpc instance_npcname("Sarah#5mir");
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "莎拉...？";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "就是這樣...";
	mes "這是Prontera城堡的地下。";
	mes "第一個Ymir心臟被密封的地方。";
	next;
	mes "[莎拉]";
	mes "那一定是...";
	mes "伊米爾的心...";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "是什麼把你一路帶到這裡？";
	mes "莎拉·艾琳。";
	mes "我沒想到你會出現在這裡。";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "Bijou，您認為您在這裡做什麼？";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "是的，我正照顧好煩人的蟲子。";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "Bijou是什麼樣的答案？";
	next;
	mes "[莎拉]";
	mes "我的問題是：您在這裡還在做什麼，完全愚蠢地走了，為什麼您不打破Ymir的心臟封鎖？";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "好吧，那是呃...我確實打破了第一封。";
	mes "這個地方將立即清理。莎拉（Sarah），沒有什麼可擔心的。";
	next;
	mes "[珠寶]";
	mes "相信我，並考慮完成。請自己成為驕傲的女武神。";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "（~嘲笑~她真是一個緊張，可悲的女武神！我不敢相信像您一樣的綠角犬和Himelmez一樣。這是不公平的。）";
	next;
	cutin "sarah_hero3_2",2;
	mes "[莎拉]";
	mes "Bijou，您一定不在腦海中...這裡沒有Himelmez。";
	next;
	cutin "bijou_03",2;
	mes "[珠寶]";
	mes "....~喘著氣~";
	mes "我喘不過氣！";
	next;
	cutin "sarah_hero3_2",2;
	mes "[莎拉]";
	mes "您最好閉嘴！";
	next;
	cutin "bijou_03",2;
	mes "[珠寶]";
	mes "~咳嗽~";
	mes "~保持咳嗽~";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "我當然可以沒有下屬的人，誰浪費時間從事這種小工作。";
	mes "讓我把伊米爾的心...我自己。";
	disablenpc instance_npcname("Sarah#5mir");
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "（她確實是女武神。哦，你能做什麼？）";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "時間是正確的！";
	mes "弗羅斯特潛水員！";
	specialeffect EF_FREEZE,AREA, instance_npcname("Bijou#5mir");
	next;
	cutin "bijou_03",2;
	mes "[珠寶]";
	mes "你說什麼？ ！";
	next;
	mes "[珠寶]";
	mes "哇...那是一個接近的；稱呼。";
	mes "這怎麼會發生？";
	next;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "這個魔術...";
	mes "正如我所期望的那樣，這必須是高級魔術。施放它非常困難。";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "（這是什麼？！她只是掌握了我在她身上使用的魔法嗎？）";
	next;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "虹膜！這是一種治愈藥水。";
	mes "喝它，您會感官。";
	next;
	sleep2 500;
	specialeffect EF_POTION1,AREA, instance_npcname("Iris#5mir");
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "~低沉的聲音~";
	next;
	sleep2 500;
	specialeffect EF_POTION1,AREA, instance_npcname("Iris#5mir");
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "哦，你現在醒了嗎？";
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "嗯...";
	mes "~嘔吐聲~";
	mes "好！這是痛苦的！";
	mes "這是什麼？";
	mes "你給我什麼？";
	next;
	cutin "fenrir_a",2;
	mes "[芬里爾]";
	mes "所以你醒了，艾里斯。";
	mes "我很放心。";
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "~嘆息~順便說一句，Fenrir！";
	mes "伊米爾的心呢？";
	next;
	cutin "bijou_02",2;
	mes "[珠寶]";
	mes "（... Fenrir？我對Ymir的心思考太多了，完全忘記了她。如果我的猜測是對的，她一定是...！）";
	next;
	mes "[珠寶]";
	mes "~呼氣~我明白了。這是正確的。";
	mes "那隻野獸的血液流過你的身體……這就是答案！";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "然後，讓我以正確的方式為您服務！ ~輕笑~";
	mes "我沒想到一千年前會看到英雄！";
	next;
	cutin "bijou_03",2;
	mes "[珠寶]";
	mes "死！ ！";
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "...當心。她即將進攻！";
	close2;
	cutin "",255;
	if ('step == 6) {
		'step = 7;
		donpcevent instance_npcname("eq#mir5") + "::OnEvent";
	}
	end;
}

1@mir,101,104,0	script	eq#mir5	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("eq#mir3");
	enablenpc instance_npcname("eq#mir5");
	disablenpc instance_npcname("Bijou#5mir");
	disablenpc instance_npcname("Fenrir#5mir");
	disablenpc instance_npcname("Iris#5mir");

	monster 'map_name$,102,95,"Bijou",3450,1, instance_npcname("eq#mir5") + "::OnMobDead";// BIJOU
	'boss_id = $@mobid[0];
	'target_event = 2;
	initnpctimer;
	end;
OnTimer1000:
	getunitdata 'boss_id, .@data;
	if (.@data[UMOB_HP] == .@data[UMOB_MAXHP]) {
		initnpctimer;
		end;
	}
	end;
OnTimer3000:
	mapannounce 'map_name$, "Iris: Let's fight together and we will win again!", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer5000:
	mapannounce 'map_name$, "Fenrir: I think so, too. This time, too, I'd like to ask you to lure it to where I am.", bc_map,0xFFFF00,FW_NORMAL,12;
	end;
OnTimer7000:
	mapannounce 'map_name$, "Bijou: ......", bc_map,0xFFFF00,FW_NORMAL,12;
	donpcevent instance_npcname("eq#mir3") + "::OnEvent2";
	donpcevent instance_npcname("eq#mir6") + "::OnTalk";
	stopnpctimer;
	end;
OnMobDead:
	if ('step != 7)
		end;
	'step = 8;
	stopnpctimer;
	donpcevent instance_npcname("eq#mir3") + "::OnStop";
	donpcevent instance_npcname("eq#mir6") + "::OnStop";
	if ('random_letter$ != "")
		donpcevent instance_npcname( "Fenrir#boss2" + 'random_letter$ ) + "::OnStop";
	disablenpc instance_npcname("eq#mir5");

	enablenpc instance_npcname("Fenrir#6mir");
	enablenpc instance_npcname("Iris#6mir");
	enablenpc instance_npcname("Bijou#6mir");

	if (playerattached() && 'exploit_disabled && ep16_royal == 19) {
		erasequest 7700;// Once More!
		setquest 7701;// Lost Imir Heart
		ep16_royal = 20;
	}
	end;
}

1@mir,101,104,0	script	eq#mir6	HIDDEN_WARP_NPC,{
	end;
OnTalk:
	enablenpc instance_npcname("eq#mir6");
	initnpctimer;
	end;
OnTimer20000:
	.@r = rand(3);
	if (.@r == 0)
		unittalk 'boss_id, "Bijou: To Lord Himelmez!";
	else if (.@r == 1)
		unittalk 'boss_id, "Bijou: Heh...You're still alive!";
	else
		unittalk 'boss_id, "Bijou: ~Chuckles~ Are you feeling the pain?";
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@mir,100,95,5	script	Iris#6mir	4_F_IRIS,{
	if (is_party_leader() == false)	// it shouldn't happen
		end;
	if ('step != 8)
		end;
	mes "[虹膜]";
	mes "~咕unt";
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "~邪惡的笑聲~死！";
	mes "牧師應該死！";
	specialeffect EF_TETRACASTING,AREA, instance_npcname("Iris#6mir");
	next;
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "虹膜！";
	mes "不！停止！ ！";
	specialeffect EF_TETRACASTING,AREA, instance_npcname("Iris#6mir");
	next;
	cutin "bijou_01",2;
	mes "[珠寶]";
	mes "~輕笑~";
	mes "所以您正在尋找伴侶，是嗎？";
	mes "好主意，但是...我認為為時已晚。";
	specialeffect EF_TETRACASTING,AREA, instance_npcname("Iris#6mir");
	next;
	mes "[珠寶]";
	mes "因為...";
	mes "這將是對您的最後致命打擊。哇哈哈！";
	specialeffect EF_TETRACASTING,AREA, instance_npcname("Iris#6mir");
	next;
	sleep2 300;
	enablenpc instance_npcname("Sarah#6mir");
	cutin "bijou_03",2;
	mes "[珠寶]";
	mes "....~嘆息~";
	mes "嗯？ ...在我的肚子裡...";
	mes "這把刀...";
	mes "sa ...莎拉...？如何？";
	specialeffect EF_HFLIMOON1,AREA, instance_npcname("Bijou#6mir");
	next;
	cutin "bijou_death",4;
	mes "[莎拉]";
	mes "這就是你想說的嗎？";
	mes "不用擔心。我會讓Himelmez知道您的勇敢的貢獻。別怪我，比約。";
	next;
	mes "[珠寶]";
	mes "為什麼...？告訴我為什麼！";
	mes "...那個小雞...是嗎？因為她是你的妹妹？是嗎？告訴我，莎拉！";
	next;
	mes "[珠寶]";
	mes "~gro吟~";
	specialeffect EF_HFLIMOON1,AREA, instance_npcname("Bijou#6mir");
	disablenpc instance_npcname("Bijou#6mir");
	next;
	mes "[莎拉]";
	mes "......";
	mes "Bijou，您的靈魂已被釋放。";
	next;
	cutin "hero_iris_01",2;
	mes "[虹膜]";
	mes "（她救了我...？）";
	next;
	mes "[虹膜]";
	mes "莎拉...";
	mes "是莎拉，對嗎？";
	mes "人們說我們小時候分手...";
	next;
	mes "[虹膜]";
	mes "...那你的計劃是什麼，是嗎？為什麼...你為什麼救我？";
	mes "您不認為這會改變我的信念！我永遠不會原諒你！";
	next;
	mes "[虹膜]";
	mes "媽媽...爸爸...我們鎮上的每個人。我要報仇！";
	next;
	cutin "sarah_hero3",2;
	mes "[莎拉]";
	mes "閉上那隻嘴！";
	mes "不要成為戲劇女王，就好像您是這裡唯一的受害者一樣。";
	mes "他們只是為自己犯罪的價格付出了代價。他們12年前殺死了我母親而犯下的罪過。";
	next;
	mes "[莎拉]";
	mes "你要我告訴你真相嗎？那些“親人”您不斷地談論...我們的偉大的長者和我們鎮上的人們實際上是...";
	next;
	mes "[莎拉]";
	mes "......";
	mes "停止...";
	mes "無論如何，伊米爾的心首先出現。";
	next;
	mes "[莎拉]";
	mes "標記我的話。";
	mes "我沒有讓你活著。";
	mes "我會讓你盡快死亡。";
	mes "我確保您下次見面時不再看到一天的光芒。";
	next;
	mes "[莎拉]";
	mes "鳶尾花，你永遠不要讓任何人殺死你。";
	mes "那麼，我最好今天告別。";
	disablenpc instance_npcname("Sarah#6mir");
	next;
	specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Iris#6mir");
	cutin "fenrir_b",2;
	mes "[芬里爾]";
	mes "~咕unt~當伊米爾的心時，女武神摧毀了天花板！";
	next;
	mes "[芬里爾]";
	mes "我們最好也離開這裡！這個地方太危險了！";
	close2;
	if (ep16_royal == 19) {
		erasequest 7700;// Once More!
		setquest 7701;// Lost Imir Heart
		ep16_royal = 20;
	}
	warp "prt_lib_q",88,83;
	end;
}

1@mir,100,40,5	duplicate(dummy_npc)	Iris#1mir	4_F_IRIS
1@mir,103,94,1	duplicate(dummy_npc)	Fenrir#2mir	4_F_FENRIR
1@mir,102,98,3	duplicate(dummy_npc)	Bijou#2mir	4_F_BIJOU

1@mir,100,85,7	duplicate(dummy_npc)	Fenrir#3mir	4_F_FENRIR
1@mir,102,88,3	duplicate(dummy_npc)	Bijou#3mir	4_F_BIJOU
1@mir,101,95,1	duplicate(dummy_npc)	Renovated Amdarais#3mir	3448

1@mir,94,73,3	duplicate(Fenrir#boss1a)	Fenrir#boss1b	4_F_FENRIR,2,2
1@mir,112,73,3	duplicate(Fenrir#boss1a)	Fenrir#boss1c	4_F_FENRIR,2,2
1@mir,103,60,3	duplicate(Fenrir#boss1a)	Fenrir#boss1d	4_F_FENRIR,2,2

1@mir,100,80,5	duplicate(dummy_npc)	Iris#boss1a	4_F_IRIS
1@mir,91,73,5	duplicate(dummy_npc)	Iris#boss1b	4_F_IRIS
1@mir,109,73,5	duplicate(dummy_npc)	Iris#boss1c	4_F_IRIS
1@mir,100,60,5	duplicate(dummy_npc)	Iris#boss1d	4_F_IRIS

1@mir,103,76,3	duplicate(Fenrir#boss1a)	Fenrir#boss2a	4_F_FENRIR,2,2
1@mir,94,70,3	duplicate(Fenrir#boss1a)	Fenrir#boss2b	4_F_FENRIR,2,2
1@mir,112,70,3	duplicate(Fenrir#boss1a)	Fenrir#boss2c	4_F_FENRIR,2,2
1@mir,103,63,3	duplicate(Fenrir#boss1a)	Fenrir#boss2d	4_F_FENRIR,2,2

1@mir,100,76,5	duplicate(dummy_npc)	Iris#boss2a	4_F_IRIS
1@mir,91,70,5	duplicate(dummy_npc)	Iris#boss2b	4_F_IRIS
1@mir,109,70,5	duplicate(dummy_npc)	Iris#boss2c	4_F_IRIS
1@mir,100,63,5	duplicate(dummy_npc)	Iris#boss2d	4_F_IRIS

1@mir,102,95,3	duplicate(dummy_npc)	Bijou#4mir	4_F_BIJOU
1@mir,103,90,1	duplicate(dummy_npc)	Fenrir#4mir	4_F_FENRIR

1@mir,102,95,3	duplicate(dummy_npc)	Bijou#5mir	4_F_BIJOU
1@mir,99,95,5	duplicate(dummy_npc)	Sarah#5mir	4_F_SARAH
1@mir,100,90,7	duplicate(dummy_npc)	Iris#5mir	4_F_IRIS

1@mir,102,95,3	duplicate(dummy_npc)	Bijou#6mir	4_F_BIJOU
1@mir,104,95,3	duplicate(dummy_npc)	Sarah#6mir	4_F_SARAH
1@mir,103,90,1	duplicate(dummy_npc)	Fenrir#6mir	4_F_FENRIR
