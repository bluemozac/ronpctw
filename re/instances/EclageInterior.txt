//===== rAthena Script ======================================= 
//= Eclage Instance Script
//===== By: ================================================== 
//= Dastgir
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Eclage Interior Instance
//===== Additional Comments: ================================= 
//= 1.0 Converted from the official Aegis script. [Dastgir]
//= 1.1 Converted from Hercules script to rAthena script & Small bug fix. [Skorm]
//============================================================ 

ecl_hub01,132,12,3	script	Chief of Staff#tl02	435,{
	.@party_id = getcharid(1);
	.@md_name$ = "Eclage Interior";

	if (.@party_id == 0 || is_party_leader() == false){
		mes "- ！警告 ！ -";
		mes "當前的任務將在紀念地牢舉行。";
		mes "只有黨的領導人才會進入。請創建您的聚會。";
		close;
	}
	if (ep14_2_mylord == 29) {
		mes "[筆記]";
		mes "這個地方設置為Orb的照明。";
		mes "如果您喜歡市長，那麼您應該在這裡謹慎。";
		next;
		switch (select("輸入它:算了。")) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[筆記]";
				mes "不久前，有一些士兵派出了市長。";
				mes "我一定希望您不是他們想要的干擾，對嗎？";
				close;
			}
			ecl_interior_time = gettimetick(2) + 20 * 60;
			mes "[筆記]";
			mes "鑑於內部安全性嚴格，您應該做好準備。";
			mes "在這裡等待一分鐘。";
			close;
		case 2:
			mes "[筆記]";
			mes "這不是一件容易的事。";
			close;
		}
	}
	mes "[高級拉平]";
	mes "唔？這個房間？";
	mes "沒關係。不用擔心。";
	close;
}

ecl_hub01,130,15,0	script	It is closed shut.	844,{
	if (ep14_2_mylord == 29) {
		mes "這門連接到廣場的天花板。";
		mes "這也是設置照明以使Orb看起來更美麗的地方。";
		next;
		if (select("輸入它:算了。") == 1) {
			.@party_id = getcharid(1);
			.@md_name$ = "Eclage Interior";
			if (.@party_id == 0 || is_party_leader() == false) {
				mes "- ！警告 ！ -";
				mes "當前的任務將在紀念地牢舉行。";
				mes "只有黨的領導人才會進入。請創建您的聚會。";
				close;
			}
			if (gettimetick(2) >= ecl_interior_time){
				mes "[筆記]";
				mes "看來里面的士兵真的很緊張。";
				mes "引導像您這樣的局外人很難。";
				close;
			}
			switch (instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "發生了未知錯誤。";
				close;
			case IE_NOINSTANCE:
				mes "它被關閉。";
				close;
			case IE_OK:
				mapannounce "ecl_hub01",getpartyname(.@party_id) + " Party leader " + strcharinfo(0) + " is entering " + .@md_name$,bc_map,"0x00ff99";
				end;
			default:
			}
		} else
			close;
	}
	mes "這門連接到廣場的天花板。";
	mes "這也是設置照明以使Orb看起來更美麗的地方。";
	mes "它被關閉。";
	close;
}

1@ecl,58,69,3	script	Shenime#ecl01	435,{
	mes "[筆記]";
	mes "哦，這是怎麼回事？";
	mes "他們摧毀了市長派遣的其他部隊？";
	next;
	select("什麼……？ :我打倒了怪物！");
	mes "[筆記]";
	mes "沒關係。";
	mes "您的工作在這裡結束。";
	mes "現在，您成為背叛者。";
	mes "告別，然後！";
	specialeffect EF_BEGINSPELL3;
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "決不！ ！ ！";
	specialeffect2 EF_LORD;
	percentheal -99,0;
	soundeffect "wander_man_move.wav",1;
	sc_start SC_BLIND,60000,0;
	close2;
	warp instance_mapname("1@ecl"),146,95;
	end;

OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,51,64,1	script	Suspicious Creature#1	2375,{
	end;
}
1@ecl,43,67,3	duplicate(Suspicious Creature#1)	Suspicious Creature#2	2375,0,0
1@ecl,55,80,3	duplicate(Suspicious Creature#1)	Suspicious Creature#3	2375,0,0
1@ecl,60,75,3	duplicate(Suspicious Creature#1)	Suspicious Creature#4	2375,0,0

1@ecl,42,80,5	script	Suspicious Creature#5	2376,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
}
1@ecl,48,86,3	duplicate(Suspicious Creature#5)	Suspicious Creature#6	2376,0,0
1@ecl,58,82,3	duplicate(Suspicious Creature#5)	Suspicious Creature#7	2376,0,0
1@ecl,56,88,3	duplicate(Suspicious Creature#5)	Suspicious Creature#8	2376,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
	end;

OnInstanceInit:
	'mob_summoned = 0;
	disablenpc();
	end;

OnEnable:
	sleep 1000;
    mapannounce instance_mapname("1@ecl"),"Warning: Abnormal occurrence in the lighting room. Abnormal occurrence in the lighting room.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	sleep 2000;
    mapannounce instance_mapname("1@ecl"),"Suspicious Creature: Kerrrrr....krwahhhh!!",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	hideoffnpc instance_npcname("Suspicious Creature#5");
	hideoffnpc instance_npcname("Suspicious Creature#6");
	hideoffnpc instance_npcname("Suspicious Creature#7");
	hideoffnpc instance_npcname("Suspicious Creature#8");
	sleep 3000;
	hideonnpc instance_npcname("#sndmaster");
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("Shenime#ecl01")+"::OnEnable";
		mapannounce instance_mapname("1@ecl"),"Shenime: Good, I got here just in time.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	}
	end;

OnTouch:
	if ('mob_summoned > 1) end;
    mes "消除了球附近的入侵者後，出現了另一個奇怪的生物。";
    next;
    mes "我們也無法與他們溝通，其中一個環顧四周並發現其研究員被擊敗了。";
    next;
    mes "[" + strcharinfo(0) + "]";
    mes "他們看起來好像是摩洛哥的化身。";
    mes "在球之後，多蒙國王是嗎？";
    mes "還是只是巧合？";
    next;
    mes "[惡魔之王的下屬]";
    mes "凱爾……呻吟啊！";
    mes "哇！";
    next;
    mes "無法避免戰鬥！";
    mes "他們會殺了我！";
	close2;
	hideonnpc instance_npcname("Suspicious Creature#5");
	hideonnpc instance_npcname("Suspicious Creature#6");
	hideonnpc instance_npcname("Suspicious Creature#7");
	hideonnpc instance_npcname("Suspicious Creature#8");
	disablenpc instance_npcname("Suspicious Creature#5");
	disablenpc instance_npcname("Suspicious Creature#6");
	disablenpc instance_npcname("Suspicious Creature#7");
	disablenpc instance_npcname("Suspicious Creature#8");
	.@eclmap$ = instance_mapname("1@ecl");
	.@sndmaster$ = instance_npcname("#sndmaster");
	monster .@eclmap$,42,80,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,48,86,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,59,82,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,56,88,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,39,72,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,42,69,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,47,64,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,50,60,"Subordinate of the King of Demons",2376,1,.@sndmaster$+"::OnMyMobDead";
	'mob_summoned = 2;
    end;
}

1@ecl,57,65,0	script	#event	139,5,5,{
	end;
OnTouch:
	if ('mob_summoned > 0) end;
	.@eclmap$ = instance_mapname("1@ecl");
	.@eventname$ = instance_npcname("#event");
	mes "一個空的房間，裡面一無所有。";
	mes "房間中間的圓形設備從圓形設備中散發出一盞燈。";
	next;
	mes "[可疑生物]";
	mes "呵呵呵呵呵……";
	next;
	mes "一群無法識別的生物正危及。";
	mes "他們似乎很驚訝，儘管三葉樹的仍然會成立，但很難交流。";
	next;
	mes "[可疑生物]";
	mes "Kyrr…ke…karryrya！";
	next;
	mes "似乎他們將在任何一分鐘內開始攻擊。";
	mes "我們不禁要戰鬥！";
	close2;
	hideonnpc instance_npcname("Suspicious Creature#1");
	hideonnpc instance_npcname("Suspicious Creature#2");
	hideonnpc instance_npcname("Suspicious Creature#3");
	hideonnpc instance_npcname("Suspicious Creature#4");
	disablenpc instance_npcname("Suspicious Creature#1");
	disablenpc instance_npcname("Suspicious Creature#2");
	disablenpc instance_npcname("Suspicious Creature#3");
	disablenpc instance_npcname("Suspicious Creature#4");
	monster .@eclmap$,51,64,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,43,67,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,55,80,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,60,75,"Suspicious Creature",2375,1,.@eventname$+"::OnMyMobDead";
	'mob_summoned = 1;
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
		enablenpc instance_npcname("#sndmaster");
		donpcevent instance_npcname("#sndmaster")+"::OnEnable";
	}
	end;
}

1@ecl,148,97,3	script	Avant#	618,{
	mes "[前衛]";
	mes "嘿，看看誰在這裡！";
	mes "你是我兒子的朋友，對嗎？";
	mes "將我們聚集在這裡的機會真是個奇怪的機會！";
	cutin "avant01",1;
	next;
	mes "[前衛]";
	mes "你有什麼事嗎？";
	mes "它有一個微妙而神奇的迴聲。";
	mes "此後，您立即被扔到這裡。";
	next;
	if (select("都是因為你！ :Shenime說……") == 1) {
		mes "[前衛]";
		mes "我不明白你在說的話。";
		mes "你不能組織你的故事嗎？您不知道“五W和一個H”規則？";
		next;
	}
	mes "[前衛]";
	mes "哦...那就是發生的事情。";
	mes "所以您是說Shenime是在軌道之後？";
	next;
	mes "[前衛]";
	mes "hehehe ...哈哈哈哈哈哈哈哈哈";
	mes "好吧，...他也得到了我嗎？";
	mes "我，所有人的前衛？哈哈哈！";
	cutin "avant02",1;
	next;
	select("你是什​​​​麼意思？");
	mes "[前衛]";
	mes "我自己對球的力量進行了研究。";
	mes "但是Shenime只是敦促我堅持不懈。";
	cutin "avant01",1;
	next;
	mes "[前衛]";
	mes "我從思考球的美麗開始，然後想知道球內積累了什麼樣的力量...";
	mes "我開始質疑球體力量的原則，因為我們都從中受益。";
	next;
	mes "[前衛]";
	mes "所有的Laphines都認為Orb是天然的空氣和水，但他是第一個讓我對其進行不同感知並研究其起源的人。";
	mes "對...石灰。他故意激動我的好奇心。";
	next;
	mes "[前衛]";
	mes "刺激一個不知道空氣和水的人，並使他認識並繼續調查！";
	mes "好吧，...不是那麼有趣！";
	cutin "avant02",1;
	next;
	mes "[前衛]";
	mes "哈...哈哈哈...";
	mes "...我對我只是意識到他是如何使用我的事實感到非常生氣，但更糟糕的是，我為我們的人民做了可怕的事情。";
	mes "我想我現在很生氣。";
	next;
	select("你還有良心嗎？");
	mes "[前衛]";
	mes "您認為我是什麼樣的人！";
	mes "是因為我的兒子嗎？";
	mes "好吧，我不能怪你太多。";
	cutin "avant01",1;
	next;
	mes "[前衛]";
	mes "...呸。";
	mes "這沒什麼好玩的。";
	mes "不再有趣了。我不再對你感興趣。";
	next;
	mes "[前衛]";
	mes "......";
	mes "好像有訪客。";
	next;
	cutin "minuel01",4;
	mes "[米努爾]";
	mes "" + strcharinfo(0) + "Sir,";
	mes "儘管您因懷疑最近在照明室發生的事件而被拘留，但";
	mes "有一些目擊者可以證明您的純真。";
	next;
	mes "[米努爾]";
	mes "我不知道細節，";
	mes "但是我也相信你沒有內gui。";
	mes "我會把你搬到治療室，因為這似乎是嚴重的傷口。";
	next;
	mes "[米努爾]";
	mes "我認為這沒什麼大不了的。";
	mes "...希望。";
	mes "我們走吧。我會指導你。";
	close2;
	cutin "",255;
	warp "1@ecl",146,29;
	end;
}
	
1@ecl,148,29,3	script	Loki#ecl01	512,{
	mes "[洛基]";
	mes "似乎我旁邊的這個傢伙比我說的要多得多。";
	mes "你為什麼不先向他解釋？";
	close;
}	
	
1@ecl,147,30,3	script	Nydhog#ecl01	510,{
	mes "[衛報Nydhog]";
	mes "我認為您應該先和Hisie談談。";
	close;
}
	
1@ecl,144,27,5	script	Hisie#ecl01	623,{
	cutin "hisie01",0;
	mes "[打嗝]";
	mes "你能告訴我到底發生了什麼嗎？";
	next;
	select("我與怪物戰鬥。");
	cutin "hisie03",0;
	mes "[打嗝]";
	mes "怪物？摩洛哥的下屬？";
	mes "您要殺死的是我們的士兵，即laphines！";
	mes "那裡發生了什麼？";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "是因為這個。";
	mes "" + strcharinfo(0) + "It was inside the body of -";
	mes "這就是石膏的強大。";
	next;
	mes "在Loki's Palm上，在那裡休息了一塊小而細的荊棘。";
	next;
	cutin "hisie03",0;
	mes "[打嗝]";
	mes "Shenime，參謀長？";
	mes "他為什麼與此有關？";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "Shenime是摩洛哥的下屬之一。";
	mes "他的權力使他能夠直接控制另一個人或通過身體的一部分間接影響該人。";
	next;
	select("現在我想起來...");
	mes "[" + strcharinfo(0) + "]";
	mes "在來到這里之前，Shenime向我伸出了手，當我握住他的手時，我感到火花。";
	mes "我以為這只是我的想像力，而忽略了……也許那是他...";
	next;
	mes "[尼德霍格]";
	mes "你可能是對的。";
	mes "他們使用邪惡的技巧。";
	mes "這裡的荊棘可能使您的判斷蒙上了困難。";
	next;
	cutin "hisie03",0;
	mes "[打嗝]";
	mes "所以你是說他欺騙了我們所有人？";
	mes "參謀長是一名腰雀，與我們呆了很長時間！他不能...";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "他可能受到了一段時間的影響。";
	mes "無論如何，這個傢伙無罪。";
	mes "我可以證明這一點。";
	next;
	mes "[尼德霍格]";
	mes "我也以Yggdrasil的監護人的名字發誓。";
	mes "" + strcharinfo(0) + "- will by no means do such a thing.";
	next;
	cutin "hisie02",0;
	mes "[打嗝]";
	mes "哈...";
	mes "但是事情失控了。";
	mes "我沒想到。";
	next;
	select("奧布發生了什麼？");
	mes "[打嗝]";
	mes "內核被盜。";
	mes "沒有人知道球體的內核已經消失了，";
	mes "這就是為什麼卡爾沒有在這裡做到這一點。";
	npcskill "AL_HEAL",9,99,80;
	next;
	mes "[打嗝]";
	mes "而且，由於您，幾名士兵受傷。";
	mes "他們都沒有死亡，但大多數人都受傷了。";
	mes "現在情況不是很好。";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "那時有人必須在這裡承擔責任。";
	mes "在所有可能性中" + strcharinfo(0) + "it will be you.";
	next;
	cutin "hisie02",0;
	mes "[打嗝]";
	mes "......這兩位先生在這裡清除了與球有關的所有指控，";
	mes "但是您和Karr都無法避免您的責任。";
	mes "您將不得不趕上石膏。";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "hisie01",0;
	mes "[打嗝]";
	mes "看來您已經休息了。讓我們開始。";
	mes "卡爾在等你。";
	mes "你們兩個會做什麼？";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "不用擔心我們。";
	mes "我們的目標是摩洛哥，惡魔之王。";
	mes "這足以使我們在這裡找到證據。";
	next;
	cutin "hisie01",0;
	mes "[打嗝]";
	mes "感謝您的幫助。";
	mes "我們現在會起飛。";
	mes "我們走吧。" + strcharinfo(0) + ".";
	ep14_2_mylord = 30;
	changequest 7450,7451;
	close2;
	cutin "",255;
	warp "ecl_in03",57,76;
	instance_destroy;
	end;
}
