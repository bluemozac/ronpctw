//===== rAthena Script =======================================
//= Isle Of Bios
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Isle Of Bios Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

moro_cav,50,64,5	script	Yellow Seed#bios	CLEAR_NPC,{
	if (BaseLevel < 160) {
		mes "^ff0000 Characters低於160級別無法進入此地牢。 ^000000";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "^ff0000 you首先必須遇到徘徊的老人才能進入這個地牢。 ^000000";
		close;
	}
	.@party_id = getcharid(1);
	if (.@party_id < 1) {
		mes "^ff0000 you首先必須形成/加入至少1人參加此地牢的聚會。 ^000000";
		close;
	}
	switch( checkquest(15005,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		if (checkquest(15007,PLAYTIME) == 2) {// 5 mins to re-enter
			mes "^ff000000000000000000000000000000000000000000000000000000000000年。";
			close;
		}
		switch( checkquest(15008,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "^0000ffthis紀念地牢在您上次訪問後的23小時內無法訪問。 ^000000";
			close;
		case 2:
			break;
		}
		break;
	case 2:
		erasequest 15005;// Memorial Dungeon: Isle of Bios
		erasequest 15007;// Memorial Dungeon: Isle of Bios
		if (isbegin_quest(15008) > 0)
			erasequest 15008;// Memorial Dungeon: Isle of Bios
		break;
	}
	if (is_party_leader() == true)
		.@create$ = "Create Memorial dungeon.";
	switch( select(.@create$, "進入比奧斯島。", "取消。" ) ) {
	case 1:
		instance_create("Isle of Bios");
		mes "^ff0000創建了紀念地牢。 ^000000";
		mes "^ff0000please單擊並再次按Enter。 ^000000";
		close;
	case 2:
		switch( instance_enter("Isle of Bios") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
		case IE_OTHER:
			if (is_party_leader(.@party_id) == false) {
				mes "^ff0000please請注意，上次訪問後23小時無法訪問此紀念地牢。 ^000000";
				next;
			}
			mes "^ff0000 your黨的領導人尚未創造時間。 ^000000";
			close;
		case IE_OK:
			mapannounce "moro_cav", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering Isle of Bios.",bc_map,"0x00FF99";
			if (isbegin_quest(15005) == 0) {
				setquest 15005;// Memorial Dungeon: Isle of Bios
				setquest 15007;// Memorial Dungeon: Isle of Bios
			}
			end;
		}
	case 3:
		end;
	}
	end;

OnInit:
	while(true) {
		sleep 500;
		specialeffect EF_ENHANCE;
	}
	end;
}

moro_cav,45,60,5	script	Wandering Old Man#sara	4_M_EINOLD,{
	if (BaseLevel < 160) {
		mes "[徘徊的老人]";
		mes "...";
		mes "恐怕您還沒有這樣做。";
		mes "您必須至少是160級。";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "[徘徊的老人]";
		mes "...我在哪裡？";
		next;
		mes "[徘徊的老人]";
		mes "我不知道什麼時候開始...";
		mes "同一個噩夢已經困擾著我很長時間了。";
		next;
		mes "[徘徊的老人]";
		mes "在我的噩夢中，惡魔國王摩洛哥喚醒了他為期一個世紀的沉睡，並充滿了黑暗和恐怖的世界。";
		next;
		mes "[徘徊的老人]";
		mes "我無法擺脫那個噩夢中看到的生動圖像。我像瘋子一樣徘徊，終於到達這裡。";
		next;
		mes "[徘徊的老人]";
		mes "我在夢中看到了這個地方。在這裡，惡魔上帝摩洛哥從沉睡中喚醒。我的夢想向我展示了可能的未來。";
		next;
		mes "[徘徊的老人]";
		mes "如果我的夢想確實是一個預感，那麼惡魔上帝摩洛哥的力量就在那個黃色的種子內。";
		next;
		mes "[徘徊的老人]";
		mes "我試圖進入種子，但無濟於事：我太老了和虛弱。但是也許你可以。";
		next;
		mes "[徘徊的老人]";
		mes "請調查那種黃色的種子。";
		mes "你一個人去太危險了。您應該至少與您同志。";
		setquest 15006;// Isle of Bios Exploration
		completequest 15006;// Isle of Bios Exploration
		close;
	}
	mes "[徘徊的老人]";
	mes "請調查那種黃色的種子。";
	mes "你一個人去太危險了。您應該至少與您同志。";
	close;
}

// warps
1@dth1,291,102,0	warp2	warp1#bios1	2,2,1@dth2,17,93
1@dth2,291,102,0	warp2	warp1#bios2	2,2,1@dth3,45,68

// zone 1
1@dth1,74,94,3	script	Grim Reaper Ankou#bios1	3029,{ end; }
1@dth1,71,97,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios1	4_M_GUILLOTINE
1@dth1,67,97,5	duplicate(Grim Reaper Ankou#bios1)	Vrid#bios1	4_M_KNIGHT_BLACK
// 1@dth1,67,97,5	script	Vrid#bios1	4_M_KNIGHT_BLACK,5,7,{// official

// 1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,{// official
1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios1");
	'zeith$ = instance_npcname("Zeith#bios1");
	'vrid$ = instance_npcname("Vrid#bios1");
	'reaper$ = instance_npcname("Grim Reaper Ankou#bios1");
	initnpctimer;
	npctalk "Vrid: Ah, someone else has come to join us.", 'vrid$;
	end;
OnTimer2500:
	npctalk "Vrid：你也是來阻止摩洛哥復活的嗎？", 'vrid$;
	end;
OnTimer5000:
	npctalk "Vrid: I'm Vrid and this is Zeith, from Prontera. We've been traveling around the world, fighting for justice.", 'vrid$;
	end;
OnTimer7500:
	npctalk "Vrid: Hey, Zeith. Say something.", 'vrid$;
	end;
OnTimer10000:
	npctalk "澤斯：……", 'zeith$;
	end;
OnTimer12500:
	npctalk "澤斯：我是澤斯。", 'zeith$;
	end;
OnTimer15000:
	npctalk "澤斯：……", 'zeith$;
	end;
OnTimer17500:
	npctalk "維里德：別理他。只有我們兩個人的時候他就不那麼安靜了。", 'vrid$;
	end;
OnTimer19500:
	npctalk "Zeith：……你對我們倆來說已經足夠了。", 'zeith$;
	end;
OnTimer21000:
	npctalk "Vrid：我們來只是因為下面那個老傢伙一直在胡言亂語地談論摩洛哥的複活。", 'vrid$;
	end;
OnTimer23500:
	npctalk "Vrid: Err, what's this place? I just touched the seed, and suddenly the scenery changed.", 'vrid$;
	end;
OnTimer26000:
	npctalk "Vrid: Holy cow, are we flying in the sky?!", 'vrid$;
	end;
OnTimer28500:
	npctalk "？ ？ ？ ：*咯咯*你想知道你在哪裡嗎？", 'reaper$;
	end;
OnTimer31000:
	npctalk "澤斯：誰在那兒？ ！", 'zeith$;
	end;
OnTimer33500:
	enablenpc 'reaper$;
	npctalk "Grim Reaper Ankou: *Chuckle* This is the Isle of Bios, a place I conjured up in my mind.", 'reaper$;
	end;
OnTimer36000:
	npctalk "Grim Reaper Ankou: You've walked into the trap, making yourself willing sacrifices for my Lord Morocc. Aren't you afraid of dying?", 'reaper$;
	end;
OnTimer38500:
	npctalk "Vrid: Pah! If I was I wouldn't have come willingly, would I?", 'vrid$;
	end;
OnTimer41000:
	npctalk "Vrid: Whoever you are, we'll kill you before you do!", 'vrid$;
	end;
OnTimer43500:
	npctalk "Grim Reaper Ankou: Don't tempt me, human.", 'reaper$;
	end;
OnTimer46000:
	npctalk "死神安口：我想看著你慢慢痛苦地死去。*咯咯笑*", 'reaper$;
	end;
OnTimer48500:
	disablenpc 'reaper$;
	end;
OnTimer51000:
	npctalk "維里德：他是誰？他是想復活摩洛哥嗎？", 'vrid$;
	end;
OnTimer53500:
	npctalk "Vrid: Let's chase him! Zeith, hurry!", 'vrid$;
	end;
OnTimer54000:
	disablenpc 'vrid$;
	end;
OnTimer56000:
	npctalk "澤斯：……嗯。", 'zeith$;
	end;
OnTimer58500:
	npctalk "Zeith：我討厭跑步。", 'zeith$;
	end;
OnTimer61000:
	disablenpc 'zeith$;
	end;
OnTimer66000:
	stopnpctimer;
	mapannounce 'map_dth1$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00";
	'zeith$ = 'vrid$ = 'reaper$ = "";
	end;
}

1@dth1,1,1,0	script	mobs#bios1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	.@label$ = instance_npcname("mobs#bios1") + "::OnMyMobDead";
	for ( .@mob_id = 3010; .@mob_id < 3013; .@mob_id++ ) {
		switch(.@mob_id) {
		case 3010:		// Corrupt Orc Baby
			setarray .@coord[0],
				90,94, 153,76, 121,53, 135,50, 145,107, 132,126, 101,125, 119,144, 124,164, 192,145, 194,116,
				203,131, 214,142, 196,156, 217,129, 227,150, 215,109, 240,130, 252,105, 249,144, 249,155, 232,87,
				316,103, 245,71, 246,71, 210,62, 214,53, 211,88;
			break;
		case 3011:		// Corrupt Baby Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 166,80, 131,101, 145,107, 108,156, 141,143, 165,130, 203,151,
				203,162, 219,151, 203,162, 206,169, 219,171, 227,150, 238,117, 240,140, 274,104, 306,103,
				237,80, 224,62, 233,57;
			break;
		case 3012:		// Corrupt Familiar
			setarray .@coord[0],
				143,49, 156,46, 170,67, 170,67, 131,101, 144,125, 101,125, 108,156, 135,152, 144,164,
				184,130, 204,112, 227,150, 205,98, 225,103, 228,124, 243,106, 225,89, 297,103, 223,72,
				232,69, 225,54, 211,73;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth1$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth1$,instance_npcname("mobs#bios1") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth1$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth1$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth1$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios1");
		enablenpc instance_npcname("warp1#bios1");
		enablenpc instance_npcname("start1#bios2");
		donpcevent instance_npcname("mobs#bios2") + "::OnStart";	// spawn at entrance
	}
	end;
}

// zone 2
1@dth2,67,97,0	script	start1#bios2	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios2");
	mapannounce 'map_dth2$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00";
	end;
}

1@dth2,1,1,0	script	mobs#bios2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("mobs#bios2");
	.@label$ = instance_npcname("mobs#bios2") + "::OnMyMobDead";
	for ( .@mob_id = 3013; .@mob_id < 3016; ++.@mob_id ) {
		switch(.@mob_id) {
		case 3013:		// Corrupt Orc Warrior
			setarray .@coord[0],
				90,94, 153,76, 116,66, 133,47, 145,107, 137,134, 101,125, 119,144, 124,164, 194,116,
				203,131, 211,88, 215,109, 214,142, 192,145, 196,156, 217,129, 227,150, 230,89, 210,62,
				214,53, 246,71, 248,81, 252,105, 246,126, 249,144, 249,155, 316,103;
			break;
		case 3014:		// Corrupt Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 165,84, 145,107, 131,101, 108,156, 138,140, 165,130, 203,151,
				203,162, 219,151, 206,169, 203,162, 227,150, 222,164, 238,117, 237,80, 224,62, 233,57,
				236,138, 274,104, 306,103;
			break;
		case 3015:		// Corrupt Phen
			setarray .@coord[0],
				142,54, 156,46, 170,67, 170,67, 131,101, 101,125, 108,156, 135,152, 144,164, 135,131,
				184,130, 204,112, 205,98, 225,103, 225,89, 227,150, 232,69, 223,72, 212,75, 225,54,
				247,110, 232,126, 297,103;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth2$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth2$,instance_npcname("mobs#bios2") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth2$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth2$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth2$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios2");
		enablenpc instance_npcname("warp1#bios2");
		enablenpc instance_npcname("Zeith#bios3");
		enablenpc instance_npcname("Vrid#bios3");
		enablenpc instance_npcname("event_boss#bios3");
	}
	end;
}

// zone 3
// 1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,5,7,{// official
1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,{
	if ('reward == 1) {
		mes "[轉動]";
		mes "我過來";
		mes "這個英雄令牌是偶然的。";
		mes "我想把它給你。你應得的。";
		next;
		mes "[轉動]";
		mes "讓我們離開這個令人毛骨悚然的地方！";
		close2;
		getitem 6684,1;// Token Of Hero
		if (ep14_3_isle == 0)
			ep14_3_isle = 1;
		if (checkquest(15007,PLAYTIME) == 0 || checkquest(15007,PLAYTIME) == 1)
			setquest 15008;// Memorial Dungeon: Isle of Bios
		warp "moro_cav",45,63;
	}
	end;
}

1@dth3,64,74,5	script	event_boss#bios3	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("event_boss#bios3");
	'party_id = getcharid(1);
	donpcevent instance_npcname("freeze#bios3") + "::OnStart";
	end;
}

// 1@dth3,69,67,0	script	freeze#bios3	HIDDEN_WARP_NPC,30,30,{// official
1@dth3,1,1,0	script	freeze#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("freeze#bios3");
	'reaper3$ = instance_npcname("Grim Reaper Ankou#bios3");
	'zeith3$ = instance_npcname("Zeith#bios3");
	'vrid3$ = instance_npcname("Vrid#bios3");
	initnpctimer;
	npctalk "Vrid：你很幸運能來到這裡。當我進入那個迷宮的那一刻，我完全迷失了方向。", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Vrid: If it weren't for Zeith, I'd still be in there, wandering. Thanks, Zeith.", 'vrid3$;
	end;
OnTimer5000:
	npctalk "Zeith：……有件事一直困擾著我。", 'zeith3$;
	end;
OnTimer7500:
	npctalk "維里德：怎麼了？", 'vrid3$;
	end;
OnTimer10000:
	npctalk "Zeith：我有一種感覺，隨著時間的推移，怪物變得越來越強大。", 'zeith3$;
	end;
OnTimer12500:
	npctalk "Zeith：就像孩子長大成人一樣。", 'zeith3$;
	end;
OnTimer15000:
	npctalk "Vrid: Um, I think you're right.", 'vrid3$;
	end;
OnTimer17500:
	npctalk "Vrid: They're all small in the beginning, and then we faced bigger and stronger ones over time.", 'vrid3$;
	end;
OnTimer20000:
	npctalk "Vrid: To me, that sounds natural. What are you worried about?", 'vrid3$;
	end;
OnTimer22500:
	npctalk "Zeith: Well, I'm curious about the monster we'll face now.", 'zeith3$;
	end;
OnTimer25000:
	npctalk "Zeith: Whether they're stronger versions of their original selves, or something completely different.", 'zeith3$;
	end;
OnTimer27000:
	enablenpc 'reaper3$;
	end;
OnTimer27500:
	npctalk "死神 Ankou：*咯咯笑* 你們中至少有一個人頭腦清醒。", 'reaper3$;
	end;
OnTimer30000:
	npctalk "維里德：又是你？", 'vrid3$;
	end;
OnTimer32500:
	npctalk "死神Ankou：你對你的下一個對手感到好奇嗎？", 'reaper3$;
	end;
OnTimer35000:
	npctalk "死神安庫：在你前面的是一支不朽的軍隊。", 'reaper3$;
	end;
OnTimer37500:
	npctalk "Grim Reaper Ankou: *Chuckle* Come out, my soldiers!", 'reaper3$;
	end;
OnTimer37700:
	specialeffect EF_READYPORTAL,AREA,'reaper3$;
	end;
OnTimer40000:
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			enablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
OnTimer42000:
	npctalk "Vrid: Shut up, you fiend! We don't have time for your lackeys!", 'vrid3$;
	end;
OnTimer45000:
	npctalk "Vrid：我會親自除掉你！", 'vrid3$;
	end;
OnTimer47500:
	npctalk "Grim Reaper Ankou: Right back at you, bigmouth.", 'reaper3$;
	end;
OnTimer50000:
	npctalk "死神安口：我來教你，在恐怖面前束手無策是多麼可怕！", 'reaper3$;
	end;
OnTimer52500:
	specialeffect EF_STORMGUST,AREA,'reaper3$;
	end;
OnTimer52700:
	specialeffect EF_FREEZED,AREA,'zeith3$;
	specialeffect EF_FREEZED,AREA,'vrid3$;
	specialeffect EF_FIREHIT,AREA,'zeith3$;
	specialeffect EF_FIREHIT,AREA,'vrid3$;
	setnpcdisplay( 'vrid3$, 4_M_FROZEN_KN );
	setnpcdisplay( 'zeith3$, 4_M_FROZEN_GC );

	getpartymember 'party_id, 0, .@player_name$;
	getpartymember 'party_id, 1, .@char_id;
	getpartymember 'party_id, 2, .@account_id;
	for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if (isloggedin(.@account_id[.@i],.@char_id[.@i]) == true) {
			getmapxy .@map$,.@x,.@y, BL_PC, .@player_name$[.@i];
			if (.@map$ == 'map_dth3$ && distance(.@x,.@y,69,67) < 30)
				sc_start SC_FREEZE,17500,0,10000, (SCSTART_NOAVOID|SCSTART_NOTICKDEF|SCSTART_NORATEDEF), .@account_id[.@i];
		}
	}
	end;
OnTimer55000:
	npctalk "維里德：呃！什、這是怎麼回事？ ！我、我動不了！", 'vrid3$;
	end;
OnTimer57500:
	npctalk "Zeith：我、我也不能！我的腳動不了了！", 'zeith3$;
	end;
OnTimer60000:
	npctalk "Grim Reaper Ankou: On the Isle of Bios, each day is a lifetime.", 'reaper3$;
	end;
OnTimer62500:
	npctalk "死神安口：你一定注意到了，我的士兵們在日落之前就已經長大了。", 'reaper3$;
	end;
OnTimer65000:
	npctalk "Grim Reaper Ankou: After nightfall, they've reached the final stage of their development: life beyond death.", 'reaper3$;
	end;
OnTimer67500:
	npctalk "死神安口：沒錯。是時候讓你體驗一下死亡了。", 'reaper3$;
	end;
OnTimer70000:
	npctalk "死神安口：準備好感受死亡的恐懼吧！*咯咯笑*", 'reaper3$;
	// note: unfreeze player
	end;
OnTimer72000:
	disablenpc 'reaper3$;
	end;
OnTimer72500:
	npctalk "Vrid: Damn it, the undead are coming, but I can't fight because I can't move my feet!", 'vrid3$;
	end;
OnTimer75000:
	npctalk "Vrid: Ack, I don't want to die!", 'vrid3$;
	end;
OnTimer77500:
	npctalk "澤斯：……呃……", 'zeith3$;
	end;
OnTimer80000:
	stopnpctimer;
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	.@label$ = instance_npcname("freeze#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Orc Zombie",3016,1, .@label$;// EP14_3_DEATH_C_MOB1
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("freeze#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("freeze#bios3");
		donpcevent instance_npcname("verit#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	verit#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("verit#bios3");
	initnpctimer;
	npctalk "Vrid: Wow, you're amazing! Did you kill them all?", 'vrid3$;
	end;
OnTimer2500:
	npctalk "澤斯：還沒有。更多即將到來！", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("verit#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Verit",3017,1, .@label$;// EP14_3_DEATH_C_MOB2
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("verit#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("verit#bios3");
		donpcevent instance_npcname("megalodon#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	megalodon#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("megalodon#bios3");
	initnpctimer;
	npctalk "Vrid：我希望那真的是他們中的最後一個。", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Zeith: Nope, we aren't so lucky--more is on the way!", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("megalodon#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Megalodon",3018,1, .@label$;// EP14_3_DEATH_C_MOB3
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("megalodon#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("megalodon#bios3");
		donpcevent instance_npcname("boss_prelude#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	boss_prelude#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc 'reaper3$;
	initnpctimer;
	end;
OnTimer500:
	npctalk "Grim Reaper Ankou: I underestimated you, or my soldiers aren't as strong as I thought.", 'reaper3$;
	end;
OnTimer3000:
	npctalk "維里德：你在作弊！釋放我們，公平戰鬥！", 'vrid3$;
	end;
OnTimer5500:
	npctalk "死神安口：你還在說話。我會親手殺了你，然後了事了！", 'reaper3$;
	end;
OnTimer7500:
	disablenpc 'reaper3$;
	end;
OnTimer8000:
	stopnpctimer;
	donpcevent instance_npcname("boss#bios3") + "::OnStart";
	end;
}

1@dth3,1,1,0	script	boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("boss#bios3");
	monster 'map_dth3$,70,67,"Grim Reaper Ankou",3029,1, instance_npcname("boss#bios3") + "::OnMyBossDead";
	'boss_id = $@mobid[0];
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStart";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStart";
	end;
OnMyBossDead:
	'boss_id = 0;
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStop";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStop";
	enablenpc 'reaper3$;
	enablenpc 'zeith3$;
	enablenpc 'vrid3$;
	initnpctimer;
	end;
OnTimer3000:
	npctalk "死神安口：你很強……", 'reaper3$;
	end;
OnTimer6500:
	npctalk "死神安口：但我的目的已經達到了……別以為這就結束了。", 'reaper3$;
	end;
OnTimer8000:
	npctalk "死神安庫：摩洛哥領主很快就會回來。*輕笑*無論你走到哪裡，對死亡的恐懼都會跟著你。", 'reaper3$;
	end;
OnTimer10500:
	disablenpc 'reaper3$;
	specialeffect EF_ICECRASH,AREA,'vrid3$;
	specialeffect EF_ICECRASH,AREA,'zeith3$;
	setnpcdisplay( 'vrid3$, 4_M_KNIGHT_BLACK );
	setnpcdisplay( 'zeith3$, 4_M_GUILLOTINE );
	end;
OnTimer13000:
	npctalk "Vrid: Ah, finally I can move!", 'vrid3$;
	end;
OnTimer16500:
	npctalk "Zeith: Hmpf, I'm ashamed of myself for being so weak...", 'zeith3$;
	end;
OnTimer18000:
	npctalk "Vrid: Whew, that reaper was much stronger than me and Zeith combined.", 'vrid3$;
	end;
OnTimer20500:
	npctalk "Vrid：如果沒有你，我們可能已經死了。", 'vrid3$;
	end;
OnTimer23000:
	stopnpctimer;
	npctalk "Vrid：我想給你一些東西來拯救我們的生命。當你準備好接受時再跟我說話。", 'vrid3$;
	'reward = 1;
	disablenpc instance_npcname("boss#bios3");
	end;
}

1@dth3,1,1,0	script	talk_friends#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer15000:
	.@r = rand(7);
	if (.@r == 0)
		npctalk "Zeith：呃……我感到很無助……", 'zeith3$;
	else if (.@r == 1)
		npctalk "Zeith：Ankou那個傢伙聞起來很危險。一旦他開始使用技能，你最好就移動。", 'zeith3$;
	else if (.@r == 2)
		npctalk "Vrid: Ack, I'm sorry we can't help.", 'vrid3$;
	else if (.@r == 3)
		npctalk "Vrid: You cheater, fight fairly!", 'vrid3$;
	else if (.@r == 4)
		npctalk "Vrid: Damn it, I can't move...", 'vrid3$;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@dth3,1,1,0	script	talk_boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer13000:
	.@r = rand(6);
	if (.@r == 0)
		unittalk 'boss_id, "Grim Reaper Ankou: *Chuckle*";
	else if (.@r == 1)
		unittalk 'boss_id, "Grim Reaper Ankou: Feel the fear of death!";
	else if (.@r == 2)
		unittalk 'boss_id, "Grim Reaper Ankou: Lord Morocc will return soon.";
	else if (.@r == 3)
		unittalk 'boss_id, "Grim Reaper Ankou: Is this the best you can do? Such weak creatures, you humans.";
	initnpctimer;
	end;;
OnStop:
	stopnpctimer;
	end;

OnInstanceInit:
	'reward = 'boss_id = 0;
	'map_dth1$ = instance_mapname("1@dth1");
	'map_dth2$ = instance_mapname("1@dth2");
	'map_dth3$ = instance_mapname("1@dth3");

	// warps
	disablenpc instance_npcname("warp1#bios1");
	disablenpc instance_npcname("warp1#bios2");

	// zone 1
	disablenpc instance_npcname("Grim Reaper Ankou#bios1");
	donpcevent instance_npcname("mobs#bios1") + "::OnStart";	// spawn at entrance

	// zone 2
	disablenpc instance_npcname("start1#bios2");
	disablenpc instance_npcname("mobs#bios2");

	// zone 3
	disablenpc instance_npcname("event_boss#bios3");
	disablenpc instance_npcname("freeze#bios3");
	disablenpc instance_npcname("verit#bios3");
	disablenpc instance_npcname("megalodon#bios3");
	disablenpc instance_npcname("boss_prelude#bios3");
	disablenpc instance_npcname("boss#bios3");
	disablenpc instance_npcname("talk_friends#bios3");
	disablenpc instance_npcname("talk_boss#bios3");
	disablenpc instance_npcname("Vrid#bios3");
	disablenpc instance_npcname("Zeith#bios3");
	disablenpc instance_npcname("Grim Reaper Ankou#bios3");
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
}

1@dth3,72,74,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios3	4_M_GUILLOTINE
1@dth3,70,67,3	duplicate(Grim Reaper Ankou#bios1)	Grim Reaper Ankou#bios3	3029

1@dth3,62,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio1	3016
1@dth3,64,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio1	3016
1@dth3,66,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio1	3016
1@dth3,68,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio1	3016
1@dth3,72,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio1	3016
1@dth3,74,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio1	3016
1@dth3,76,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio1	3016
1@dth3,78,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio1	3016
1@dth3,62,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio2	3016
1@dth3,64,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio2	3016
1@dth3,66,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio2	3016
1@dth3,68,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio2	3016
1@dth3,72,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio2	3016
1@dth3,74,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio2	3016
1@dth3,76,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio2	3016
1@dth3,78,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio2	3016
1@dth3,62,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio3	3016
1@dth3,64,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio3	3016
1@dth3,66,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio3	3016
1@dth3,68,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio3	3016
1@dth3,72,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio3	3016
1@dth3,74,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio3	3016
1@dth3,76,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio3	3016
1@dth3,78,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio3	3016
