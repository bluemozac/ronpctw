//===== rAthena Script =======================================
//= Friday Memorial Dungeon
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Friday Memorial Dungeon
//===== Changelogs: ==========================================
//= 1.0 Initial release [Everade]
//= 1.1 Clean-up [Capuche]
//============================================================

gef_tower,57,170,3	script	Marry Jay	4_F_JOB_BLACKSMITH,{
	if (BaseLevel < 130) {
		mes "[結婚傑伊]";
		mes "您的基本水平太低，當您達到130級時回來。";
		close;
	}
	mes "^ff0000如果您進行任何怪物處理，例如在地牢內進行怪物馴服，它不會算在內。請注意。 ^000000";
	next;
	.@day = gettime(DT_DAYOFWEEK);
	if (.@day != FRIDAY) {
		mes "[結婚傑伊]";
		mes "哦，一個好奇的旁觀者？我嫁給了傑伊（Jay），這是一周中的星期五的負責人。";
		next;
		select("開放時間是什麼時候？");
		mes "[結婚傑伊]";
		mes "地牢將在以下過程中開放：";
		mes "^0000ffriday 00:00~星期五23：59^000000";
		next;
		mes "[結婚傑伊]";
		mes "我希望星期五到來時能見到你~";
		close;
	}
	.@md_name$ = "Friday Dungeon";

	switch(checkquest(12379,PLAYTIME)) {
	case -1:
		break;
	case 0:
	case 1:
		mes "[結婚傑伊]";
		mes "抱歉，但是進入" + .@md_name$ + " has expired.";
		mes "在等待地牢再次可用時休息。";
		close;
	case 2:
		erasequest 12379;
		mes "[結婚傑伊]";
		mes "冷卻時間已經過期。";
		mes "您可以重新進入紀念地牢。";
		close;
	}
	mes "[結婚傑伊]";
	mes "你及時來了。";
	mes "紀念地牢剛剛準備好打開。";
	mes "您想現在輸入嗎？";
	next;

	if (is_party_leader() == true)
		.@menu$ = "Prepare Friday Memorial";

	switch( select( .@menu$, "Enter Friday Memorial", "Cancel" ) ) {
	case 1:
		instance_create(.@md_name$);
		mes "[結婚傑伊]";
		mes "紀念地牢已創建。";
		mes "您現在可以輸入。";
		close;
	case 2:
		switch( instance_enter(.@md_name$) ) {
		case IE_NOMEMBER:
			if (is_party_leader() == true)
				end;
			mes "只有黨員可以進入這個紀念地牢。";
			close;
		case IE_NOINSTANCE:
			if (is_party_leader() == true) {
				mes "沒有紀念地牢註冊。";
				close;
			}
			mes "週五紀念地牢不存在。";
			mes "您的黨領袖尚未創建紀念地牢。";
			close;
		case IE_OTHER:
			mes "發生了未知錯誤。";
			close;
		case IE_OK:
			mes "[結婚傑伊]";
			mes "你走了！";
			mapannounce "gef_tower", "" + strcharinfo(0) + " of the party, " + getpartyname( getcharid(1) ) + ", is entering the " + .@md_name$ + ".", bc_map, 0xFF99;
			if (isbegin_quest(12379) < 1)
				setquest 12379;
			end;
		}
		end;
	case 3:
		mes "[結婚傑伊]";
		mes "你還沒有準備嗎？";
		mes "我猜真的無法幫助";
		close;
	}
	end;
}

//Instance
1@md_gef,185,81	monster	Shining Plant	1083,1,1800000,900000

1@md_gef,110,129,6	script	Marry Jay#0_1	4_F_JOB_BLACKSMITH,{
	if (is_party_leader() == false)
		end;
	mes "[結婚傑伊]";
	mes "在我們開始在這裡設置環境之前，我們將為您提供選擇適當的難度級別的機會。";
	mes "我可以做出自己的選擇，但是每個人都根據他們的能力來不同。";
	next;
	switch( select( "Cancel", "LV130 ~ 199", "LV200 + " ) ) {
	case 1:
		close;
	case 2:
		//MinLevel, Lich Lord, Nightmare, Jakk, Ghoul, Drainliar
		'hard = 0;
		setarray 'mob[0],130,3658,3660,3662,3664,3666;
		break;
	case 3:
		if (BaseLevel < 200) {
			mes "[結婚傑伊]";
			mes "您不能選擇此困難，因為您的基本水平低於200。";
			close;
		}
		'hard = 1;
		setarray 'mob[0],200,3659,3661,3663,3665,3667;
		break;
	}
	if (BaseLevel < 'mob[0]) {
		mes "[結婚傑伊]";
		mes "您的基本水平太低而無法選擇此，您需要保持水平" + 'mob[0] + " or above.";
		close;
	}
	close2;
	npctalk "Now, this place is about to become a hunting ground. I'll get out and close the front door.";
	sleep 4000;
	npctalk "Good Luck~";
	sleep 3000;
	if ('step == 0) {
		'step = 1;
		donpcevent instance_npcname("md_gef_mobs_spawn") + "::OnStart";
		disablenpc();
		enablenpc instance_npcname("The Stranger#cadaver");
		enablenpc instance_npcname("Bizzare Sculpture#boss");
	}
	end;
}

1@md_gef,1,1,6	script	md_gef_mobs_spawn	-1,{
	end;
OnStart:
	.@npc_name$ = instance_npcname("md_gef_mobs_spawn");
	monster 'map_md_gef$,0,0,"Nightmare",'mob[2],20, .@npc_name$ + "::OnNightmareDead";
	monster 'map_md_gef$,0,0,"Jakk",'mob[3],30, .@npc_name$ + "::OnJakkDead";
	monster 'map_md_gef$,0,0,"Ghoul",'mob[4],30, .@npc_name$ + "::OnGhoulDead";
	monster 'map_md_gef$,0,0,"Drainliar",'mob[5],20, .@npc_name$ + "::OnDrainliarDead";
	end;

OnNightmareDead:
	callsub( S_Mob, 'mob[2], "Nightmare", "OnNightmareDead" );
OnJakkDead:
	callsub( S_Mob, 'mob[3], "Jakk", "OnJakkDead" );
OnGhoulDead:
	callsub( S_Mob, 'mob[4], "Ghoul", "OnGhoulDead" );
OnDrainliarDead:
	callsub( S_Mob, 'mob[5], "Drainliar", "OnDrainliarDead" );

S_Mob:
	'count++;
	if (('count % 100) == 0) {
		'rand_chest++;
		if ('rand_chest > 4)
			'rand_chest = 1;
		// Treasure Chest Spawn
		donpcevent instance_npcname("#fd_box" + 'rand_chest) + "::OnStart";
	}
	sleep 10000;
	monster 'map_md_gef$,0,0, getarg(1), getarg(0),1, instance_npcname("md_gef_mobs_spawn") + "::" + getarg(2);
	end;

OnInstanceInit:
	'step = 0;
	'hard = 0;
	'count = 0;
	'rand_chest = 0;
	'freestones = 0;
	'boss_actv = 0;
	'map_md_gef$ = instance_mapname("1@md_gef");
	end;
}


1@md_gef,110,129,6	script(DISABLED)	Mary Juj#0_2	4_F_JOB_BLACKSMITH,{
	npctalk "Hello";
	end;
}

1@md_gef,183,222,5	script(DISABLED)	The Stranger#cadaver	4_TOWER_11,{
	mes "這是一個陌生人的屍體。";
	mes "也許他在尋找寶藏時在這個地方徘徊時死亡。";
	next;
	if (select( "Calm down", "Investigate the body" ) == 1) {
		mes "您尊重死者，並決定離開身體。";
		close;
	}
	mes "看著他口袋裡匆忙亂塗的便條，也許冒險家的名字叫沃爾特。";
	next;
	mes "[冒險家的日誌]";
	mes "我叫沃爾特。對於將來會去這個地方的冒險家，我要離開這個地方，這樣您就不會再犯同樣的錯誤了。";
	next;
	mes "[冒險家的日誌]";
	mes "第一天，我好奇地把手放在附近一個奇怪的雕塑的平板上。";
	mes "...我醒了一些我不應該有的東西。";
	next;
	mes "[冒險家的日誌]";
	mes "現在，復活的古代國王正在我面前徘徊。";
	mes "也許他對他的前世沒有回憶。";
	mes "可以肯定的是，他會殺了我。";
	next;
	mes "[冒險家的日誌]";
	mes "對於將來會去這個地方的冒險家來說，我希望您不要犯與我一樣的錯誤。";
	mes "如果您確實看到可疑的平板，請不要表現出任何興趣並通過它。";
	next;
	mes "[冒險家的日誌]";
	mes "再一次，切勿觸摸平板。無論您有什麼誘惑。";
	if ('freestones != 0)
		close;
	next;
	mes "幾根工藝石從他的身體掉下來。他們可能是在探索這個地方時收集的。";
	close2;
	if ('freestones == 0) {
		'freestones = 1;
		if ('hard == 1)
			.@num = rand(7,9);
		else
			.@num = rand(5,7);
		for ( .@i = 0; .@i < .@num; ++.@i )
			makeitem 25235,1, 'map_md_gef$, rand(181,183), rand(221,224);
	}
	end;
}

1@md_gef,199,73,0	script(DISABLED)	Bizzare Sculpture#boss	CLEAR_NPC,{
	if ('count < 100) {
		mes "這是一個奇怪的雕塑，堆積了很多灰塵。我看不到任何可疑的東西。";
		close;
	}
	if ('boss_actv >= 1) {
		mes "工藝石已經被強大的力量融化到插座上。";
		mes "似乎無法使用插座。";
		close;
	}
	mes "在某個時候，堆積的灰塵散射，雕塑下方的平板上可見10個空心孔。";
	next;
	mes "10個空心插座中的每個插座似乎都非常適合一塊工藝石。";
	next;
	if (select( "Ignore", "Try inserting craft stones" ) == 1)
		end;
	if (countitem(25235) < 10) {
		mes "工藝石已經被強大的力量融化到插座上。";
		mes "似乎無法使用插座。";
		close;
	}
	mes "當您將工藝石插入一個洞中時，它會發出明亮的光線並融化。";
	next;
	delitem 25235,10;
	specialeffect EF_ANGELUS;

	if (rand(1,10) == 10) {
		mes "我周圍的空氣似乎發生了變化。";
		instance_announce 0, "Lord of the Dead: Who Awakened Me? I will meet you in person and ask you why.", bc_map, "0xFFFF00";
		if ('boss_actv == 0) {
			'boss_actv = 1;
			monster 'map_md_gef$,210,110,"Lich King",'mob[1],1;
		}
	}
	else {
		mes "但是什麼也沒發生。";
		close2;
		if ('boss_actv == 0) {
			disablenpc();	// Disable NPC for 5 minutes when MVP didn't spawn.
			initnpctimer;
		}
		end;
	}
	close;

OnTimer300000:
	stopnpctimer;
	enablenpc();
	end;
}


// Treasure Chest Reward
1@md_gef,212,212,3	script(DISABLED)	#fd_box1	4_TREASURE_BOX,{
	specialeffect EF_COIN;
	disablenpc();
	stopnpctimer;
	sleep 500;

	if ('hard == 1)
		.@num = rand(7,9);
	else
		.@num = rand(5,7);

	.@npc_name$ = strnpcinfo(2);

	if (.@npc_name$ == "fd_box1") {
		for ( .@i = 0; .@i < .@num; ++.@i )
			makeitem 25235,1, 'map_md_gef$, rand(210,214), rand(210,214);
	}
	if (.@npc_name$ == "fd_box2") {
		for ( .@i = 0; .@i < .@num; ++.@i )
			makeitem 25235,1, 'map_md_gef$, rand(188,192), rand(54,58);
	}
	if (.@npc_name$ == "fd_box3") {
		for ( .@i = 0; .@i < .@num; ++.@i )
			makeitem 25235,1, 'map_md_gef$, rand(47,51), rand(55,59);
	}
	if (.@npc_name$ == "fd_box4") {
		for ( .@i = 0; .@i < .@num; ++.@i )
			makeitem 25235,1, 'map_md_gef$, rand(42,46), rand(209,213);
	}
	end;

OnStart:
	enablenpc();
	instance_announce 0, "A treasure chest appeared somewhere in the dungeon. It'll disappear after a while, so let's find it.", bc_map, "0xff5500";
	initnpctimer;
	end;
OnTimer180000:	//3min before chest disappears
	disablenpc();
	stopnpctimer;
	end;
}
1@md_gef,190,56,3	duplicate(#fd_box1)	#fd_box2	4_TREASURE_BOX
1@md_gef,49,57,3	duplicate(#fd_box1)	#fd_box3	4_TREASURE_BOX
1@md_gef,44,211,3	duplicate(#fd_box1)	#fd_box4	4_TREASURE_BOX


//Merchant
gef_tower,57,167,3	script	Amateur Collector#pa0829	1_F_01,{
	disable_items;
	function F_equip_menu;
	function F_enchant;

	if (checkweight(1201,1) == 0) {
		mes "您有太多的對象。清除您的庫存。";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "無法繼續，因為您的重物太多。通過清除庫存來減輕總重量。";
		close;
	}
	mes "[業餘收藏家]";
	mes "如果你帶我一個" + mesitemlink(25235, false) + ", I can enchant a wonderful effect on your accessory!";
	next;
	switch( select( "What can you do with the Crafting Stone?", "Accessory Enchantment", "Reset Enchant" ) ) {
	case 1:
		mes "[業餘收藏家]";
		mes "我從地牢中交易昂貴的石頭，以換取我的工藝。我仍然是初學者，但我的技能是可以通過的。";
		mes "請照顧我。";
		next;
		if (select( "I've heard this before.", "I'm listening." ) == 1) {
			mes "[業餘收藏家]";
			mes "是這樣嗎？";
			close;
		}
		mes "[業餘收藏家]";
		mes "通過使用工藝石，我可以具有能力的您的配件之一。";
		next;
		mes "[業餘收藏家]";
		mes "我需要10 x" + mesitemlink(25235, false) + " and 100,000 Zeny.";
		mes "結界過程沒有失敗。";
		next;
		mes "[業餘收藏家]";
		mes "但是 ^0000ffif附件有一個隨機選項，隨機選項將在附魔之後刪除。 ^000000";
		next;
		select("還要別的嗎？");
		mes "[業餘收藏家]";
		mes "附魔後，插入附件的卡將保留。";
		next;
		mes "[業餘收藏家]";
		mes "重置是可能的，但我不能說我經歷了經驗。";
		mes "我的成功率是：";
		mes "皇家戒指：80％";
		mes "其他配件：20％";
		next;
		mes "[業餘收藏家]";
		mes "^0000ffif附件有一個隨機選項，初始化後將刪除隨機選項。 ^000000";
		close;
	case 2:
		.@type = 1;
		.@type_name$ = "enchant";
		break;
	case 3:
		.@type = 2;
		.@type_name$ = "reset";
		break;
	}

	.@slot = F_equip_menu();
	.@equip_id = getequipid(.@slot);
	setarray .@equip_card[0], getequipcardid(.@slot,0), getequipcardid(.@slot,1), getequipcardid(.@slot,2), getequipcardid(.@slot,3);
    copyarray .@card[0], .@equip_card[0], 4;
	
	switch( .@equip_id ) {
	case 0:
		mes "[業餘收藏家]";
		mes "請裝備您的配件。";
		close;
	case 28483:
	case 2601:
	case 2602:
	case 2603:
	case 2605:
	case 2607:
	case 2608:
	case 2609:
	case 2610:
	case 2611:
	case 2612:
	case 2613:
	case 2614:
	case 2615:
	case 2616:
	case 2617:
	case 2618:
	case 2619:
	case 2620:
	case 2621:
	case 2622:
	case 2623:
	case 2625:
	case 2626:
	case 2627:
	case 2628:
	case 2636:
	case 2637:
	case 2638:
	case 2639:
	case 2640:
	case 2641:
	case 2645:
	case 2648:
	case 2649:
	case 2650:
	case 2651:
	case 2652:
	case 2653:
	case 2654:
	case 2655:
	case 2656:
	case 2658:
	case 2663:
	case 2664:
	case 2665:
	case 2666:
	case 2667:
	case 2671:
	case 2680:
	case 2692:
	case 2700:
	case 2701:
	case 2702:
	case 2703:
	case 2712:
	case 2714:
	case 2715:
	case 2716:
	case 2718:
	case 2719:
	case 2720:
	case 2721:
	case 2722:
	case 2723:
	case 2724:
	case 2725:
	case 2726:
	case 2727:
	case 2728:
	case 2729:
	case 2730:
	case 2731:
	case 2732:
	case 2736:
	case 2737:
	case 2743:
	case 2744:
	case 2745:
	case 2746:
	case 2747:
	case 2748:
	case 2749:
	case 2766:
	case 2767:
	case 2768:
	case 2769:
	case 2770:
	case 2771:
	case 2772:
	case 2773:
	case 2774:
	case 2777:
	case 2778:
	case 2779:
	case 2780:
	case 2781:
	case 2783:
	case 2784:
	case 2787:
	case 2788:
	case 2789:
	case 2790:
	case 2794:
	case 2795:
	case 2800:
	case 2801:
	case 2802:
	case 2803:
	case 2804:
	case 2805:
	case 2807:
	case 2808:
	case 2809:
	case 2810:
	case 2826:
	case 2827:
	case 2843:
	case 2853:
	case 2854:
	case 2881:
	case 2956:
		break;
	default:
		mes "[業餘收藏家]";
		mes "我不能" + .@type_name$ + " this " + mesitemlink(.@equip_id, false) + " accessory.";
		close;
	}

	if (.@type == 1) {	// enchant
		switch( .@equip_id ) {
		case 28483:	//Royal Ring Enchantment
			if (.@equip_card[2] != 0) {
				mes "[業餘收藏家]";
				mes "這" + mesitemlink(.@equip_id, false) + " already has maximum enchantment.";
				mes "我不能再做這件事。";
				close;
			}
			.@pos = .@equip_card[3] != 0 ? 2 : 3;
			mes "[業餘收藏家]";
			mes "請選擇一個結界類型~";
			next;
			switch( select( "Physical", "Magical", "Ranged" ) ) {
			case 1:
				if (.@pos == 2)
					.@equip_card[2] = F_enchant(2);
				else
					.@equip_card[3] = F_enchant(3);
				break;
			case 2:
				if (.@pos == 2)
					.@equip_card[2] = F_enchant(4);
				else
					.@equip_card[3] = F_enchant(5);
				break;
			case 3:
				if (.@pos == 2)
					.@equip_card[2] = F_enchant(6);
				else
					.@equip_card[3] = F_enchant(7);
				break;
			}
			if (.@pos == 2) {
				mes "[業餘收藏家]";
				mes "您一定會增強" + mesitemlink(.@equip_id, false) + "?";
				mes "^0000ffthere是失敗的機會^000000。";
				next;
				if (select( "Yes, please proceed", "Cancel" ) == 2)
					end;
			}
			if (countitem(25235) < 10) {
				mes "[業餘收藏家]";
				mes "請帶我10 x" + mesitemlink(25235, false) + ".";
				close;
			}
			if (Zeny < 100000) {
				mes "[業餘收藏家]";
				mes "我需要100,000 Zeny！";
				close;
			}
			delitem 25235,10; //Catalyst
			Zeny -= 100000;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
				end;

			delequip .@slot;
			specialeffect2 EF_REPAIRWEAPON;
			mes "[業餘收藏家]";
			mes "這" + mesitemlink(.@equip_id, false) + " has been enchanted!!";
			getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
			close;

		default:	// Normal Accessory Enchantment
			if (.@equip_card[3] != 0) {
				mes "[業餘收藏家]";
				mes "這" + mesitemlink(.@equip_id, false) + " already has an enchant.";
				mes "我不能再做這件事。";
				close;
			}
			mes "[業餘收藏家]";
			mes "您確定要增強" + mesitemlink(.@equip_id, false) + "?";
			mes "^0000ffthere是失敗的機會^000000。";
			next;
			if (select( "Yes, please proceed", "Cancel") == 2)
				end;
			if (countitem(25235) < 10) {
				mes "[業餘收藏家]";
				mes "請帶我10 x" + mesitemlink(25235, false) + ".";
				close;
			}
			if (Zeny < 100000) {
				mes "[業餘收藏家]";
				mes "我需要100,000 Zeny！";
				close;
			}
			delitem 25235,10; //Catalyst
			Zeny -= 100000;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
				end;

			delequip .@slot;
			.@enchant = F_enchant(1);
			specialeffect2 EF_REPAIRWEAPON;
			mes "[業餘收藏家]";
			mes "這" + mesitemlink(.@equip_id, false) + " has been enchanted with " + mesitemlink(.@enchant, false) + ".";
			getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@enchant;
			close;
		}
		end;
	}
	else {	// reset
		if (.@equip_id == 28483) {
			.@pos = 2;
			.@success = 80;	// success royal
		} else {
			.@pos = 3;
			.@success = 20;	// success normal
		}
		if (.@equip_card[.@pos] == 0) {
			mes "[業餘收藏家]";
			mes "這" + mesitemlink(.@equip_id, false) + " is not at its maximum enchantment.";
			mes "我無法重置此配件。";
			close;
		}
		mes "[業餘收藏家]";
		mes "所有附魔效果將從" + mesitemlink(.@equip_id, false) + "?";
		mes "^ff0000以下是" + .@success + "% chance of success. The accessory will be destroyed if the reset fails!^000000";
		next;
		if (select( "I'll be back later", "Please proceed") == 1)
			end;
		if (countitem(25235) < 10) {
			mes "[業餘收藏家]";
			mes "請帶我10 x" + mesitemlink(25235, false) + "";
			close;
		}
		if (Zeny < 100000) {
			mes "[業餘收藏家]";
			mes "我需要100,000 Zeny！";
			close;
		}
		delitem 25235,10; //Catalyst
		Zeny -= 100000;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@card[0], .@card[1], .@card[2], .@card[3]))
			end;

		delequip .@slot;
		if (rand(100) > .@success) {
			specialeffect2 EF_SUI_EXPLOSION;
			mes "[業餘收藏家]";
			mes "該死...";
			mes "我失敗了，配件被摧毀了。我很抱歉。";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[業餘收藏家]";
		mes "我成功了。";
		mes "這" + mesitemlink(.@equip_id, false) + " has been initialized.";
		getitem2 .@equip_id,1,1,0,0,.@equip_card[0],.@equip_card[1],0,0;
		close;
	}
	end;

function	F_equip_menu {
	.@l$ = getitemname(getequipid(EQI_ACC_L));
	.@r$ = getitemname(getequipid(EQI_ACC_R));
	.@acc_l$ = .@l$ == "null" ? "" : "Left Acc - " + .@l$;
	.@acc_r$ = .@r$ == "null" ? "" : "Right Acc - " + .@r$;

	switch( select( .@acc_l$, .@acc_r$ ) ) {
	case 1:
		.@slot = EQI_ACC_L;
		break;
	case 2:
		.@slot = EQI_ACC_R;
		break;
	}
	return .@slot;
}

function	F_enchant {
	.@type = getarg(0);

	switch(.@type) {
	case 1: //Normal Enchant
		.@i = rand(920);
			 if (.@i < 30) .@enchant = 4701; //STR+2
		else if (.@i < 60) .@enchant = 4702; //STR+3
		else if (.@i < 90) .@enchant = 4703; //STR+4
		else if (.@i < 120) .@enchant = 4711; //INT+2
		else if (.@i < 150) .@enchant = 4712; //INT+3
		else if (.@i < 180) .@enchant = 4713; //INT+4
		else if (.@i < 210) .@enchant = 4721; //DEX+2
		else if (.@i < 240) .@enchant = 4722; //DEX+3
		else if (.@i < 270) .@enchant = 4723; //DEX+4
		else if (.@i < 300) .@enchant = 4731; //AGI+2
		else if (.@i < 330) .@enchant = 4732; //AGI+3
		else if (.@i < 360) .@enchant = 4733; //AGI+4
		else if (.@i < 390) .@enchant = 4741; //VIT+2
		else if (.@i < 420) .@enchant = 4742; //VIT+3
		else if (.@i < 450) .@enchant = 4743; //VIT+4
		else if (.@i < 480) .@enchant = 4796; //HP+200
		else if (.@i < 510) .@enchant = 4798; //HP+400
		else if (.@i < 540) .@enchant = 4792; //DEF+6
		else if (.@i < 570) .@enchant = 4794; //DEF+12
		else if (.@i < 600) .@enchant = 4800; //SP+50
		else if (.@i < 630) .@enchant = 4801; //SP+100
		else if (.@i < 660) .@enchant = 4786; //MDEF+2
		else if (.@i < 690) .@enchant = 4787; //MDEF+4
		else if (.@i < 710) .@enchant = 4861; //MHP+1%
		else if (.@i < 730) .@enchant = 4862; //MHP+2%
		else if (.@i < 750) .@enchant = 4929; //MSP+1%
		else if (.@i < 770) .@enchant = 4882; //ATK+1%
		else if (.@i < 790) .@enchant = 4883; //MATK+1%
		else if (.@i < 800) .@enchant = 4817; //Sharp2
		else if (.@i < 810) .@enchant = 4816; //Sharp3
		else if (.@i < 815) .@enchant = 4843; //Sharp4
		else if (.@i < 825) .@enchant = 29047; //Fatal
		else if (.@i < 835) .@enchant = 4863; //Fatal1Lv
		else if (.@i < 840) .@enchant = 4864; //Fatal2Lv
		else if (.@i < 850) .@enchant = 4832; //Expert Archer1
		else if (.@i < 860) .@enchant = 4813; //Spell3
		else if (.@i < 870) .@enchant = 4812; //Spell4
		else if (.@i < 875) .@enchant = 4826; //Spell5
		else if (.@i < 885) .@enchant = 4805; //Archbishop1Lv
		else if (.@i < 895) .@enchant = 4850; //Heal Amount2
		else if (.@i < 905) .@enchant = 4809; //Fighting Spirit3
		else if (.@i < 915) .@enchant = 4808; //Fighting Spirit4
		else .@enchant = 4820; //Fighting Spirit5
		break;
	case 2: //Royal Physical Slot 3
		.@i = rand(225);
			 if (.@i < 30) .@enchant = 4809; //Fighting Spirit3
		else if (.@i < 60) .@enchant = 4808; //Fighting Spirit4
		else if (.@i < 70) .@enchant = 4820; //Fighting Spirit5
		else if (.@i < 80) .@enchant = 4821; //Fighting Spirit6
		else if (.@i < 85) .@enchant = 4822; //Fighting Spirit7
		else if (.@i < 115) .@enchant = 4818; //Sharp1
		else if (.@i < 145) .@enchant = 4817; //Sharp2
		else if (.@i < 155) .@enchant = 4816; //Sharp3
		else if (.@i < 165) .@enchant = 4843; //Sharp4
		else if (.@i < 170) .@enchant = 4844; //Sharp5
		else if (.@i < 200) .@enchant = 4819; //Atk1
		else if (.@i < 210) .@enchant = 4766; //ATK+2%
		else if (.@i < 215) .@enchant = 4767; //ATK+3%
		else if (.@i < 220) .@enchant = 4807; //Atk Speed1
		else .@enchant = 4842; //Atk Speed2
		break;
	case 3: //Royal Physical Slot 4
		.@i = rand(375);
			 if (.@i < 50) .@enchant = 4702; //STR+3
		else if (.@i < 100) .@enchant = 4703; //STR+4
		else if (.@i < 110) .@enchant = 4704; //STR+5
		else if (.@i < 120) .@enchant = 4705; //STR+6
		else if (.@i < 125) .@enchant = 4706; //STR+7
		else if (.@i < 175) .@enchant = 4732; //AGI+3
		else if (.@i < 225) .@enchant = 4733; //AGI+4
		else if (.@i < 235) .@enchant = 4734; //AGI+5
		else if (.@i < 245) .@enchant = 4735; //AGI+6
		else if (.@i < 250) .@enchant = 4736; //AGI+7
		else if (.@i < 300) .@enchant = 4742; //VIT+3
		else if (.@i < 350) .@enchant = 4743; //VIT+4
		else if (.@i < 360) .@enchant = 4744; //VIT+5
		else if (.@i < 370) .@enchant = 4745; //VIT+6
		else .@enchant = 4746; //VIT+7
		break;
	case 4: //Royal Magical Slot 3
		.@i = rand(230);
			 if (.@i < 30) .@enchant = 4813; //Spell3
		else if (.@i < 60) .@enchant = 4812; //Spell4
		else if (.@i < 70) .@enchant = 4826; //Spell5
		else if (.@i < 80) .@enchant = 4827; //Spell6
		else if (.@i < 85) .@enchant = 4828; //Spell7
		else if (.@i < 115) .@enchant = 4929; //MSP+1%
		else if (.@i < 145) .@enchant = 4805; //Archbishop1Lv
		else if (.@i < 155) .@enchant = 4850; //Archbishop2Lv
		else if (.@i < 165) .@enchant = 4851; //Archbishop3Lv
		else if (.@i < 170) .@enchant = 4852; //Archbishop4Lv
		else if (.@i < 200) .@enchant = 4883; //MATK + 1%
		else if (.@i < 210) .@enchant = 4896; //MATK + 2%
		else if (.@i < 215) .@enchant = 4897; //MATK + 3%
		else if (.@i < 225) .@enchant = 4760; //MATK 1Lv
		else .@enchant = 4761; //MATK 2Lv
		break;
	case 5: //Royal Magical Slot 4
		.@i = rand(375);
			 if (.@i < 50) .@enchant = 4712; //INT+3
		else if (.@i < 100) .@enchant = 4713; //INT+4
		else if (.@i < 110) .@enchant = 4714; //INT+5
		else if (.@i < 120) .@enchant = 4715; //INT+6
		else if (.@i < 125) .@enchant = 4716; //INT+7
		else if (.@i < 175) .@enchant = 4722; //DEX+3
		else if (.@i < 225) .@enchant = 4723; //DEX+4
		else if (.@i < 235) .@enchant = 4724; //DEX+5
		else if (.@i < 245) .@enchant = 4725; //DEX+6
		else if (.@i < 250) .@enchant = 4726; //DEX+7
		else if (.@i < 300) .@enchant = 4742; //VIT+3
		else if (.@i < 350) .@enchant = 4743; //VIT+4
		else if (.@i < 360) .@enchant = 4744; //VIT+5
		else if (.@i < 370) .@enchant = 4745; //VIT+6
		else .@enchant = 4746; //VIT+7
		break;
	case 6: //Royal Ranged Slot 3
		.@i = rand(225);
			 if (.@i < 30) .@enchant = 29047; //Fatal
		else if (.@i < 60) .@enchant = 4863; //Fatal1Lv
		else if (.@i < 70) .@enchant = 4864; //Fatal2Lv
		else if (.@i < 80) .@enchant = 4865; //Fatal3Lv
		else if (.@i < 85) .@enchant = 4866; //Fatal4Lv
		else if (.@i < 115) .@enchant = 4818; //Sharp1
		else if (.@i < 145) .@enchant = 4817; //Sharp2
		else if (.@i < 155) .@enchant = 4816; //Sharp3
		else if (.@i < 165) .@enchant = 4843; //Sharp4
		else if (.@i < 170) .@enchant = 4844; //Sharp5
		else if (.@i < 200) .@enchant = 4832; //Expert Archer1
		else if (.@i < 210) .@enchant = 4833; //Expert Archer2
		else if (.@i < 215) .@enchant = 4834; //Expert Archer3
		else if (.@i < 220) .@enchant = 4807; //Atk Speed1
		else .@enchant = 4842; //Atk Speed2
		break;
	case 7: //Royal Ranged Slot 4
		.@i = rand(375);
			 if (.@i < 50) .@enchant = 4732; //AGI+3
		else if (.@i < 100) .@enchant = 4733; //AGI+4
		else if (.@i < 110) .@enchant = 4734; //AGI+5
		else if (.@i < 120) .@enchant = 4735; //AGI+6
		else if (.@i < 125) .@enchant = 4736; //AGI+7
		else if (.@i < 175) .@enchant = 4722; //DEX+3
		else if (.@i < 225) .@enchant = 4723; //DEX+4
		else if (.@i < 235) .@enchant = 4724; //DEX+5
		else if (.@i < 245) .@enchant = 4725; //DEX+6
		else if (.@i < 250) .@enchant = 4726; //DEX+7
		else if (.@i < 300) .@enchant = 4752; //LUK+3
		else if (.@i < 350) .@enchant = 4753; //LUK+4
		else if (.@i < 360) .@enchant = 4754; //LUK+5
		else if (.@i < 370) .@enchant = 4755; //LUK+6
		else .@enchant = 4756; //LUK+7
		break;
	}
	return .@enchant;
}
}
