//===== rAthena Script ======================================= 
//= God Item Quest - Variable Management
//===== By: ================================================== 
//= MasterOfMuppets
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Use this NPC if the seals quest variables get messed up.
//===== Additional Comments: =================================
//= 1.0 First version. [MasterOfMuppets]
//= 1.1 Updated several aspects of the script. [L0ne_W0lf]
//= 1.2 Renewal script update. [Euphy]
//=    Added GM management function.
//============================================================

sec_in02,15,170,0	script	Golbal var	817,{
	callfunc "F_GM_NPC";
	mes "[查看]";
	mes "請輸入密碼。";
	next;
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "[查看]";
		mes "請選擇菜單。";
		next;
		switch(select("現在:第一項:第二項:第三項:第四項:重置:god_sl_1:god_eremes:god_brising:god_mjo_0:god_mjo_1:god_mjo_2:god_mjo_3:god_mjo_4")) {
		case 1:
			mes "^0000ff $ god1 ^000000 = ^ff0000" + $God1 + "^000000.";
			mes "^0000ff $ god2 ^000000 = ^ff0000" + $God2 + "^000000.";
			mes "^0000FF $ GOD3 ^000000 = ^ff0000" + $God3 + "^000000.";
			mes "^0000ff $ god4 ^000000 = ^ff0000" + $God4 + "^000000.";
			close;
		case 2: callsub L_Number,"$God1"; break;
		case 3: callsub L_Number,"$God2"; break;
		case 4: callsub L_Number,"$God3"; break;
		case 5: callsub L_Number,"$God4"; break;
		case 6:
			mes "[查看]";
			mes "您是否真的確定要重置God globalvar的整個列表？";
			mes "請輸入密碼。";
			next;
			if (callfunc("F_GM_NPC",68392411,0) == 1) {
				mes "[查看]";
				mes "現在，整個Globalvar的整個清單正在重置。";
				next;
				set $God1,0;
				set $God2,0;
				set $God3,0;
				set $God4,0;
				mes "^0000ff $ god1 ^000000 = ^ff0000" + $God1 + "^000000.";
				mes "^0000ff $ god2 ^000000 = ^ff0000" + $God2 + "^000000.";
				mes "^0000FF $ GOD3 ^000000 = ^ff0000" + $God3 + "^000000.";
				mes "^0000ff $ god4 ^000000 = ^ff0000" + $God4 + "^000000.";
				close;
			} else {
				mes "[查看]";
				mes "該命令已取消。";
				close;
			}
		case 7: callsub L_Var,"god_sl_1"; break;
		case 8: callsub L_Var,"god_eremes"; break;
		case 9: callsub L_Var,"god_brising"; break;
		case 10: callsub L_Var,"god_mjo_0"; break;
		case 11: callsub L_Var,"god_mjo_1"; break;
		case 12: callsub L_Var,"god_mjo_2"; break;
		case 13: callsub L_Var,"god_mjo_3"; break;
		case 14: callsub L_Var,"god_mjo_4"; break;
		}
	} else {
		mes "[查看]";
		mes "密碼不正確。";
		close;
	}

L_Number:
	input .@input,0,$@god_check2;
	mes "[查看]";
	mes "你想改變"+.@input+"?";
	mes "請輸入密碼。";
	next;
	if (callfunc("F_GM_NPC",68392411,0) == 1) {
		mes "[查看]";
		mes "命令"+.@input+" has been confirmed.";
		next;
		setd getarg(0),.@input;
		mes getarg(0)+" "+.@input;
		close;
	} else {
		mes "[查看]";
		mes "該命令已取消。";
		close;
	}

L_Var:
	input .@input;
	setd getarg(0),.@input;
	next;
	mes "[查看]";
	mes "完畢。";
	close;

OnInit:
	// Seals roll at 25/50 in Renewal and 50/100 in Pre-Renewal.
	if (checkre(0)) {
		set $@god_check1,25;
		set $@god_check2,50;
	} else {
		set $@god_check1,50;
		set $@god_check2,100;
	}
	end;
}
