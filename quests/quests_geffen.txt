//===== rAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By: ================================================== 
//= kobra_k88 & Samuray22
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project; 7.15 +
//===== Description: ========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments: ================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//= 1.5 Added Number Match Game. [Euphy]
//============================================================ 

// Blacksmith: 'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	Blacksmith	63,{
	mes "[鐵匠]";
	mes "你從未聽說過我嗎？為什麼，我是這片土地的資深鐵匠。我在這種熱血的地獄火上度過了30年，彎曲了鋼鐵！";
	next;
	mes "[鐵匠]";
	mes "但...";
	mes "我的年齡越大，我的能力隨著年齡的增長而開始消失。因此，我為年輕，熱門，即將到來的鐵匠發明了一些東西，希望他們也能掌握我的手藝...";
	next;
	switch(select("它是什麼？ :...")) {
	case 1:
		mes "[鐵匠]";
		mes "我們的鐵匠必須始終在極高的熱量下操縱鋼，而且熱空氣和融化的金屬比大多數人所能站立的要多。";
		next;
		mes "[鐵匠]";
		mes "為了我們的手藝，我們美麗而陽剛的面孔處於危險之中...";
		next;
		mes "[鐵匠]";
		mes "但是，有了這種技術奇蹟，無需擔心！看， ^3355ffwelding mask ^000000 !!";
		next;
		mes "[鐵匠]";
		mes "它可以用脂肪金屬板覆蓋您的臉部。的確，這是每個人都想要的項目！如果不是你自己，最好把這個送給一個正在學習smithing的朋友。";
		next;
		mes "[鐵匠]";
		mes "那怎麼樣？如果您給我 ^2244ff50鋼 ^000000和 ^4422ffonly 2000 Zeny ^000000，我會立即為您提供~！";
		next;
		switch(select("嗯……不錯。好的.: 怎麼又起作用了？")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[鐵匠]";
				mes "啊，我看到您喜歡鐵匠的作品！好吧，我會馬上做！ ！";
				next;
				delitem 999,50;  //Steel
				set Zeny,Zeny-2000;
				getitem 2292,1; // Welding_Mask
				mes "[鐵匠]";
				mes "哈哈哈~！充分利用它！";
				close;
			}
			else {
				mes "[鐵匠]";
				mes "嗯...";
				mes "您沒有我需要的物品。";
				next;
				mes "[鐵匠]";
				mes "一個共同的鐵匠會將其解釋為不尊重的跡象，但我會原諒您。只是不要忘記如果您想讓我做這個面具，我需要什麼。";
				close;
			}
		case 2:
			mes "[鐵匠]";
			mes "^3355ffwelding面膜 ^000000通過用脂肪金屬板覆蓋臉部。這項發明是錘子和鐵砧30年來無休止的辛勞的結果。";
			close;
		}
	case 2:
		mes "[鐵匠]";
		mes "啊...！";
		mes "我想這是您第一次遇到像我一樣偉大的鐵匠，而您對敬畏無語。請花點時間在準備好時說話。";
		next;
		mes "^3355ff這個傢伙";
		mes "有點滿";
		mes "他自己。 ^000000";
		close;
	}
}

// Eric: 'Headset' Quest
//============================================================
geffen_in,30,71,4	script	Eric	83,{
	mes "[埃里克]";
	mes "請聽";
	mes "關於我的故事";
	mes "我的祝福";
	mes "祖父。";
	next;
	switch(select("談話:詢問所需物品:製造:退出")) {
	case 1:
		mes "[埃里克]";
		mes "幾年前去世的祖父對我來說很特別。他總是和我在一起，照顧我。他比我總是忙於工作的老人要好得多。";
		next;
		mes "[埃里克]";
		mes "對我來說，祖父的圖書館總是像我可以逃脫的幻想之地。我大部分時間都在那裡度過。";
		next;
		mes "[埃里克]";
		mes "我從圖書館的書中學到了很多東西。祖父對煉金術和音樂感興趣，他的圖書館裡有許多神秘的舊書。";
		next;
		mes "[埃里克]";
		mes "一天...";
		mes "像往常一樣，我去了圖書館，發現了一個有趣的示意圖。";
		next;
		mes "[埃里克]";
		mes "它顯示了一些看起來像耳罩的儀器，當戴在頭上時，可以通過細線向耳朵傳遞音樂或竊竊私語。";
		next;
		mes "[埃里克]";
		mes "我猜...";
		mes "這是我祖父未完成的工作。我遇到了一些煉金術士，並要求他們生產它，但他們拒絕了我的提議...";
		next;
		mes "[埃里克]";
		mes "最後，我認為“我必須自己做。”因此，我研究了幾年，現在我完全了解如何製作那件事。但是，我永遠無法收集材料來實現它！";
		next;
		mes "[埃里克]";
		mes "如果我能收到這些文章，我會立即構建它。如果您對我的想法感興趣，您會為我帶來嗎？";
		next;
		mes "[埃里克]";
		mes "我想做的就是為了紀念我的祖父而建立這個東西，並完成他開始的工作。如果您要求我保留它，我不會介意。";
		close;
	case 2:
		mes "[埃里克]";
		mes "40^3355ff鋼^000000";
		mes "1 ^3355fforidecon ^000000";
		mes "1 ^3355ffalCohol ^000000";
		mes "1 ^3355ff煤^000000";
		mes "這些就是我對祖父的傑作所需的東西。";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			getitem 5001,1; //Headset
			mes "*輕敲！輕敲！輕敲！ *";
			next;
			mes "[埃里克]";
			mes "謝謝。";
			mes "因為你，我可以實現祖父的願望。如果他從天上低頭看著我，他會很高興。";
			next;
			mes "[埃里克]";
			mes "正如我說的，請接受。我為我自己做這件事而感到自豪。";
			close;
		}
		else {
			mes "[埃里克]";
			mes "感謝您嘗試幫助我，但您沒有帶上我需要的所有物品。我認為您很快就會找到它們。";
			close;
		}
	case 4:
		mes "[埃里克]";
		mes "沒有？";
		mes "出色地...";
		mes "你有自己的";
		mes "我想擔心。";
		mes "告別。";
		close;
	}
}

// Number Match Game :: gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	700,{
	mes "[NIA]";
	mes "你好，我是尼亞，第五";
	mes "Mawong的門徒，";
	mes "遊戲策劃者。現在，";
	mes "您想如何玩";
	mes "與我的數字比賽遊戲？";
	next;
	switch(select("號碼匹配遊戲？ :練習號碼匹配:玩號碼匹配:記錄保持者:取消")) {
	case 1:
		mes "[NIA]";
		mes "啊，嗯，目的";
		mes "數字匹配遊戲是";
		mes "猜猜我在想的數字";
		mes "的。當然，有一些";
		mes "限制數字的規則";
		mes "我可以選擇。";
		next;
		mes "[NIA]";
		mes "我製作的所有數字";
		mes "上升必須是3位數字，我不能";
		mes "使用0用於任何數字，";
		mes "而且我不能使用相同的";
		mes "任何數字的數字";
		mes "不止一次。";
		next;
		mes "[NIA]";
		mes "所以，例如，我不能";
		mes "使用數字103、112，";
		mes "252，或701。這些數字";
		mes "要么有0，要么使用";
		mes "數字中的數字相同";
		mes "不止一次。現在知道了嗎？";
		next;
		mes "[NIA]";
		mes "現在，您只有5個猜測，";
		mes "每次猜測之後，我會告訴";
		mes "你猜到了多少位數字";
		mes "正確地，然後是正確猜測的數字的數量";
		mes "正確的位置。";
		next;
		mes "[NIA]";
		mes "例如，假設";
		mes "我想出的數字是168。";
		mes "如果您的第一個猜測是678，那麼您猜到了兩個數字，";
		mes "6和8，正確。然而，";
		mes "只有8位在正確的位置。";
		next;
		mes "[NIA]";
		mes "你知道，這是個好主意";
		mes "如果您嘗試練習模式";
		mes "第一的。然後，當你更多";
		mes "舒適，玩實際";
		mes "數字匹配遊戲。不要忘記您的得分速度！";
		next;
		mes "[NIA]";
		mes "啊，如果你碰巧";
		mes "在";
		mes "數字匹配遊戲，你會";
		mes "記錄了您的名字，";
		mes "所以不要錯過這個機會";
		mes "名望，甚至是財富~";
		close;
	case 2:
		mes "[NIA]";
		mes "太好了，讓我們做一個練習";
		mes "數字匹配遊戲。請";
		mes "嘗試猜測我的電話號碼，好嗎？";
		mes "如果要取消，請輸入大於1,000的數字。";
		set .@practice,1;
		break;
	case 3:
		mes "[NIA]";
		mes "太好了，讓我們玩號碼匹配！";
		mes "您將有5個機會來猜測我想的數字。";
		mes "如果要取消，請輸入大於1,000的數字。";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(null)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[NIA]";
		mes "^ff0000" + .@name_record$ + "^000000 is the";
		mes "記錄持有人的數字";
		mes "比賽時間";
		mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
		close;
	case 5:
		mes "[NIA]";
		mes "照顧好你自己，";
		mes "冒險家。如果你曾經";
		mes "感覺像玩號碼";
		mes "比賽遊戲，來談談";
		mes "對我來說，好嗎？見~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("%H%M%S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[NIA]";
				mes "您已經取消了";
				mes "數字匹配遊戲。";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[NIA]";
			mes "你進入了其中一個";
			mes "數字為0，或輸入";
			mes "相同的數值";
			mes "數字不止一次。";
			mes "請輸入另一個猜測。";
			next;
		}

		set .@retry, .@retry+1;
		mes "[NIA]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "您的猜測是 ^0000ff"+.@input+"^000000.";
		mes "給我一點";
		mes "提出您的結果。";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[NIA]";
				mes "是的，數字";
				mes "我猜是 ^ff0000"+.@yagu+"^000000!";
				mes "恭喜，你";
				mes "剛贏得了這種練習";
				mes "遊戲！現在你應該";
				mes "準備真實的東西~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(null)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[NIA]";
					mes "偉大的！你猜了";
					mes "我的電話號碼是";
					mes "^ff0000" + .@yagu + "^000000. You finished in";
					mes "^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000,";
					mes "打破先前的記錄";
					mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
					next;
					mes "[NIA]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "設置舊記錄，";
					mes "但是現在你是";
					mes "新人擊敗";
					mes "比賽遊戲。恭喜";
					mes "為了設置新記錄！";
					close;
				}
				mes "[NIA]";
				mes "做得好，你猜想了";
				mes "我的電話號碼！答案，的";
				mes "當然，是 ^ff0000" + .@yagu + "^000000. You managed";
				mes "正確猜出這個數字";
				mes "在 ^ff0000中" + .@total_min + " minutes, " + .@total_sec + " seconds^000000.";
				next;
				mes "[NIA]";
				mes "^ff0000" + .@name_record$ + "^000000 is";
				mes "當前數字匹配";
				mes "一段時間記錄持有人";
				mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
				mes "祝你好運，希望你";
				mes "有一天可以打破此唱片~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[NIA]";
				mes "您的猜測有 ^ff0000" + .@ball + "^000000 of the";
				mes "正確的數字。 ^ff0000" + .@strike + "^000000 of the";
				mes "您猜測的數字";
				mes "正確的數字";
				mes "正確的位置。";
				mes "好吧，再試一次~";
				next;
			} else {
				mes "[NIA]";
				mes "您的最後一個猜測有 ^ff0000" + .@ball + "^000000";
				mes "正確的數字，";
				mes "和 ^ff0000" + .@strike + "^000000 digits in your guess";
				mes "有正確的數字";
				mes "正確的位置。";
				switch(.@retry) {
					case 1: mes "你還剩四種猜測。"; break;
					case 2: mes "你還剩下三個猜測。"; break;
					case 3: mes "你還有兩個猜測。"; break;
					case 4: mes "你只剩下一個猜測了..."; break;
					case 5: mes "答案是 ^ff0000" + .@yagu + "^000000."; close;
				}
				next;
			}
		}
	}
}
