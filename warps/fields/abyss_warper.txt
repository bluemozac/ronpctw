//===== rAthena Script =======================================
//= Abyss Cave Warper
//===== By: ==================================================
//= erKURITA
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Warper to Abyss Cave. and warper out
//===== Additional Comments: =================================
//= 1.0 Added by Nexon [Nexon]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Fixed tab - missing pillar's appeared [Lupus]
//= 1.3 Updated Abyss Lake entrance based on official [SinSloth]
//= 1.3a Removed .GATs [Lupus]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//============================================================

hu_fild05,168,304,0	script	Column#abyss1	111,{

	mes "^3355FF有3個奇怪的";
	mes "上有異形凹槽";
	mes "這個的表面";
	mes "柱子。看來";
	mes "某些龍的身體部位";
	mes "會完美地融入";
	mes "柱槽。 ^000000";
	if((countitem(1035)) && (countitem(1036)) && (countitem(1037)))
	{
		next;
		switch( select("插入龍身體部件","吃龍的身體部位" ) )
		{
			case 1:
				mes "^3355FF您小心地放置了";
				mes "龍犬變成其中之一";
				mes "凹槽，然後你";
				mes "聽到強有力的隆隆聲";
				mes "從列內。 ^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF你慢慢插入一個";
				mes "龍鱗變成另一個";
				mes "柱的凹槽，";
				mes "觸發另一個小";
				mes "柱子的震動。 ^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF您慎重插入";
				mes "一條龍尾進​​入";
				mes "最後的凹槽。燈光閃耀";
				mes "從裂縫中出來";
				mes "柱的表面...^000000";
				specialeffect EF_WINDHIT;
				next;
				mes "^3355FF下面的地面";
				mes "你的腳開始";
				mes "劇烈搖晃。 ^000000";
				specialeffect EF_BOWLINGBASH;
				donpcevent "AbyssWarp::OnWarp";
				specialeffect2 EF_PORTAL;
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				close2;
				warp "hu_fild05",184,204;
				end;

			case 2:
				mes "^3355FF你小心翼翼地放置一個";
				mes "龍之犬，龍";
				mes "鱗片，一條龍尾化為";
				mes "你的嘴慢慢開始";
				mes "咀嚼。什麼也沒發生";
				mes "以及這些物品的味道";
				mes "令人驚訝的是腐爛了。 ^000000";
				next;
				emotion ET_HUNGRY, playerattached();
				delitem 1035,1;
				delitem 1036,1;
				delitem 1037,1;
				percentheal -10,0;
				close;
		}
	}
	close;
}

hu_fild05,171,211,0	script	Column#abyss2	111,1,1,{

	mes "^3355FF此欄目看起來";
	mes "非常類似於";
	mes "你見過的一個";
	mes "湖。有";
	mes "顯眼的藍色";
	mes "表面有凹槽。 ^000000";
	next;
	switch( select("觸摸凹槽","從凹槽中移除項目" ) )
	{
		case 1:
			mes "^3355FF*咔嗒咔嗒*^000000";
			next;
			mes "^3355FF發出的光";
			mes "凹槽扭曲並且";
			mes "柱子開始搖晃。";
			mes "你聽到微弱的隆隆聲";
			mes "從列內部。 ^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "*嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎嘎！";
			mes "*砰！ *";
			next;
			mes "^3355FF發出的光";
			mes "柱槽增大";
			mes "當你感覺到時更亮";
			mes "你腳下的土地";
			mes "開始慢慢下沉...^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",169,305;
			end;

		case 2:
			mes "^3355FF*噗噗噗！ *";
			mes "你一不小心";
			mes "損壞了該物品。 ^000000";
			specialeffect EF_WINDHIT;
			next;
			mes "^3355FF*嗚嗚嗚嗚嗚嗚嗚嗚嗚嗚嗚嗚嗚！ *^000000";
			next;
			mes "^3355FF發出的光";
			mes "凹槽扭曲並且";
			mes "柱子開始搖晃。";
			mes "地面的震動";
			mes "使人難以站立";
			mes "穩步地，但突然你";
			mes "在其他地方扭曲了...^000000";
			next;
			specialeffect EF_BOWLINGBASH;
			specialeffect2 EF_PORTAL;
			close2;
			warp "hu_fild05",157,284;
			end;
	}
}

hu_fild05,196,210,1	script	AbyssWarp	45,2,2,{

OnInit:
	disablenpc "AbyssWarp";
	end;

OnTouch_:
	warp "abyss_01",260,268;
	end;

OnWarp:
	initnpctimer;
	enablenpc "AbyssWarp";
	end;

OnTimer30000:
	stopnpctimer;
	disablenpc "AbyssWarp";
	end;
}
