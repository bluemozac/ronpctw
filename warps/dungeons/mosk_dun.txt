//===== rAthena Script =======================================
//= Moscovia Dungeon Warp Script
//===== By: ==================================================
//= Kisuka, $ephiroth
//===== Current Version: =====================================
//= 2.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Warp Points for Moscovia Dungeon
//===== Additional Comments: =================================
//= 1.0 Initial version [$ephiroth]
//= 1.1, 1.2 Updated from jA [$ephiroth]
//= 1.3 Added warp npcs. Removed place holder warps. [Kisuka]
//= 1.4 Fixed cords on milestone 1-3. [Kisuka]
//= 2.0 Clean-up. [Capuche]
//============================================================

mosk_fild02,190,257,0	warp	moscoviawarp0401	1,1,mosk_dun01,189,45
mosk_dun01,189,42,0	warp	moscoviawarp0501	1,1,mosk_fild02,190,254
mosk_in,215,36,0	warp2	babayagaout	1,1,mosk_dun02,53,217

mosk_dun02,53,220,4	script	#babayagain	WARPNPC,1,1,{
	end;
OnTouch:
	if (mos_nowinter == 11) {
		donpcevent "Soldier1#mos::OnEnable";
		donpcevent "Soldier2#mos::OnEnable";
		donpcevent "Soldier3#mos::OnEnable";
		mes "[士兵]";
		mes "就停在那裡！";
		next;
		mes "[士兵]";
		mes "我們有信息";
		mes "你一直在秘密地";
		mes "與…溝通";
		mes "巴巴雅嘎，可怕的。";
		next;
		mes "[士兵]";
		mes "如果你有話要說，";
		mes "與沙皇交談。";
		mes "跟我們一起來吧。";
		close2;
		donpcevent "Soldier1#mos::OnInit";
		donpcevent "Soldier2#mos::OnInit";
		donpcevent "Soldier3#mos::OnInit";
		set mos_nowinter,12;
		warp "mosk_in",125,92;
		end;
	}
	else if (mos_nowinter == 12 || mos_nowinter == 13) {
		donpcevent "Soldier1#mos::OnEnable";
		donpcevent "Soldier2#mos::OnEnable";
		donpcevent "Soldier3#mos::OnEnable";
		mes "[士兵]";
		mes "我們預計";
		mes "你會再次出現。";
		mes "你跟沙皇有什麼關係？";
		next;
		if(select("逃跑:說明情況。") == 1) {
			mes "["+strcharinfo(0)+"]";
			mes "我應該離開這裡...";
			close2;
			warp "mosk_dun02",100,100;
		}
		else {
			mes "-我解釋一下到現在為止發生的事情。 -";
			next;
			mes "[士兵]";
			mes "如果你有話要說，請說";
			mes "給沙皇。";
			mes "如果你離開這個地方，";
			mes "你將被視為我們的敵人。";
			close2;
		}
		donpcevent "Soldier1#mos::OnInit";
		donpcevent "Soldier2#mos::OnInit";
		donpcevent "Soldier3#mos::OnInit";
		end;
	}
	else {
		warp "mosk_in",215,41;
		end;
	}
}

mosk_dun02,53,215,3	script	Soldier1#mos	4_M_RUSKNIGHT,{
	mes "[士兵]";
	mes "我們是沙皇的士兵。";
	mes "我們接到緊急通知，";
	mes "別再打擾我們了。";
	close;
	
OnInit:
	hideonnpc strnpcinfo(0);
	end;
OnEnable:
	hideoffnpc strnpcinfo(0);
	end;
}
mosk_dun02,50,217,3	duplicate(Soldier1#mos)	Soldier2#mos	4_M_RUSKNIGHT
mosk_dun02,56,217,5	duplicate(Soldier1#mos)	Soldier3#mos	4_M_RUSKNIGHT

mosk_dun01,170,164,3	script	Milestone#1	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 侍奉太陽的人都會死，去北方吧。-^000000";
	close;
}

mosk_dun01,207,276,3	script	Milestone#1-2	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 前進者可能受傷，但內心勇敢，後退者安全。";
	mes "如果你有勇氣，就勇往直前。否則，就退一步。-^000000";
	next;
	if(select("向前一步:留下來。") == 1) {
		mes "- 你沿著前面的路走。-";
		close2;
		warp "mosk_dun02",165,30;
		end;
	}
	mes "- 你不會進步。-";
	close;
}

mosk_dun01,264,191,3	script	Milestone#1-3	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 前進的人可能會受傷，但勇敢的人，後退的人會安全。";
	mes "如果你有勇氣，那就向前邁出一步。否則，退後一步。-^000000";
	next;
	if(select("向前一步:留下來。") == 1) {
		mes "- 你沿著前面的路走。-";
		close2;
		warp "mosk_dun03",117,36;
		end;
	}
	mes "- 你決定留下來。-";
	close;
}

mosk_dun02,168,28,3	script	Milestone#2-1	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 進者有傷，但心勇者，後退者安然。";
	mes "如果你勇敢，那就向前邁出一步。否則，退後一步。-^000000";
	next;
	if(select("回去吧:留下來。") == 1) {
		mes "- 你感覺到危險並決定回去 -";
		close2;
		warp "mosk_dun01",201,269;
		end;
	}
	mes "- 你決定留下來。-";
	close;
}

mosk_dun02,268,117,3	script	Milestone#2-3	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 前進者有榮，但可能會死；後退者有勇氣，但仍可能會受傷。";
	mes "如果你真的勇敢，就勇敢地向前邁出一步。否則，退後一步 -^000000";
	next;
	if(select("向前一步:留下來。") == 1) {
		mes "——你勇敢地向前走。-";
		close2;
		warp "mosk_dun03",32,135;
		end;
	}
	mes "- 你決定留下來。-";
	close;
}

mosk_dun03,27,131,3	script	Milestone#3-2	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "^3131FF- 留下的人可能會死，但會獲得榮譽，回去的人可能會受傷，但會獲得勇氣。";
	mes "有勇氣的人就回去，沒有勇氣的人就留下來。-^000000";
	next;
	if(select("回去吧:留下來。") == 1) {
		mes "- 你感覺到危險並決定回去 -";
		close2;
		warp "mosk_dun02",262,118;
		end;
	}
	mes "- 你決定留下來。-";
	close;
}

mosk_dun03,119,46,3	script	Milestone#3-1	HIDDEN_NPC,{
	mes "一塊大石頭上寫著字。";
	next;
	mes "^3131FF- 留下的人可能會死，但會獲得榮譽，回去的人可能會受傷，但會獲得勇氣。";
	mes "怕死的人就退去，否則就向前走，留下來。-^000000";
	next;
	if(select("回去吧:留下來。") == 1) {
		mes "- 你感覺到危險並決定回去 -";
		close2;
		warp "mosk_dun01",257,190;
		end;
	}
	mes "- 你決定留下來。-";
	close;
}

mosk_dun01,194,43,3	script	Milestone#1-1	HIDDEN_NPC,{
	mes "- 一塊大石頭上寫著字。-";
	next;
	mes "——如果你想保命，就回去吧——";
	close;
}

mosk_fild02,198,53,3	script	Ship#mos_dun	HIDDEN_NPC,{
	mes "- 你發現一艘停泊的船。-";
	next;
	if(select("乘船返回村莊。 :探索更多。") == 1) {
		mes "- 你登上船並拉槳。-";
		close2;
		warp "moscovia",140,54;
		end;
	}
	mes "- 那個未知的領域引起了你的興趣。-";
	close;
}
